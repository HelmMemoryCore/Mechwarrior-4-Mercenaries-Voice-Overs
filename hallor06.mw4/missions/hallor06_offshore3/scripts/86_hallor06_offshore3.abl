
//*********************************************************************************

fsm HalloranV_06 : integer;		

//------------------------------------------------------------------
//
//     Constant Definitions
//
//------------------------------------------------------------------
     const

		#include_ <content\ABLScripts\mwconst.abi>
		#include_ <content\ABLScripts\Katie\katie_consts.abi>
//------------------------------------------------------------------
//
//     Type Definitions
//
//------------------------------------------------------------------
     type

		#include_ <content\ABLScripts\mwtype.abi>

//------------------------------------------------------------------
//
//     Variable Declarations
//
//------------------------------------------------------------------
      var
			
		eternal integer playermech;
		
		eternal Boolean	play1;			//Is this the first time the mission is being played?
		eternal Boolean play2;			//Is this the second time the mission is being played?
		eternal Boolean play3;			//Is this the third time the mission is being played?
		
		eternal integer numFuelTanks;	//Is the number of objects in group 15.
		
		eternal boolean obj1;			//Has Objective one been completed?
		eternal Boolean obj2;			//has Objective two been completed?
		eternal Boolean obj3;			//has Objective three been completed?
		eternal integer enemyGroup;		//All the enemies in the mission.
		
		eternal Boolean launchPlanes;	//Used in planes.abl, launches the planes, set in Destroyers.abl and Mechs.abl
		eternal boolean angryCobras;	//did the player piss off the Cobras last time.
		
		static integer	cinema_cut;
		eternal boolean startTheFun;
		
		eternal Integer DavionRating;
		eternal Integer SteinerRating;
		eternal Integer Nobility;
		eternal Integer Infamy;
		
		static boolean FirstAlphaVO;
		static boolean SecondAlphaVO;
		static boolean FirstCobraVO;
		static boolean secondCobraVO;
		static boolean thirdCobraVO;
		static boolean FirstturretVO;
		static boolean SecondturretVO;
		
		static 	integer Group1Size;
		
		static boolean AlphaReached;
		static boolean BetaReached;
		static boolean GammaReached;
		static boolean DeltaReached;
		static integer CheckRange;
		
		
//------------------------------------------------------------------
//
//     Init Function    (automatically run first time through)
//
//------------------------------------------------------------------

function init;
	code

		playermech = getplayervehicle(epl_player0);	
		if (GetLancemate(1) <> NO_UNIT) then
			GroupAddObject(GroupObjectId(5),GetLancemate(1));
		endif;
		if (GetLancemate(2) <> NO_UNIT) then
			GroupAddObject(GroupObjectId(5),GetLancemate(2));
		endif;
		if (GetLancemate(3) <> NO_UNIT) then
			GroupAddObject(GroupObjectId(5),GetLancemate(3));
		endif;
		if (GetSecondaryLancemate(1) <> NO_UNIT) then
			GroupAddObject(GroupObjectId(5),GetSecondaryLancemate(1));
		endif;
		if (GetSecondaryLancemate(2) <> NO_UNIT) then
			GroupAddObject(GroupObjectId(5),GetSecondaryLancemate(2));
		endif;
		if (GetSecondaryLancemate(3) <> NO_UNIT) then
			GroupAddObject(GroupObjectId(5),GetSecondaryLancemate(3));
		endif;
		if (GetSecondaryLancemate(4) <> NO_UNIT) then
			GroupAddObject(GroupObjectId(5),GetSecondaryLancemate(4));
		endif;
		Group1Size = GroupSize(GroupObjectId(5));
		
		obj1 = false;
		obj2 = false;
		obj3 = false;
		angryCobras = GetEventFlag("HV04_AngryCobras");
		
		// Create the objectives group
		GroupAddObject(GroupObjectID(10), ebu_ObjectiveBuilding02);
		GroupAddObject(GroupObjectID(10), ebu_ObjectiveBuilding03);
		GroupAddObject(GroupObjectID(10), ebu_ObjectiveBuilding04);
		
		//Turrets
		GroupAddObject(GroupObjectID(16), etu_turret01);
		GroupAddObject(GroupObjectID(16), etu_turret02);
		SetGroupAI	  (GroupObjectID(16), GroupAI_RADIOSQUAD);
		
		// Create the secondary objectives group
		GroupAddObject(GroupObjectID(15), ebu_FuelTank01);
		GroupAddObject(GroupObjectID(15), ebu_FuelTank02);
		GroupAddObject(GroupObjectID(15), ebu_FuelTank03);
		GroupAddObject(GroupObjectID(15), ebu_FuelTank04);
		GroupAddObject(GroupObjectID(15), ebu_FuelTank05);
		GroupAddObject(GroupObjectID(15), ebu_FuelTank06);
		GroupAddObject(GroupObjectID(15), ebu_FuelTank07);
		GroupAddObject(GroupObjectID(15), ebu_FuelTank08);
		GroupAddObject(GroupObjectID(15), ebu_FuelTank09);
		GroupAddObject(GroupObjectID(15), ebu_FuelTank10);
		GroupAddObject(GroupObjectID(15), ebu_FuelTank11);
		GroupAddObject(GroupObjectID(15), ebu_FuelTank12);
		GroupAddObject(GroupObjectID(15), ebu_FuelTank13);
		GroupAddObject(GroupObjectID(15), ebu_FuelTank14);
		GroupAddObject(GroupObjectID(15), eve_FuelTruck01);
		GroupAddObject(GroupObjectID(15), eve_FuelTruck02);
		GroupAddObject(GroupObjectID(15), eve_FuelTruck03);
		GroupAddObject(GroupObjectID(15), eve_FuelTruck04);
		GroupAddObject(GroupObjectID(15), eve_FuelTruck05);
		GroupAddObject(GroupObjectID(15), eve_barge01);
		GroupAddObject(GroupObjectID(15), eve_barge02);
		GroupAddObject(GroupObjectID(15), eve_barge03);
		GroupAddObject(GroupObjectID(15), eve_barge04);
		GroupAddObject(GroupObjectID(15), eve_barge05);
		SetGroupAI	  (GroupObjectID(15), GroupAI_MOODSQUAD);
		numFuelTanks = GroupSize(GroupObjectID(15));
		
		//Wave 1; boats
		GroupAddObject(GroupObjectID(20), eve_Wave101);
		GroupAddObject(GroupObjectID(20), eve_Wave102);
		GroupAddObject(GroupObjectID(20), eve_Wave103);
		GroupAddObject(GroupObjectID(20), eve_Wave104);
		GroupAddObject(GroupObjectID(20), eve_Wave105);
		GroupAddObject(GroupObjectID(20), eve_Wave106);
		SetGroupAI	  (GroupObjectID(20), GroupAI_RadioSquad);
		//Wave 2; hovers
		GroupAddObject(GroupObjectID(25), eve_Wave201);
		GroupAddObject(GroupObjectID(25), eve_Wave202);
		GroupAddObject(GroupObjectID(25), eve_Wave203);
		GroupAddObject(GroupObjectID(25), eve_Wave204);
		GroupAddObject(GroupObjectID(25), eve_Wave205);
		GroupAddObject(GroupObjectID(25), eve_Wave206);
		SetGroupAI	  (GroupObjectID(25), GroupAI_RadioSquad);
		// the Cobras
		GroupAddObject(GroupObjectID(41), eve_Cobra01);
		GroupAddObject(GroupObjectID(41), eve_Cobra02);
		GroupAddObject(GroupObjectID(41), eve_Cobra03);
		GroupAddObject(GroupObjectID(41), eve_Cobra04);
		GroupAddObject(GroupObjectID(41), eve_Cobra05);
		SetGroupAI	  (GroupObjectID(41), GroupAI_RadioSquad);
		
		//EnemyGroup
		enemyGroup = GroupObjectID(45); 
		GroupAddObject(enemyGroup, eve_Wave101);
		GroupAddObject(enemyGroup, eve_Wave102);
		GroupAddObject(enemyGroup, eve_Wave103);
		GroupAddObject(enemyGroup, eve_Wave104);
		GroupAddObject(enemyGroup, eve_Wave105);
		GroupAddObject(enemyGroup, eve_Wave106);
		GroupAddObject(enemyGroup, eve_Wave201);
		GroupAddObject(enemyGroup, eve_Wave202);
		GroupAddObject(enemyGroup, eve_Wave203);
		GroupAddObject(enemyGroup, eve_Wave204);
		GroupAddObject(enemyGroup, eve_Wave205);
		GroupAddObject(enemyGroup, eve_Wave206);
		GroupAddObject(enemyGroup, eve_plane01);
		GroupAddObject(enemyGroup, eve_plane02);
		GroupAddObject(enemyGroup, eve_plane03);
		GroupAddObject(enemyGroup, eve_plane04);
		GroupAddObject(enemyGroup, eve_plane05);
		GroupAddObject(enemyGroup, eve_plane06);
		GroupAddObject(enemyGroup, eve_plane07);
		GroupAddObject(enemyGroup, eve_plane08);
	  	GroupAddObject(enemyGroup, eve_Cobra01);	
	  	GroupAddObject(enemyGroup, eve_Cobra02);	
	  	GroupAddObject(enemyGroup, eve_Cobra03);	
	  	GroupAddObject(enemyGroup, eve_Cobra04);	
	  	GroupAddObject(enemyGroup, eve_Cobra05);	
	  	GroupAddObject(enemyGroup, eve_Chopper01);	
	  	GroupAddObject(enemyGroup, eve_Chopper02);	
	  	GroupAddObject(enemyGroup, eve_Chopper03);	
	  	GroupAddObject(enemyGroup, eve_Chopper04);	
		SetGroupAI	  (enemyGroup, GroupAI_NONE);
		
		cinema_cut = 0;
		
		DavionRating = GetPreviousDavionFactionRating;
		SteinerRating = GetPreviousSteinerFactionRating;
		SteinerRating = SteinerRating + 3;
		Nobility = GetPreviousNobility;
		Nobility = Nobility + 4;
		Infamy = GetPreviousInfamy;
		CheckRange = 100;
		
endfunction;

		#include_ <content\ABLScripts\Katie\katie_script.abi>	// <-- my stuff

//------------------------------------------------------------------
//
//     Main Code
//
//------------------------------------------------------------------
state startState;

	code	
		// Intro VO
		PlayVoiceOverOnce("VO\Halloran\MI1c\Intro1", talker_CAS);
		PlayVoiceOverOnce("VO\Halloran\MI1c\Intro2", talker_SPE);
		PlayVoiceOverOnce("VO\Halloran\MI1c\Intro3", talker_JUN);
		PlayVoiceOverOnce("VO\Halloran\MI1c\Intro4", talker_SPE);
		PlayVoiceOverOnce("VO\Halloran\MI1c\Intro5", talker_JUN);
		PlayVoiceOverOnce("VO\Halloran\MI1c\Intro6", talker_CAS);
		StartMusic("Music\behemoths",0.0,3.0,40);
			
		// Reveal the Nav Points	   
		setRadarNav(ena_Nav_Alpha, 1);
		setRadarNav(ena_Nav_Beta, 2);
		setRadarNav(ena_Nav_Gamma, 3);
		setRadarNav(ena_Nav_Delta, 4);
		revealNavpoint(ena_Nav_Alpha);
		revealNavpoint(ena_Nav_Beta);
		revealNavpoint(ena_Nav_Gamma);
		revealNavpoint(ena_Nav_Delta);
		setNavpoint (ena_Nav_Alpha);
			
		//Reveal the objectives
		RevealObjective(eob_Objective01);
		RevealObjective(eob_Objective02);
		RevealObjective(eob_Objective03);

		//Salvage Chance
// Shark - replaced these with Katie calls
//		SetKillSalvageChance(eve_Cobra02, 50);		//Uziel
//		SetKillSalvageChance(eve_Cobra04, 50);		//Uziel
//		SetKillSalvageChance(eve_Cobra01, 100);		//Argus
		
		trans opening;

endstate;

	
state deadState;
	code
endstate;
	
//------------------------------------------------------------------
//
//     Sit state : description
//
//------------------------------------------------------------------
state sit;

	code
	//Music
	if (not ismusicplaying) then
		StartMusic("Music\arcticambloop",0.0,3.0,40);
	endif;
	
	//VO
	if (not firstAlphaVO) then
		if timeGreater(1, 5) then
			if (Group1Size > 0) then
				PlayVoiceOverOnce("VO\Halloran\MI1c\GiveReadyReport", talker_SPE);
				PlayLanceMateSound("vo\generic\ReadyOp",1);
			endif;
			firstAlphaVO = true;
		endif;
	endif;
	if (not secondAlphaVO) then
		if IsWithin(playerMech, GroupObjectID(20), 900) and firstAlphaVO then
			PlayLanceMateSound("vo\generic\Contact900",1);
			setNavpoint (ena_Nav_Beta);
			secondAlphaVO = true;
		endif;
	endif;
	if (not firstCobraVO) then
		if LaunchPlanes then
			PlayVoiceOverOnce("VO\Halloran\MI1c\InfamousSpectre", talker_DRA);
			PlayVoiceOverOnce("VO\Halloran\MI1c\InfamousSpectreRetort", talker_Spe);
			PlayVoiceOverOnce("VO\Halloran\MI1c\DragonFlyHere", talker_DRA);
			if AngryCobras then
				PlayVoiceOverOnce("VO\Halloran\MI1c\MakeYouPay", talker_DRA);
			endif;
			PlayVoiceOverOnce("VO\Halloran\MI1c\WelcomeToTry", talker_Spe);
			PlayVoiceOverOnce("VO\Halloran\MI1c\ChoppersIncoming", talker_Cas);
			PlayVoiceOverOnce("VO\Halloran\MI1c\AnyNightWing", talker_Spe);
			PlayVoiceOverOnce("VO\Halloran\MI1c\Negative", talker_Cas);
			PlayVoiceOverOnce("VO\Halloran\MI1c\MaintainAirwatch", talker_Spe);
			firstCobraVO = true;
			resettimer(1);
		endif;
	endif;
	if (not SecondCobraVO) then
		if TimeGreater(1, 60) and FirstCobraVO then
			PlayVoiceOverOnce("VO\Halloran\MI1c\NightShadesInbound", talker_Cas);
			PlayVoiceOverOnce("VO\Halloran\MI1c\TanglingCobras", talker_Spe);
			secondCobraVO = true;
		endif;
	endif;
	if (not ThirdCobraVO) then
		if groupNumDead(GroupObjectID(41)) > 0 then
			PlayVoiceOverOnce("VO\Halloran\MI1c\CobraTaunt", talker_SPE);
			PlayVoiceOverOnce("VO\Halloran\MI1c\YouWillPay", talker_DRA);
			ThirdCobraVO = true;
		endif;
	endif;
	if (not FirstTurretVO) then
		if (GroupNumDead(groupObjectID(16)) > 0 ) then
			PlayVoiceOverOnce("VO\Halloran\MI1c\CalliopeOffline", talker_JUN);
			FirstTurretVO = true;
		endif;
	endif;
	if (not SecondTurretVO) then
		if (GroupNumDead(groupObjectID(16)) > 1 ) then
			PlayVoiceOverOnce("VO\Halloran\MI1c\Calliope2down", talker_JUN);
			PlayVoiceOverOnce("VO\Halloran\MI1c\CalliopesDestroyed", talker_JUN);
			PlayVoiceOverOnce("VO\Halloran\MI1c\NoFaith", talker_SPE);
			PlayLanceMateSound("vo\generic\Amused",1);
			SecondTurretVO = true;
		endif;
	endif;
	
	// Betty speaks when reaching Nav Point
	if (not alphareached) then
		if (isnavrevealed(ena_nav_alpha)) then
			if (iswithin(playermech,ena_nav_alpha,checkrange)) then
				playvoiceover("VO\generic\navalpha",talker_Bet);
				alphareached = true;
			endif;
		endif;
	endif;
	if (not betareached) then
		if (isnavrevealed(ena_nav_beta)) then
			if (iswithin(playermech,ena_nav_beta,checkrange)) then
				playvoiceover("VO\generic\navbeta",talker_Bet);
				betareached = true;
			endif;
		endif;
	endif;
	if (not gammareached) then
		if (isnavrevealed(ena_nav_gamma)) then
			if (iswithin(playermech,ena_nav_gamma,checkrange)) then
				playvoiceover("VO\generic\navgamma",talker_Bet);
				gammareached = true;
			endif;
		endif;
	endif;
	if (not Deltareached) then
		if (isnavrevealed(ena_nav_Delta)) then
			if (iswithin(playermech,ena_nav_Delta,checkrange)) then
				playvoiceover("VO\generic\navDelta",talker_Bet);
				Deltareached = true;
			endif;
		endif;
	endif;
	
	//Vo end
	
	
	// If half of the Fuel industry vehicles and buildings are destroyed, fail the secondary objective
	if (not obj3) then
		if ( groupNumDead(GroupObjectID(15)) > (numFuelTanks/2) ) then
			failObjective(eob_objective03);
			obj3 = true;
		endif;
	endif;

	// If an Oil drill dies, mission fail
	if (not obj1) then
		if isDead(ebu_ObjectiveBuilding01) then
			failObjective(eob_objective01);
			obj1 = true;
			
			cinema_cut = 0;
			resetTimer(21);
			trans lostobj1;
		endif;
	endif;
	
	if (not obj2) then
		if (GroupNumDead(GroupObjectID(10)) > 0 ) then
			failObjective(eob_objective02);
			obj2 = true;
			
			cinema_cut = 0;
			resetTimer(21);
			trans lostobj2;
		endif;
	endif;
	
	// if all attackers are dead, you win
	if (not obj1) and (not obj2) then
		if groupAllDead(enemyGroup) then
			successObjective(eob_Objective01);
			obj1 = true;
			successObjective(eob_Objective02);
			obj2 = true;
			if (not obj3) then
				successObjective(eob_Objective03);
				AddMissionMoneyToBalance(500000);
				obj3 = true;
			endif;
			// Salvage
			//AddMechInstanceSalvageDamaged("Argus");
			//
			cinema_cut = 0;
			resetTimer(21);
			
			/////////////////////
			// MISSION SALVAGE //
			/////////////////////
			
			Katie(eve_Cobra01,70);
			Katie(eve_Cobra02,60);
			Katie(eve_Cobra03,50);
			Katie(eve_Cobra04,80);

			trans won;
		endif;
	endif;
	
			 
//Fail the mission when the player dies
	if (isdead(playermech)) then
		failobjectiveall(9);
		
		cinema_cut = 0;
		resetTimer(21);
		trans lostPlayer;
	endif;
	
endstate;

//-----------------------------------------
//HERE THERE BE CINEMAS
//-----------------------------------------

state opening;
	code

		if cinemaSkip then
			CinemaEnd;
			SetCompositingEnabled(true);			
			playerAI(playermech, false);
			KillVoiceOvers;
			startTheFun = true;
			resetTimer(1);
			trans sit;
		endif;
 	
		switch (cinema_cut)
			case 0:
				SetActiveCamera(eca_CameraShip);
				SpinUnderCamera(playermech, playermech, 30.0, 35.0, true);				
				cinema_cut = cinema_cut + 1;
			endcase;
			case 1:
				if (not isVoiceOverPlaying) then
				//if TimeGreater(21, 13) then			//Timer 21, standard cinema timer
					cinema_cut = cinema_cut + 1;
				endif;
			endcase;
			case 2:
				CinemaEnd;
				SetCompositingEnabled(true);			
				playerAI(playermech, false);
				startTheFun = true;
				resetTimer(1);
				trans sit;
			endcase;
		endswitch;
endstate;

state won;
	code
	
		switch cinema_cut
			case 0:
				switch rand(1,2)
					case 1:
						StartMusic("Music\genericVictory1",3.5,3.0,50);
					endcase;
					case 2:
						StartMusic("Music\genericVictory2",3.5,3.0,50);
					endcase;
				endswitch;
				PlayVoiceOverOnce("VO\Halloran\MI1c\Close1", talker_CAS);
				PlayVoiceOverOnce("VO\Halloran\MI1c\Close2", talker_SPE);
				PlayVoiceOverOnce("VO\Halloran\MI1c\Close3", talker_CAS);
				cinema_cut = cinema_cut + 1;
			endcase;
			case 1:
				if TimeGreater(21,7) then
					cinema_cut = cinema_cut + 1;
				endif;
			endcase;
			case 2:	
				//EndMission(true, 10);
				SetActiveCamera(eca_CameraShip);
				PullOutCamera(playermech, playermech, 250.0, 10.0, true);
				UpdateCameraPosition(eca_CameraShip);     
								
				SetCurrentInfamy(Infamy);
				SetCurrentNobility(Nobility);
				SetCurrentDavionFactionRating(DavionRating);
				SetCurrentSteinerFactionrating(SteinerRating);
				
				cinema_cut = cinema_cut + 1;
			endcase;
			case 3:
				if (not IsVoiceOverPlaying) then
					EndMission(true, 3);
					cinema_cut = cinema_cut + 1;
				endif;
			endcase;
		endswitch;
 
endstate;

state lostPlayer;
	code
	
		switch cinema_cut
			case 0:
				KillVoiceOvers;
				switch rand(1,2)
					case 1:
						StartMusic("Music\genericLoss1",3.5,3.0,50);
					endcase;
					case 2:
						StartMusic("Music\genericLoss2",3.5,3.0,50);
					endcase;
				endswitch;
				switch rand(1,3)
					case 1:
						PlayVoiceOver("VO\Generic\SpecterDestroyed", talker_CAS);
					endcase;
					case 2:
						PlayVoiceOver("VO\Generic\SpecterDestroyed2", talker_CAS);
					endcase;
					case 3:
						PlayVoiceOver("VO\Generic\SpecterDestroyed3", talker_CAS);
					endcase;
				endswitch;
				cinema_cut = cinema_cut + 1;
			endcase;
			case 1:	
				EndMission(false, 10);
				//SetActiveCamera(eca_CameraShip);
				//PullOutCamera(playermech, playermech, 250.0, 10.0, true);				
				cinema_cut = cinema_cut + 1;
			endcase;
		endswitch;
	
endstate;

state lostobj1;
	code
	
		switch cinema_cut
			case 0:
				KillVoiceOvers;
				switch rand(1,2)
					case 1:
						StartMusic("Music\genericLoss1",3.5,3.0,50);
					endcase;
					case 2:
						StartMusic("Music\genericLoss2",3.5,3.0,50);
					endcase;
				endswitch;
				switch rand(1,5)
					case 1:
						PlayVoiceOver("VO\Generic\Abort", talker_CAS);
					endcase;
					case 2:
						PlayVoiceOver("VO\Generic\Abort2", talker_CAS);
					endcase;
					case 3:
						PlayVoiceOver("VO\Generic\Abort3", talker_CAS);
					endcase;
					case 4:
						PlayVoiceOver("VO\Generic\Abort4", talker_CAS);
					endcase;
					case 5:
						PlayVoiceOver("VO\Generic\Abort5", talker_CAS);
					endcase;
				endswitch;
				cinema_cut = cinema_cut + 1;
			endcase;
			case 1:
				if TimeGreater(21,5) then
					cinema_cut = cinema_cut +1;
				endif;
			endcase;
			case 2:	
				EndMission(false, 10);
				SetActiveCamera(eca_CameraShip);
				HeadOnCamera(ebu_ObjectiveBuilding01, ebu_ObjectiveBuilding01, 25.0, 10.0, true);				
				UpdateCameraPosition(eca_CameraShip);     
				cinema_cut = cinema_cut + 1;
			endcase;
		endswitch;
	
endstate;

state lostobj2;
	var
		static ObjectID deadRig;
	code
	
		switch cinema_cut
			case 0:
				KillVoiceOvers;
				switch rand(1,2)
					case 1:
						StartMusic("Music\genericLoss1",3.5,3.0,50);
					endcase;
					case 2:
						StartMusic("Music\genericLoss2",3.5,3.0,50);
					endcase;
				endswitch;
				switch rand(1,5)
					case 1:
						PlayVoiceOver("VO\Generic\Abort", talker_CAS);
					endcase;
					case 2:
						PlayVoiceOver("VO\Generic\Abort2", talker_CAS);
					endcase;
					case 3:
						PlayVoiceOver("VO\Generic\Abort3", talker_CAS);
					endcase;
					case 4:
						PlayVoiceOver("VO\Generic\Abort4", talker_CAS);
					endcase;
					case 5:
						PlayVoiceOver("VO\Generic\Abort5", talker_CAS);
					endcase;
				endswitch;
				cinema_cut = cinema_cut + 1;
			endcase;
			case 1:
				if TimeGreater(21,5) then
					if isDead(ebu_ObjectiveBuilding02) then
						deadRig = ebu_ObjectiveBuilding02;
					else
						if isDead(ebu_ObjectiveBuilding03) then
							deadRig = ebu_ObjectiveBuilding03;
						else
							deadRig = ebu_ObjectiveBuilding04;
						endif;
					endif;
					cinema_cut = cinema_cut + 1;
				endif;
			endcase;
			case 2:	
				EndMission(false, 10);
				SetActiveCamera(eca_CameraShip);
				HeadOnCamera(deadRig, deadRig, 25.0, 10.0, true);				
				UpdateCameraPosition(eca_CameraShip);     
				cinema_cut = cinema_cut + 1;
			endcase;
		endswitch;
	
endstate;

endfsm.
//******************************************************************

