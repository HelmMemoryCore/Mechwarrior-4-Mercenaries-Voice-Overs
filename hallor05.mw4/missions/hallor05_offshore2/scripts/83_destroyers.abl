//*********************************************************************************

fsm HalloranV01_Destroyers : integer;		

//------------------------------------------------------------------
//
//     Constant Definitions
//
//------------------------------------------------------------------
     const

		#include_ <content\ABLScripts\mwconst.abi>
//------------------------------------------------------------------
//
//     Type Definitions
//
//------------------------------------------------------------------
     type

		#include_ <content\ABLScripts\mwtype.abi>

//------------------------------------------------------------------
//
//     Variable Declarations
//
//------------------------------------------------------------------
	var
		static integer			attackRange;	// At what range do I start shooting?
		static integer			withdrawRange;	// At what range do I stop attacking?
		static integer			skillLevel;
		
		static ObjectID			unitPath;		// the path the units follow	
		static LocPoint			spawnPoint;
		static integer			memint9;		//Memory integer 9
		static Integer			launchTime;		//Time when unit launches
		static boolean			pathEndReached;
		static boolean			earlyLaunch;
		
 function Init;
	code
		// script-specific variables
		attackRange				= 800;
		withdrawRange			= attackRange * 3 / 2;
		skillLevel				= 4;
		pathEndReached			= false;
		earlyLaunch				= false;
		
		memint9 = getMemoryInteger(me,9);
		switch(memint9)
			case 1:
				launchTime = 125;
				GetLocation(ena_DestroyerSpawn, spawnPoint);
				unitPath = epa_DestroyerPath;
			endcase;
			case 2:
				launchTime = 165;
				GetLocation(ena_DestroyerSpawn02, spawnPoint);
				unitPath = epa_DestroyerPath02;
			endcase;
		endswitch;
		
endfunction;
		
state StartState;

	code
	
		AssignSkill(skillLevel);
		ClearMoveOrder(me);
		shutdown(me);
		SetSensorVisibility(me, false);
		resetTimer(25);		
		trans waitForLaunch;
		
endstate;	   
 	
state waitForLaunch;
	
	code
	
	if timeGreater(25, launchTime) then
		teleport(me, spawnPoint);
		startup(me);
		SetSensorVisibility(me, true);
		launchPlanes = true;				// Planes Launch when Destroyers shows up
		StartMusic("Music\imminentaction",3.5,3.0,50);
		trans sit;
	endif;
	if GroupAllDead(groupObjectID(25)) and (not earlyLaunch) then
		launchTime = launchTime - (125 - GetTimer(25));			//Launches destroyers right away
		earlyLaunch = true;
	endif;
		
endstate;

state sit;
	code
	
		if pathEndReached then
			orderMoveLookOut;
		else
			if OrderMoveToRigid(unitPath, 40, MOVE_NOCYCLE, true, false) then
				pathEndReached = true;
			endif;
		endif;
	
		if FindEnemy(attackRange, FT_DEFAULT+FT_BUILDING+FT_UNARMED) then
			trans attackState;
		endif;
		
		if ( isShot ( me ) ) then
			setTarget(me, whoShot(me) );
			trans AttackState;
		endif;
		
		if ( isDead(me)) then
			trans deadstate;
		endif;
		
		if IsWithin(me, ena_HidePoint, 500) then
			ClearMoveOrder(me);
			teleport(me, spawnPoint);
		endif;
		
endstate;

state AttackState;

	code
	
		if( leaveAttackState(withdrawrange) ) then
			orderStopAttacking;
			settarget(me, NO_UNIT);
			trans sit;
		else
			OrderAttackTactic(TACTIC_PICK_BEST, false);
		endif;
		
		if ( isDead(me)) then
			trans deadstate;
		endif;
		
		if IsWithin(me, ena_HidePoint, 500) then
			ClearMoveOrder(me);
			teleport(me, spawnPoint);
		endif;
		
			
endstate;

state deadstate;
	code
		
endstate;


endfsm.
