//*********************************************************************************

fsm mechs : integer;		

//------------------------------------------------------------------
//
//     Constant Definitions
//
//------------------------------------------------------------------
     const

		#include_ <content\ABLScripts\mwconst.abi>
//------------------------------------------------------------------
//
//     Type Definitions
//
//------------------------------------------------------------------
     type

		#include_ <content\ABLScripts\mwtype.abi>

//------------------------------------------------------------------
//
//     Variable Declarations
//
//------------------------------------------------------------------
	var
		static integer			attackRange;	// At what range do I start shooting?
		static integer			withdrawRange;	// At what range do I stop attacking?
		static integer			skillLevel;		// 1 - 10
		static integer			memInt9;		// value of Memory Integer number 9
		static integer			MoveType;		
		static boolean			pathEndReached;

		static ObjectID			unitPath;		// the path the units follow
		static LocPoint			spawnPoint;
		static LocPoint			leashPoint;
		
		static integer			waitTimer;

 function Init;
	code
		// script-specific variables
		attackRange				= 800;
		withdrawRange			= attackRange * 3 / 2;
		skillLevel				= 2;						// 1 - 10
		memInt9 				= GetMemoryInteger(me, 9);	// Value of memory integer number 9
		pathEndReached 			= false;
		moveType 				= MOVE_NOCYCLE;
		unitPath 				= NO_UNIT;
		waitTimer				= gti_timer_29;

		switch (memint9)
			case 1:
			endcase;
			case 2:
			endcase;
			case 3:
				unitPath = epa_PatrolPath01;
				getLocation(me, spawnPoint);
				clearMoveOrder(me);
				shutdown(me);
				teleportToHell(me);
			endcase;
			case 4:
				unitPath = epa_PatrolPath02;
				getLocation(me, spawnPoint);
				clearMoveOrder(me);
				shutdown(me);
				teleportToHell(me);
			endcase;
		endswitch;
		
endfunction;
		
state StartState;

	code
	
	AssignSkill(skillLevel);
	trans WaitforLaunch; 	

endstate;

state WaitforLaunch;
	code
	
		switch (memint9)
			case 1:
				trans sit;
			endcase;
			case 2:
				trans sit;
			endcase;
			case 3:
				if obj2 then	//Base Defenders Dead
					teleport(me, spawnPoint);
					startup(me);
					resetTimer(waitTimer);
					trans sit;
				endif;
			endcase;
			case 4:
				//if techsLanded then		//Techs are on the ground
				if timeGreater(waitTimer, 90) then
					teleport(me, spawnPoint);
					startup(me);
					trans sit;
				endif;
			endcase;
		endswitch;
		
endstate;		

state sit;
	code
		
		if pathEndReached or (unitPath == NO_UNIT) then
			OrderMoveLookout;
		else
			if OrderMoveResumePatrol(unitPath, 200, MoveType, true, true) then
				pathEndReached = true;
			endif;
		endif;
		
		if FindEnemy(attackRange, FT_DEFAULT) then
			trans attackState;
		endif;
		
		if ( isShot ( me ) ) then
			setTarget(me, whoShot(me) );
			trans AttackState;
		endif;
		
		if isdead(me) then
			trans deadState;
		endif;
		
endstate;

state AttackState;

	code
	
		if( leaveAttackState(withdrawrange) ) then
			orderStopAttacking;
			settarget(me, NO_UNIT);
			trans sit;
		else
			OrderAttackTactic(TACTIC_PICK_BEST, true);
		endif;
		
		if ( isShot ( me ) ) then
			setTarget(me, whoShot(me) );
		endif;
		
		if isdead(me) then
			trans deadState;
		endif;
		
endstate;

state deadstate;
	code
		
endstate;

endfsm.
