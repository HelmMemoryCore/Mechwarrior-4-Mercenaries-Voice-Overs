//*********************************************************************************

fsm Fact05_Championship : integer;

//------------------------------------------------------------------
//
//	 Constant Definitions
//
//------------------------------------------------------------------
	 const

		#include_ <content\ABLScripts\mwconst.abi>
//------------------------------------------------------------------
//
//	 Type Definitions
//
//------------------------------------------------------------------
	 type

		#include_ <content\ABLScripts\mwtype.abi>

//------------------------------------------------------------------
//
//	 Variable Declarations
//
//------------------------------------------------------------------
	  var

		eternal integer		playermech;			// This is the variable for the player's 'Mech
			
		// Standard Nav and Objective Variables
		static boolean		alphareached;		// Nav Alpha reached
		static boolean		betareached;		// Nav Beta reached
		static boolean		gammareached;		// Nav Gamma reached
		static boolean		deltareached;		// Nav Delta reached
												//
		static boolean		alpharevealed;		// Nav Alpha is shown
		static boolean		betarevealed;		// Nav Beta is shown
		static boolean		gammarevealed;		// Nav Gamma is shown
		static boolean		deltarevealed;		// Nav Delta is shown
		static integer		checkrange;			// How far from the Nav Point you have to be											
												//
		eternal boolean		obj1;				// This indicates whether Objective 1 was completed
		eternal boolean		obj2;				// This indicates whether Objective 2 was completed
		eternal boolean		obj3;				// This indicates whether Objective 3 was completed
		eternal boolean		obj4;				// This indicates whether Objective 4 was completed
		eternal boolean		obj5;				// This indicates whether Objective 5 was completed
												//
		static boolean		obj1revealed;		// This indicates that Objective 1 has been revealed
		static boolean		obj2revealed;		// This indicates that Objective 2 has been revealed
		static boolean		obj3revealed;		// This indicates that Objective 3 has been revealed
		static boolean		obj4revealed;		// This indicates that Objective 4 has been revealed
		static boolean		obj5revealed;		// This indicates that Objective 5 has been revealed
		//	
		
		static integer		cinema_cut;			// This variable keeps track of what scene a cinema is on.
		static locpoint		temploc;			// Used for commands that need objects converted to locpoint format
		static boolean		loser;				// You have lost the mission
 
		// Trigger States
		eternal integer		RoundNumber;		// Which Solaris VII round this is
		eternal integer		PlayerSpawn;		// Which location the player spawns at
		eternal integer		KillGroup;			// Which group the player is up against this round
		eternal boolean		AllClear;			// Enemies wait until cineractive is over to go into motion
		eternal integer		Combatants;			// Number of combatants
		static integer		Ranking;			// What # the player finishes in
		static integer		TorchesDead;		// Number of Torches dead

		// 2Pass - These are the variables added for second pass
		eternal integer		NumberDead;			// How many are dead right now
		static integer		Fame;				// How famous your character is
		static integer		PlayerKiller;		// The number the player has killed

		static integer		Combatant1;
		static integer		Combatant2;
		static integer		Combatant3;
		static integer		Combatant4;
		static integer		Combatant5;
		static integer		Combatant6;
		static integer		Combatant7;
		static integer		Combatant8;
		static integer		Combatant9;
		static integer		Combatant10;
		static integer		Combatant11;
		static integer		Combatant12;
		static integer		Combatant13;
		static integer		Combatant14;
		static integer		Combatant15;
		
		static integer		ColChant;			// Random value for Coliseum Chant
		//
		
//------------------------------------------------------------------
//
//	 Init Function	(automatically run first time through)
//
//------------------------------------------------------------------

function init;
	var
		integer i;

	code

			playermech = getplayervehicle(epl_player0);

			// 10 - First Round
			GroupAddObject(GroupObjectID(10),eve_Alex_Haight);
			GroupAddObject(GroupObjectID(10),eve_Alice_Newkirk);
			GroupAddObject(GroupObjectID(10),eve_Kayla_Tirrel);
			GroupAddObject(GroupObjectID(10),eve_Peter_Dexter);
			GroupAddObject(GroupObjectID(10),eve_Robert_Yearly);
			GroupAddObject(GroupObjectID(10),eve_Michael_Rose);
			GroupAddObject(GroupObjectID(10),eve_Ricardo_Vecci);
			GroupAddObject(GroupObjectID(10),eve_Fen_Cheng);
			GroupAddObject(GroupObjectID(10),eve_Srin_Odessa);
			GroupAddObject(GroupObjectID(10),eve_Samantha_Coleman);
			GroupAddObject(GroupObjectID(10),eve_Nako_Toyuma);
			GroupAddObject(GroupObjectID(10),eve_1enemy12);
			GroupAddObject(GroupObjectID(10),eve_1enemy13);
			GroupAddObject(GroupObjectID(10),eve_1enemy14);
			GroupAddObject(GroupObjectID(10),eve_1enemy15);

			// 20 - Torches
			GroupAddObject(GroupObjectID(20),ebu_colsm90_torch);
			GroupAddObject(GroupObjectID(20),ebu_colsm90_torch_0007);
			GroupAddObject(GroupObjectID(20),ebu_colsm90_torch_0008);
			GroupAddObject(GroupObjectID(20),ebu_colsm90_torch_0009);
			GroupAddObject(GroupObjectID(20),ebu_colsm90_torch_0010);
			GroupAddObject(GroupObjectID(20),ebu_colsm90_torch_0011);
			GroupAddObject(GroupObjectID(20),ebu_colsm90_torch_0012);
			GroupAddObject(GroupObjectID(20),ebu_colsm90_torch_0013);

			cinema_cut = 0;

			// Culturals
			//SetTarget(ede_example2,eve_example3);
		
endfunction;

function SeasonOver;		// When the season is over, Post-Season script stuff is used
	var

	code
		
		SetEventFlag("SolGrand",True);
		SetEventInteger("SolLight",3);
		SetEventInteger("SolMedium",3);
		SetEventInteger("SolHeavy",3);
		SetEventInteger("SolAssault",3);
		SetEventInteger("SolLightColor",3);
		SetEventInteger("SolMediumColor",3);
		SetEventInteger("SolHeavyColor",3);
		SetEventInteger("SolAssaultColor",3);
		SetEventInteger("SolSecondLightColor",6);
		SetEventInteger("SolSecondMediumColor",6);
		SetEventInteger("SolSecondHeavyColor",6);
		SetEventInteger("SolSecondAssaultColor",6);
		SetEventInteger("SolLightWin",2);
		SetEventInteger("SolMediumWin",2);
		SetEventInteger("SolHeavyWin",2);
		SetEventInteger("SolAssaultWin",2);
		
endfunction;

//------------------------------------------------------------------
//
//	 Main Code
//
//------------------------------------------------------------------
state startState;

	code	

		StartTimer(10);		// Boom timer - enemies teleport in timer
		SetActivationDistance(1600);
		SetMissionNoSalvage(1);
		// Grand Championship specific
		RoundNumber		= 1;
		
				//PlayerSpawn 	= ena_9;
				KillGroup		= GroupObjectID(10);
				Combatants		= 15;
				Combatant1      = eve_Alex_Haight;
				Combatant2      = eve_Alice_Newkirk;
				Combatant3      = eve_Kayla_Tirrel;
				Combatant4      = eve_Peter_Dexter;
				Combatant5      = eve_Robert_Yearly;
				Combatant6      = eve_Michael_Rose;
				Combatant7      = eve_Ricardo_Vecci;
				Combatant8      = eve_Fen_Cheng;
				Combatant9      = eve_Srin_Odessa;
				Combatant10      = eve_Samantha_Coleman;
				Combatant11      = eve_Nako_Toyuma;
				Combatant12		= eve_1enemy12;
				Combatant13		= eve_1enemy13;
				Combatant14		= eve_1enemy14;
				Combatant15		= eve_1enemy15;
				//SetTargetDesirability(PlayerMech,10);
		
		//
		
		SetRadarNav(ena_Nav_Center,1);
		RevealNavpoint(ena_Nav_Center);
		SetNavpoint(ena_Nav_Center);

		//setsensorvisibility(eve_example4,false);
		
		checkrange	= 100;		// How far from a nav point before the Betty voice says you've arrived.

		// 2Pass - Intro settings
		StartMusic("Music\ColiseumAmb",3.0,3.5,50);
		
		ColChant	= rand(30,80);
		StartTimer(4);		// Noises
		NumberDead	= 0;
		Fame		= 0;		// 0 - not famous, 1- getting famous, 2- famous, 3- championship
		//
		
		trans opening;

endstate;

	
state deadState;
	code
	
endstate;
	
//------------------------------------------------------------------
//
//	 Sit state : description
//
//------------------------------------------------------------------
state sit;

	code

	if IsMusicPlaying == false then
		StartMusic("Music\ColiseumAmb",0.0,0.1,50);
	endif;

// Coliseum Triggers

	if (GroupNumDead(KillGroup) > NumberDead) then		// Air horn
		PlayBettySound(COLISEUM_HORN_WAV);
		switch (rand(1,15))
			case 1:
				PlayBettySound(COLISEUM_AHH_WAV);
			endcase;
			case 2:
				PlayBettySound(COLISEUM_AWW_WAV);
			endcase;
			case 3:
				PlayBettySound(COLISEUM_CHEER1_WAV);
			endcase;
			case 4:
				PlayBettySound(COLISEUM_CHEER2_WAV);
			endcase;
			case 5:
				PlayBettySound(COLISEUM_CHEER3_WAV);
			endcase;
			case 6:
				PlayBettySound(COLISEUM_CHEER4_WAV);
			endcase;
			case 7:
				PlayBettySound(COLISEUM_CHEER5_WAV);
			endcase;
			case 10:
				PlayBettySound(COLISEUM_CHEER8_WAV);
			endcase;
			case 11:
				PlayBettySound(COLISEUM_CHEER9_WAV);
			endcase;
		endswitch;
	endif;
	
	if (not GetGlobalTrigger(10)) then
		if (timegreater(1,ColChant)) then
			SetGlobalTrigger(10,true);
			switch (rand(1,2))
				case 1:
					PlayBettySound(COLISEUM_CHANT1_WAV);
				endcase;
				case 2:
					PlayBettySound(COLISEUM_CHANT3_WAV);
				endcase;
			endswitch;
		endif;
	endif;

	if (not GetGlobalTrigger(11)) then
		if (timegreater(2,3)) then		// You've won, make it for next weight arena is cleared
			SetGlobalTrigger(11,true);
			SetEventInteger("Coliseum",0);		// Reset for next match
		endif;
	endif;

	if (GroupNumDead(GroupObjectID(20))<>TorchesDead) then		// If a torch is killed, kill effect
		TorchesDead = GroupNumDead(GroupObjectID(20));

			if (IsDead(ebu_colsm90_torch)) then
				KillEffect(ede_effect_0013);
			endif;
			if (IsDead(ebu_colsm90_torch_0007)) then
				KillEffect(ede_effect_0005);
			endif;
			if (IsDead(ebu_colsm90_torch_0008)) then
				KillEffect(ede_effect_0015);
			endif;
			if (IsDead(ebu_colsm90_torch_0009)) then
				KillEffect(ede_effect_0011);
			endif;
			if (IsDead(ebu_colsm90_torch_0010)) then
				KillEffect(ede_effect_0019);
			endif;
			if (IsDead(ebu_colsm90_torch_0011)) then
				KillEffect(ede_effect_0017);
			endif;
			if (IsDead(ebu_colsm90_torch_0012)) then
				KillEffect(ede_effect_0009);
			endif;
			if (IsDead(ebu_colsm90_torch_0013)) then
				KillEffect(ede_effect_0007);
			endif;
		
	endif;

// Solaris Triggers

	if (SolCycleCheck(PlayerMech)==1) then
		FailObjective(eob_objective1);
		Loser = True;
	endif;

	if (GroupNumDead(KillGroup) > NumberDead) then		// This is death messages
		NumberDead = GroupNumDead(KillGroup);
		StartTimer(3);		// This is for lull messages
		ResetTimer(3);
		if (NumberDead==1) then
			SolDeath(0);
		else
			if (NumberDead==3) then
				SolDeath(1);
			endif;
		endif;
		
		if (Combatants - GroupNumDead(KillGroup) == 2) then		// Final Three
			SolFinalThree(Fame);
		endif;

		if (Combatants - GroupNumDead(KillGroup) == 1) then		// Final Duel
			SolFinalTwo(Fame);
		endif;
		
		PlayerKiller = 0;
		
		if (WhoDestroyed(Combatant1)==PlayerMech) then
			PlayerKiller = PlayerKiller + 1;
		endif;
		if (WhoDestroyed(Combatant2)==PlayerMech) then
			PlayerKiller = PlayerKiller + 1;
		endif;
		if (WhoDestroyed(Combatant3)==PlayerMech) then
			PlayerKiller = PlayerKiller + 1;
		endif;
		if (WhoDestroyed(Combatant4)==PlayerMech) then
			PlayerKiller = PlayerKiller + 1;
		endif;
		if (WhoDestroyed(Combatant5)==PlayerMech) then
			PlayerKiller = PlayerKiller + 1;
		endif;
		
		if (Combatants > 5) then
			if (WhoDestroyed(Combatant6)==PlayerMech) then
				PlayerKiller = PlayerKiller + 1;
			endif;
		endif;
		
		if (Combatants > 6) then
			if (WhoDestroyed(Combatant7)==PlayerMech) then
				PlayerKiller = PlayerKiller + 1;
			endif;
		endif;
		
		if (PlayerKiller == 2) then
			SolKillTwo(Fame);
		endif;
		
		if (PlayerKiller == 3) then
			SolKillThree(Fame);
		endif;
		
	endif;
	
	if (not GetGlobalTrigger(1)) then		// Lull in the Fight code
		if (timegreater(3,60)) then
			SetGlobalTrigger(1,true);
			SolLull;
		endif;
	endif;
	
	if (not GetGlobalTrigger(2)) then
		if (timegreater(1,70)) then		// Color Commentary for the Grand Championship category
			SetGlobalTrigger(2,true);
			PlayVoiceOverOnce("VO\Solaris\GrandColorCommentary1",TALKER_FIS);
		endif;
	endif;
	
	if (not GetGlobalTrigger(3)) then
		if (timegreater(1,35)) then		// Streak messages
			SetGlobalTrigger(3,true);
			SolStreak;
		endif;
	endif;
	
	if (not GetGlobalTrigger(4)) then
		if (timegreater(2,3)) then		// You've won, add to streak
			SetGlobalTrigger(4,true);
			SetEventInteger("SolStreak",GetEventInteger("SolStreak") + 1);
		endif;
	endif;
	
	if (not GetGlobalTrigger(5)) then
		if (IsDead(PlayerMech)) then	// You've died, your streak ends
			SetGlobalTrigger(5,true);
			SetEventInteger("SolStreak",0);
		endif;
	endif;

	if (not GetGlobalTrigger(6)) then		// Second Color Commentary
		if (timegreater(1,150)) then
			SetGlobalTrigger(6,true);
			if (not IsDead(eve_Srin_Odessa)) then
				PlayVoiceOverOnce("VO\Solaris\SecondGrandChampionshipColor",TALKER_FIS);
			else
				PlayVoiceOverOnce("VO\Solaris\SecondGrandChampionshipColorAlt",TALKER_FIS);
			endif;
		endif;
	endif;
	
	if (not GetGlobalTrigger(7)) then		// This plays right away
		SetGlobalTrigger(7,true);
		PlayVoiceOverOnce("VO\SolarisStart",TALKER_SPE);
		StartTimer(3);		// This is for the lull messages
		SolOpening;
	endif;
	
	if (not GetGlobalTrigger(8)) then
		if (obj1) then		// Mission complete
			SetGlobalTrigger(8,true);
			KillVoiceOvers;
			PlayVoiceOverOnce("VO\SolarisEnd",TALKER_SPE);
			ResetTimer(1);
			KillTimer(1);
			SolClosing;
		endif;
	endif;

// Obj 1 - Destroy all enemy 'Mechs

	if (not Obj1) then
		if (GroupAllDead(KillGroup)) then
			SuccessObjective(eob_objective1);
			StartTimer(2);		// You've won!
			Obj1 = True;
		endif;
	endif;

// Victory and Loss conditions
													   
	if (timegreater(2,5)) or (IsDead(PlayerMech)) then
		  
		// Salvage Added by WHO on DATE
		//
		
		SeasonOver;
		SolEnding(3,6,PlayerKiller,Combatants - NumberDead + 1);
		// Arena, Class, Number of Kills, Placing

		switch (Combatants - NumberDead)
			case 0:		// FIRST PLACE
				SetSolarisPlacement(1);
				AddSolarisMoneyToBalance(6000000);
			endcase;
			case 1:		// SECOND PLACE
				SetSolarisPlacement(2);
				SolSecondPlace;
				AddSolarisMoneyToBalance(4800000);
			endcase;
			case 2:		// THIRD PLACE
				SetSolarisPlacement(3);
				SolThirdPlace;
				AddSolarisMoneyToBalance(3600000);
			endcase;
		endswitch;
		
		if (IsDead(PlayerMech)) then
			endmission(true,7);
			//KillVoiceOvers;
			trans DeadState;
		endif;

		if (loser) then
			EndMission(false,5);
			trans lost;
		else
			trans won;	
		endif;
		
	endif;

endstate;

//-----------------------------------------
//HERE THERE BE CINEMAS
//-----------------------------------------

state opening;	  

	code

		if (cinemaskip) then
			KillVoiceOvers;	 
			cinema_cut = 0;	 
			KillTimer(gti_timer_21);
			SetGlobalTrigger(0,true);
			playerai(epl_player0,false);
			StartTimer(1);		// This is the mission start timer
			ResetTimer(1);
			CinemaEnd;		  
			SetCompositingEnabled(True);
			revealobjective(eob_objective1);
			AllClear = True;
			trans sit;
		endif;
		
		switch (cinema_cut)

			case 0:
				//GetLocation(PlayerSpawn,TempLoc);
				//Teleport(PlayerMech,TempLoc);
				CinemaStart;
				SetActiveCamera(eca_CameraShip);
				
				// Intro Message Checker
				RandIntroCamera(PlayerMech,2);		// Coliseum Night = 2
				
				PlayVoiceOverOnce("VO\Solaris\IntroGrandChampionship",TALKER_FIS);
				
				//
				
				cinema_cut = cinema_cut + 1;
				
				starttimer(21);			// This is the cineractive counter.
				resettimer(21);
			endcase;
			
			case 1:
				if (IsVoiceOverPlaying==False) then
					ResetTimer(21);
					cinema_cut = cinema_cut + 1;
				endif;
			endcase;
				
			case 2:
				if (timegreater(21,2)) then
					KillVoiceOvers;
					cinema_cut = 0;
					KillTimer(gti_timer_21);
					SetGlobalTrigger(0,true);
					playerai(epl_player0,false);
					StartTimer(1);		// This is the mission start timer
					ResetTimer(1);
					CinemaEnd;
					revealobjective(eob_objective1);	// Reveal the objectives
					SetCompositingEnabled(True);
					AllClear = True;
					trans sit;
				endif;
			endcase;
				
		endswitch;
	
endstate;

state won;	  

	code
		
		if (cinemaskip) then
			cinema_cut = 20;	
			playerai(epl_player0,false);
			KillVoiceOvers;
			Endmission(true,1); 
		endif;

		switch cinema_cut
			case 0:	
				StartMusic("Music\GenericVictory1",3.0,3.5,50);					 
				SetCompositingEnabled(False);
				CinemaStart;
				setactivecamera (eca_cameraship);				
				
				// Grand Championship Win Message Check
				RandClosingCamera(PlayerMech);
				PlayVoiceOverOnce("VO\Solaris\VictoryGrandChampion",TALKER_FIS);
				//
				
				playerai(epl_player0,true);
				Cinema_cut=Cinema_cut+1;
			endcase;
			
			case 1:
				if (IsVoiceOverPlaying==False) then
					cinema_cut = cinema_cut + 1;
				endif;
			endcase;			

			case 2:
				endmission(true,2);
				Cinema_cut=Cinema_cut+1;
			endcase;
			
		endswitch;
	
endstate;

state lost;

		code
		
		switch cinema_cut
		
			case 0:	
			
			endcase;
			
			case 1:
			
			endcase;
			
		endswitch;
	
endstate;

endfsm.
//******************************************************************
