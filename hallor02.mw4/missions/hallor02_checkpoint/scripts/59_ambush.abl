//*********************************************************************************

fsm Ambush : integer;		

//------------------------------------------------------------------
//
//     Constant Definitions
//
//------------------------------------------------------------------
     const

		#include_ <content\ABLScripts\mwconst.abi>
//------------------------------------------------------------------
//
//     Type Definitions
//
//------------------------------------------------------------------
     type

		#include_ <content\ABLScripts\mwtype.abi>

//------------------------------------------------------------------
//
//     Variable Declarations
//
//------------------------------------------------------------------
	var
		static integer			attackRange;	// At what range do I start shooting?
		static integer			withdrawRange;	// At what range do I stop attacking?
		static integer			skillLevel;		// 1 - 10
		static integer			memInt9;		// value of Memory Integer number 9
		static Boolean			spotted;		// Has the player been seen


 function Init;
	code
		// script-specific variables
		attackRange				= 500;
		withdrawRange			= 2000;
		skillLevel				= 3;						// 1 - 10
		memInt9 				= GetMemoryInteger(me, 9);	// Value of memory integer number 9
		spotted					= false;					// has the ambush been triggered?


endfunction;
		
state StartState;

	code
		AssignSkill(skillLevel);
		
        trans sit; 	
endstate;			

state sit;
	code
		if ( SetMyTarget(attackRange) ) then
			trans AttackState;
		endif;
		
		if ( isShot ( me ) ) then
			setTarget(me, whoShot(me) );
			trans AttackState;
		endif;
		
		if isdead(me) then
			trans deadState;
		endif;
		
		if ( not spotted ) then
			setCrouchState(me, true);	//hide
			shutdown(me);
		else;
			orderMoveLookout;
		endif;
		
endstate;

state AttackState;

	code
	
		if ( not spotted ) then
			startup(me);
			setCrouchState(me, false);	// come out from hiding
			spotted = true;
			GroupAddObject(GroupObjectID(20), me);
		endif;
			
		if( leaveAttackState(withdrawrange) ) then
			orderStopAttacking;
			settarget(me, NO_UNIT);
			trans sit;
		else;
			OrderAttackTactic(TACTIC_PICK_BEST, true);
		endif;
		
		if isdead(me) then
			trans deadState;
		endif;
			
endstate;

state deadstate;
	code
		
endstate;

endfsm.
