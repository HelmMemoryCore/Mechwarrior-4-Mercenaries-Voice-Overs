//*********************************************************************************

fsm Tanks : integer;		

//------------------------------------------------------------------
//
//     Constant Definitions
//
//------------------------------------------------------------------
     const

		#include_ <content\ABLScripts\mwconst.abi>
//------------------------------------------------------------------
//
//     Type Definitions
//
//------------------------------------------------------------------
     type

		#include_ <content\ABLScripts\mwtype.abi>

//------------------------------------------------------------------
//
//     Variable Declarations
//
//------------------------------------------------------------------
	var
		static integer			attackRange;	// At what range do I start shooting?
		static integer			withdrawRange;	// At what range do I stop attacking?
		static integer			skillLevel;		// 1 - 10
		static integer			memInt9;		// value of Memory Integer number 9


		static ObjectID			Unitpath;		// the path the units follow
 function Init;
	code
		// script-specific variables
		attackRange				= 600;
		withdrawRange			= attackRange * 3 / 2;
		skillLevel				= 2;						// 1 - 10
		memInt9 				= GetMemoryInteger(me, 9);	// Value of memory integer number 9

		switch(memint9)
			case 1:
				attackRange	= 600;
				withdrawRange = attackRange * 3 / 2;
			endcase;
			case 2:
				attackRange	= 800;
				withdrawRange = attackRange * 3 / 2;
				skillLevel = 3;
			endcase;
			case 3:
				ShutDown(me);
			endcase;
			case 4:		//Osiris
				attackRange	= 800;
				withdrawRange = attackRange * 3 / 2;
			endcase;
		endswitch;
		
endfunction;
		
state StartState;

	code
	
		if (memint9 == 3) then
			trans deadState;
		else
			AssignSkill(skillLevel);
			if (memint9 == 4) then
				SetAttackThrottle(me, 10);	//Osiris is slow mover
			endif;   
			trans sit;
		endif;
		 	
endstate;			

state sit;
	code
		if ( SetMyTarget(attackRange) ) then
			trans AttackState;
		endif;
		if ( isShot ( me ) ) then
			setTarget(me, whoShot(me) );
			trans AttackState;
		endif;
		
		if isdead(me) then
			trans deadState;
		endif;
		
		orderMoveLookout;
		
endstate;

state AttackState;

	code
	
		if( leaveAttackState(withdrawrange) ) then
			orderStopAttacking;
			settarget(me, NO_UNIT);
			trans sit;
		else;
			OrderAttackTactic(TACTIC_PICK_BEST, true);
		endif;
		
		if isdead(me) then
			trans deadState;
		endif;
			
endstate;

state deadstate;
	code
		
endstate;

endfsm.
