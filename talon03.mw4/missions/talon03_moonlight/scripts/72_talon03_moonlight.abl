//*********************************************************************************

fsm Talon03_Moonlight : integer;

//------------------------------------------------------------------
//
//	 Constant Definitions
//
//------------------------------------------------------------------
	 const

		#include_ <content\ABLScripts\mwconst.abi>
		#include_ <content\ABLScripts\Katie\katie_consts.abi> // <-- mektek
//------------------------------------------------------------------
//
//	 Type Definitions
//
//------------------------------------------------------------------
	 type

		#include_ <content\ABLScripts\mwtype.abi>

//------------------------------------------------------------------
//
//	 Variable Declarations
//
//------------------------------------------------------------------
	  var

		eternal integer		playermech;			// This is the variable for the player's 'Mech
			
		// Standard Nav and Objective Variables
		static boolean		alphareached;		// Nav Alpha reached
		static boolean		betareached;		// Nav Beta reached
		static boolean		gammareached;		// Nav Gamma reached
		static boolean		deltareached;		// Nav Delta reached
												//
		static boolean		alpharevealed;		// Nav Alpha is shown
		static boolean		betarevealed;		// Nav Beta is shown
		static boolean		gammarevealed;		// Nav Gamma is shown
		static boolean		deltarevealed;		// Nav Delta is shown
		static integer		checkrange;			// How far from the Nav Point you have to be											
												//
		eternal boolean		obj1;				// This indicates whether Objective 1 was completed
		eternal boolean		obj2;				// This indicates whether Objective 2 was completed
		eternal boolean		obj3;				// This indicates whether Objective 3 was completed
		eternal boolean		obj4;				// This indicates whether Objective 4 was completed
		eternal boolean		obj5;				// This indicates whether Objective 5 was completed
												//
		static boolean		obj1revealed;		// This indicates that Objective 1 has been revealed
		static boolean		obj2revealed;		// This indicates that Objective 2 has been revealed
		static boolean		obj3revealed;		// This indicates that Objective 3 has been revealed
		static boolean		obj4revealed;		// This indicates that Objective 4 has been revealed
		static boolean		obj5revealed;		// This indicates that Objective 5 has been revealed
		//	
		
		static integer		cinema_cut;			// This variable keeps track of what scene a cinema is on.
		static locpoint		temploc;			// Used for commands that need objects converted to locpoint format
		static boolean		loser;				// You have lost the mission
  		eternal integer 	DavionRating;		// This is your Davion rating
		eternal integer 	SteinerRating;		// This is your Steiner rating
		eternal integer 	Nobility;			// This is your Nobility rating
		eternal integer 	Infamy;				// This is your Infamy rating

		// Trigger States
		eternal boolean		DropshipPower;		// The dropship powerup toggle
		eternal boolean		BlackOut;			// When true then the ECCM is no longer taking out sensors and communications
		static integer		GiveorTake;			// Makes it so that the ECCM is triggered with some degree of randomness
		eternal boolean		ReinforceNow;		// Reinforcements come on the map
		eternal boolean		FirstContact;		// The enemy are on to the player
		static boolean		CobraEnemy;			// Halloran's Cobra - you fought them
		eternal boolean		Spotted;			// Spotted the enemy
		
//------------------------------------------------------------------
//
//	 Init Function	(automatically run first time through)
//
//------------------------------------------------------------------

function init;
	var
		integer i;

	code

			playermech = getplayervehicle(epl_player0);

			// 10 - Sacrifice Group (Governor)
			/*GroupAddObject(GroupObjectID(10),eve_sacrifice02);
			GetLocation(ena_leash,temploc);
			SetCombatLeash(GroupObjectID(10),temploc,250);*/

			// 20 - First Set of Vehicles
			GroupAddObject(GroupObjectID(20),eve_atank01);
			GroupAddObject(GroupObjectID(20),eve_atank02);
			GroupAddObject(GroupObjectID(20),eve_atank03);
			GroupAddObject(GroupObjectID(20),eve_atank04);
			GroupAddObject(GroupObjectID(20),eve_atank05);
			SetGroupAI(GroupObjectID(20),GROUPAI_MOODSQUAD);

			// 21 - Forward Scout
			GroupAddObject(GroupObjectID(21),eve_forwardscout);

			// 23 - Mercenary Relief Lance
			GroupAddObject(GroupObjectID(23),eve_reinforce01);
			GroupAddObject(GroupObjectID(23),eve_reinforce02);
			GroupAddObject(GroupObjectID(23),eve_reinforce03);
			GroupAddObject(GroupObjectID(23),eve_reinforce04);
			GroupAddObject(GroupObjectID(23),eve_reinforce05);
			GroupAddObject(GroupObjectID(23),eve_reinforce06);
			GroupAddObject(GroupObjectID(23),eve_reinforce07);
			GroupAddObject(GroupObjectID(23),eve_colonel_burr);
			SetGroupAI(GroupObjectID(23),GROUPAI_MOODSQUAD);

			// 24 - Forward Scout
			GroupAddObject(GroupObjectID(24),eve_basedefense01);
			GroupAddObject(GroupObjectID(24),eve_basedefense02);
			GroupAddObject(GroupObjectID(24),eve_basedefense03);
			GroupAddObject(GroupObjectID(24),eve_basedefense04);
			GroupAddObject(GroupObjectID(24),eve_basedefense05);
			GroupAddObject(GroupObjectID(24),eve_basedefense06);
			GroupAddObject(GroupObjectID(24),eve_basedefense07);
			GroupAddObject(GroupObjectID(24),eve_basedefense08);
			GroupAddObject(GroupObjectID(24),eve_basedefense09);
			GroupAddObject(GroupObjectID(24),eve_basedefense10);
			GroupAddObject(GroupObjectID(24),eve_basedefense11);
			GroupAddObject(GroupObjectID(24),eve_basedefense12);
			GroupAddObject(GroupObjectID(24),eve_basedefense13);
			GroupAddObject(GroupObjectID(24),eve_basedefense14);
			GroupAddObject(GroupObjectID(24),eve_basedefense15);
			GroupAddObject(GroupObjectID(24),eve_basedefense16);
			GroupAddObject(GroupObjectID(24),eve_basedefense17);
			GroupAddObject(GroupObjectID(24),eve_basedefense18);
			GroupAddObject(GroupObjectID(24),eve_basedefense19);
			GroupAddObject(GroupObjectID(24),eve_basedefense20);
			GroupAddObject(GroupObjectID(24),eve_basepounder01);
			GroupAddObject(GroupObjectID(24),eve_basepounder02);
			GroupAddObject(GroupObjectID(24),eve_basepounder03);
			GroupAddObject(GroupObjectID(24),eve_basepounder04);
			GroupAddObject(GroupObjectID(24),eve_basepounder05);
			GroupAddObject(GroupObjectID(24),eve_basepounder06);
			GroupAddObject(GroupObjectID(24),eve_basepounder07);
			GroupAddObject(GroupObjectID(24),eve_baseguard01);
			GroupAddObject(GroupObjectID(24),eve_baseguard02);
			SetGroupAI(GroupObjectID(24),GROUPAI_RADIOSQUAD);
			
			// 30 - Mobile APUs for dropship powerup
			GroupAddObject(GroupObjectID(30),eve_apc);
			GroupAddObject(GroupObjectID(30),eve_apc_0000);
			GroupAddObject(GroupObjectID(30),eve_apc_0001);

			cinema_cut = 0;

			// Culturals
			//SetTarget(ede_example2,eve_example3);
		
endfunction;
		#include_ <content\ABLScripts\Katie\katie_script.abi> // <-- mektek
//------------------------------------------------------------------
//
//	 Main Code
//
//------------------------------------------------------------------
state startState;

	code	
/* MekTek: Shark removed these and replaced them with Katie calls
			SetKillSalvageChance(eve_reinforce02,100);	// Ryoken
			SetKillSalvageChance(eve_reinforce05,50);	// Uziel
			SetKillSalvageChance(eve_reinforce06,50);	// Argus
*/
		// Talon 3 specific
			GiveorTake = Rand(0,60);
		//

		// These are your MetaMission variables, second part is how this mission effects your ratings
			DavionRating 	= GetPreviousDavionFactionRating;
			SteinerRating 	= GetPreviousSteinerFactionRating;
			Nobility 		= GetPreviousNobility;
			Infamy 			= GetPreviousInfamy;
			
			DavionRating	= DavionRating + 11;
			SteinerRating	= SteinerRating - 6;
			Nobility		= Nobility + 0;
			Infamy			= Infamy + 0;
		// 
		
		SetRadarNav(ena_Nav_Alpha,1);
		SetNavpoint(ena_Nav_Alpha);
		
		if (GetEventFlag("HV04_AngryCobras")) then
			CobraEnemy = True;
		endif;

		//setsensorvisibility(eve_example4,false);
		
		checkrange	= 150;		// How far from a nav point before the Betty voice says you've arrived.

		BlackOut 	= True;		// TEMP!!

		StartMusic("Music\SneakPeek",3.0,3.5,50);
		
		trans opening;

endstate;

	
state deadState;
	code
	
endstate;
	
//------------------------------------------------------------------
//
//	 Sit state : description
//
//------------------------------------------------------------------
state sit;

	code

	if IsMusicPlaying == false then
		StartMusic("music\LavaAmbLoop",0.0,3.0,40);
	endif;

// Sound Triggers

	if (not GetGlobalTrigger(6)) then
		if (timegreater(1,2)) then		// Getting near Alpha
			SetGlobalTrigger(6,true);
			PlayVoiceOverOnce("vo\Talon\mi3\DepotMarkedAsAlpha",TALKER_CAS);
			PlayLancemateSound("VO\Generic\ColdStart",1);
			RevealNavpoint(ena_Nav_Alpha);
			SetNavpoint(ena_Nav_Alpha);
		endif;
	endif;
	
	if (not GetGlobalTrigger(7)) then
		if (IsWithin(PlayerMech,GroupObjectID(21),550)) then	// Scout is near you
			SetGlobalTrigger(7,true);
			PlayVoiceOverOnce("vo\Talon\mi3\MakingTracksForDepot",TALKER_SPE);
		endif;
	endif;
	
	if (not GetGlobalTrigger(8)) then
		if (GroupAllDead(GroupObjectID(21))) then		// Kill the scout
			SetGlobalTrigger(8,true);
			//PlayVoiceOverOnce("vo\Talon\mi3\KnowWereComing",TALKER_SPE);
		endif;
	endif;
	
	if (not GetGlobalTrigger(9)) then
		if (IsWithin(PlayerMech,GroupObjectID(20),870)) then	// Lance says contact
			SetGlobalTrigger(9,true);
			PlayLancemateSound("VO\Generic\Contact900",1);
			PlayLancemateSound("VO\Generic\VehicleContact",1);
			PlayVoiceOverOnce("vo\Talon\mi3\ContactNoMarkings",TALKER_SPE);
			PlayVoiceOverOnce("vo\Talon\mi3\Acknowledged",TALKER_CAS);
		endif;
	endif;
	
	if (not GetGlobalTrigger(10)) then
		if (DropshipPower) and (not IsDead(eve_mercenary_dropship)) then		// If the Dropship powers up
			SetGlobalTrigger(10,true);
			PlayVoiceOverOnce("vo\Talon\mi3\PowerSpikingHrothgar",TALKER_CAS);
		endif;
	endif;
	
	/*if (not GetGlobalTrigger(11)) then
		if (ReinforceNow) then		// The 'Mech reinforcements arrive
			PlayVoiceOverOnce("vo\Talon\mi3\TwoMoreLancesInbound",TALKER_CAS);
			PlayVoiceOverOnce("vo\Talon\mi3\SettleItNow",TALKER_SPE);
		endif;
	endif;*/
	
	if (not GetGlobalTrigger(12)) then
		if (not CobraEnemy) then		// If you aren't their enemy kill this check
			SetGlobalTrigger(12,true);
		else
			if (ReinforceNow) then
				if (IsWithin(PlayerMech,GroupObjectID(23),900)) then	// Once Cobras near you
					SetGlobalTrigger(12,true);
					PlayVoiceOverOnce("vo\Talon\mi3\ULearnLesson",TALKER_BUR);
					PlayVoiceOverOnce("vo\Talon\mi3\UOutClassedHere",TALKER_SPE);
					PlayLancemateSound("VO\Generic\DeterminedEnemy",1);
				endif;
			endif;
		endif;
	endif;
	
// Talon 3 Specific Triggers

	if (not GetGlobalTrigger(1)) then		// If you have shot any of the defenders, the Dropship begins powering up
		if (IsShot(GroupObjectID(24))) then
			StartTimer(3);			// This timer controls when the Dropship's defenses go on-line
		endif;
	endif;
	
	if (not DropshipPower) then		// After some time the dropship powers up
		if (timegreater(3,40 + (GroupNumDead(GroupObjectID(30)))*20 )) then		// It slows down if the APUs have been destroyed
			DropshipPower = True;
			KillTimer(3);
		endif;
	endif;
	
	if (not GetGlobalTrigger(2)) then		// Once a certain period of time passes then the ECCM blackout ends
		//if (timegreater(1,240 + GiveorTake)) then	// Was 240
			BlackOut = True;
			SetGlobalTrigger(2,true);
		//endif;
	endif;
	
	if (not GetGlobalTrigger(3)) then
		if (IsShot(GroupObjectID(21))) then		// If the scout is shot he will run shortly
			StartTimer(4);		// This timer controls when the scout will flee
			SetGlobalTrigger(3,true);
		endif;
	endif;
	
	/*if (not ReinforceNow) then			// If enough base defenders are killed Merc lance comes in, or after some time
		if (GroupNumDead(GroupObjectID(24))>11) then
			ReinforceNow = True;
		endif;
	else
		if (timegreater(1,260 + GiveorTake)) then
			ReinforceNow = True;
		else
			if (Obj2) then
				ReinforceNow = True;
			endif;
		endif;
	endif;*/
	
	if (not ReinforceNow) then
		if (obj2) then
			if (GroupAllDead(GroupObjectID(24))) then	// Get reinforced after the base is taken out
				ReinforceNow = true;
				PlayVoiceOverOnce("vo\Talon\mi3\DepotSecure",TALKER_SPE);
				PlayVoiceOverOnce("vo\Talon\mi3\TwoMoreLancesInbound",TALKER_CAS);
				if (CobraEnemy) then
					PlayVoiceOverOnce("vo\Talon\mi3\SettleItNow",TALKER_SPE);
				else
					PlayVoiceOverOnce("vo\Talon\mi3\DefendDepot",TALKER_SPE);
				endif;
				PlayLancemateSound("VO\Generic\Roger",1);
			endif;
		endif;
	endif;
	
	if (not GetGlobalTrigger(4)) then		// So we bring in the reinforcements
		if (ReinforceNow) then
			if (not IsWithin(PlayerMech,ena_tel1,650)) then
				RevealObjective(eob_objective3);
				GetLocation(ena_tel1,temploc);
				Teleport(eve_reinforce01,temploc);
				Startup(eve_reinforce01);
				GetLocation(ena_tel2,temploc);
				Teleport(eve_reinforce02,temploc);
				Startup(eve_reinforce02);
				GetLocation(ena_tel3,temploc);
				Teleport(eve_reinforce03,temploc);
				Startup(eve_reinforce03);
				GetLocation(ena_tel4,temploc);
				Teleport(eve_reinforce04,temploc);
				Startup(eve_reinforce04);
				GetLocation(ena_tel5,temploc);
				Teleport(eve_reinforce05,temploc);
				Startup(eve_reinforce05);
				GetLocation(ena_tel6,temploc);
				Teleport(eve_reinforce06,temploc);
				Startup(eve_reinforce06);
				GetLocation(ena_tel7,temploc);
				Teleport(eve_reinforce07,temploc);
				Startup(eve_reinforce07);
				GetLocation(ena_tel8,temploc);
				Teleport(eve_colonel_burr,temploc);
				Startup(eve_colonel_burr);
				SetGlobalTrigger(4,true);
				StartMusic("Music\Hunter",3.0,3.5,50);
				StartTimer(6);		// For when the enemy start moving
			endif;
		endif;
	endif;
	
	if (not GetGlobalTrigger(5)) then
		if (timegreater(5,4)) then
			SetGlobalTrigger(5,true);
			FirstContact = true;
			KillTimer(5);
		endif;
	endif;

// Obj 1 - Investigate Nav Alpha

	if (not Obj1) then
		if (IsWithin(PlayerMech,ena_nav_alpha,650)) then
			SuccessObjective(eob_objective1);
			Obj2Revealed = True;
			RevealObjective(eob_objective2);
			RevealObjective(eob_objective4);
			PlayVoiceOverOnce("vo\Talon\mi3\HrothgarOnSite",TALKER_SPE);
			PlayLancemateSound("VO\Generic\Acknowledged",1);
			PlayVoiceOverOnce("vo\Talon\mi3\DealWithItNow",TALKER_CAS);
			PlayVoiceOverOnce("vo\generic\Copy",TALKER_SPE);
			PlayVoiceOverOnce("vo\Talon\mi3\GotIt",TALKER_SPE);
			
			if (CobraEnemy) then
				PlayVoiceOverOnce("vo\Talon\mi3\HowQuaint",TALKER_CAS);
				PlayVoiceOverOnce("vo\Talon\mi3\TheyveBeenBusy",TALKER_SPE);
				PlayVoiceOverOnce("vo\Talon\mi3\WatchYourself",TALKER_CAS);
				PlayLancemateSound("VO\Generic\Revenge",1);
			else
				PlayVoiceOverOnce("vo\Talon\mi3\Slander",TALKER_CAS);
				PlayVoiceOverOnce("vo\Talon\mi3\MoreSlander",TALKER_SPE);
				PlayVoiceOverOnce("vo\Talon\mi3\DontGetTooCocky",TALKER_CAS);
			endif;
			
			//PlayVoiceOverOnce("vo\Talon\mi3\TwoLancesOnSite",TALKER_SPE);
			//PlayVoiceOverOnce("vo\generic\Copy",TALKER_CAS);
			
			Obj1 = True;
		endif;
	endif;
	
// Obj 2 - Destroy Mercenary Dropship

	if (not Obj2Revealed) then		// If you shoot the dropship then display the objective
		if (IsShot(eve_Mercenary_Dropship)) then
			RevealObjective(eob_objective2);
			Obj2Revealed = True;
		endif;
	endif;
	
	if (not Obj2) then
		if (IsDead(eve_Mercenary_Dropship)) then
			SuccessObjective(eob_objective2);
			//RevealObjective(eob_objective3);
			PlayVoiceOverOnce("vo\Talon\mi3\HrothgarToast",TALKER_SPE);
			Obj2 = true;
			if (obj3) then
				StartTimer(2);		// You win, gratz!
			endif;
		endif;
	endif;

// Obj 3 - Destroy the Mercenary Lance
	
	if (not Obj3) then
		if (GroupAllDead(GroupObjectID(23))) then
			SuccessObjective(eob_objective3);
			PlayVoiceOverOnce("vo\Talon\mi3\CobrasHistory",TALKER_SPE);
			Obj3 = True;
			if (obj2) then
				StartTimer(2);		// You win the game, congrats!
			endif;
		endif;
	endif;
	
// Obj 4 - All vehicles destroyed

	if (not Obj4) then
		if (GroupAllDead(GroupObjectID(24))) and (GroupAllDead(GroupObjectID(20))) then
			Obj4 = True;
			SuccessObjective(eob_objective4);
		endif;
	endif;

// Victory and Loss conditions

	if (timegreater(2,5)) then
		  
		// Salvage - FAB  9/5
			AddWeaponSalvage("LargeLaser",1); 
			AddWeaponSalvage("PPC",1);
			AddWeaponSalvage("MachineGun",2);
			AddWeaponSalvage("MediumLaser",2);
			AddWeaponSalvage("UltraAC5",1);
			/////////////////////
			// MISSION SALVAGE //
			/////////////////////
			Katie(eve_colonel_burr,70);

			Katie(eve_reinforce01,60);
			Katie(eve_reinforce02,50);
			Katie(eve_reinforce03,40);
			Katie(eve_reinforce04,30);
			Katie(eve_reinforce05,20);
			Katie(eve_reinforce06,30);
			Katie(eve_reinforce07,40);
		
		if (loser) then
			EndMission(false,5);
			trans lost;
		else
			
			SetCurrentInfamy(Infamy);
			SetCurrentNobility(Nobility);
			SetCurrentDavionFactionRating(DavionRating);
			SetCurrentSteinerFactionrating(SteinerRating);
			
			trans won;	
		endif;
		
	endif;

// Fail the mission when the player dies
	if (isdead(playermech)) then
		KillVoiceOvers;
		switch rand(1,3)
			case 1:
				PlayVoiceOver("VO\Generic\SpecterDestroyed", talker_CAS);
			endcase;
			case 2:
				PlayVoiceOver("VO\Generic\SpecterDestroyed2", talker_CAS);
			endcase;
			case 3:
				PlayVoiceOver("VO\Generic\SpecterDestroyed3", talker_CAS);
			endcase;
		endswitch;
		StartMusic("Music\GenericLoss1",3.0,3.5,70);
		endmission(false,14);
		trans deadstate;
	endif;

// Betty speaks when reaching Nav Point

	if (not alphareached) then
		if (isnavrevealed(ena_nav_alpha)) then
			if (iswithin(playermech,ena_nav_alpha,checkrange)) then
				playvoiceover("VO\generic\navalpha",talker_Bet);
				alphareached = true;
				//setnavpoint(ena_nav_beta);
			endif;
		endif;
	endif;

endstate;

//-----------------------------------------
//HERE THERE BE CINEMAS
//-----------------------------------------

state opening;	  

	code

		if (cinemaskip) then
			KillVoiceOvers;	 
			cinema_cut = 0;	 
			KillTimer(gti_timer_21);
			SetGlobalTrigger(0,true);
			playerai(epl_player0,false);
			StartTimer(1);		// This is the mission start timer
			ResetTimer(1);
			CinemaEnd;		  
			SetCompositingEnabled(True);
			revealobjective(eob_objective1);
			trans sit;
		endif;
		
		switch (cinema_cut)

			case 0:
				CinemaStart;
				SetActiveCamera(eca_CameraShip);
				
				SpinCamera1(PlayerMech,PlayerMech,16.0,9.0,true);
				
				PlayVoiceOverOnce("vo\talon\mi3\Intro1",TALKER_SPE);
				PlayLancemateSound("VO\Generic\Roger",1);
				
				cinema_cut = cinema_cut + 1;
				
				starttimer(21);			// This is the cineractive counter.
				resettimer(21);
			endcase;
			
			case 1:
				if (not IsVoiceOverPlaying) then
					ResetTimer(21);
					Cinema_Cut = Cinema_Cut + 1;
				endif;
			endcase;
			
			case 2:
				if (timegreater(21,2)) then
					KillVoiceOvers;
					cinema_cut = 0;
					KillTimer(gti_timer_21);
					SetGlobalTrigger(0,true);
					playerai(epl_player0,false);
					StartTimer(1);		// This is the mission start timer
					ResetTimer(1);
					CinemaEnd;
					revealobjective(eob_objective1);	// Reveal the objectives
					SetCompositingEnabled(True);
					
					trans sit;
				endif;
			endcase;
				
		endswitch;
	
endstate;

state won;	  

	code
		
		if (cinemaskip) then
			cinema_cut = 20;	
			playerai(epl_player0,false);
			KillVoiceOvers;
			Endmission(true,1); 
		endif;

		switch cinema_cut
		
			case 0:	
				StartMusic("Music\GenericVictory1",3.0,3.5,50);					 
				PlayVoiceOverOnce("vo\Talon\mi3\Close1",TALKER_SPE);
				PlayVoiceOverOnce("vo\Talon\mi3\Close2",TALKER_CAS);
				SetCompositingEnabled(False);
				CinemaStart;
				setactivecamera (eca_cameraship);
				Targetfollowobject(playermech);
				Camerafollowobject(playermech);
				Targetoffset(0.0,4.0,0.0,0.0,false);
				Cameraoffset(-30.0,125.0,75.0,0.0,false);
				Cameraoffset(60.0,20.0,25.0,14.0,false);
				playerai(epl_player0,true);
				Cinema_cut=Cinema_cut+1;
			endcase;
			
			case 1:
				if (not IsVoiceOverPlaying) then
					EndMission(true,2);
					Cinema_Cut = Cinema_Cut + 1;
				endif;
			endcase;			
			
		endswitch;
	
endstate;

state lost;

		code
		
		switch cinema_cut
		
			case 0:	
			
			endcase;
			
			case 1:
			
			endcase;
			
		endswitch;
	
endstate;

endfsm.
//******************************************************************
