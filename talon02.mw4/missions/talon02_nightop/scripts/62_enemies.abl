
//------------------------------------------------------------------
// NOTE: The fsm name below MUST be changed in order for the
// script to be considered a unique entity!
//------------------------------------------------------------------

fsm Militia : integer;


//------------------------------------------------------------------
// Moving AI:
//   This script controls movers that do not go aggressive.
//   MILITIA - This script controls the Steiner militia, the enemy of the PCs.
//------------------------------------------------------------------

//------------------------------------------------------------------
//     Constants
//------------------------------------------------------------------

	const
		#include_ <content\ABLScripts\mwconst.abi>

//------------------------------------------------------------------
//     Types
//------------------------------------------------------------------

	type
		#include_ <content\ABLScripts\mwtype.abi>
	
//------------------------------------------------------------------
//     Variables
//------------------------------------------------------------------

	var
		
    // Standard Mech and Vehicle variables
        static integer			attackRange;		// At what range do I start shooting?
		static integer			withdrawRange;		// At what range do I withdraw from combat completely?
                                                    //
		static integer			SkillLevel;			// Used by Assign Skill for the difficulty of an enemy
		static integer			piloting;			// Piloting skill
		static integer			gunnery;			// Gunnery skill/chance to hit
		static real				minDelay;			// Minimum amount of time I will wait between shots once a weapon is reloaded
		static real				maxDelay;			// Maximum amount of time I will wait between shots once a weapon is reloaded
		static integer 			eliteLevel;			// Elite level.  This helps determine tactics, defensive manuevers, opportunity fire
													// and other things.  It indicates a general level of combat experience.
		static integer			attackThrottle;		// My attack throttle
		static integer			mytactic;			// Combat tactic I use - 0 means standard attack
		static integer			findTypes;			// What kind of enemies to look for
													//
        static integer			path;	            // What path to take.
		static integer			myobject;			// Where this unit will head to
        static integer			speed;	            // Speed on the path.
		static integer			MyGroup;			// Which MemoryInteger you work for
		static integer			GroupNumber;		// What's my group -- i.e. I will use GroupObjectID(groupNumber) to identify my group
		static integer			formationType;		// What kind of formation am I in?
		static integer			formationDensity;	// How densely are we packed?
		static integer			moveType;			// How I move along the path
                                                    //
 		static integer			isShotRadius;		// How far away from me will I detect an enemy shot?
                                                    //
		static integer 			attackSound;		// The attack sound I play when I first attack
		static integer 			deathSound;			// The sound I play when I die
		                                            //
		static integer			mood;				// My default 
		static integer			takeOffDistance;	// My take-off distance if I'm an airplane (ignored if not an airplane)
    //
		
	// Generic Special Unit Functions	
		static boolean			ToHell;				// I am going to teleport to hell
		static boolean			PowerDown;		   	// I start off shutdown
		static boolean			Visible;			// Am I on radar
		static boolean			RadarOff;			// This 'Mech doesn't use radar (no missile boats!)
		static boolean			AmPlane;			// I am a plane unit
		static boolean			SlowDown;			// I am slower than the average bear w/attack throttle (Light 'Mech)
		static integer			MyTimer;			// When I show up timewise
	// 
		
		static boolean			Destination;		// Truck has made it to its destination
		static integer			WaitAround;			// How much time the unit will pait at each point
		static integer			SpotDistance;		// How far away a unit will spot things
		static integer			DelayTimer;			// How long it will take me to power up
		
//------------------------------------------------------------------
//     Init: my initialization function
//------------------------------------------------------------------

function Init;
	code
		// script-specific variables
		attackRange		= 700;
		withdrawRange	= AttackRange * 3 / 2;
		path			= -1;
		moveType		= move_circle;
		speed			= 90;
		formationType	= 1;
		formationDensity= formtype_sparse;
		findTypes		= FT_DEFAULT;

		takeOffDistance	= 150;

		// driver settings
		SkillLevel		= 5;
		attackSound		= -1; // no sound
		deathSound		= -1; // no sound
		mood			= NEUTRAL_START;
		
        MyGroup			= GetMemoryInteger(me,9);
		switch (MyGroup)
        
            case 1:		// North Patrol
				path			= epa_NorthPath;
				SpotDistance	= 300;
				groupNumber		= 20;
            endcase;

            case 2:		// Going between road there
				moveType		= move_seesaw;
				SpotDistance	= 300;
				path			= epa_RoadPath;
				groupNumber		= 21;
            endcase;

            case 3:		// Other Patrols
				path			= epa_SouthPath;
				SpotDistance	= 300;
				groupNumber		= 22;
				
				if (GetMemoryInteger(me,8)==1) then
					moveType		= move_seesaw;
					path			= epa_WestPath;
					SpotDistance	= 400;
					groupNumber		= 23;
				endif;
            
				if (GetMemoryInteger(me,8)==2) then
					path			= epa_SouthPath2;
				endif;
			endcase;
			
			case 4:		// Base 'Mechs
				PowerDown		= True;
				groupNumber		= 23;
				DelayTimer		= rand(15,45);
			endcase;
			
			case 5:		// East Patrol
				path			= epa_EastPath;
				SpotDistance	= 300;
				groupNumber		= 22;
				
				if (GetMemoryInteger(me,8)==1) then
					path			= epa_EastPath2;
				endif;
			endcase;
			
        endswitch;

endfunction;

//------------------------------------------------------------------
//	StartState: my initial state
//------------------------------------------------------------------

state StartState;

	code
		SetSearchLight(me,true);
		AssignSkill(SkillLevel);
		
		/* Use a Select Statement to change these for a particular unit
		SetFiringDelay			(ME,minDelay,maxDelay);
		SetIgnoreFriendlyFire	(ME,true);
		SetIsShotRadius			(ME,isShotRadius);
		SetEntropyMood			(ME,mood);
		SetCurMood				(ME,mood);
		SetSkillLevel			(ME,piloting,gunnery,eliteLevel);
		SetAttackThrottle		(ME,attackThrottle);*/
		SetIsShotRadius			(ME,900);
		
		if (AmPlane) then
			SetAttackThrottle	(ME,100);
		else
			if (SlowDown) then
				SetAttackThrottle	(ME,AttackThrottle);
			endif;
		endif;
		
		SetAttackThrottle	(ME,100);		// Everyone moves fast in combat
		SetIsShotRadius		(ME,SpotDistance + 100);

		if (PowerDown) then
			Shutdown(me);
		endif;
		
		trans Lobby;        

endstate;			

state Lobby;
	code

		if (MyGroup==2) then	// This is to make the mission easier, can be turned on later.
			Shutdown(me);
			trans DeadState;
		endif;

		if (MyGroup==1) or (MyGroup==2) or (MyGroup==3) or (MyGroup==5) then	// Aircraft at Beginning
			trans FollowPathState;
		endif;

		if (MyGroup==4) then
			if (timegreater(5,DelayTimer)) then
				Startup(me);
				SetSearchLight(me,true);
				trans GotoPlayer;
			endif;
		endif;

		if (IsShot(ME)) then
			FirstContact	= True;
		endif;
		
endstate;

//------------------------------------------------------------------
//	FollowPathState: follow our path, but keep an eye out for enemies
//------------------------------------------------------------------

state FollowPathState;
	code

		OrderMoveResumePatrol(path,speed,movetype,true,true);
		//OrderFormationMove(GroupObjectID(GroupNumber),path,speed,movetype,formationtype,formationdensity,false);

		if (not FirstContact) then		// First case to spot is whether the player's 'Mech is seen
			if (IsWithin(me,PlayerMech, SpotDistance + DistanceMod)) then
				if (CanSee(me,PlayerMech)) then
					Strikes = Strikes + 1;
				endif;
			endif;
		endif;
		
		if (not FirstContact) then		// If you get too close you are automatically spotted
			if (IsWithin(me,PlayerMech,300)) then
				Balls = Balls + 1;
				if (IsWithin(me,PlayerMech,200)) then
					FirstContact = True;
				endif;
			endif;
		endif;
		
		if (not FirstContact) then		// If you don't have your sensors on you are screwed, too
			if (SensorsOn) then
				if (IsWithin(me,PlayerMech,1000)) then
					FirstContact = True;
				endif;
			endif;
		endif;
		
		if (MyGroup == 2) then
			if (not Destination) then
				if (IsWithin(me,ena_end1,65)) or (IsWithin(me,ena_end2,65)) then
					StartTimer(4);		// Now wait around
					WaitAround = 30 + rand(0,50);
					ClearMoveOrder(me);
					trans SitTight;
				endif;
			else
				if (timegreater(4,30)) then
					Destination = False;
					KillTimer(4);
				endif;
			endif;
		endif;

		if (IsShot(ME)) then
			FirstContact	= True;
		endif;

		if (FirstContact) then
			if (FindEnemy(AttackRange,FindTypes + FT_UNARMED + FT_BUILDING)) then
				trans Attack;
			endif;
		endif;

endstate;

state SitTight;
	code

		if (timegreater(4,WaitAround)) then
			Destination = True;
			ResetTimer(4);
			trans FollowPathState;
		endif;
		

endstate;

state GoToLocation;
	code

		OrderMovetoObject(MyObject,speed);

		if (FindEnemy(AttackRange,FindTypes + FT_UNARMED + FT_BUILDING)) then
			trans Attack;
		endif;
		
endstate;

state GoToPlayer;
	code

		OrderMovetoObject(PlayerMech,100);
		
		if (RadarOff) then
			if (IsWithin(me,PlayerMech,200)) then
				SetSensorVisibility(me,TRUE);
				Visible = True;
			else
				SetSensorVisibility(me,FALSE);
				Visible = False;
			endif;
		endif;
		
		if (FindEnemy(AttackRange,FindTypes + FT_UNARMED + FT_BUILDING)) then
			trans Attack;
		endif;

endstate;

state Attack;
	code

		/* if (not FirstContact) then
			FirstContact = True;		// As soon as any enemy attacks a PC they warn their base
		endif; */
		
		if (LeaveAttackState(WithdrawRange)) then
			trans GoToPlayer;
		endif;
		
		if (MyTactic == 0) then
			OrderAttack(true);
		else
			OrderAttackTactic(MyTactic,true);
		endif;

		/* if (MyTactic == TACTIC_REAR) then		// If you are the last of Clan MacLeod then stop hitting the rear
			if (GroupSize(GroupObjectID(GroupNumber)) == 1) then
				MyTactic = 0;
			endif;
		endif; */
		
		if (RadarOff) then		// This is to mimic your radar being turned off
			if (IsWithin(me,PlayerMech,200)) then
				SetSensorVisibility(me,TRUE);
				Visible = True;
			else
				SetSensorVisibility(me,FALSE);
				Visible = False;
			endif;
		endif;
		
		if (LetHimRun) then
			OrderStopAttacking;
			trans Ending;
		endif;
			
endstate;

state Ending;
	code

		OrderMovetoObject(ena_nav_alpha,100);

endstate;

//------------------------------------------------------------------
//	DeadState: OK, I kicked the bucket.
//------------------------------------------------------------------

state DeadState;
	code
		if (deathSound <> -1) then	
			PlaySoundOnce(deathSound);
		endif;		

		orderDie;

endstate;


endfsm.
