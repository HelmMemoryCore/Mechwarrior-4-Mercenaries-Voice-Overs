
//*********************************************************************************

fsm Talon06_DefendMechs : integer;		

//------------------------------------------------------------------
//
//     Constant Definitions
//
//------------------------------------------------------------------
     const

		#include_ <content\ABLScripts\mwconst.abi>
		#include_ <content\ABLScripts\Katie\katie_consts.abi> // <-- mektek
//------------------------------------------------------------------
//
//     Type Definitions
//
//------------------------------------------------------------------
     type

		#include_ <content\ABLScripts\mwtype.abi>

//------------------------------------------------------------------
//
//     Variable Declarations
//
//------------------------------------------------------------------
      var
			
		eternal integer playermech;
		
		eternal boolean obj1;
		eternal boolean obj2;
		eternal boolean obj3;
		eternal boolean obj4;
		
		eternal integer alphaGroup;
		eternal integer AllEnemiesGroup;
		eternal integer TemplarGroup;
		
		
		static integer	cinema_cut;
		Eternal boolean StartTheFun;
		eternal boolean templarsLive;	//Set in friendlies.abl
		
		static boolean firstAlphaVO;
		static boolean SecondAlphaVO;
		static boolean firstBetaVO;
		static boolean SecondBetaVO;
		static boolean ThirdBetaVO;
		static boolean firstWinVO;
		static boolean SecondWinVO;
		static boolean TemplarVO;
		
		eternal Integer DavionRating;
		eternal Integer SteinerRating;
		eternal Integer Nobility;
		eternal Integer Infamy;
		
		static integer CheckRange;
		static boolean AlphaReached;
		static boolean BetaReached;
		static boolean GammaReached;
		
		
 //------------------------------------------------------------------
//
//     Init Function    (automatically run first time through)
//
//------------------------------------------------------------------

function init;
	var
		integer i;

	code
		
		playermech = getplayervehicle(epl_player0);	
		GroupAddObject(GroupObjectId(1),playermech);

		// Mechs at alpha
        GroupAddObject(GroupObjectID(10),eve_AlphaMech01);
        GroupAddObject(GroupObjectID(10),eve_AlphaMech02);
        GroupAddObject(GroupObjectID(10),eve_AlphaMech03);
        GroupAddObject(GroupObjectID(10),eve_AlphaMech04);
        GroupAddObject(GroupObjectID(10),eve_AlphaMech05);
        SetGroupAI(GroupObjectID(10),GROUPAI_RADIOSQUAD);
		alphaGroup = GroupObjectID(10);
	
 		//Demolisher tanks
		GroupAddObject(GroupObjectID(22),eve_tank07);
		//GroupAddObject(GroupObjectID(22),eve_tank08);
		//GroupAddObject(GroupObjectID(22),eve_tank09);
		GroupAddObject(GroupObjectID(22),eve_tank10);
        SetGroupAI(GroupObjectID(22),GROUPAI_RADIOSQUAD);
		
		//Mechs at Beta
        GroupAddObject(GroupObjectID(30),eve_mech01);
        GroupAddObject(GroupObjectID(30),eve_mech02);
        GroupAddObject(GroupObjectID(30),eve_mech03);
        GroupAddObject(GroupObjectID(30),eve_mech04);
        SetGroupAI(GroupObjectID(30),GROUPAI_RADIOSQUAD);
		
        GroupAddObject(GroupObjectID(31),eve_mech05);
        GroupAddObject(GroupObjectID(31),eve_mech06);
		GroupAddObject(GroupObjectID(31),eve_mech07);
        GroupAddObject(GroupObjectID(31),eve_mech08);
        SetGroupAI(GroupObjectID(31),GROUPAI_RADIOSQUAD);
		
        //Mechs from the West
		GroupAddObject(GroupObjectID(32),eve_mech11);
        GroupAddObject(GroupObjectID(32),eve_mech12);
        GroupAddObject(GroupObjectID(32),eve_mech09);
        GroupAddObject(GroupObjectID(32),eve_mech10);
        SetGroupAI(GroupObjectID(32),GROUPAI_RADIOSQUAD);
        
		//all enemies
		GroupAddObject(GroupObjectID(33),eve_mech01);
        GroupAddObject(GroupObjectID(33),eve_mech02);
        GroupAddObject(GroupObjectID(33),eve_mech03);
        GroupAddObject(GroupObjectID(33),eve_mech04);
        GroupAddObject(GroupObjectID(33),eve_mech05);
        GroupAddObject(GroupObjectID(33),eve_mech06);
        GroupAddObject(GroupObjectID(33),eve_mech07);
        GroupAddObject(GroupObjectID(33),eve_mech08);
        GroupAddObject(GroupObjectID(33),eve_mech09);
        GroupAddObject(GroupObjectID(33),eve_mech10);
        GroupAddObject(GroupObjectID(33),eve_mech11);
        GroupAddObject(GroupObjectID(33),eve_mech12);
        GroupAddObject(GroupObjectID(33),eve_Plane01);
        GroupAddObject(GroupObjectID(33),eve_Plane02);
        GroupAddObject(GroupObjectID(33),eve_Plane03);
        GroupAddObject(GroupObjectID(33),eve_Plane04);
        GroupAddObject(GroupObjectID(33),eve_Tank07);
        //GroupAddObject(GroupObjectID(33),eve_Tank08);
        //GroupAddObject(GroupObjectID(33),eve_Tank09);
        GroupAddObject(GroupObjectID(33),eve_Tank10);
        SetGroupAI(GroupObjectID(33),GROUPAI_NONE);
		AllEnemiesGroup = GroupObjectID(33);
		
		//Friendly tanks
        GroupAddObject(GroupObjectID(40),eve_ftank01);
        GroupAddObject(GroupObjectID(40),eve_ftank02);
        GroupAddObject(GroupObjectID(40),eve_ftank03);
        GroupAddObject(GroupObjectID(40),eve_ftank04);
        SetGroupAI(GroupObjectID(40),GROUPAI_RADIOSQUAD);
		
		//templars
        GroupAddObject(GroupObjectID(45),eve_Templar01);
        GroupAddObject(GroupObjectID(45),eve_Templar02);
        GroupAddObject(GroupObjectID(45),eve_Templar_Prototype);
        SetGroupAI(GroupObjectID(45),GROUPAI_RADIOSQUAD);
		templarGroup = GroupObjectID(45);
		
		
		obj1 = false;
		obj2 = false;
		obj3 = false;
		obj4 = false;
		checkRange = 100;					
		
		cinema_cut = 0; 						
		
		DavionRating = GetPreviousDavionFactionRating;
		Davionrating = DavionRating + 15;
		SteinerRating = GetPreviousSteinerFactionRating;
		SteinerRating = SteinerRating - 10;
		Nobility = GetPreviousNobility;
		nobility = nobility + 7;
		Infamy = GetPreviousInfamy;
		infamy = infamy;
		
		
endfunction;
		#include_ <content\ABLScripts\Katie\katie_script.abi> // <-- mektek
//------------------------------------------------------------------
//
//     Main Code
//
//------------------------------------------------------------------
state startState;

	code	
	RevealObjective(eob_Objective01);
	RevealObjective(eob_Objective04);
	
	RevealNavpoint(ena_Nav_Alpha);		
	RevealNavpoint(ena_Nav_Beta);
	RevealNavpoint(ena_Nav_Gamma);
	setRadarNav(ena_Nav_Alpha, 1);
	setRadarNav(ena_Nav_Beta, 2);
	setRadarNav(ena_Nav_Gamma, 3);
	SetNavPoint(ena_Nav_Alpha);
	
	//Salvage Chance
	/* MekTek: Shark removed these and replaced them with Katie calls
	SetKillSalvageChance(eve_Mech01, 50);			//Hauptman
	SetKillSalvageChance(eve_Mech04, 50);			//Hauptman
	SetKillSalvageChance(eve_Mech05, 25);			//Gladiator
	SetKillSalvageChance(eve_Mech06, 25);			//Gladiator
	SetKillSalvageChance(eve_Mech07, 25);			//Gladiator
	SetKillSalvageChance(eve_Mech08, 25);			//Gladiator
	*/
	//Intro VO
	PlayVoiceOverOnce("VO\Talon\MI6\Intro1", talker_CAS);
	PlayVoiceOverOnce("VO\Talon\MI6\Intro2", talker_SPE);
	//PlayLanceMateSound("vo\generic\Agree",1);
	StartMusic("Music\FloodGate",0.0,3.0,40);
	
	trans opening;

endstate;

	
state deadState;
	code
endstate;
	
//------------------------------------------------------------------
//
//     Sit state : description
//
//------------------------------------------------------------------
state sit;

	code
	//Music
	if (not ismusicplaying) then
		StartMusic("Music\Arcticambloop",0.0,3.0,40);
	endif;
	
	//VO
	if (not firstAlphaVO) then
		if IsWithin(playerMech, ena_nav_alpha, 900) then
			PlayVoiceOverOnce("VO\Talon\MI6\EngagingAtAlpha", talker_SPE);
			firstAlphaVO = true;
		endif;
	endif;
	if (not SecondAlphaVO) then
		if GroupAllDead(alphaGroup) then
			PlayVoiceOverOnce("VO\Talon\MI6\MediumsBreakingOff", talker_SPE);
			PlayVoiceOverOnce("VO\Talon\MI6\ReturnToBeta", talker_CAS);
			PlayVoiceOverOnce("VO\Talon\MI6\BackToBeta", talker_SPE);
			SecondAlphaVO = true;
		endif;
	endif;
	if (not firstBetaVO) then
		if IsWithin(playerMech, GroupObjectID(30), 1000) then
			PlayVoiceOverOnce("VO\Talon\MI6\TwoAssaultLances", talker_SPE);
			firstBetaVO = true;
		endif;
	endif;
	if (not SecondBetaVO) then
		if IsWithin(playerMech, GroupObjectID(32), 1500) then
			PlayVoiceOverOnce("VO\Talon\MI6\ThirdLanceMovingIn", talker_CAS);
			PlayVoiceOverOnce("VO\Talon\MI6\AnotherLance", talker_SPE);
			SecondBetaVO = true;
		endif;
	endif;
	if (not thirdBetaVO) then
		if IsWithin(playerMech, GroupObjectID(22), 900) then
			PlayLanceMateSound("vo\generic\Demolishers",1);
			thirdBetaVO = true;
		endif;
	endif;
	if (not TemplarVO) then
		if templarsLive then
			PlayVoiceOverOnce("VO\Talon\MI6\TemplarsOnline", talker_CAS);
			PlayVoiceOverOnce("VO\Talon\MI6\MindIfWeJoin", talker_CAN);
			PlayVoiceOverOnce("VO\Talon\MI6\ByAllMeans", talker_SPE);
			PlayVoiceOverOnce("VO\Talon\MI6\WeaponsHot", talker_CAN);
			TemplarVO = true;
		endif;
	endif;
	if (not FirstWinVO) then
		if GroupAllDead(groupObjectID(30)) then
			PlayVoiceOverOnce("VO\Talon\MI6\DownOneSteinerLance", talker_SPE);
			FirstWinVO = true;
		endif;
	endif;
	if (not SecondWinVO) then
		if GroupAllDead(groupObjectID(31)) then
			PlayVoiceOverOnce("VO\Talon\MI6\SecondLanceElim", talker_SPE);
			SecondWinVO = true;
		endif;
	endif;
		
		// Betty speaks when reaching Nav Point
		if (not alphareached) then
			if (isnavrevealed(ena_nav_alpha)) then
				if (iswithin(playermech,ena_nav_alpha,checkrange)) then
					playvoiceover("VO\generic\navalpha",talker_Bet);
					alphareached = true;
				endif;
			endif;
		endif;
		if (not betareached) then
			if (isnavrevealed(ena_nav_beta)) then
				if (iswithin(playermech,ena_nav_beta,checkrange)) then
					playvoiceover("VO\generic\navbeta",talker_Bet);
					betareached = true;
				endif;
			endif;
		endif;
		if (not gammareached) then
			if (isnavrevealed(ena_nav_gamma)) then
				if (iswithin(playermech,ena_nav_gamma,checkrange)) then
					playvoiceover("VO\generic\navgamma",talker_Bet);
					gammareached = true;
				endif;
			endif;
		endif;
	
	//VO End
	
	// Objective 1
	if isDead(ebu_Listening_Post) then
		failObjective(eob_Objective01);
		resetTimer(21);
		cinema_cut = 0;
		trans lostPost;
	endif;
	
	if (not obj1) then
		if GroupAllDead(alphaGroup) then
			successObjective(eob_objective01);
			RevealObjective(eob_Objective02);
			RevealObjective(eob_Objective03);
			SetNavPoint(ena_Nav_Beta);
			obj1 = true;
		endif;
	endif;
			
	// Objective 2
	if isDead(ebu_ObjectiveBuilding02) then
		failObjective(eob_Objective02);
		resetTimer(21);
		cinema_cut = 0;
		trans lostPost;
	endif;
	
	// Objective 3
	if isDead(ebu_ObjectiveBuilding03) then
		failObjective(eob_Objective03);
		resetTimer(21);
		cinema_cut = 0;
		trans lostPost;
	endif;
	
	//Win condition
	if GroupAllDead(AllEnemiesGroup) then
		successObjective(eob_objective02);
		successObjective(eob_objective03);
		obj2 = true;
		obj3 = true;
	endif;
	
	if obj1 and obj2 and obj3 then
		if (not obj4) then
			if GroupNumDead(templarGroup) > 0 then
				failObjective(eob_objective04);
			else
				successObjective(eob_objective04);
				AddMissionMoneyToBalance(1000000);
			endif;
			obj4 = true;
		endif;
		resetTimer(21);
		cinema_cut = 0;
		/////////////////////
		// MISSION SALVAGE //
		/////////////////////
		Katie(eve_AlphaMech01,40);
		Katie(eve_AlphaMech02,30);
		Katie(eve_AlphaMech03,20);
		Katie(eve_AlphaMech04,30);
		Katie(eve_AlphaMech05,40);

		Katie(eve_mech01,50);
		Katie(eve_mech02,40);
		Katie(eve_mech03,30);
		Katie(eve_mech04,20);
		Katie(eve_mech05,20);
		Katie(eve_mech06,30);
		Katie(eve_mech07,30);
		Katie(eve_mech08,40);
		Katie(eve_mech09,50);
		Katie(eve_mech10,20);
		Katie(eve_mech11,30);
		Katie(eve_mech12,40);


		trans won;
	endif;
	
	if (not obj4) then
		if GroupNumDead(templarGroup) > 0 then
			failObjective(eob_objective04);
			obj4 = true;
		endif;
	endif;

//Fail the mission when the player dies
	if (isdead(playermech)) then
		failobjectiveall(9);
		resetTimer(21);
		cinema_cut = 0;
		trans lostPlayer;
	endif;

				
endstate;

//-----------------------------------------
//HERE THERE BE CINEMAS
//-----------------------------------------

state opening;
	code

		if cinemaSkip then
			CinemaEnd;
			SetCompositingEnabled(true);			
			playerAI(playermech, false);
			killVoiceOvers;
			startTheFun = true;
			trans sit;
		endif;
 	
		switch (cinema_cut)
			case 0:
				SetActiveCamera(eca_CameraShip);
				playerAI(epl_player0, true);
				SetCompositingEnabled(false);
				CinemaStart;
				FadeToBlack(0.0);
				Fadefromblack(2.0);
				Targetfollowobject(ena_Nav_Alpha);
				camerafollowobject(ena_Nav_Alpha);
				targetoffset(0.0, 5.0, 0.0, 0.0, false);
                Cameraoffset(0.0, 10.0, -300.0, 0.0, false);       
                Cameraoffset(300.0, 10.0, 0.0, 10.0, false);       
				resetTimer(21);
				cinema_cut = cinema_cut + 1;
			endcase;
			case 1:
				if TimeGreater(21, 10) then			//Timer 21, standard cinema timer
					resetTimer(21);
					cinema_cut = cinema_cut + 1;
				endif;
			endcase;
			case 2:
				CinemaEnd;
				SetCompositingEnabled(true);			
				playerAI(playermech, false);
				
				startTheFun = true;
				trans sit;
			endcase;
		endswitch;
endstate;

state won;
	code
		
		switch cinema_cut
			case 0:
				PlayVoiceOverOnce("VO\Talon\MI6\LastOfThem", talker_SPE);
				PlayVoiceOverOnce("VO\Talon\MI6\thanksCanary", talker_SPE);
				PlayVoiceOverOnce("VO\Talon\MI6\Close1", talker_CAN);
				PlayVoiceOverOnce("VO\Talon\MI6\Close2", talker_SPE);
				PlayVoiceOverOnce("VO\Talon\MI6\Close3", talker_CAN);
				PlayVoiceOverOnce("VO\Talon\MI6\Close4", talker_SPE);
				switch rand(1,2)
					case 1:
						StartMusic("Music\genericVictory1",3.5,3.0,50);
					endcase;
					case 2:
						StartMusic("Music\genericVictory2",3.5,3.0,50);
					endcase;
				endswitch;
				cinema_cut = cinema_cut +1;
			endcase;
			case 1:
				if TimeGreater(21,10) then
					cinema_cut = cinema_cut +1;
				endif;
			endcase;
			case 2:	
				SetActiveCamera(eca_CameraShip);
				PullOutCamera(playermech, playermech, 150.0, 15.0, true);				
				UpdateCameraPosition(eca_CameraShip);     
								
				SetCurrentInfamy(Infamy);
				SetCurrentNobility(Nobility);
				SetCurrentDavionFactionRating(DavionRating);
				SetCurrentSteinerFactionrating(SteinerRating);
				
				AddMechInstanceSalvage("Templar");
				AddMechInstanceSalvage("Templar");
				AddWeaponSalvage("LightGauss", 4);
				AddWeaponSalvage("LargeXPulseLaser", 4);
				
				cinema_cut = cinema_cut + 1;
			endcase;
			case 3:
				if (not isVoiceOverPlaying) then
					EndMission(true, 3);
					cinema_cut = cinema_cut + 1;
				endif;
			endcase;
		endswitch;
	
endstate;

state lostPost;
	var
		static ObjectID DeadBuilding;
	code
		
		switch cinema_cut
			case 0:
				killVoiceOvers;
				switch rand(1,2)
					case 1:
						StartMusic("Music\genericLoss1",3.5,3.0,50);
					endcase;
					case 2:
						StartMusic("Music\genericLoss2",3.5,3.0,50);
					endcase;
				endswitch;
				switch rand(1,5)
					case 1:
						PlayVoiceOver("VO\Generic\Abort", talker_CAS);
					endcase;
					case 2:
						PlayVoiceOver("VO\Generic\Abort2", talker_CAS);
					endcase;
					case 3:
						PlayVoiceOver("VO\Generic\Abort3", talker_CAS);
					endcase;
					case 4:
						PlayVoiceOver("VO\Generic\Abort4", talker_CAS);
					endcase;
					case 5:
						PlayVoiceOver("VO\Generic\Abort5", talker_CAS);
					endcase;
				endswitch;
				cinema_cut = cinema_cut +1;
			endcase;
			case 1:
				if TimeGreater(21, 5) then
					cinema_cut = cinema_cut + 1;
					if IsDead(ebu_Listening_Post) then
						DeadBuilding = ebu_Listening_Post;
					else
						if IsDead(ebu_ObjectiveBuilding02) then
							DeadBuilding = ebu_ObjectiveBuilding02;
						else
							DeadBuilding = ebu_ObjectiveBuilding03;
						endif;
					endif;
				endif;
			endcase;
			case 2:	
				EndMission(false, 10);
				SetActiveCamera(eca_CameraShip);
				PullOutCamera(DeadBuilding, DeadBuilding, 150.0, 10.0, true);				
				UpdateCameraPosition(eca_CameraShip);     
				cinema_cut = cinema_cut + 1;
			endcase;
		endswitch;
	
endstate;

state lostPlayer;
	code
	
		switch cinema_cut
			case 0:
				KillVoiceOvers;
				switch rand(1,2)
					case 1:
						StartMusic("Music\genericLoss1",3.5,3.0,50);
					endcase;
					case 2:
						StartMusic("Music\genericLoss2",3.5,3.0,50);
					endcase;
				endswitch;
				switch rand(1,3)
					case 1:
						PlayVoiceOver("VO\Generic\SpecterDestroyed", talker_CAS);
					endcase;
					case 2:
						PlayVoiceOver("VO\Generic\SpecterDestroyed2", talker_CAS);
					endcase;
					case 3:
						PlayVoiceOver("VO\Generic\SpecterDestroyed3", talker_CAS);
					endcase;
				endswitch;
				cinema_cut = cinema_cut + 1;
			endcase;
			case 1:	
				EndMission(false, 10);
				//SetActiveCamera(eca_CameraShip);
				//PullOutCamera(playermech, playermech, 250.0, 10.0, true);				
				cinema_cut = cinema_cut + 1;
			endcase;
		endswitch;
	
endstate;


endfsm.
//******************************************************************

