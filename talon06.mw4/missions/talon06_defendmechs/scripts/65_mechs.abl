//*********************************************************************************

fsm mechs : integer;		

//------------------------------------------------------------------
//
//     Constant Definitions
//
//------------------------------------------------------------------
     const

		#include_ <content\ABLScripts\mwconst.abi>
//------------------------------------------------------------------
//
//     Type Definitions
//
//------------------------------------------------------------------
     type

		#include_ <content\ABLScripts\mwtype.abi>

//------------------------------------------------------------------
//
//     Variable Declarations
//
//------------------------------------------------------------------
	var
		static integer			attackRange;	// At what range do I start shooting?
		static integer			withdrawRange;	// At what range do I stop attacking?
		static integer			skillLevel;		// 1 - 10
		static integer			memInt9;		// value of Memory Integer number 9
		static integer			MoveType;		
		static boolean			pathEndReached;
		static boolean			timerStarted;

		static ObjectID			unitPath;		// the path the units follow
		static LocPoint			spawnPoint;
		static LocPoint			leashPoint;

 function Init;
	code
		// script-specific variables
		attackRange				= 800;
		withdrawRange			= attackRange * 3 / 2;
		skillLevel				= 6;						// 1 - 10
		memInt9 				= GetMemoryInteger(me, 9);	// Value of memory integer number 9
		pathEndReached 			= false;
		moveType 				= MOVE_NOCYCLE;
		timerStarted 			= false;
		GetLocation(me, spawnPoint);
		
		switch memint9
			case 1:
				unitPath = epa_mechPath01;
			endcase;
		 	case 2:
				unitPath = epa_mechPath02;
			endcase;
			case 3:
				unitPath = epa_mechPath01;
			endcase;
		endswitch;
  	

endfunction;
		
state StartState;

	code
	
	AssignSkill(skillLevel);
	clearMoveOrder(me);
	shutDown(me);
	teleportToHell(me);
	trans WaitforLaunch; 	

endstate;

state WaitforLaunch;
	
	code
	if obj1 and (not timerStarted) then
		resetTimer(gti_timer_30);
		timerStarted = true;
	endif;
			
	switch(memInt9)
		case 1:
			if timerStarted then
				if timeGreater(gti_timer_30, 120) then
					teleport(me, spawnPoint);
					startUp(me);
					trans sit;
				endif;
			endif;
		endcase;
		case 2:
			if timerStarted then
				if timeGreater(gti_timer_30, 220) or groupAllDead(groupObjectID(30)) then
					teleport(me, spawnPoint);
					startUp(me);
					trans sit;
				endif;
			endif;
		endcase;
		case 3:
			if timerStarted then
				if timeGreater(gti_timer_30, 80) then
					teleport(me, spawnPoint);
					startUp(me);
					trans sit;
				endif;
			endif;
		endcase;
	endswitch;
		
endstate;		

state sit;
	code
		if (not pathEndReached) then
			if OrderMoveResumePatrol(unitPath, 200, MoveType, true, true) then
				pathEndReached = true;
			endif;
		else
			OrderMoveLookout;
		endif;
		
		if FindEnemy(attackRange, FT_DEFAULT + FT_BUILDING + FT_UNARMED) then
			trans attackState;
		endif;
		
		if ( isShot ( me ) ) then
			setTarget(me, whoShot(me) );
			trans AttackState;
		endif;
		
		if isdead(me) then
			trans deadState;
		endif;
		
endstate;

state AttackState;

	code
	
		if( leaveAttackState(withdrawrange) ) then
			orderStopAttacking;
			settarget(me, NO_UNIT);
			trans sit;
		else;
			OrderAttackTactic(TACTIC_PICK_BEST, true);
		endif;
		
		if ( isShot ( me ) ) then
			setTarget(me, whoShot(me) );
		endif;
		
		if isdead(me) then
			trans deadState;
		endif;
			
endstate;

state deadstate;
	code
		
endstate;

endfsm.
