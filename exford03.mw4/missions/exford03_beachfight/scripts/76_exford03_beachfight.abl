//*********************************************************************************

fsm Exford03_Beachfight : integer;

//------------------------------------------------------------------
//
//	 Constant Definitions
//
//------------------------------------------------------------------
	 const

		#include_ <content\ABLScripts\mwconst.abi>
		#include_ <content\ABLScripts\Katie\katie_consts.abi>  // <-- mektek
//------------------------------------------------------------------
//
//	 Type Definitions
//
//------------------------------------------------------------------
	 type

		#include_ <content\ABLScripts\mwtype.abi>

//------------------------------------------------------------------
//
//	 Variable Declarations
//
//------------------------------------------------------------------
	  var

		eternal integer		playermech;			// This is the variable for the player's 'Mech
			
		// Standard Nav and Objective Variables
		static boolean		alphareached;		// Nav Alpha reached
		static boolean		betareached;		// Nav Beta reached
		static boolean		gammareached;		// Nav Gamma reached
		static boolean		deltareached;		// Nav Delta reached
												//
		static boolean		alpharevealed;		// Nav Alpha is shown
		static boolean		betarevealed;		// Nav Beta is shown
		static boolean		gammarevealed;		// Nav Gamma is shown
		static boolean		deltarevealed;		// Nav Delta is shown
		static integer		checkrange;			// How far from the Nav Point you have to be											
												//
		eternal boolean		obj1;				// This indicates whether Objective 1 was completed
		eternal boolean		obj2;				// This indicates whether Objective 2 was completed
		eternal boolean		obj3;				// This indicates whether Objective 3 was completed
		eternal boolean		obj4;				// This indicates whether Objective 4 was completed
		eternal boolean		obj5;				// This indicates whether Objective 5 was completed
												//
		static boolean		obj1revealed;		// This indicates that Objective 1 has been revealed
		static boolean		obj2revealed;		// This indicates that Objective 2 has been revealed
		static boolean		obj3revealed;		// This indicates that Objective 3 has been revealed
		static boolean		obj4revealed;		// This indicates that Objective 4 has been revealed
		static boolean		obj5revealed;		// This indicates that Objective 5 has been revealed
		//	
		
		static integer		cinema_cut;			// This variable keeps track of what scene a cinema is on.
		static locpoint		temploc;			// Used for commands that need objects converted to locpoint format
		static boolean		loser;				// You have lost the mission
 		eternal integer 	DavionRating;		// This is your Davion rating
		eternal integer 	SteinerRating;		// This is your Steiner rating
		eternal integer 	Nobility;			// This is your Nobility rating
		eternal integer 	Infamy;				// This is your Infamy rating
 
		// Trigger States
		static integer		ObjCounter;			// How many objectives you've completed
		eternal integer		RoE;				// Rules of Engagement - solo combat
		eternal integer		ClanLeader;			// Who is the leader who challenges PCs to combat
		eternal boolean		AirSweep;			// An air sweep is en route
		static integer		Sponsor;
		
//------------------------------------------------------------------
//
//	 Init Function	(automatically run first time through)
//
//------------------------------------------------------------------

function init;
	var
		integer i;

	code

			playermech = getplayervehicle(epl_player0);
			
			if (GetLancemate(1)<>NO_UNIT) then
				SetTargetDesirability(GetLancemate(1),70);
				GroupAddObject(GroupObjectID(1),GetLancemate(1));
			endif;

			if (GetLancemate(2)<>NO_UNIT) then
				SetTargetDesirability(GetLancemate(2),60);
				GroupAddObject(GroupObjectID(1),GetLancemate(2));
			endif;

			if (GetLancemate(3)<>NO_UNIT) then
				SetTargetDesirability(GetLancemate(3),50);
				GroupAddObject(GroupObjectID(1),GetLancemate(3));
			endif;

			if (GetSecondaryLancemate(1)<>NO_UNIT) then
				SetTargetDesirability(GetSecondaryLancemate(1),50);
				GroupAddObject(GroupObjectID(2),GetSecondaryLancemate(1));
			endif;

			if (GetSecondaryLancemate(2)<>NO_UNIT) then
				SetTargetDesirability(GetSecondaryLancemate(2),50);
				GroupAddObject(GroupObjectID(2),GetSecondaryLancemate(2));
			endif;

			if (GetSecondaryLancemate(3)<>NO_UNIT) then
				SetTargetDesirability(GetSecondaryLancemate(3),50);
				GroupAddObject(GroupObjectID(2),GetSecondaryLancemate(3));
			endif;

			if (GetSecondaryLancemate(4)<>NO_UNIT) then
				SetTargetDesirability(GetSecondaryLancemate(4),50);
				GroupAddObject(GroupObjectID(2),GetSecondaryLancemate(4));
			endif;

			// 20 - Showdown 'Mechs
			GroupAddObject(GroupObjectID(20),eve_Aisa_Thastus);
			GroupAddObject(GroupObjectID(20),eve_1wave01);
			GroupAddObject(GroupObjectID(20),eve_1wave02);
			GroupAddObject(GroupObjectID(20),eve_1wave03);
			GroupAddObject(GroupObjectID(20),eve_1wave04);
			GroupAddObject(GroupObjectID(20),eve_1wave05);
			GroupAddObject(GroupObjectID(20),eve_1wave06);
			GroupAddObject(GroupObjectID(20),eve_1wave07);
			GroupAddObject(GroupObjectID(20),eve_1wave08);
			GroupAddObject(GroupObjectID(20),eve_1wave09);
			SetGroupAI(GroupObjectID(20),GROUPAI_MOODSQUAD);
			
			ClanLeader		= eve_Aisa_Thastus;
			RoE				= 1;

			cinema_cut = 0;

			// Culturals
			//SetTarget(ede_example2,eve_example3);
		
endfunction;
		#include_ <content\ABLScripts\Katie\katie_script.abi>  // <-- mektek
//------------------------------------------------------------------
//
//	 Main Code
//
//------------------------------------------------------------------
state startState;

	code	
/* MekTek: Shark removed these and replaced them with Katie calls
			SetKillSalvageChance(eve_1wave02,100);	// Loki
			SetKillSalvageChance(eve_1wave03,100);	// Vulture
			SetKillSalvageChance(eve_1wave07,50);	// Mad Cat
			SetKillSalvageChance(eve_1wave08,50);	// Novacat
*/
		// These are your MetaMission variables, second part is how this mission effects your ratings
			DavionRating 	= GetPreviousDavionFactionRating;
			SteinerRating 	= GetPreviousSteinerFactionRating;
			Nobility 		= GetPreviousNobility;
			Infamy 			= GetPreviousInfamy;
			
			DavionRating	= DavionRating + 0;
			SteinerRating	= SteinerRating - 0;
			Nobility		= Nobility + 8;
			Infamy			= Infamy + 0;
		// 
		
		SetRadarNav(ena_Nav_Alpha,1);
		RevealNavpoint(ena_Nav_Alpha);
		SetNavpoint(ena_Nav_Alpha);

		SetTargetDesirability(ClanLeader,-1);

		StartMusic("Music\Rally",3.0,3.5,50);

		//setsensorvisibility(eve_example4,false);
		
		checkrange	= 100;		// How far from a nav point before the Betty voice says you've arrived.
		
		trans opening;

endstate;

	
state deadState;
	code
	
endstate;
	
//------------------------------------------------------------------
//
//	 Sit state : description
//
//------------------------------------------------------------------
state sit;

	code

	if IsMusicPlaying == false then
		StartMusic("Music\Rally",3.0,3.5,50);
	endif;
	
// Sound Triggers

	if (not GetGlobalTrigger(2)) then
		if (timegreater(1,15)) then		// A random comment from Castle
			PlayVoiceOverOnce("vo\exford\mi3\EnjoyingTooMuch",TALKER_CAS);
			PlayVoiceOverOnce("vo\exford\mi3\BornForThisFight",TALKER_SPE);
			PlayVoiceOverOnce("vo\exford\mi3\KeepAlive",TALKER_CAS);
			PlayVoiceOverOnce("vo\exford\mi3\PlanningOnIt",TALKER_SPE);
			SetGlobalTrigger(2,true);
		endif;
	endif;
	
	if (not GetGlobalTrigger(3)) then	// Half left
		if (GroupNumDead(GroupObjectID(20))>4) then
			SetGlobalTrigger(3,true);
			PlayVoiceOverOnce("vo\exford\mi3\ForcesHalfStrength",TALKER_CAS);
			if (GroupNumDead(GroupObjectID(1)) + GroupNumDead(GroupObjectID(2)) >3) then
				PlayVoiceOverOnce("vo\exford\mi3\DoingSlightlyWorse",TALKER_CAS);
			else
				if (GroupNumDead(GroupObjectID(1)) + GroupNumDead(GroupObjectID(2)) <3) then
					PlayVoiceOverOnce("vo\exford\mi3\DoingSlightlyBetter",TALKER_CAS);
				endif;
			endif;
		endif;
	endif;
	
	if (not GetGlobalTrigger(4)) then
		if (GroupNumDead(GroupObjectID(20))>6) then		// Three left
			SetGlobalTrigger(4,true);
			PlayVoiceOverOnce("vo\exford\mi3\FinishThemOff",TALKER_CAS);
			PlayVoiceOverOnce("vo\exford\mi3\ImOnIt",TALKER_SPE);
		endif;
	endif;
	
	if (not GetGlobalTrigger(5)) then
		if (timegreater(2,1)) then		// The mission is over
		//if (IsDead(ClanLeader)) then		// Binary leader killed
			SetGlobalTrigger(5,true);
			PlayVoiceOverOnce("vo\exford\mi3\CurseYou",TALKER_AIS);
			PlayVoiceOverOnce("vo\exford\mi3\YouMeBondsman",TALKER_SPE);
			Sponsor = GetSponsorRegiment;
			switch (Sponsor)	// Reply depends on which regiment
				case REGIMENT_KLHND:
					PlayVoiceOverOnce("vo\exford\mi3\UServeKell",TALKER_SPE);
				endcase;
				case REGIMENT_GDLEG:
					PlayVoiceOverOnce("vo\exford\mi3\UServeGray",TALKER_SPE);
				endcase;
				case REGIMENT_WODRG:
					PlayVoiceOverOnce("vo\exford\mi3\UServeWolfs",TALKER_SPE);
				endcase;
				case REGIMENT_NOHI:
					PlayVoiceOverOnce("vo\exford\mi3\UServeHighlanders",TALKER_SPE);
				endcase;
			endswitch;
		endif;
	endif;

// New Exford 03 Specific Triggers

	if (RoE==1) then
		if (IsDead(ClanLeader)) then
			RoE = 2;
		endif;
	endif;
	
	if (not GetGlobalTrigger(1)) then		// Target the Clan Leader
		if (not IsDead(ClanLeader)) then		
			if (IsWithin(PlayerMech,ClanLeader,950)) then
				SetGlobalTrigger(1,true);
				SetGUITarget(PlayerMech,ClanLeader);
			endif;
		endif;
	endif;

// Obj 1 - Search the Waypoints

	if (not Obj1) then
		if (GroupAllDead(GroupObjectID(20))) then
			Obj1 = true;
			SuccessObjective(eob_objective1);
			StartTimer(2);		// You win!
		endif;
	endif;
	
// Victory and Loss conditions
													   
	if (timegreater(2,5)) then
		  
		// Salvage - FAB  7/30
			RevealLancemate("Falcon");

		//
		
		if (loser) then
			EndMission(false,5);
			trans lost;
		else

			SetCurrentInfamy(Infamy);
			SetCurrentNobility(Nobility);
			SetCurrentDavionFactionRating(DavionRating);
			SetCurrentSteinerFactionrating(SteinerRating);
			/////////////////////
			// MISSION SALVAGE //
			/////////////////////
			Katie(eve_Aisa_Thastus, 80);
			Katie(eve_1wave01,60);
			Katie(eve_1wave02,50);
			Katie(eve_1wave03,40);
			Katie(eve_1wave04,30);
			Katie(eve_1wave05,40);
			Katie(eve_1wave06,50);
			Katie(eve_1wave07,60);
			Katie(eve_1wave08,50);
			Katie(eve_1wave09, 40);
			
			trans won;	
		endif;
		
	endif;

// Fail the mission when the player dies
	if (isdead(playermech)) then
		KillVoiceOvers;
		switch rand(1,3)
			case 1:
				PlayVoiceOver("VO\Generic\SpecterDestroyed", talker_CAS);
			endcase;
			case 2:
				PlayVoiceOver("VO\Generic\SpecterDestroyed2", talker_CAS);
			endcase;
			case 3:
				PlayVoiceOver("VO\Generic\SpecterDestroyed3", talker_CAS);
			endcase;
		endswitch;
		StartMusic("Music\GenericLoss1",3.0,3.5,70);
		endmission(false,14);
		trans deadstate;
	endif;

// Betty speaks when reaching Nav Point

	if (not alphareached) then
		if (isnavrevealed(ena_nav_alpha)) then
			if (iswithin(playermech,ena_nav_alpha,checkrange)) then
				playvoiceover("VO\generic\navalpha",talker_Bet);
				alphareached = true;
				//SetNavpoint(ena_Nav_Beta);
			endif;
		endif;
	endif;

endstate;

//-----------------------------------------
//HERE THERE BE CINEMAS
//-----------------------------------------

state opening;	  

	code

		if (cinemaskip) then
			KillVoiceOvers;	 
			cinema_cut = 0;	 
			KillTimer(gti_timer_21);
			SetGlobalTrigger(0,true);
			playerai(epl_player0,false);
			StartTimer(1);		// This is the mission start timer
			ResetTimer(1);
			CinemaEnd;		  
			SetCompositingEnabled(True);
			revealobjective(eob_objective1);
			if (GetSecondaryLancemate(1)<>NO_UNIT) then
				Startup(GetSecondaryLancemate(1));
			endif;

			if (GetSecondaryLancemate(2)<>NO_UNIT) then
				Startup(GetSecondaryLancemate(2));
			endif;

			if (GetSecondaryLancemate(3)<>NO_UNIT) then
				Startup(GetSecondaryLancemate(3));
			endif;

			if (GetSecondaryLancemate(4)<>NO_UNIT) then
				Startup(GetSecondaryLancemate(4));
			endif;
							
			trans sit;
		endif;
		
		switch (cinema_cut)

			case 0:
				CinemaStart;
				SetActiveCamera(eca_CameraShip);
				
				PlayVoiceOverOnce("vo\exford\mi3\intro1",TALKER_AIS);
				
				if (GetSecondaryLancemate(1)<>NO_UNIT) then
					Shutdown(GetSecondaryLancemate(1));
				endif;
	
				if (GetSecondaryLancemate(2)<>NO_UNIT) then
					Shutdown(GetSecondaryLancemate(2));
				endif;
	
				if (GetSecondaryLancemate(3)<>NO_UNIT) then
					Shutdown(GetSecondaryLancemate(3));
				endif;
	
				if (GetSecondaryLancemate(4)<>NO_UNIT) then
					Shutdown(GetSecondaryLancemate(4));
				endif;
				
				switch (GetSponsorRegiment)	// Reply depends on which regiment
					case REGIMENT_KLHND:
						PlayVoiceOverOnce("vo\exford\mi3\intro2a",TALKER_SPE);
					endcase;
					case REGIMENT_GDLEG:
						PlayVoiceOverOnce("vo\exford\mi3\intro2b",TALKER_SPE);
					endcase;
					case REGIMENT_WODRG:
						PlayVoiceOverOnce("vo\exford\mi3\intro2c",TALKER_SPE);
					endcase;
					case REGIMENT_NOHI:
						PlayVoiceOverOnce("vo\exford\mi3\intro2d",TALKER_SPE);
					endcase;
				endswitch;
				
				PlayVoiceOverOnce("vo\exford\mi3\intro3",TALKER_SPE);
				PlayVoiceOverOnce("vo\exford\mi3\intro4",TALKER_AIS);
				PlayVoiceOverOnce("vo\exford\mi3\intro5",TALKER_SPE);
				PlayVoiceOverOnce("vo\exford\mi3\intro6",TALKER_AIS);
				PlayVoiceOverOnce("vo\exford\mi3\intro7",TALKER_SPE);
				
				SpinOverCamera(ClanLeader,ClanLeader,17.0,10.0,true);
				FadeToBlack(0.0);
				FadeFromBlack(3.0);
				
				cinema_cut = cinema_cut + 1;
				
				starttimer(21);			// This is the cineractive counter.
				resettimer(21);
			endcase;
			
			case 1:
				if (timegreater(21,11)) then
					SpinOverCamera(PlayerMech,PlayerMech,17.0,10.0,true);
					cinema_cut = cinema_cut + 1;
				endif;
			endcase;
			
			case 2:
				if (not IsVoiceOverPlaying) then
					StartTimer(21);
					ResetTimer(21);
					cinema_cut = cinema_cut + 1;
				endif;
			endcase;	
			
			case 3:
				if (timegreater(21,1)) then
					KillVoiceOvers;
					cinema_cut = 0;
					KillTimer(gti_timer_21);
					SetGlobalTrigger(0,true);
					playerai(epl_player0,false);
					StartTimer(1);		// This is the mission start timer
					ResetTimer(1);
					CinemaEnd;
					revealobjective(eob_objective1);	// Reveal the objectives
					SetCompositingEnabled(True);

					if (GetSecondaryLancemate(1)<>NO_UNIT) then
						Startup(GetSecondaryLancemate(1));
					endif;
		
					if (GetSecondaryLancemate(2)<>NO_UNIT) then
						Startup(GetSecondaryLancemate(2));
					endif;
		
					if (GetSecondaryLancemate(3)<>NO_UNIT) then
						Startup(GetSecondaryLancemate(3));
					endif;
		
					if (GetSecondaryLancemate(4)<>NO_UNIT) then
						Startup(GetSecondaryLancemate(4));
					endif;
					
					trans sit;
				endif;
			endcase;
				
		endswitch;
	
endstate;

state won;	  

	code
		
		if (cinemaskip) then
			cinema_cut = 20;	
			playerai(epl_player0,false);
			KillVoiceOvers;
			Endmission(true,1); 
		endif;

		switch cinema_cut
		
			case 0:	
				StartMusic("Music\GenericVictory1",3.0,3.5,50);					 
				PlayVoiceOverOnce("vo\exford\mi3\Close1",TALKER_CAS);
				PlayVoiceOverOnce("vo\exford\mi3\Close2",TALKER_SPE);
				PlayVoiceOverOnce("vo\exford\mi3\Close3",TALKER_CAS);
				PlayVoiceOverOnce("vo\exford\mi3\Close4",TALKER_SPE);
				SetCompositingEnabled(False);
				CinemaStart;
				setactivecamera (eca_cameraship);
				Targetfollowobject(playermech);
				Camerafollowobject(playermech);
				Targetoffset(0.0,4.0,0.0,0.0,false);
				Cameraoffset(-30.0,125.0,75.0,0.0,false);
				Cameraoffset(60.0,20.0,25.0,14.0,false);
				playerai(epl_player0,true);
				Cinema_cut=Cinema_cut+1;
			endcase;
			
			case 1:
				if (not IsVoiceOverPlaying) then
					endmission(true,2);
					Cinema_cut = Cinema_cut + 1;
				endif;
			endcase;			
			
		endswitch;
	
endstate;

state lost;

		code
		
		switch cinema_cut
		
			case 0:	
			
			endcase;
			
			case 1:
			
			endcase;
			
		endswitch;
	
endstate;

endfsm.
//******************************************************************
