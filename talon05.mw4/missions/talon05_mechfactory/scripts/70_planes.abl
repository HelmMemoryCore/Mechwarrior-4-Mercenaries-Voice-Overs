//*********************************************************************************

fsm planes : integer;		

//------------------------------------------------------------------
//
//     Constant Definitions
//
//------------------------------------------------------------------
     const

		#include_ <content\ABLScripts\mwconst.abi>
//------------------------------------------------------------------
//
//     Type Definitions
//
//------------------------------------------------------------------
     type

		#include_ <content\ABLScripts\mwtype.abi>

//------------------------------------------------------------------
//
//     Variable Declarations
//
//------------------------------------------------------------------
	var
		static integer			attackRange;	// At what range do I start shooting?
		static integer			withdrawRange;	// At what range do I stop attacking?
		static integer			skillLevel;		// 1 - 10
		static integer			memInt9;		// value of Memory Integer number 9
		static boolean			pathEndReached;

		static integer			delayTime;		// The timer used to for taxi delay	
	
		static ObjectID			destination;
		static LocPoint			rallyPoint1;
		static LocPoint			rallyPoint2;
		
 function Init;
	code
		// script-specific variables
		attackRange				= 800;
		withdrawRange			= attackRange * 3 / 2;
		skillLevel				= 7;						// 1 - 10
		memInt9 				= GetMemoryInteger(me, 9);	// Value of memory integer number 9
		pathEndReached 			= false;
		
		GetLocation(ena_planeNav01, rallyPoint1);
		GetLocation(ena_planeNav02, rallyPoint2);
		
		Switch(memInt9)
			case 1:
				DelayTime = 0;
			endcase;
			case 2:
				DelayTime = 10;
			endcase;
			case 3:
				DelayTime = 20;
				memint9 = 2;
			endcase;
			case 4:
				DelayTime = 30;
				memint9 = 2;
			endcase;
			case 5:
				DelayTime = 40;
				memint9 = 2;
			endcase;
			case 6:
				DelayTime = 50;
				memint9 = 2;
			endcase;
			case 7:
				DelayTime = 60;
				memint9 = 2;
			endcase;
			case 8:
				DelayTime = 70;
				memint9 = 2;
			endcase;
		endswitch;
		
		destination = playerMech;
		
endfunction;

function updateDestination;
	var
		static locpoint playerPos;
		static integer x;
		static integer z;
		static integer distance1;
		static integer distance2;
		
	code
		
		GetLocation(playerMech, playerPos);
		x = playerPos[0] - rallyPoint1[0];
		z = playerPos[2] - rallyPoint1[2];
		distance1 = (x*x) + (z*z);
		x = playerPos[0] - rallyPoint2[0];
		z = playerPos[2] - rallyPoint2[2];
		distance2 = (x*x) + (z*z);
		if distance1 > distance2 then
			destination = ena_planeNav01;
		else
			destination = ena_planeNav02;
		endif;
		
endfunction;
		
state StartState;

	code
	
		if startTheFun then
			if IsWithin(playerMech, GroupObjectID(10), 900) then
				AssignSkill(skillLevel);
				resetTimer(GTI_TIMER_10);
				SetAutoTargeting(me, false);
				SetTarget(me, PlayerMech);
				trans WaitforLaunch;
			endif;
		endif; 	

endstate;

state WaitforLaunch;
	
	code
	
		if TimeGreater(GTI_TIMER_10, delayTime) then
			orderTakeOff(1);
			trans StrafeState;
		endif;
		
endstate;	  

state StrafeState;
	code
	
		orderMoveToObject(destination, 600);
		
		if IsWithin(me, destination, 100) then
			if destination == playermech then
				updateDestination;
			else
				destination = playerMech;
			endif;
		endif;
		
		OrderAttackTactic(TACTIC_PICK_BEST, false);
		
		if IsDead(me) then
			trans deadState;
		endif;
		
endstate;  

state deadstate;
	code
		
endstate;

endfsm.
