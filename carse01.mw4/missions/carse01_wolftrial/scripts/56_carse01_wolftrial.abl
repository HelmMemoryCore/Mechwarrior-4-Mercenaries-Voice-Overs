//*********************************************************************************

fsm Carse01_WolfTrial : integer;

//------------------------------------------------------------------
//
//	 Constant Definitions
//
//------------------------------------------------------------------
	 const

		#include_ <content\ABLScripts\mwconst.abi>
//------------------------------------------------------------------
//
//	 Type Definitions
//
//------------------------------------------------------------------
	 type

		#include_ <content\ABLScripts\mwtype.abi>

//------------------------------------------------------------------
//
//	 Variable Declarations
//
//------------------------------------------------------------------
	  var

		eternal integer		playermech;			// This is the variable for the player's 'Mech
			
		// Standard Nav and Objective Variables
		static boolean		alphareached;		// Nav Alpha reached
		static boolean		betareached;		// Nav Beta reached
		static boolean		gammareached;		// Nav Gamma reached
		static boolean		deltareached;		// Nav Delta reached
												//
		static boolean		alpharevealed;		// Nav Alpha is shown
		static boolean		betarevealed;		// Nav Beta is shown
		static boolean		gammarevealed;		// Nav Gamma is shown
		static boolean		deltarevealed;		// Nav Delta is shown
		static integer		checkrange;			// How far from the Nav Point you have to be											
												//
		eternal boolean		obj1;				// This indicates whether Objective 1 was completed
		eternal boolean		obj2;				// This indicates whether Objective 2 was completed
		eternal boolean		obj3;				// This indicates whether Objective 3 was completed
		eternal boolean		obj4;				// This indicates whether Objective 4 was completed
		eternal boolean		obj5;				// This indicates whether Objective 5 was completed
												//
		static boolean		obj1revealed;		// This indicates that Objective 1 has been revealed
		static boolean		obj2revealed;		// This indicates that Objective 2 has been revealed
		static boolean		obj3revealed;		// This indicates that Objective 3 has been revealed
		static boolean		obj4revealed;		// This indicates that Objective 4 has been revealed
		static boolean		obj5revealed;		// This indicates that Objective 5 has been revealed
		//	
		
		static integer		cinema_cut;			// This variable keeps track of what scene a cinema is on.
		static locpoint		temploc;			// Used for commands that need objects converted to locpoint format
		eternal boolean		loser;				// You have lost the mission
 		eternal integer 	DavionRating;		// This is your Davion rating
		eternal integer 	SteinerRating;		// This is your Steiner rating
		eternal integer 	Nobility;			// This is your Nobility rating
		eternal integer 	Infamy;				// This is your Infamy rating
 
		// Trigger States
		eternal boolean		FirstContact;		// The player has been spotted
		eternal integer		RoundNumber;		// Which round we are on
		eternal	boolean		AdvanceRound;		// Current 'Mech died, advance the round
		eternal integer		Strikes;			// You get strikes against you if you attack Clan Wolf
		
//------------------------------------------------------------------
//
//	 Init Function	(automatically run first time through)
//
//------------------------------------------------------------------

function init;
	var
		integer i;

	code

			if (GetLancemate(1)<>NO_UNIT) then
				SetTargetDesirability(GetLancemate(1),50);
			endif;

			if (GetLancemate(2)<>NO_UNIT) then
				SetTargetDesirability(GetLancemate(2),60);
			endif;

			if (GetLancemate(3)<>NO_UNIT) then
				SetTargetDesirability(GetLancemate(3),70);
			endif;
			
			SetTargetDesirability(PlayerMech,15);

			playermech = getplayervehicle(epl_player0);

			// 10 - Patrol 'Mechs
			/*GroupAddObject(GroupObjectID(10),eve_patrol05);
			SetGroupAI(GroupObjectID(10),GROUPAI_MOODSQUAD);*/
			
			cinema_cut = 0;

			// Culturals
			//SetTarget(ede_example2,eve_example3);
		
endfunction;

//------------------------------------------------------------------
//
//	 Main Code
//
//------------------------------------------------------------------
state startState;

	code	

		// These are your MetaMission variables, second part is how this mission effects your ratings
			DavionRating 	= GetPreviousDavionFactionRating;
			SteinerRating 	= GetPreviousSteinerFactionRating;
			Nobility 		= GetPreviousNobility;
			Infamy 			= GetPreviousInfamy;
			
			DavionRating	= DavionRating + 0;
			SteinerRating	= SteinerRating + 23;
			Nobility		= Nobility + 0;
			Infamy			= Infamy + 21;
		// 
		
		SetRadarNav(ena_Nav_Alpha,1);
		RevealNavpoint(ena_Nav_Alpha);
		SetNavpoint(ena_Nav_Alpha);

		StartMusic("Music\Behemoths",3.0,3.5,50);

		RoundNumber = 1;
		//setsensorvisibility(eve_example4,false);
		
		checkrange	= 100;		// How far from a nav point before the Betty voice says you've arrived.
		StartTimer(6);			// This is the timer for the APC, whether it goes or stays
		
		trans opening;

endstate;

	
state deadState;
	code
	
endstate;
	
//------------------------------------------------------------------
//
//	 Sit state : description
//
//------------------------------------------------------------------
state sit;

	code

	if IsMusicPlaying == false then
		StartMusic("Music\Hunters",3.0,3.5,50);
	endif;
	
// Sound Triggers

	if (not GetGlobalTrigger(2)) then
		SetGlobalTrigger(2,true);
		PlayVoiceOverOnce("VO\ThreeSecond",TALKER_SPE);
		PlayVoiceOverOnce("VO\Carse\MI1\Taunt",TALKER_ONE);
	endif;

// Carse01 Specific Triggers

	switch (RoundNumber)
		case 1:
			if (IsDead(eve_Jules_Sender)) then
				RoundNumber = 2;
				PlayVoiceOverOnce("VO\Carse\MI1\OneFallen",TALKER_KHA);
				PlayVoiceOverOnce("VO\ThreeSecond",TALKER_SPE);
				PlayVoiceOverOnce("VO\Carse\MI1\Taunt",TALKER_TWO);
				//SetGUITarget(PlayerMech,eve_Rhanda_Stiles);
				ResetTimer(3);
			endif;
		endcase;
		case 2:
			if (IsDead(eve_Rhanda_Stiles)) then
				RoundNumber = 3;
				PlayVoiceOverOnce("VO\Carse\MI1\TwoFallen",TALKER_KHA);
				PlayVoiceOverOnce("VO\ThreeSecond",TALKER_SPE);
				PlayVoiceOverOnce("VO\Carse\MI1\Taunt",TALKER_THR);
				PlayVoiceOverOnce("VO\Carse\MI1\NegItWillBeYours",TALKER_SPE);
				//SetGUITarget(PlayerMech,eve_Zel_Vickers);
				ResetTimer(3);
			endif;
		endcase;
		case 3:
			if (IsDead(eve_Zel_Vickers)) then
				RoundNumber = 4;
				PlayVoiceOverOnce("VO\Carse\MI1\Imressive",TALKER_KHA);
				PlayVoiceOverOnce("VO\Carse\MI1\Aff",TALKER_FOU);
				PlayVoiceOverOnce("VO\ThreeSecond",TALKER_SPE);
				//SetGUITarget(PlayerMech,eve_Hanse_Noruff);
				ResetTimer(3);
			endif;
		endcase;
		case 4:
			if (IsDead(eve_Hanse_Noruff)) then
				RoundNumber = 5;
				PlayVoiceOverOnce("VO\Carse\MI1\OnlyYouAndI",TALKER_SPE);
				PlayVoiceOverOnce("VO\Carse\MI1\Aggression",TALKER_FOU);
				//SetGUITarget(PlayerMech,eve_Manda_Ruby);
				ResetTimer(3);
			endif;
		endcase;
		case 5:
			if (IsDead(eve_Manda_Ruby)) then
				RoundNumber = 0;
				Obj1Revealed	= true;		// If you kill everyone, then you win
			endif;
		endcase;
	endswitch;
	
	if (not GetGlobalTrigger(1)) then		// If the people outside the circle get shot a couple of times, you lose
		if (Strikes > 2) then
			StartTimer(2);		// You have lost
			Loser = True;
			PlayVoiceOverOnce("VO\Carse\MI1\RulesViolated",TALKER_KHA);
			FailObjective(eob_objective1);
			SetGlobalTrigger(1,true);
		endif;
	endif;

// Obj 1 - Clear out the base defenders

	if (not obj1) then
		if (Obj1Revealed) then
			SuccessObjective(eob_objective1);
			KillVoiceOvers;
			PlayVoiceOverOnce("VO\Carse\MI1\IClaimRight",TALKER_SPE);
			StartTimer(2);		// You win!
			Obj1 = True;
		endif;
	endif;
	
// Victory and Loss conditions
													   
	if (timegreater(2,5)) then
		  
		// Salvage Added by WHO on DATE
		//

		SetCurrentInfamy(Infamy);
		SetCurrentNobility(Nobility);
		SetCurrentDavionFactionRating(DavionRating);
		SetCurrentSteinerFactionrating(SteinerRating);
		
		if (loser) then
			EndMission(false,5);
			trans lost;
		else
			trans won;	
		endif;
		
	endif;

// Fail the mission when the player dies
	if (isdead(playermech)) then
		KillVoiceOvers;
		switch rand(1,3)
			case 1:
				PlayVoiceOver("VO\Generic\SpecterDestroyed", talker_CAS);
			endcase;
			case 2:
				PlayVoiceOver("VO\Generic\SpecterDestroyed2", talker_CAS);
			endcase;
			case 3:
				PlayVoiceOver("VO\Generic\SpecterDestroyed3", talker_CAS);
			endcase;
		endswitch;
		StartMusic("Music\GenericLoss1",3.0,3.5,70);
		endmission(false,14);
		trans deadstate;
	endif;

// Betty speaks when reaching Nav Point

	if (not alphareached) then
		if (isnavrevealed(ena_nav_alpha)) then
			if (iswithin(playermech,ena_nav_alpha,checkrange)) then
				playvoiceover("VO\generic\navalpha",talker_Bet);
				alphareached = true;
				//SetNavpoint(ena_Nav_Beta);
			endif;
		endif;
	endif;

endstate;

//-----------------------------------------
//HERE THERE BE CINEMAS
//-----------------------------------------

state opening;	  

	code

		if (cinemaskip) then
			KillVoiceOvers;	 
			cinema_cut = 0;	 
			KillTimer(gti_timer_21);
			SetGlobalTrigger(0,true);
			playerai(epl_player0,false);
			StartTimer(1);		// This is the mission start timer
			ResetTimer(1);
			CinemaEnd;		  
			SetCompositingEnabled(True);
			revealobjective(eob_objective1);
			HelpMessage(eob_Rules,10);
			StartTimer(3);		// Round Timer
			trans sit;
		endif;
		
		switch (cinema_cut)

			case 0:
				CinemaStart;
				SetActiveCamera(eca_CameraShip);
				
				SpinUnderCamera(PlayerMech,PlayerMech,17.0,32.0,true);
				//SpinOverCamera(PlayerMech,PlayerMech,17.0,17.0,true);
				
				PlayVoiceOverOnce("VO\Carse\MI1\Intro1",TALKER_FOX);
				PlayVoiceOverOnce("VO\Carse\MI1\Intro2",TALKER_SPE);
				PlayVoiceOverOnce("VO\Carse\MI1\Intro3a",TALKER_ONE);
				PlayVoiceOverOnce("VO\Carse\MI1\Intro3b",TALKER_TWO);
				PlayVoiceOverOnce("VO\Carse\MI1\Intro3c",TALKER_THR);
				PlayVoiceOverOnce("VO\Carse\MI1\Intro3d",TALKER_FOU);
				PlayVoiceOverOnce("VO\Carse\MI1\Intro3e",TALKER_FIV);
				PlayVoiceOverOnce("VO\Carse\MI1\Intro4",TALKER_KHA);
				
				cinema_cut = cinema_cut + 1;
				
				starttimer(21);			// This is the cineractive counter.
				resettimer(21);
			endcase;
			
			case 1:
				if (not IsVoiceOverPlaying) then
					ResetTimer(21);
					cinema_cut = cinema_cut + 1;
				endif;
			endcase;
			
			case 2:
				if (timegreater(21,2)) then
					KillVoiceOvers;
					cinema_cut = 0;
					KillTimer(gti_timer_21);
					SetGlobalTrigger(0,true);
					playerai(epl_player0,false);
					StartTimer(1);		// This is the mission start timer
					ResetTimer(1);
					CinemaEnd;
					revealobjective(eob_objective1);	// Reveal the objectives
					HelpMessage(eob_Rules,10);
					SetCompositingEnabled(True);
					StartTimer(3);		// Round Timer
					
					trans sit;
				endif;
			endcase;
				
		endswitch;
	
endstate;

state won;	  

	code
		
		if (cinemaskip) then
			cinema_cut = 20;	
			playerai(epl_player0,false);
			KillVoiceOvers;
			Endmission(true,1); 
		endif;

		switch cinema_cut
		
			case 0:	
				StartMusic("Music\GenericVictory1",3.0,3.5,50);					 
				PlayVoiceOver("VO\ThreeSecond",TALKER_SPE);
				PlayVoiceOverOnce("VO\Carse\MI1\Close1",TALKER_KHA);
				SetCompositingEnabled(False);
				CinemaStart;
				setactivecamera (eca_cameraship);
				Targetfollowobject(playermech);
				Camerafollowobject(playermech);
				Targetoffset(0.0,4.0,0.0,0.0,false);
				Cameraoffset(-30.0,125.0,75.0,0.0,false);
				Cameraoffset(60.0,20.0,25.0,9.0,false);
				playerai(epl_player0,true);
				Cinema_cut=Cinema_cut+1;
			endcase;
			
			case 1:
				if (not IsVoiceOverPlaying) then
					cinema_cut = 20;	
					Endmission(true,3); 
				endif;
			endcase;			
			
		endswitch;
	
endstate;

state lost;

		code
		
		switch cinema_cut
		
			case 0:	
			
			endcase;
			
			case 1:
			
			endcase;
			
		endswitch;
	
endstate;

endfsm.
//******************************************************************
