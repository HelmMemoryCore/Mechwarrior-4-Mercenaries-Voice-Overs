//*********************************************************************************

fsm Avalon02_HackerRun : integer;

//------------------------------------------------------------------
//
//	 Constant Definitions
//
//------------------------------------------------------------------
	 const

		#include_ <content\ABLScripts\mwconst.abi>
		#include_ <content\ABLScripts\Katie\katie_consts.abi>
//------------------------------------------------------------------
//
//	 Type Definitions
//
//------------------------------------------------------------------
	 type

		#include_ <content\ABLScripts\mwtype.abi>

//------------------------------------------------------------------
//
//	 Variable Declarations
//
//------------------------------------------------------------------
	  var

		eternal integer		playermech;			// This is the variable for the player's 'Mech
			
		// Standard Nav and Objective Variables
		static boolean		alphareached;		// Nav Alpha reached
		static boolean		betareached;		// Nav Beta reached
		static boolean		gammareached;		// Nav Gamma reached
		static boolean		deltareached;		// Nav Delta reached
												//
		static boolean		alpharevealed;		// Nav Alpha is shown
		static boolean		betarevealed;		// Nav Beta is shown
		static boolean		gammarevealed;		// Nav Gamma is shown
		static boolean		deltarevealed;		// Nav Delta is shown
		static integer		checkrange;			// How far from the Nav Point you have to be											
												//
		eternal boolean		obj1;				// This indicates whether Objective 1 was completed
		eternal boolean		obj2;				// This indicates whether Objective 2 was completed
		eternal boolean		obj3;				// This indicates whether Objective 3 was completed
		eternal boolean		obj4;				// This indicates whether Objective 4 was completed
		eternal boolean		obj5;				// This indicates whether Objective 5 was completed
												//
		static boolean		obj1revealed;		// This indicates that Objective 1 has been revealed
		static boolean		obj2revealed;		// This indicates that Objective 2 has been revealed
		static boolean		obj3revealed;		// This indicates that Objective 3 has been revealed
		static boolean		obj4revealed;		// This indicates that Objective 4 has been revealed
		static boolean		obj5revealed;		// This indicates that Objective 5 has been revealed
		//	
		
		static integer		cinema_cut;			// This variable keeps track of what scene a cinema is on.
		static locpoint		temploc;			// Used for commands that need objects converted to locpoint format
		static boolean		loser;				// You have lost the mission
 		eternal integer 	DavionRating;		// This is your Davion rating
		eternal integer 	SteinerRating;		// This is your Steiner rating
		eternal integer 	Nobility;			// This is your Nobility rating
		eternal integer 	Infamy;				// This is your Infamy rating
 
		// Trigger States
		eternal boolean		FirstContact;		// The player has been spotted
		eternal boolean		BaseMobile;			// The base mobilizes
		eternal boolean		LastWave;			// This is the last wave of enemy 'Mechs
		eternal integer		SF_APC;				// This is the special forces' APC
		eternal boolean		ReinforceNow;		// This is when you reinforce
		eternal integer		APCHalt;			// If the APC is too close to a live turret it will stop
		eternal boolean		ChopperLanded;		// The chopper has landed
		eternal boolean		FrontCleared;			// The front defenses are cleared
		
//------------------------------------------------------------------
//
//	 Init Function	(automatically run first time through)
//
//------------------------------------------------------------------

function init;
	var
		integer i;

	code

			if (GetLancemate(1)<>NO_UNIT) then
				SetTargetDesirability(GetLancemate(1),50);
			endif;

			if (GetLancemate(2)<>NO_UNIT) then
				SetTargetDesirability(GetLancemate(2),60);
			endif;

			if (GetLancemate(3)<>NO_UNIT) then
				SetTargetDesirability(GetLancemate(3),70);
			endif;
			
			SetTargetDesirability(PlayerMech,15);

			playermech = getplayervehicle(epl_player0);

			// 10 - Patrol 'Mechs
			GroupAddObject(GroupObjectID(10),eve_patrol01);
			GroupAddObject(GroupObjectID(10),eve_patrol02);
			GroupAddObject(GroupObjectID(10),eve_patrol03);
			GroupAddObject(GroupObjectID(10),eve_patrol04);
			GroupAddObject(GroupObjectID(10),eve_patrol05);
			SetGroupAI(GroupObjectID(10),GROUPAI_MOODSQUAD);
			
			// 11 - Base 'Mechs
			GroupAddObject(GroupObjectID(11),eve_basemech01);
			GroupAddObject(GroupObjectID(11),eve_basemech02);
			GroupAddObject(GroupObjectID(11),eve_basemech03);
			SetGroupAI(GroupObjectID(11),GROUPAI_MOODSQUAD);

			// 12 - Base 'Mechs (lt 'Mechs at Beta)
			GroupAddObject(GroupObjectID(12),eve_basemech04);
			GroupAddObject(GroupObjectID(12),eve_basemech05);
			GroupAddObject(GroupObjectID(12),eve_basemech06);
			GroupAddObject(GroupObjectID(12),eve_basemech07);
			SetGroupAI(GroupObjectID(12),GROUPAI_MOODSQUAD);

			// 13 - Base Turrets			
			GroupAddObject(GroupObjectID(13),etu_per_turret01);
			GroupAddObject(GroupObjectID(13),etu_per_turret02);
			GroupAddObject(GroupObjectID(13),etu_per_turret03);
			GroupAddObject(GroupObjectID(13),etu_per_turret04);
			GroupAddObject(GroupObjectID(13),etu_per_turret05);
			GroupAddObject(GroupObjectID(13),etu_per_turret06);
			GroupAddObject(GroupObjectID(13),etu_per_turret07);
			GroupAddObject(GroupObjectID(13),etu_per_turret08);
			GroupAddObject(GroupObjectID(13),etu_per_turret09);
			GroupAddObject(GroupObjectID(13),etu_per_turret11);
			GroupAddObject(GroupObjectID(13),etu_per_turret12);
			GroupAddObject(GroupObjectID(13),etu_strongturret01);
			GroupAddObject(GroupObjectID(13),etu_strongturret02);
			GroupAddObject(GroupObjectID(13),etu_strongturret03);
			GroupAddObject(GroupObjectID(13),etu_large_guardtower);
			GroupAddObject(GroupObjectID(13),etu_large_guardtower_0001);
			SetGroupAI(GroupObjectID(13),GROUPAI_MOODSQUAD);

			// 14 - Reinforcement 'Mechs
			GroupAddObject(GroupObjectID(14),eve_reinforce01);
			GroupAddObject(GroupObjectID(14),eve_reinforce02);
			GroupAddObject(GroupObjectID(14),eve_reinforce03);
			GroupAddObject(GroupObjectID(14),eve_reinforce04);
			GroupAddObject(GroupObjectID(14),eve_2reinforce01);
			GroupAddObject(GroupObjectID(14),eve_2reinforce02);
			GroupAddObject(GroupObjectID(14),eve_2reinforce03);
			GroupAddObject(GroupObjectID(14),eve_2reinforce04);
			SetGroupAI(GroupObjectID(14),GROUPAI_MOODSQUAD);

			// 15 - Just the Assault Drop
			GroupAddObject(GroupObjectID(15),eve_2reinforce01);
			GroupAddObject(GroupObjectID(15),eve_2reinforce02);
			GroupAddObject(GroupObjectID(15),eve_2reinforce03);
			GroupAddObject(GroupObjectID(15),eve_2reinforce04);
			
			// 20 - Alpha Defenses (for Helo)
			GroupAddObject(GroupObjectID(20),eve_basemech01);
			GroupAddObject(GroupObjectID(20),eve_basemech02);
			GroupAddObject(GroupObjectID(20),eve_basemech03);
			GroupAddObject(GroupObjectID(20),etu_strongturret01);
			GroupAddObject(GroupObjectID(20),etu_strongturret02);
			GroupAddObject(GroupObjectID(20),etu_per_turret01);
			GroupAddObject(GroupObjectID(20),etu_per_turret02);
			GroupAddObject(GroupObjectID(20),etu_per_turret03);
			GroupAddObject(GroupObjectID(20),etu_per_turret08);
			GroupAddObject(GroupObjectID(20),etu_per_turret09);
			GroupAddObject(GroupObjectID(20),eve_basetank01);
			GroupAddObject(GroupObjectID(20),eve_basetank02);
			GroupAddObject(GroupObjectID(20),eve_basetank03);
			GroupAddObject(GroupObjectID(20),eve_basetank04);
			
			
			SF_APC	=	eve_Special_Forces;
			
			cinema_cut = 0;

			// Culturals
			//SetTarget(ede_example2,eve_example3);
		
endfunction;
		#include_ <content\ABLScripts\Katie\katie_script.abi>	// <-- mektek
//------------------------------------------------------------------
//
//	 Main Code
//
//------------------------------------------------------------------
state startState;

	code	

		// These are your MetaMission variables, second part is how this mission effects your ratings
			DavionRating 	= GetPreviousDavionFactionRating;
			SteinerRating 	= GetPreviousSteinerFactionRating;
			Nobility 		= GetPreviousNobility;
			Infamy 			= GetPreviousInfamy;
			
			DavionRating	= DavionRating + 0;
			SteinerRating	= SteinerRating + 19;
			Nobility		= Nobility + 0;
			Infamy			= Infamy + 11;
		// 
		
		SetRadarNav(ena_Nav_Alpha,1);
		SetRadarNav(ena_Nav_Beta,2);
		SetRadarNav(ena_Nav_Gamma,3);
		RevealNavpoint(ena_Nav_Alpha);
		RevealNavpoint(ena_Nav_Beta);
		SetNavpoint(ena_Nav_Alpha);

		//setsensorvisibility(eve_example4,false);

		StartMusic("Music\Rally",3.0,3.5,50);
		
		checkrange	= 100;		// How far from a nav point before the Betty voice says you've arrived.
		
		trans opening;

endstate;

	
state deadState;
	code
	
endstate;
	
//------------------------------------------------------------------
//
//	 Sit state : description
//
//------------------------------------------------------------------
state sit;

	code

	if IsMusicPlaying == false then
		//StartMusic("VO\Rain1",0.1,0.1,300);
	endif;
	
// Sound Triggers

	if (not GetGlobalTrigger(6)) then
		if (IsWithin(PlayerMech,ena_nav_alpha,795)) then	// Once you get near the base, VO
			SetGlobalTrigger(6,true);
			StartMusic("Music\Rescue",3.0,3.5,50);
			PlayVoiceOverOnce("VO\Avalon\MI2\TheresTheBase",TALKER_SPE);
			PlayLancemateSound("VO\Generic\Roger",1);
		endif;
	endif;
	
	if (not GetGlobalTrigger(7)) then
		if (timegreater(6,5)) then		// This plays once the Copters get on site
			SetGlobalTrigger(7,true);
			PlayVoiceOverOnce("VO\Avalon\MI2\OnsightChoppersClear",TALKER_LEA);
		endif;
	endif;
	
	if (not GetGlobalTrigger(8)) then
		if (timegreater(6,18)) then	// A delay message about what is going on with insertion team
			SetGlobalTrigger(8,true);
			PlayVoiceOverOnce("VO\Avalon\MI2\HackInPlace",TALKER_LEA);
		endif;
	endif;
	
	if (not GetGlobalTrigger(9)) then
		if (timegreater(6,29)) then	// After some more time warning about incoming reinforcements
			SetGlobalTrigger(9,true);
			PlayVoiceOverOnce("VO\Avalon\MI2\IncomingForces",TALKER_FOX);
			PlayVoiceOverOnce("VO\Avalon\MI2\NeedTwoMinutes",TALKER_LEA);
			PlayVoiceOverOnce("VO\Avalon\MI2\EgangingForces",TALKER_SPE);
			PlayVoiceOverOnce("VO\Avalon\MI2\TheyUseRange",TALKER_FOX);
		endif;
	endif;
	
	if (not GetGlobalTrigger(10)) then
		if (IsWithin(PlayerMech,GroupObjectID(14),875)) then		// Spectre engaging north reinforcements
			SetGlobalTrigger(10,true);
			PlayVoiceOverOnce("VO\Avalon\MI2\WereThere",TALKER_SPE);
		endif;
	endif;
	
	if (not GetGlobalTrigger(11)) then
		if (obj3) then
			if (timegreater(6,129)) then		// Chopper destroyed
				SetGlobalTrigger(11,true);
				Destroy(SF_APC);
				PlayVoiceOverOnce("VO\Avalon\MI2\ChopperDestroyed",TALKER_LEA);
				PlayVoiceOverOnce("VO\Avalon\MI2\SeekAlternate",TALKER_LEA);
				PlayVoiceOverOnce("VO\Avalon\MI2\APCsAtBase",TALKER_SPE);
				PlayVoiceOverOnce("VO\Avalon\MI2\GetPoepleOut",TALKER_FOX);
				PlayVoiceOverOnce("VO\Avalon\MI2\ButWeCan",TALKER_SPE);
				PlayVoiceOverOnce("VO\Avalon\MI2\Negative",TALKER_FOX);
				PlayVoiceOverOnce("VO\Avalon\MI2\GoodLuch",TALKER_SPE);
			endif;
		endif;
	endif;

// Avalon 02 Specific Triggers

	if (not GetGlobalTrigger(1)) then
		if (FirstContact) then
			StartTimer(3);		// This is the timer for the aircraft to take off
			SetGlobalTrigger(1,true);
		endif;
	endif;
	
	if (not BaseMobile) then	// Once you get near the mesa the defenses will start up and scramble
		if (FirstContact) then
			if (IsWithin(PlayerMech,ena_nav_beta,1200)) then
				BaseMobile	= True;
			endif;
		endif;
	endif;
	
	if (not ChopperLanded) then
		if (IsWithin(ena_copterpoint,SF_APC,120)) then		// This plays once the Copters get on site
			ChopperLanded = True;
			StartTimer(6);		// Timer for events once chopper is on site
		endif;
	endif;
	
	if (not ReinforceNow) then		// Reinforce after the copter lands (25 seconds in)
		if (timegreater(6,15)) then
			ReinforceNow = true;
		endif;
	endif;
	
	if (not LastWave) then		// This is for the final wave of 'Mechs to come online ans smoke the player
		if (timegreater(6,135)) then
			LastWave = True;
		endif;
	endif;
	
	if (not FrontCleared) then		// Have the forward defenses been eliminated (for chopper)
		if (GroupAllDead(GroupObjectID(20))) then
			FrontCleared = True;
		endif;
	endif;
	
// Obj 1 - Clear out the base defenders

	if (not obj1) then
		if GroupAllDead(GroupObjectID(11)) and GroupAllDead(GroupObjectID(12)) and GroupAllDead(GroupObjectID(13)) then
			Obj1	= True;
			SuccessObjective(eob_objective1);
			PlayVoiceOverOnce("VO\Avalon\MI2\BaseClear",TALKER_SPE);
			PlayVoiceOverOnce("VO\Avalon\MI2\HeadingIn",TALKER_LEA);
		endif;
	endif;
	
// Obj 2 - Protect the special forces' APC

	if (not obj2) then
		if (IsDead(SF_APC)) then
			if (not timegreater(6,10)) then
				FailObjective(eob_objective2);
				if (not obj1) then
					FailObjective(eob_objective1);
				endif;
				if (not obj3) then
					FailObjective(eob_objective3);
				endif;
				if (not obj4) then
					FailObjective(eob_objective4);
				endif;
				Loser	= True;
				obj2	= True;
				StartTimer(2);		// You lose
			endif;
		endif;
	endif;
	
// Obj 3 - Extract the information on Katrina Steiner at Beta

	if (not Obj3) then
		if (IsDead(ebu_computer_core)) then
			Obj3 = True;
			FailObjective(eob_objective3);
			Loser = True;
			StartTimer(2);		// You lose!
		endif;
	endif;
	
	if (not obj3) then
		if (timegreater(6,112)) then
			SuccessObjective(eob_objective3);
			RevealObjective(eob_objective4);
			PlayVoiceOverOnce("VO\Avalon\MI2\TheyMovedHerTo",TALKER_LEA);
			PlayVoiceOverOnce("VO\Avalon\MI2\ExtractNow",TALKER_FOX);
			RevealNavpoint(ena_Nav_Gamma);
			SetNavpoint(ena_Nav_Gamma);
			Obj3 = true;
		endif;
	endif;
	
// Obj 4 - Retreat to Gamma

	if (not obj4) then		// One way to win is that both you and the APC are near Gamma
		if (obj3) then
			if (IsWithin(PlayerMech,ena_nav_gamma,100)) then
				SuccessObjective(eob_objective4);
				SuccessObjective(eob_objective2);
				StartTimer(2);		// You win
				Obj4	= True;
			endif;
		endif;
	endif;
	
	/* if (not obj4) then
		if (GroupAllDead(GroupObjectID(14))) then	// If no more reinforcements, you win
			SuccessObjective(eob_objective4);
			SuccessObjective(eob_objective2);
			StartTimer(2);		// You win
			Obj4	= True;
		endif;
	endif; */
		
// Victory and Loss conditions
													   
	if (timegreater(2,5)) then
		  
		// Salvage Added by WHO on DATE
		//
		/////////////////////
		// MISSION SALVAGE //
		/////////////////////
		
		Katie(eve_patrol01,60);
		Katie(eve_patrol02,50);
		Katie(eve_patrol03,40);
		Katie(eve_patrol04,30);
		Katie(eve_patrol05,30);

		Katie(eve_basemech01,60);
		Katie(eve_basemech02,50);
		Katie(eve_basemech03,40);

		Katie(eve_basemech04,60);
		Katie(eve_basemech05,50);
		Katie(eve_basemech06,40);
		Katie(eve_basemech07,30);

		Katie(eve_reinforce01,60);
		Katie(eve_reinforce02,50);
		Katie(eve_reinforce03,40);
		Katie(eve_reinforce04,30);

		Katie(eve_2reinforce01,60);
		Katie(eve_2reinforce02,60);
		Katie(eve_2reinforce03,40);
		Katie(eve_2reinforce04,40);

		SetCurrentInfamy(Infamy);
		SetCurrentNobility(Nobility);
		SetCurrentDavionFactionRating(DavionRating);
		SetCurrentSteinerFactionrating(SteinerRating);
		
		if (loser) then
			EndMission(false,5);
			trans lost;
		else
			trans won;	
		endif;
		
	endif;

// Fail the mission when the player dies
	if (isdead(playermech)) then
		KillVoiceOvers;
		StartMusic("Music\GenericLoss1",3.0,3.5,70);
		switch rand(1,3)
			case 1:
				PlayVoiceOver("VO\Generic\SpecterDestroyed", talker_CAS);
			endcase;
			case 2:
				PlayVoiceOver("VO\Generic\SpecterDestroyed2", talker_CAS);
			endcase;
			case 3:
				PlayVoiceOver("VO\Generic\SpecterDestroyed3", talker_CAS);
			endcase;
		endswitch;
		endmission(false,14);
		trans deadstate;
	endif;

// Betty speaks when reaching Nav Point

	if (not alphareached) then
		if (isnavrevealed(ena_nav_alpha)) then
			if (iswithin(playermech,ena_nav_alpha,checkrange)) then
				playvoiceover("VO\generic\navalpha",talker_Bet);
				alphareached = true;
				//SetNavpoint(ena_Nav_Beta);
			endif;
		endif;
	endif;

	if (not betareached) then
		if (isnavrevealed(ena_nav_beta)) then
			if (iswithin(playermech,ena_nav_beta,checkrange)) then
				playvoiceover("VO\generic\navbeta",talker_Bet);
				betareached = true;
				//SetNavpoint(ena_Nav_Gamma);
			endif;
		endif;
	endif;

endstate;

//-----------------------------------------
//HERE THERE BE CINEMAS
//-----------------------------------------

state opening;	  

	code

		if (cinemaskip) then
			KillVoiceOvers;	 
			cinema_cut = 0;	 
			KillTimer(gti_timer_21);
			SetGlobalTrigger(0,true);
			playerai(epl_player0,false);
			StartTimer(1);		// This is the mission start timer
			ResetTimer(1);
			CinemaEnd;		  
			SetCompositingEnabled(True);
			revealobjective(eob_objective1);
			RevealObjective(eob_objective3);
			trans sit;
		endif;
		
		switch (cinema_cut)

			case 0:
				CinemaStart;
				SetActiveCamera(eca_CameraShip);
				
				ChaseCamera(PlayerMech,PlayerMech,3.0,17.0,true);
				
				PlayVoiceOverOnce("VO\Avalon\MI2\Intro",TALKER_SPE);
				PlayLancemateSound("VO\Generic\ReadyOp",3);
				
				cinema_cut = cinema_cut + 1;
				
				starttimer(21);			// This is the cineractive counter.
				resettimer(21);
			endcase;
			
			case 1:
				if (timegreater(21,17)) then
					KillVoiceOvers;
					cinema_cut = 0;
					KillTimer(gti_timer_21);
					SetGlobalTrigger(0,true);
					playerai(epl_player0,false);
					StartTimer(1);		// This is the mission start timer
					ResetTimer(1);
					CinemaEnd;
					revealobjective(eob_objective1);	// Reveal the objectives
					RevealObjective(eob_objective3);
					SetCompositingEnabled(True);
					
					trans sit;
				endif;
			endcase;
				
		endswitch;
	
endstate;

state won;	  

	code
		
		if (cinemaskip) then
			cinema_cut = 20;	
			playerai(epl_player0,false);
			KillVoiceOvers;
			Endmission(true,1); 
		endif;

		switch cinema_cut
		
			case 0:	
				StartMusic("Music\GenericVictory1",3.0,3.5,50);					 
				SetCompositingEnabled(False);
				CinemaStart;
				
				PlayVoiceOverOnce("VO\Avalon\MI2\Close1",TALKER_LEA);
				PlayVoiceOverOnce("VO\Avalon\MI2\Close2",TALKER_SPE);
				PlayVoiceOverOnce("VO\Avalon\MI2\Close3",TALKER_FOX);
				PlayVoiceOverOnce("VO\Avalon\MI2\Close4",TALKER_SPE);
				PlayVoiceOverOnce("VO\Avalon\MI2\Close5",TALKER_FOX);
				
				setactivecamera (eca_cameraship);
				Targetfollowobject(playermech);
				Camerafollowobject(playermech);
				Targetoffset(0.0,4.0,0.0,0.0,false);
				Cameraoffset(-30.0,125.0,75.0,0.0,false);
				Cameraoffset(60.0,20.0,25.0,14.0,false);
				playerai(epl_player0,true);
				Cinema_cut=Cinema_cut+1;
			endcase;
			
			case 1:
				if (not IsVoiceOverPlaying) then
					EndMission(true,2);
					Cinema_cut=Cinema_cut+1;
				endif;
			endcase;			
			
		endswitch;
	
endstate;

state lost;

		code
		
		switch cinema_cut
		
			case 0:	
			
			endcase;
			
			case 1:
			
			endcase;
			
		endswitch;
	
endstate;

endfsm.
//******************************************************************
