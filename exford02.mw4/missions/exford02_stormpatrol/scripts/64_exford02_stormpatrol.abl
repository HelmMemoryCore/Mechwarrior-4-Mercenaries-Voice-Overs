//*********************************************************************************

fsm Exford02_StormPatrol : integer;

//------------------------------------------------------------------
//
//	 Constant Definitions
//
//------------------------------------------------------------------
	 const

		#include_ <content\ABLScripts\mwconst.abi>
		#include_ <content\ABLScripts\Katie\katie_consts.abi>  // <-- mektek
//------------------------------------------------------------------
//
//	 Type Definitions
//
//------------------------------------------------------------------
	 type

		#include_ <content\ABLScripts\mwtype.abi>

//------------------------------------------------------------------
//
//	 Variable Declarations
//
//------------------------------------------------------------------
	  var

		eternal integer		playermech;			// This is the variable for the player's 'Mech
			
		// Standard Nav and Objective Variables
		static boolean		alphareached;		// Nav Alpha reached
		static boolean		betareached;		// Nav Beta reached
		static boolean		gammareached;		// Nav Gamma reached
		static boolean		deltareached;		// Nav Delta reached
												//
		static boolean		alpharevealed;		// Nav Alpha is shown
		static boolean		betarevealed;		// Nav Beta is shown
		static boolean		gammarevealed;		// Nav Gamma is shown
		static boolean		deltarevealed;		// Nav Delta is shown
		static integer		checkrange;			// How far from the Nav Point you have to be											
												//
		eternal boolean		obj1;				// This indicates whether Objective 1 was completed
		eternal boolean		obj2;				// This indicates whether Objective 2 was completed
		eternal boolean		obj3;				// This indicates whether Objective 3 was completed
		eternal boolean		obj4;				// This indicates whether Objective 4 was completed
		eternal boolean		obj5;				// This indicates whether Objective 5 was completed
												//
		static boolean		obj1revealed;		// This indicates that Objective 1 has been revealed
		static boolean		obj2revealed;		// This indicates that Objective 2 has been revealed
		static boolean		obj3revealed;		// This indicates that Objective 3 has been revealed
		static boolean		obj4revealed;		// This indicates that Objective 4 has been revealed
		static boolean		obj5revealed;		// This indicates that Objective 5 has been revealed
		//	
		
		static integer		cinema_cut;			// This variable keeps track of what scene a cinema is on.
		static locpoint		temploc;			// Used for commands that need objects converted to locpoint format
		static boolean		loser;				// You have lost the mission
 		eternal integer 	DavionRating;		// This is your Davion rating
		eternal integer 	SteinerRating;		// This is your Steiner rating
		eternal integer 	Nobility;			// This is your Nobility rating
		eternal integer 	Infamy;				// This is your Infamy rating
 
		// Trigger States
		eternal boolean		FirstContact;		// Enemies have made the PC or allies
		eternal boolean		MineField;			// The player now knows about the Mine field
		static integer		UnitStrength;
				
//------------------------------------------------------------------
//
//	 Init Function	(automatically run first time through)
//
//------------------------------------------------------------------

function init;
	var
		integer i;

	code

			playermech = getplayervehicle(epl_player0);
			GroupAddObject(GroupObjectID(1),PlayerMech);
			
			if (GetLancemate(1)<>NO_UNIT) then
				GroupAddObject(GroupObjectID(1),GetLancemate(1));
				SetTargetDesirability(GetLancemate(1),70);
			endif;

			if (GetLancemate(2)<>NO_UNIT) then
				GroupAddObject(GroupObjectID(1),GetLancemate(2));
				SetTargetDesirability(GetLancemate(2),60);
			endif;

			if (GetLancemate(3)<>NO_UNIT) then
				GroupAddObject(GroupObjectID(1),GetLancemate(3));
				SetTargetDesirability(GetLancemate(3),50);
			endif;

			// 10 - First Set of Allies that storm the hill with player
			/* GroupAddObject(GroupObjectID(10),eve_1lance01);
			GroupAddObject(GroupObjectID(10),eve_1lance02);
			GroupAddObject(GroupObjectID(10),eve_1lance03);
			GroupAddObject(GroupObjectID(10),eve_2lance01);
			GroupAddObject(GroupObjectID(10),eve_2lance02);
			SetGroupAI(GroupObjectID(20),GROUPAI_MOODSQUAD); */
			
			// 20 - First Wave of 'Mechs (scouts)
			GroupAddObject(GroupObjectID(20),eve_2wave01);
			SetGroupAI(GroupObjectID(20),GROUPAI_MOODSQUAD);

			// 21 - Nav Alpha 'Mechs
			GroupAddObject(GroupObjectID(21),eve_1wave01);
			GroupAddObject(GroupObjectID(21),eve_1wave02);
			GroupAddObject(GroupObjectID(21),eve_1wave03);
			GroupAddObject(GroupObjectID(21),eve_1wave04);
			GroupAddObject(GroupObjectID(21),eve_1wave05);
			//GroupAddObject(GroupObjectID(21),eve_1wave06);
			SetGroupAI(GroupObjectID(21),GROUPAI_MOODSQUAD);
			
			// 22 - Lagoon 'Mechs at Gamma
			GroupAddObject(GroupObjectID(22),eve_3wave01);
			GroupAddObject(GroupObjectID(22),eve_3wave02);
			GroupAddObject(GroupObjectID(22),eve_3wave03);
			//GroupAddObject(GroupObjectID(22),eve_3wave04);
			SetGroupAI(GroupObjectID(22),GROUPAI_MOODSQUAD);
			
			// 23 - Last 'Mechs at Delta
			GroupAddObject(GroupObjectID(23),eve_4wave01);
			GroupAddObject(GroupObjectID(23),eve_4wave02);
			GroupAddObject(GroupObjectID(23),eve_4wave03);
			GroupAddObject(GroupObjectID(23),eve_4wave04);
			SetGroupAI(GroupObjectID(23),GROUPAI_MOODSQUAD);
			
			// 30 - Land Mines
			GroupAddObject(GroupObjectID(30),ebu_landmine);
			GroupAddObject(GroupObjectID(30),ebu_landmine_0001);
			GroupAddObject(GroupObjectID(30),ebu_landmine_0003);
			GroupAddObject(GroupObjectID(30),ebu_landmine_0005);
			GroupAddObject(GroupObjectID(30),ebu_landmine_0009);
			GroupAddObject(GroupObjectID(30),ebu_landmine_0007);
			GroupAddObject(GroupObjectID(30),ebu_landmine_0011);
			GroupAddObject(GroupObjectID(30),ebu_landmine_0013);
			GroupAddObject(GroupObjectID(30),ebu_landmine_0015);
			GroupAddObject(GroupObjectID(30),ebu_landmine_0017);
			GroupAddObject(GroupObjectID(30),ebu_landmine_0019);
			GroupAddObject(GroupObjectID(30),ebu_landmine_0021);
			GroupAddObject(GroupObjectID(30),ebu_landmine_0023);
			GroupAddObject(GroupObjectID(30),ebu_landmine_0025);
			
			cinema_cut = 0;

			// Culturals
			//SetTarget(ede_example2,eve_example3);
		
endfunction;
		#include_ <content\ABLScripts\Katie\katie_script.abi>  // <-- mektek
//------------------------------------------------------------------
//
//	 Main Code
//
//------------------------------------------------------------------
state startState;

	code	
/* Mektek: Shark removed these and replaced them with Katie calls
			SetKillSalvageChance(eve_1wave05,50);	// Uller
			SetKillSalvageChance(eve_3wave02,100);	// Shadow Cat
			SetKillSalvageChance(eve_2wave01,50);	// Thor
*/
		UnitStrength = GetCurrentUnitStrength;
		if (UnitStrength > 2100) then
			GroupAddObject(GroupObjectID(23),eve_4wave05);
		else
			Shutdown(eve_4wave05);
			TeleporttoHell(eve_4wave05);
			Destroy(eve_4wave05);
		endif;

		// These are your MetaMission variables, second part is how this mission effects your ratings
			DavionRating 	= GetPreviousDavionFactionRating;
			SteinerRating 	= GetPreviousSteinerFactionRating;
			Nobility 		= GetPreviousNobility;
			Infamy 			= GetPreviousInfamy;
			
			DavionRating	= DavionRating + 0;
			SteinerRating	= SteinerRating - 0;
			Nobility		= Nobility + 4;
			Infamy			= Infamy + 0;
		// 
		
		SetRadarNav(ena_Nav_Alpha,1);
		SetRadarNav(ena_Nav_Beta,2);
		SetRadarNav(ena_Nav_Gamma,3);
		SetRadarNav(ena_Nav_Delta,4);
		RevealNavpoint(ena_Nav_Alpha);
		SetNavpoint(ena_Nav_Alpha);

		//setsensorvisibility(eve_example4,false);
		
		checkrange	= 100;		// How far from a nav point before the Betty voice says you've arrived.
		
		trans opening;
		//trans won;

endstate;

	
state deadState;
	code
	
endstate;
	
//------------------------------------------------------------------
//
//	 Sit state : description
//
//------------------------------------------------------------------
state sit;

	code

	if IsMusicPlaying == false then
		StartMusic("VO\Rain1",0.1,0.1,300);
	endif;
	
// Sound Triggers

	if (not GetGlobalTrigger(2)) then
		if (timegreater(1,5)) then		// A few seconds in some words of warning
			PlayVoiceOverOnce("vo\exford\mi2\DoNotUnderestimte",TALKER_SPE);
			PlayLancemateSound("vo\generic\clanprejudice",1);
			SetGlobalTrigger(2,true);
		endif;
	endif;
	
	if (not GetGlobalTrigger(3)) then
		if (IsWithin(PlayerMech,GroupObjectID(21),500)) then	// First contact msg
			SetGlobalTrigger(3,true);
			PlayVoiceOverOnce("vo\exford\mi2\ContactWatchOut",TALKER_SPE);
			PlayLancemateSound("vo\generic\VisContact",1);
			PlayLancemateSound("vo\generic\RogerHarried",1);
			PlayVoiceOverOnce("vo\exford\mi2\EngagingStar",TALKER_SPE);
			PlayVoiceOverOnce("vo\exford\mi2\GiveThemHell",TALKER_CAS);
			StartMusic("Music\Hunter",3.0,3.5,50);
		endif;
	endif;
	
	if (not GetGlobalTrigger(4)) then
		if (GroupAllDead(GroupObjectID(21))) then	// First star killed
			SetGlobalTrigger(4,true);
			PlayVoiceOverOnce("vo\exford\mi2\FalconStarEliminated",TALKER_SPE);
			PlayVoiceOverOnce("vo\exford\mi2\ClanPrejudice",TALKER_CAS);
			if (not BetaRevealed) then
				PlayVoiceOverOnce("vo\exford\mi2\ToNavBeta",TALKER_SPE);
			endif;
		endif;
	endif;
	
	if (not GetGlobalTrigger(5)) then
		if (GetGlobalTrigger(4)) then
			if (BetaReached) then		// Got to Beta
				PlayVoiceOverOnce("vo\exford\mi2\BetaClear",TALKER_SPE);
				PlayVoiceOverOnce("vo\generic\RogerSir",TALKER_CAS);
				PlayLancemateSound("vo\generic\amused",1);
				SetGlobalTrigger(5,true);
			endif;
		endif;
	endif;
	
	if (not GetGlobalTrigger(6)) then
		if (IsWithin(PlayerMech,GroupObjectID(22),500)) then	// At second lance
			SetGlobalTrigger(6,true);
			PlayVoiceOverOnce("vo\exford\mi2\EngagingSecondStar",TALKER_SPE);
			PlayLancemateSound("vo\generic\determinedenemy",1);
			PlayVoiceOverOnce("vo\exford\mi2\MedsAndHeavy",TALKER_SPE);
		endif;
	endif;
	
	if (not GetGlobalTrigger(7)) then
		if (GroupAllDead(GroupObjectID(22))) then		// Second star killed
			SetGlobalTrigger(7,true);
			PlayVoiceOverOnce("vo\exford\mi2\SecondStarDestroyed",TALKER_SPE);
			PlayLancemateSound("vo\generic\goodnews",1);
		endif;
	endif;
	
	if (not GetGlobalTrigger(8)) then
		if (IsWithin(eve_Hrothgar,PlayerMech,900)) then		// Close to dropship
			SetGlobalTrigger(8,true);
			PlayVoiceOverOnce("vo\exford\mi2\HeresTheNest",TALKER_SPE);
			PlayVoiceOverOnce("vo\exford\mi2\DropshipNearDelta",TALKER_SPE);
			PlayVoiceOverOnce("vo\exford\mi2\TakeItOut",TALKER_CAS);
			PlayVoiceOverOnce("vo\exford\mi2\WatchYourselves",TALKER_SPE);
		endif;
	endif;
	
	if (not GetGlobalTrigger(9)) then		// If any mines blow up, Castle will warn you
		if (GroupNumDead(GroupObjectID(30))>0) then
			SetGlobalTrigger(9,true);
			PlayVoiceOverOnce("vo\exford\mi2\Minefield",TALKER_CAS);
		endif;
	endif;

// New Exford 02 Specific Triggers
	if (not GetGlobalTrigger(1)) then
		if (IsWithin(PlayerMech,ena_nav_gamma,700)) then
			SetGlobalTrigger(1,true);
		else
			if (IsWithin(PlayerMech,ena_gamma2,700)) then
				SetGlobalTrigger(1,true);
			endif;
		endif;
	endif;

// Obj 1 - Search the Waypoints

	if (not Obj1) then
		if (IsWithin(PlayerMech,ena_Nav_Delta,500)) then
			SuccessObjective(eob_objective1);
			RevealObjective(eob_objective3);
			Obj1 = True;
		endif;
	endif;
	
// Obj2 - Destroy all 'Mechs

	if (not Obj2) then
		if (GroupAllDead(GroupObjectID(20))) and GroupAllDead(GroupObjectID(21)) and GroupAllDead(GroupObjectID(22)) and GroupAllDead(GroupObjectID(23)) then
			Obj2 = true;
			SuccessObjective(eob_objective2);
			if (obj3) then
				StartTimer(2);		// You win!
				PlayVoiceOverOnce("vo\exford\mi2\GreatWork",TALKER_SPE);
			endif;
		endif;
	endif;
	
// Obj3 - Destroy the Jade Falcon dropship

	if (not Obj3) then
		if (IsDead(eve_hrothgar)) then
			SuccessObjective(eob_objective3);
			Obj3 = true;
			if (obj2) then
				PlayVoiceOverOnce("vo\exford\mi2\GreatWork",TALKER_SPE);
				StartTimer(2);		// You win!
			endif;
		endif;
	endif;

// Victory and Loss conditions
													   
	if (timegreater(2,5)) then
		  
		// Salvage - FAB  9/5
			AddWeaponSalvage("ClanFlamer",3);
			AddWeaponSalvage("ClanLRM5",2);
			AddWeaponSalvage("ERLargeLaser",2);
			AddWeaponSalvage("ClanUltraAC5",3); 
			AddWeaponSalvage("ClanLRM20",1); 
		//

		SetCurrentInfamy(Infamy);
		SetCurrentNobility(Nobility);
		SetCurrentDavionFactionRating(DavionRating);
		SetCurrentSteinerFactionrating(SteinerRating);

		AddMissionMoneyToBalance(2000000);
		/////////////////////
		// MISSION SALVAGE //
		/////////////////////
		Katie(eve_2wave01,50);

		Katie(eve_1wave01,50);
		Katie(eve_1wave02,40);
		Katie(eve_1wave03,30);
		Katie(eve_1wave04,20);
		Katie(eve_1wave05,20);

		Katie(eve_3wave01,50);
		Katie(eve_3wave02,40);
		Katie(eve_3wave03,30);

		Katie(eve_4wave01,50);
		Katie(eve_4wave02,40);
		Katie(eve_4wave03,30);
		Katie(eve_4wave04,20);

		if(GetCurrentUnitStrength > 2100) then
			Katie(eve_4wave05,50);
		endif;
		// end of mission salvage
		if (loser) then
			EndMission(false,5);
			trans lost;
		else
			trans won;	
		endif;
		
	endif;

// Fail the mission when the player dies
	if (isdead(playermech)) then
		KillVoiceOvers;
		switch rand(1,3)
			case 1:
				PlayVoiceOver("VO\Generic\SpecterDestroyed", talker_CAS);
			endcase;
			case 2:
				PlayVoiceOver("VO\Generic\SpecterDestroyed2", talker_CAS);
			endcase;
			case 3:
				PlayVoiceOver("VO\Generic\SpecterDestroyed3", talker_CAS);
			endcase;
		endswitch;
		StartMusic("Music\GenericLoss1",3.0,3.5,70);
		endmission(false,14);
		trans deadstate;
	endif;

// Betty speaks when reaching Nav Point

	if (not alphareached) then
		if (isnavrevealed(ena_nav_alpha)) then
			if (iswithin(playermech,ena_nav_alpha,checkrange)) then
				playvoiceover("VO\generic\navalpha",talker_Bet);
				alphareached = true;
				SetNavpoint(ena_Nav_Beta);
			endif;
		endif;
	endif;

	if (not betareached) then
		if (isnavrevealed(ena_nav_beta)) then
			if (iswithin(playermech,ena_nav_beta,checkrange)) then
				playvoiceover("VO\generic\navbeta",talker_Bet);
				betareached = true;
				SetNavpoint(ena_Nav_Gamma);
			endif;
		endif;
	endif;

	if (not gammareached) then
		if (isnavrevealed(ena_nav_gamma)) then
			if (iswithin(playermech,ena_nav_gamma,checkrange)) then
				playvoiceover("VO\generic\navgamma",talker_Bet);
				gammareached = true;
				SetNavpoint(ena_Nav_Delta);
			endif;
		endif;
	endif;

	if (not deltareached) then
		if (isnavrevealed(ena_nav_delta)) then
			if (iswithin(playermech,ena_nav_delta,checkrange)) then
				playvoiceover("VO\generic\navdelta",talker_Bet);
				deltareached = true;
				//SetNavpoint(ena_Nav_Epsilon);
			endif;
		endif;
	endif;
	
endstate;

//-----------------------------------------
//HERE THERE BE CINEMAS
//-----------------------------------------

state opening;	  

	code

		if (cinemaskip) then
			KillVoiceOvers;	 
			cinema_cut = 0;	 
			KillTimer(gti_timer_21);
			SetGlobalTrigger(0,true);
			playerai(epl_player0,false);
			StartTimer(1);		// This is the mission start timer
			ResetTimer(1);
			CinemaEnd;		  
			SetCompositingEnabled(True);
			revealobjective(eob_objective1);
			RevealObjective(eob_objective2);
			HelpMessage(eob_lightamp,10);
			trans sit;
		endif;
		
		switch (cinema_cut)

			case 0:
				CinemaStart;
				SetActiveCamera(eca_CameraShip);
				
				PlayVoiceOverOnce("vo\exford\mi2\intro1",TALKER_SPE);
				PlayLancemateSound("vo\generic\roger",3);
				PlayVoiceOverOnce("vo\exford\mi2\intro2",TALKER_CAS);
				PlayVoiceOverOnce("vo\exford\mi2\intro3",TALKER_SPE);
				
				//SpinOverCamera(PlayerMech,PlayerMech,17.0,17.0,true);
				HeadOnCamera(PlayerMech,PlayerMech,3.0,17.0,false);
				
				cinema_cut = cinema_cut + 1;
				
				starttimer(21);			// This is the cineractive counter.
				resettimer(21);
			endcase;

			case 1:
				if (not IsVoiceOverPlaying) then
					cinema_cut = cinema_cut + 1;
					resettimer(21);
				endif;
			endcase;
			
			case 2:
				if (timegreater(21,3)) then
					KillVoiceOvers;
					cinema_cut = 0;
					KillTimer(gti_timer_21);
					SetGlobalTrigger(0,true);
					playerai(epl_player0,false);
					StartTimer(1);		// This is the mission start timer
					ResetTimer(1);
					CinemaEnd;
					revealobjective(eob_objective1);	// Reveal the objectives
					RevealObjective(eob_objective2);
					SetCompositingEnabled(True);
					HelpMessage(eob_lightamp,10);					

					trans sit;
				endif;
			endcase;
				
		endswitch;
	
endstate;

state won;	  

	code
		
		if (cinemaskip) then
			cinema_cut = 20;	
			playerai(epl_player0,false);
			KillVoiceOvers;
			Endmission(true,1); 
		endif;

		switch cinema_cut
		
			case 0:	
				StartMusic("Music\GenericVictory1",3.0,3.5,50);					 
				PlayVoiceOverOnce("vo\exford\mi2\Close1",TALKER_SPE);
				PlayVoiceOverOnce("vo\exford\mi2\Close2",TALKER_CAS);
				SetCompositingEnabled(False);
				playerai(epl_player0,true);
				
				CinemaStart;
				setactivecamera (eca_cameraship);
				SpinOverCamera(PlayerMech,PlayerMech,17.0,17.0,true);
				
				Cinema_cut=Cinema_cut+1;
			endcase;
			
			case 1:
				if (not IsVoiceOverPlaying) then
					EndMission(true,2);
					Cinema_Cut = Cinema_Cut + 1;
				endif;
			endcase;			
			
		endswitch;
	
endstate;

state lost;

		code
		
		switch cinema_cut
		
			case 0:	
			
			endcase;
			
			case 1:
			
			endcase;
			
		endswitch;
	
endstate;

endfsm.
//******************************************************************
