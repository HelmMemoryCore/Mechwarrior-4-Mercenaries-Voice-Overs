
//*********************************************************************************

fsm Eaton01_MercAlley : integer;

//------------------------------------------------------------------
//
//	 Constant Definitions
//
//------------------------------------------------------------------
	 const

		#include_ <content\ABLScripts\mwconst.abi>
//------------------------------------------------------------------
//
//	 Type Definitions
//
//------------------------------------------------------------------
	 type

		#include_ <content\ABLScripts\mwtype.abi>

//------------------------------------------------------------------
//
//	 Variable Declarations
//
//------------------------------------------------------------------
	  var

		eternal integer		playermech;			// This is the variable for the player's 'Mech
			
		// Standard Nav and Objective Variables
		static boolean		alphareached;		// Nav Alpha reached
		static boolean		betareached;		// Nav Beta reached
		static boolean		gammareached;		// Nav Gamma reached
		static boolean		deltareached;		// Nav Delta reached
												//
		static boolean		alpharevealed;		// Nav Alpha is shown
		static boolean		betarevealed;		// Nav Beta is shown
		static boolean		gammarevealed;		// Nav Gamma is shown
		static boolean		deltarevealed;		// Nav Delta is shown
		static integer		checkrange;			// How far from the Nav Point you have to be											
												//
		eternal boolean		obj1;				// This indicates whether Objective 1 was completed
		eternal boolean		obj2;				// This indicates whether Objective 2 was completed
		eternal boolean		obj3;				// This indicates whether Objective 3 was completed
		eternal boolean		obj4;				// This indicates whether Objective 4 was completed
		eternal boolean		obj5;				// This indicates whether Objective 5 was completed
												//
		static boolean		obj1revealed;		// This indicates that Objective 1 has been revealed
		static boolean		obj2revealed;		// This indicates that Objective 2 has been revealed
		static boolean		obj3revealed;		// This indicates that Objective 3 has been revealed
		static boolean		obj4revealed;		// This indicates that Objective 4 has been revealed
		static boolean		obj5revealed;		// This indicates that Objective 5 has been revealed
		//	
		
		static integer		cinema_cut;			// This variable keeps track of what scene a cinema is on.
		static boolean		loser;				// You have lost the mission
		eternal integer 	DavionRating;		// This is your Davion rating
		eternal integer 	SteinerRating;		// This is your Steiner rating
		eternal integer 	Nobility;			// This is your Nobility rating
		eternal integer 	Infamy;				// This is your Infamy rating
 
		// Trigger States
		eternal boolean		Harass;				// Enter the harassers
		eternal boolean		Relief;				// Enter the support group for the convoy guards
		
		static integer		ReliefTime;			// How long after engagement the Relief come online
		static integer		HarassTime;			// How long after engagement the Harass come online
		static integer		ReinforceNum;		// How many guys have to be nailed before reinforcements start coming in
		eternal boolean		EnemyEngaged;		// Once the enemy has been seen, this is flagged
		
		static boolean		Fail1;				// You have failed the mission once
		static boolean		Replay1;			// You are playing the mission again
		static boolean		Replay2;			// You are playing the mission yet again
		static locpoint		MyNav;				// Used for teleports
		
		// Global Variables will handle this later
		static integer		E1_Harass;			// Number of Harass
		static integer		E1_Mainforce;		// Number of Harass
		static integer		E1_Relief;			// Number of Harass
		//
		
//------------------------------------------------------------------
//
//	 Init Function	(automatically run first time through)
//
//------------------------------------------------------------------

function init;
	var
		integer i;

	code

			playermech = getplayervehicle(epl_player0);

			// 10 - Eaton Convoy (Convoy Group)
			GroupAddObject(GroupObjectId(10),eve_convoy01);  
			GroupAddObject(GroupObjectId(10),eve_convoy02);  
			GroupAddObject(GroupObjectId(10),eve_convoy03);  
			GroupAddObject(GroupObjectId(10),eve_convoy04);  
			GroupAddObject(GroupObjectId(10),eve_convoy05);  
			GroupAddObject(GroupObjectId(10),eve_convoy06);  
			SetGroupAI(groupobjectid(10),GROUPAI_MOODSQUAD);
			
			// 20 - Militia Bandits (Convoy Attackers)
			GroupAddObject(GroupObjectID(20),eve_mainforce01);
			GroupAddObject(GroupObjectID(20),eve_mainforce02);
			GroupAddObject(GroupObjectID(20),eve_mainforce03);
			GroupAddObject(GroupObjectID(20),eve_mainforce04);
			GroupAddObject(GroupObjectID(20),eve_mainforce05);
			GroupAddObject(GroupObjectID(20),eve_mainforce06);
			SetGroupAI(groupobjectid(20),GROUPAI_MOODSQUAD);

			// 21 - Relief Group
			GroupAddObject(GroupObjectID(21),eve_relief01);
			GroupAddObject(GroupObjectID(21),eve_relief02);
			GroupAddObject(GroupObjectID(21),eve_relief03);
			GroupAddObject(GroupObjectID(21),eve_relief04);
			GroupAddObject(GroupObjectID(21),eve_relief05);
			SetGroupAI(groupobjectid(21),GROUPAI_MOODSQUAD);
			
			// 23 - Harasser Group
			GroupAddObject(GroupObjectID(23),eve_harass01);
			GroupAddObject(GroupObjectID(23),eve_harass02);
			GroupAddObject(GroupObjectID(23),eve_harass03);
			GroupAddObject(GroupObjectID(23),eve_harass04);
			GroupAddObject(GroupObjectID(23),eve_harass05);
			GroupAddObject(GroupObjectID(23),eve_harass06);
			SetGroupAI(groupobjectid(23),GROUPAI_MOODSQUAD);
			
			cinema_cut = 0;
			
			// Culturals
			//SetTarget(ede_example2,eve_example3);
		
endfunction;

//------------------------------------------------------------------
//
//	 Main Code
//
//------------------------------------------------------------------
state startState;

	code	

		// Eaton 1 specific
			ReliefTime		= 30;	// How long after engagement the Relief come online
			HarassTime		= 70;	// How long after engagement the Harass come online
			ReinforceNum	= 1;	// How many of the main force have to be nailed before reinforced
		//

		// These are your MetaMission variables, second part is how this mission effects your ratings
			DavionRating 	= GetPreviousDavionFactionRating;
			SteinerRating 	= GetPreviousSteinerFactionRating;
			Nobility 		= GetPreviousNobility;
			Infamy 			= GetPreviousInfamy;
			
			DavionRating	= DavionRating + 2;		// Working for Pro-Davion governor
			SteinerRating	= SteinerRating - 1;
			Nobility		= Nobility + 0;
			Infamy			= Infamy + 3;
		// 
		
		SetRadarNav(ena_Nav_Alpha,1);
		SetRadarNav(ena_Nav_Beta,2);
		SetRadarNav(ena_Nav_Gamma,3);
		
		RevealNavpoint(ena_Nav_Alpha);
		RevealNavpoint(ena_Nav_Beta);
		RevealNavpoint(ena_Nav_Gamma);
		
		SetNavpoint(ena_Nav_Alpha);

		//setsensorvisibility(eve_example4,false);
		
		checkrange	= 150;		// How far from a nav point before the Betty voice says you've arrived.

		StartMusic("Music\SneakPeek",3.0,3.5,50);

		trans opening;

endstate;

	
state deadState;
	code
	
endstate;
	
//------------------------------------------------------------------
//
//	 Sit state : description
//
//------------------------------------------------------------------
state sit;

	code

	if IsMusicPlaying == false then
		StartMusic("music\ArcticAmbLoop",0.0,3.0,40);
	endif;

// Eaton 1 Specific Triggers
	
// Sound Triggers

	if (not GetGlobalTrigger(2)) then		// A bit more intro
		if (timegreater(1,6)) then	 
			SetGlobalTrigger(2,true);
			PlayVoiceOverOnce("vo\eaton\mi1\listenup",TALKER_SPE);
			PlayLancemateSound("VO\Generic\Roger",1);
		endif;
	endif;
	
	if (not GetGlobalTrigger(4)) then
		if (IsWithin(PlayerMech,GroupObjectID(20),900)) then		// Close to convoy defenders
			SetGlobalTrigger(4,true);
			PlayLancemateSound("VO\Generic\VehicleContact",1);
			PlayVoiceOverOnce("vo\eaton\mi1\NothingToHeavy",TALKER_SPE);
		endif;
	endif;
	
	if (not GetGlobalTrigger(5)) then
		if (GroupAllDead(GroupObjectID(23))) then		// Helos are destroyed
			SetGlobalTrigger(5,true);
			PlayVoiceOverOnce("vo\eaton\mi1\AirThreatNeutralized",TALKER_SPE);
		endif;
	endif;
	
	if (not GetGlobalTrigger(8)) then		// Tanks are incoming
		if (Relief) then
			if (IsWithin(PlayerMech,GroupObjectID(21),1100)) and (not GroupAllDead(GroupObjectID(21))) then
				SetGlobalTrigger(8,true);
				PlayVoiceOverOnce("vo\eaton\mi1\TakeoutTanksToo",TALKER_SPE);
				SetNavpoint(ena_Nav_Beta);
			endif;
		endif;
	endif;
	
	if (not GetGlobalTrigger(6)) then
		if (Relief) then		// Near Myrmidons of the relief force
			if (IsWithin(PlayerMech,GroupObjectID(21),800)) then
				SetGlobalTrigger(6,true);
				PlayVoiceOverOnce("vo\eaton\mi1\AttackMyrmidons",TALKER_SPE);
				PlayLancemateSound("VO\Generic\BestEffort",1);
			endif;
		endif;
	endif;
	
	if (not GetGlobalTrigger(7)) then
		if (timegreater(3,ReliefTime + 30)) then
			SetGlobalTrigger(7,true);
			//PlayVoiceOverOnce("vo\eaton\mi1\TakeoutTanksToo",TALKER_SPE);
		endif;
	endif;
	
// Triggers	- Harass Strike Relief

	if (not GetGlobalTrigger(3)) then		// It will take the convoy awhile to call in for support
		if (EnemyEngaged) then
			SetGlobalTrigger(3,true);
			StartTimer(3);		// Reinforcement timer
		endif;
	endif;
	
	if (not Harass) then	// Harassers come in
		if (timegreater(3,HarassTime)) and (not GroupAllDead(GroupObjectID(23))) then
			Harass = True;
			PlayVoiceOverOnce("vo\eaton\mi1\ChoppersETA",TALKER_CAS);
			PlayVoiceOverOnce("vo\eaton\mi1\LanceAirwatch",TALKER_SPE);
		endif;
	endif;
	
	if (not Relief) then	// Relief guys come in
		if (GroupNumDead(GroupObjectID(20))>ReinforceNum) then
			if (timegreater(3,ReliefTime)) then
				Relief = True;
			endif;
		endif;
	endif;
	
	if (not Relief) then	// If you are going the wrong way to the relief force they wake up
		if (IsWithin(PlayerMech,ena_torelief,150)) then
			Relief = True;
			Harass = True;
		endif;
	endif;

// Obj 1 - Stop the Eaton Convoy

	if (not obj1) then
		if (groupalldead(GroupObjectID(20))) then
			SuccessObjective(eob_objective1);
			PlayVoiceOverOnce("vo\eaton\mi1\VanguardDestroyed",TALKER_SPE);
			PlayVoiceOverOnce("VO\OneSecond",TALKER_SPE);
			if (not GroupAllDead(GroupObjectID(21))) then	// Make sure the tanks aren't dead
				PlayVoiceOverOnce("vo\eaton\mi1\SomeonesOutHere",TALKER_SPE);
				PlayVoiceOverOnce("VO\HalfSecond",TALKER_SPE);
				PlayVoiceOverOnce("vo\eaton\mi1\TakeoutTanksToo",TALKER_SPE);
			endif;
			Relief = True;
			obj1 = true;
			if (obj2) then
				StartTimer(2);		// End game timer
			endif;
		endif;
	endif;
	
	if (not Obj1Revealed) then
		if (GroupAllDead(GroupObjectID(10))) then		// Can't destroy the entire convoy
			Obj1Revealed = True;
			FailObjective(eob_objective1);
			if (not obj2) then
				FailObjective(eob_objective2);
			endif;
			Loser = True;
			StartTimer(2);		// You lost
		endif;
	endif;
	
// Obj 2 - Destroy all militia forces

	if (not obj2) then
		if (GroupAllDead(GroupObjectID(20))) then
			if (GroupAllDead(GroupObjectID(21))) and GroupAllDead(GroupObjectID(23)) then
				obj2 = true;
				SuccessObjective(eob_objective2);
				if (obj1) then
					HideObjective(eob_objective3);
					StartTimer(2);		// End game timer
				endif;
			endif;
		endif;
	endif;

// Victory and Loss conditions

	if (timegreater(2,5)) then
		  
		if (loser) then
			EndMission(false,5);
			trans lost;
		else
			// Salvage - FAB  7/30
				AddWeaponSalvage("AC10",3);
				AddWeaponSalvage("LightGauss",1);
				AddWeaponSalvage("LBXAC10",2);
				AddWeaponSalvage("PPC",2);
				AddWeaponSalvage("LargePulseLaser",3);
				AddWeaponSalvage("MediumPulseLaser",4);
			//

			SetCurrentInfamy(Infamy);
			SetCurrentNobility(Nobility);
			SetCurrentDavionFactionRating(DavionRating);
			SetCurrentSteinerFactionrating(SteinerRating);
			
			trans won;	
		endif;
		
	endif;

// Fail the mission when the player dies
	if (isdead(playermech)) then
		KillVoiceOvers;
		switch rand(1,3)
			case 1:
				PlayVoiceOver("VO\Generic\SpecterDestroyed", talker_CAS);
			endcase;
			case 2:
				PlayVoiceOver("VO\Generic\SpecterDestroyed2", talker_CAS);
			endcase;
			case 3:
				PlayVoiceOver("VO\Generic\SpecterDestroyed3", talker_CAS);
			endcase;
		endswitch;
		StartMusic("Music\GenericLoss1",3.0,3.5,70);
		endmission(false,14);
		trans deadstate;
	endif;

// Betty speaks when reaching Nav Point

	if (not alphareached) then
		if (isnavrevealed(ena_nav_alpha)) then
			if (iswithin(playermech,ena_nav_alpha,checkrange)) then
				playvoiceover("VO\generic\navalpha",talker_Bet);
				alphareached = true;
				//setnavpoint(ena_nav_beta);
			endif;
		endif;
	endif;

	if (not betareached) then
		if (isnavrevealed(ena_nav_beta)) then
			if (iswithin(playermech,ena_nav_beta,checkrange)) then
				playvoiceover("VO\generic\navbeta",talker_Bet);
				betareached = true;
				//setnavpoint(ena_nav_gamma);
			endif;
		endif;
	endif;
	
	if (not gammareached) then
		if (isnavrevealed(ena_nav_gamma)) then
			if (iswithin(playermech,ena_nav_gamma,checkrange)) then
				playvoiceover("VO\generic\navgamma",talker_Bet);
				gammareached = true;
				//setnavpoint(ena_nav_delta);
			endif;
		endif;
	endif;
	
endstate;

//-----------------------------------------
//HERE THERE BE CINEMAS
//-----------------------------------------

state opening;	  

	code

		if (cinemaskip) then
			KillVoiceOvers;	 
			cinema_cut = 0;	 
			KillTimer(gti_timer_21);
			SetGlobalTrigger(0,true);
			playerai(epl_player0,false);
			StartTimer(1);		// This is the mission start timer
			ResetTimer(1);
			CinemaEnd;		  
			SetCompositingEnabled(True);
			revealobjective(eob_objective1);
			RevealObjective(eob_objective2);
			trans sit;
		endif;
		
		switch (cinema_cut)
				
			case 0:	 

				SetActiveCamera(eca_CameraShip);
				
				SpinUnderCamera(PlayerMech,PlayerMech,16.0,17.0,true);

				PlayVoiceOverOnce("vo\eaton\mi1\Intro1",TALKER_SPE);
				PlayLancemateSound("VO\Generic\ReadyOp",3);
				PlayVoiceOverOnce("vo\eaton\mi1\Intro2",TALKER_CAS);
				
				cinema_cut = cinema_cut + 1;
			endcase;
			
			case 1:
				if (not IsVoiceOverPlaying) then
					ResetTimer(21);
					cinema_cut = cinema_cut + 1;
				endif;
			endcase;
			
			case 2:
				if (timegreater(21,2)) then
					KillVoiceOvers;
					cinema_cut = 0;
					KillTimer(gti_timer_21);
					SetGlobalTrigger(0,true);
					playerai(epl_player0,false);
					StartTimer(1);		// This is the mission start timer
					ResetTimer(1);
					CinemaEnd;
					revealobjective(eob_objective1);	// Reveal the objectives
					RevealObjective(eob_objective2);
					SetCompositingEnabled(True);
					
					trans sit;
				endif;
			endcase;
				
		endswitch;
	
endstate;

state won;	  

	code
		
		if (cinemaskip) then
			cinema_cut = 20;	
			playerai(epl_player0,false);
			KillVoiceOvers;
			Endmission(true,1); 
		endif;

		switch cinema_cut
		
			case 0:	
				if (not IsDead(eve_convoy01)) then
					Shutdown(eve_convoy01);
					GetLocation(ena_end1,mynav);
					Teleport(eve_convoy01,mynav);
					SetRotation(eve_convoy01,-10);
				endif;
				if (not IsDead(eve_convoy02)) then
					Shutdown(eve_convoy02);
					GetLocation(ena_end2,mynav);
					Teleport(eve_convoy02,mynav);
					SetRotation(eve_convoy02,5);
				endif;
				if (not IsDead(eve_convoy03)) then
					Shutdown(eve_convoy03);
					GetLocation(ena_end3,mynav);
					Teleport(eve_convoy03,mynav);
					SetRotation(eve_convoy03,10);
				endif;
				if (not IsDead(eve_convoy04)) then
					Shutdown(eve_convoy04);
					GetLocation(ena_end4,mynav);
					Teleport(eve_convoy04,mynav);
					SetRotation(eve_convoy04,10);
				endif;
				if (not IsDead(eve_convoy05)) then
					Shutdown(eve_convoy05);
					GetLocation(ena_end5,mynav);
					Teleport(eve_convoy05,mynav);
					SetRotation(eve_convoy05,0);
				endif;
				if (not IsDead(eve_convoy06)) then
					Shutdown(eve_convoy06);
					GetLocation(ena_end6,mynav);
					Teleport(eve_convoy06,mynav);
					SetRotation(eve_convoy01,5);
				endif;
				Shutdown(PlayerMech);
				GetLocation(ena_endp,mynav);
				Teleport(PlayerMech,mynav);
				SetRotation(PlayerMech,-90);
				Startup(PlayerMech);
				
				StartMusic("Music\GenericVictory1",3.0,3.5,50);					 
				PlayVoiceOverOnce("VO\ThreeSecond",TALKER_SPE);
				PlayVoiceOverOnce("vo\eaton\mi1\Close1",TALKER_SPE);
				PlayVoiceOverOnce("vo\eaton\mi1\Close2",TALKER_CAS);
				PlayVoiceOverOnce("vo\eaton\mi1\Close3",TALKER_SPE);
				PlayVoiceOverOnce("vo\eaton\mi1\Close4",TALKER_CAS);
				SetCompositingEnabled(False);
				CinemaStart;
				
				setactivecamera (eca_cameraship);
				FadeToBlack(0.0);
				StartTimer(21);
				
				playerai(epl_player0,true);
				Cinema_cut=Cinema_cut+1;
			endcase;
			
			case 1:
				if (timegreater(21,1)) then
					//HeadOnCamera(PlayerMech,PlayerMech,16.0,17.0,true);
					Targetfollowobject(PlayerMech);
					Camerafollowobject(eve_convoy01);
					Targetoffset(0.0,4.0,0.0,0.0,true);
					Cameraoffset(-10.0,10.0,-200.0,0.0,true);
					Cameraoffset(-40.0,10.0,40.0,22.0,true);
					FadeFromBlack(3.0);
					ResetTimer(21);
					Cinema_cut=Cinema_cut+1;
				endif;
			endcase;
			
			case 2:
				if (not IsVoiceOverPlaying) then
					StartTimer(21);
					ResetTimer(21);
					cinema_cut = cinema_cut +1;
				endif;
			endcase;			

			case 3:
				cinema_cut = 20;	
				playerai(epl_player0,false);
				Endmission(true,2); 
			endcase;
			
		endswitch;
	
endstate;

state lost;

		code
		
		switch cinema_cut
		
			case 0:	
			
			endcase;
			
			case 1:
			
			endcase;
			
		endswitch;
	
endstate;

endfsm.
//******************************************************************
