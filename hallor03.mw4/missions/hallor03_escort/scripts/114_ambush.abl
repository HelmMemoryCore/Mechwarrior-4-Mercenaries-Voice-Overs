//*********************************************************************************

fsm Ambush : integer;		

//------------------------------------------------------------------
//
//     Constant Definitions
//
//------------------------------------------------------------------
     const

		#include_ <content\ABLScripts\mwconst.abi>
//------------------------------------------------------------------
//
//     Type Definitions
//
//------------------------------------------------------------------
     type

		#include_ <content\ABLScripts\mwtype.abi>

//------------------------------------------------------------------
//
//     Variable Declarations
//
//------------------------------------------------------------------
	var
		static integer			attackRange;	// At what range do I start shooting?
		static integer			withdrawRange;	// At what range do I stop attacking?
		static integer			skillLevel;		// 1 - 10
		static integer			memInt9;		// value of Memory Integer number 9
		static Boolean			spotted;		// Has the player been seen
		static locpoint			leashPoint;


 function Init;
	code
		// script-specific variables
		attackRange				= 300;						// Set to 300 to draw the player in to the ambush
		withdrawRange			= 3000;						// Mech won't loose target 
		skillLevel				= 3;						// 1 - 10
		memInt9 				= GetMemoryInteger(me, 9);	// Value of memory integer number 9
		spotted					= false;					// has the ambush been triggered?
		GetLocation(me, leashPoint);

endfunction;
		
state StartState;

	code
		AssignSkill(skillLevel);
		SetCombatLeash(me, leashPoint, 300);
		
        trans sit; 	
endstate;			

state sit;
	code
		if FindEnemy(attackRange, FT_DEFAULT + FT_UNARMED) then
			trans attackState;
		endif;
		
		if ( isShot ( me ) ) then
			setTarget(me, whoShot(me) );
			trans AttackState;
		endif;
		
		if isdead(me) then
			trans deadState;
		endif;
		
		if ( not spotted ) then
			ShutDown(me);		//hide
		else;
			orderMoveLookout;
		endif;
		
endstate;

state AttackState;

	code
	
		if ( not spotted ) then
			startUp(me);
			attackRange = 800;	//reset attackRange to it's standard value
			spotted = true;
			stopConvoy = true;
		endif;
			
		if( leaveAttackState(withdrawrange) ) then
			orderStopAttacking;
			settarget(me, NO_UNIT);
			trans sit;
		else;
			OrderAttackTactic(TACTIC_PICK_BEST, true);
		endif;
		
		if isdead(me) then
			trans deadState;
		endif;
			
endstate;

state deadstate;
	code
	
		stopConvoy = false;
		
endstate;

endfsm.
