//*********************************************************************************

fsm Canton01_GriffonBase : integer;

//------------------------------------------------------------------
//
//	 Constant Definitions
//
//------------------------------------------------------------------
	 const

		#include_ <content\ABLScripts\mwconst.abi>
		#include_ <content\ABLScripts\Katie\katie_consts.abi>	// <-- mektek
//------------------------------------------------------------------
//
//	 Type Definitions
//
//------------------------------------------------------------------
	 type

		#include_ <content\ABLScripts\mwtype.abi>

//------------------------------------------------------------------
//
//	 Variable Declarations
//
//------------------------------------------------------------------
	  var

		eternal integer		playermech;			// This is the variable for the player's 'Mech
			
		// Standard Nav and Objective Variables
		static boolean		alphareached;		// Nav Alpha reached
		static boolean		betareached;		// Nav Beta reached
		static boolean		gammareached;		// Nav Gamma reached
		static boolean		deltareached;		// Nav Delta reached
												//
		static boolean		alpharevealed;		// Nav Alpha is shown
		static boolean		betarevealed;		// Nav Beta is shown
		static boolean		gammarevealed;		// Nav Gamma is shown
		static boolean		deltarevealed;		// Nav Delta is shown
		static integer		checkrange;			// How far from the Nav Point you have to be											
												//
		eternal boolean		obj1;				// This indicates whether Objective 1 was completed
		eternal boolean		obj2;				// This indicates whether Objective 2 was completed
		eternal boolean		obj3;				// This indicates whether Objective 3 was completed
		eternal boolean		obj4;				// This indicates whether Objective 4 was completed
		eternal boolean		obj5;				// This indicates whether Objective 5 was completed
												//
		static boolean		obj1revealed;		// This indicates that Objective 1 has been revealed
		static boolean		obj2revealed;		// This indicates that Objective 2 has been revealed
		static boolean		obj3revealed;		// This indicates that Objective 3 has been revealed
		static boolean		obj4revealed;		// This indicates that Objective 4 has been revealed
		static boolean		obj5revealed;		// This indicates that Objective 5 has been revealed
		//	
		
		static integer		cinema_cut;			// This variable keeps track of what scene a cinema is on.
		static locpoint		temploc;			// Used for commands that need objects converted to locpoint format
		static boolean		loser;				// You have lost the mission
 		eternal integer 	DavionRating;		// This is your Davion rating
		eternal integer 	SteinerRating;		// This is your Steiner rating
		eternal integer 	Nobility;			// This is your Nobility rating
		eternal integer 	Infamy;				// This is your Infamy rating
 
		// Trigger States
		eternal boolean		WaveTwo;			// This determines when Wave 2 takes off.
		eternal boolean		WaveThree;			// This determines when Wave 3 takes off.
		static integer		KuoHP;				// How well is Kuo doing
		static integer		PlayerHP;			// Player's HP
		eternal boolean		CeaseFire;			// Cease Fire - Capellans are giving up
		
//------------------------------------------------------------------
//
//	 Init Function	(automatically run first time through)
//
//------------------------------------------------------------------

function init;
	var
		integer i;

	code

			if (GetLancemate(1)<>NO_UNIT) then
				SetTargetDesirability(GetLancemate(1),50);
			endif;

			if (GetLancemate(2)<>NO_UNIT) then
				SetTargetDesirability(GetLancemate(2),60);
			endif;

			if (GetLancemate(3)<>NO_UNIT) then
				SetTargetDesirability(GetLancemate(3),70);
			endif;
			
			SetTargetDesirability(PlayerMech,15);

			playermech = getplayervehicle(epl_player0);

			// 10 - Showdown 'Mechs
			GroupAddObject(GroupObjectID(10),eve_dropfriend01);
			GroupAddObject(GroupObjectID(10),eve_dropfriend02);
			GroupAddObject(GroupObjectID(10),eve_dropfriend03);
			GroupAddObject(GroupObjectID(10),eve_dropfriend04);
			SetGroupAI(GroupObjectID(10),GROUPAI_MOODSQUAD);
			GetLocation(ena_baseguard_leash,temploc);
			SetCombatLeash(GroupObjectID(10),temploc,600);
			
			// 11 - Protect these buildings
			GroupAddObject(GroupObjectID(11),eve_talon);
			GroupAddObject(GroupObjectID(11),eve_hrothgar);
			
			// 20 - First Wave of Enemies
			GroupAddObject(GroupObjectID(20),eve_1wave01);
			GroupAddObject(GroupObjectID(20),eve_1wave02);
			GroupAddObject(GroupObjectID(20),eve_1wave03);
			GroupAddObject(GroupObjectID(20),eve_1wave04);
			GroupAddObject(GroupObjectID(20),eve_1wave05);
			GroupAddObject(GroupObjectID(20),eve_1wave06);
			SetGroupAI(GroupObjectID(20),GROUPAI_MOODSQUAD);

			// 21 - Second Wave of Enemies
			GroupAddObject(GroupObjectID(21),eve_2wave01);
			GroupAddObject(GroupObjectID(21),eve_2wave02);
			GroupAddObject(GroupObjectID(21),eve_2wave03);
			GroupAddObject(GroupObjectID(21),eve_2wave04);
			SetGroupAI(GroupObjectID(21),GROUPAI_MOODSQUAD);

			// 22 - Third Wave of Enemies
			GroupAddObject(GroupObjectID(22),eve_3wave01);
			GroupAddObject(GroupObjectID(22),eve_3wave02);
			GroupAddObject(GroupObjectID(22),eve_3wave03);
			GroupAddObject(GroupObjectID(22),eve_3wave04);
			GroupAddObject(GroupObjectID(22),eve_3wave05);
			GroupAddObject(GroupObjectID(22),eve_3wave06);
			GroupAddObject(GroupObjectID(22),eve_3wave07);
			GroupAddObject(GroupObjectID(22),eve_Wu_Kang_Kuo);
			SetGroupAI(GroupObjectID(22),GROUPAI_MOODSQUAD);
			
			// 23 - Demolisher Company
			GroupAddObject(GroupObjectID(23),eve_tank01);
			GroupAddObject(GroupObjectID(23),eve_tank02);
			GroupAddObject(GroupObjectID(23),eve_tank03);
			GroupAddObject(GroupObjectID(23),eve_tank04);
			GroupAddObject(GroupObjectID(23),eve_tank05);
			GroupAddObject(GroupObjectID(23),eve_tank06);
			GroupAddObject(GroupObjectID(23),eve_tank07);
			GroupAddObject(GroupObjectID(23),eve_tank08);
			GroupAddObject(GroupObjectID(23),eve_tank09);
			GroupAddObject(GroupObjectID(23),eve_tank10);
			GroupAddObject(GroupObjectID(23),eve_tank11);
			GroupAddObject(GroupObjectID(23),eve_tank12);
			
			cinema_cut = 0;

			// Culturals
			//SetTarget(ede_example2,eve_example3);
		
endfunction;
		#include_ <content\ABLScripts\Katie\katie_script.abi>	// <-- mektek
//------------------------------------------------------------------
//
//	 Main Code
//
//------------------------------------------------------------------
state startState;

	code	

		// These are your MetaMission variables, second part is how this mission effects your ratings
			DavionRating 	= GetPreviousDavionFactionRating;
			SteinerRating 	= GetPreviousSteinerFactionRating;
			Nobility 		= GetPreviousNobility;
			Infamy 			= GetPreviousInfamy;
			
			DavionRating	= DavionRating + 0;
			SteinerRating	= SteinerRating + 0;
			Nobility		= Nobility + 11;
			Infamy			= Infamy + 0;
		// 
		
		SetRadarNav(ena_Nav_Alpha,1);
		SetRadarNav(ena_Nav_Beta,2);
		SetRadarNav(ena_Nav_Gamma,3);
		RevealNavpoint(ena_Nav_Alpha);
		SetNavpoint(ena_Nav_Alpha);

		StartMusic("Music\SkitterSprint",3.0,3.5,50);

		//setsensorvisibility(eve_example4,false);
		
		checkrange	= 100;		// How far from a nav point before the Betty voice says you've arrived.
		
		Destroy(ebu_boom1);		// Damage allied 'Mechs at the beginning
		Destroy(ebu_boom2);
		
		PlayerHP	= GetHP(PlayerMech);
		
		trans opening;

endstate;

	
state deadState;
	code
	
endstate;
	
//------------------------------------------------------------------
//
//	 Sit state : description
//
//------------------------------------------------------------------
state sit;

	code

	if IsMusicPlaying == false then
		//StartMusic("VO\Rain1",0.1,0.1,300);
	endif;
	
// Sound Triggers

	if (not GetGlobalTrigger(1)) then
		if (timegreater(1,3)) then		// Some delay then dialogue
			SetGlobalTrigger(1,true);
			PlayVoiceOverOnce("VO\Canton\MI1\HarlocsRaidersDeployed",TALKER_SAN);
			PlayVoiceOverOnce("VO\Canton\MI1\HopeItsWorthFight",TALKER_SPE);
			PlayVoiceOverOnce("VO\Canton\MI1\86MillionCBills",TALKER_SAN);
			PlayVoiceOverOnce("VO\Canton\MI1\WorthIt",TALKER_SPE);
			PlayVoiceOverOnce("VO\ThreeSeconds",TALKER_SPE);
			PlayVoiceOverOnce("VO\Canton\MI1\FastAndHard",TALKER_SPE);
			if (PlayLanceMateSound("VO\Generic\BestEffort",1)) then
				PlayVoiceOverOnce("VO\Canton\MI1\NothingLess",TALKER_SPE);
			endif;
		endif;
	endif;
	
	if (not GetGlobalTrigger(2)) then
		if (PlayerHP <> GetHP(PlayerMech))	 then	// Engaging first group of enemies
			SetGlobalTrigger(2,true);
			PlayVoiceOverOnce("VO\Canton\MI1\EngageTwoHeavys",TALKER_SPE);
		endif;
	endif;
	
	if (not GetGlobalTrigger(3)) then
		if (GroupAllDead(GroupObjectID(20))) then	// Original 'Mechs killed
			SetGlobalTrigger(3,true);
			PlayVoiceOverOnce("VO\Canton\MI1\MechsAtAlphaElim",TALKER_SPE);
		endif;
	endif;
	
	if (not GetGlobalTrigger(4)) then
		if (GroupNumDead(GroupObjectID(20))>3) then		// One lance down at Alpha
			SetGlobalTrigger(4,true);
			PlayVoiceOverOnce("VO\Canton\MI1\HeavyLanceElim",TALKER_SPE);
		endif;
	endif;
	
	if (not GetGlobalTrigger(5)) then
		if (IsWithin(ena_nav_alpha,GroupObjectID(23),900)) then	// Castle warns about tanks
			SetGlobalTrigger(5,true);
			PlayVoiceOverOnce("VO\Canton\MI1\LargeForceMovingIn",TALKER_CAS);
		endif;
	endif;
	
	if (not GetGlobalTrigger(6)) then
		if (IsWithin(PlayerMech,GroupObjectID(23),800)) then	// Spectre says there are tanks
			SetGlobalTrigger(6,true);
			PlayVoiceOverOnce("VO\Canton\MI1\IHaveAVisual",TALKER_SPE);
		endif;
	endif;
	
	if (not GetGlobalTrigger(7)) then
		if (IsWithin(etu_eturret01,GroupObjectID(21),1000)) then	// Turrets spot assault lance
			SetGlobalTrigger(7,true);
			PlayVoiceOverOnce("VO\Canton\MI1\EnemyLanceAtRidge",TALKER_RID);
			PlayVoiceOverOnce("VO\Canton\MI1\UploadingEasternRidge",TALKER_CAS);
			RevealNavpoint(ena_Nav_Beta);
			if (GroupAllDead(GroupObjectID(23))) then
				PlayVoiceOverOnce("VO\Canton\MI1\AcknoledgeToBeta",TALKER_SPE);
			else
				PlayVoiceOverOnce("VO\Canton\MI1\AcknowledgeStillEngaged",TALKER_SPE);
			endif;
		endif;
	endif;
	
	if (not GetGlobalTrigger(8)) then
		if (WaveThree) then		// En route to Gamma
			if (IsWithin(PlayerMech,ena_nav_gamma,800)) then
				SetGlobalTrigger(8,true);
				PlayVoiceOverOnce("VO\Canton\MI1\MovingtoGamma",TALKER_SPE);
			endif;
		endif;
	endif;
	
	if (not GetGlobalTrigger(9)) then
		if (IsWithin(PlayerMech,eve_Wu_Kang_Kuo,1000)) then		// Capellan warns you
			SetGlobalTrigger(9,true);
			StartMusic("Music\Hunter",3.0,3.5,50);			
			PlayVoiceOverOnce("VO\Canton\MI1\DepartorDie",TALKER_KUO);
			PlayVoiceOverOnce("VO\Canton\MI1\DepartorDieRetort",TALKER_SPE);
			PlayLanceMateSound("VO\Generic\CapellanPrejudice",1);
			PlayVoiceOverOnce("VO\Canton\MI1\ThanksIllPass",TALKER_SPE);
			PlayVoiceOverOnce("VO\Canton\MI1\PrepareToDie",TALKER_KUO);
			KuoHp = GetHP(eve_Wu_Kang_Kuo);
		endif;
	endif;
	
	if (not GetGlobalTrigger(10)) then
		if (GetGlobalTrigger(9)) then
			if (GetHP(eve_Wu_Kang_Kuo) <> KuoHP) then		// Once Kuo is damaged
				SetGlobalTrigger(10,true);
				PlayVoiceOverOnce("VO\Canton\MI1\BetterPunchOutKuo",TALKER_SPE);
				PlayVoiceOverOnce("VO\Canton\MI1\InsolentWretch",TALKER_KUO);
				PlayVoiceOverOnce("VO\Canton\MI1\BestShot",TALKER_SPE);
			endif;
		endif;
	endif;
	
	if (not GetGlobalTrigger(11)) then
		if (IsDead(eve_Wu_Kang_Kuo)) then	// Have you killed Kuo yet
			SetGlobalTrigger(11,true);
			PlayVoiceOverOnce("VO\Canton\MI1\WuKangIsDead",TALKER_SPE);
		endif;
	endif;
	
	if (not GetGlobalTrigger(14)) then
		if (GroupAllDead(GroupObjectID(22))) then	// You've killed everyone
			SetGlobalTrigger(14,true);
			PlayVoiceOverOnce("VO\Canton\MI1\TheEndOfThem",TALKER_SPE);
		endif;
	endif;

// Donegal 03 Specific Triggers

	if (not WaveTwo) then
		if (GroupNumDead(GroupObjectID(10)) + GroupNumDead(GroupObjectID(20)) > 3) then
			if (timegreater(1,80)) then
				PlayVoiceOverOnce("VO\Canton\MI1\MoreMechsApproaching",TALKER_CAS);
				WaveTwo	= True;
			endif;
		else
			if (IsWithin(PlayerMech,ena_2wavetele,1300)) then
				PlayVoiceOverOnce("VO\Canton\MI1\MoreMechsApproaching",TALKER_CAS);
				WaveTwo = True;
			endif;
		endif;
	endif;

	if (not WaveThree) then
		if (GroupNumDead(GroupObjectID(10)) + GroupNumDead(GroupObjectID(20)) + GroupNumDead(GroupObjectID(21)) > 9) then
			if (timegreater(1,130)) then
				PlayVoiceOverOnce("VO\Canton\MI1\FinalPush",TALKER_SAN);
				PlayVoiceOverOnce("VO\Canton\MI1\PullBack",TALKER_SPE);
				PlayVoiceOverOnce("VO\Canton\MI1\ReturningToBase",TALKER_SAN);
				PlayVoiceOverOnce("VO\Canton\MI1\InterceptAtGamme",TALKER_CAS);
				RevealNavpoint(ena_Nav_Gamma);
				SetNavpoint(ena_Nav_Gamma);
				WaveThree	= True;
			endif;
		else
			if (IsWithin(PlayerMech,ena_3wavetele,1300)) then
				PlayVoiceOverOnce("VO\Canton\MI1\FinalPush",TALKER_SAN);
				PlayVoiceOverOnce("VO\Canton\MI1\PullBack",TALKER_SPE);
				PlayVoiceOverOnce("VO\Canton\MI1\ReturningToBase",TALKER_SAN);
				PlayVoiceOverOnce("VO\Canton\MI1\InterceptAtGamme",TALKER_CAS);
				RevealNavpoint(ena_Nav_Gamma);
				SetNavpoint(ena_Nav_Gamma);
				WaveThree = True;
			endif;
		endif;
	endif;
	
	if (not GetGlobalTrigger(12)) then
		if (GroupNumDead(GroupObjectID(22)) > 4) then		// Hurt us bad and we'll give up
			if (IsDead(eve_Wu_Kang_Kuo)) then
				SetGlobalTrigger(12,true);
				CeaseFire = true;
				StartTimer(3);		// If no one responds with shooting you'll win
				SetTargetDesirability(eve_3wave01,-1);
				SetTargetDesirability(eve_3wave02,-1);
				SetTargetDesirability(eve_3wave03,-1);
				SetTargetDesirability(eve_3wave04,-1);
				SetTargetDesirability(eve_3wave05,-1);
				SetTargetDesirability(eve_3wave06,-1);
				SetTargetDesirability(eve_3wave07,-1);
				if (not GroupAllDead(GroupObjectID(22))) then
					PlayVoiceOverOnce("VO\Canton\MI1\WeConcede",TALKER_CHO);
					PlayVoiceOverOnce("VO\Canton\MI1\CeaseFire",TALKER_CHO);
				endif;
			endif;
		endif;
	endif;
	
	if (not GetGlobalTrigger(13)) then		// Shoot us during a truce, and we'll fight back
		if (not CeaseFire) then
			if (timegreater(3,5)) then
				SetGlobalTrigger(13,true);
				SetTargetDesirability(eve_3wave01,25);
				SetTargetDesirability(eve_3wave02,25);
				SetTargetDesirability(eve_3wave03,25);
				SetTargetDesirability(eve_3wave04,25);
				SetTargetDesirability(eve_3wave05,25);
				SetTargetDesirability(eve_3wave06,25);
				SetTargetDesirability(eve_3wave07,25);
				PlayVoiceOverOnce("VO\Canton\MI1\NotSoFast",TALKER_SPE);
				PlayVoiceOverOnce("VO\OneSecond",TALKER_SPE);
				PlayVoiceOverOnce("VO\Canton\MI1\ReturnFire",TALKER_CHO);
			endif;
		endif;
	endif;
	
	if (not GetGlobalTrigger(15)) then		// Wait too long and the truce stands
		if (CeaseFire) then
			if (timegreater(3,30)) then
				SetGlobalTrigger(15,true);
				KillTimer(3);
				StartTimer(2);		// You win! :)
				FailObjective(eob_objective2);		// Optional objective failed
			endif;
		endif;
	endif;

// Obj 1 - Repel invaders
	if (not Obj1) then
		if (GetGlobalTrigger(12)) then
			obj1 = true;
			SuccessObjective(eob_objective1);
		endif;
	endif;
	
	if (obj1revealed) then
		if (obj1) then
			if (CeaseFire) then
				if (timegreater(3,15)) then
					Obj1Revealed = true;
					FailObjective(eob_objective2);
					StartTimer(2);		// You win!
				endif;
			endif;
		endif;
	endif;

// Obj 2 - Destroy all Enemies

	if (not Obj2) then
		if (GroupAllDead(GroupObjectID(20))) and (GroupAllDead(GroupObjectID(21))) and (GroupAllDead(GroupObjectID(22))) then
			Obj2 = true;
			SuccessObjective(eob_objective2);
			StartTimer(2);		// You win!
		endif;
	endif;
	
// Victory and Loss conditions
													   
	if (timegreater(2,5)) then
		  
		// Salvage Added by WHO on DATE
		//
		
		SetCurrentInfamy(Infamy);
		SetCurrentNobility(Nobility);
		SetCurrentDavionFactionRating(DavionRating);
		SetCurrentSteinerFactionrating(SteinerRating);
		/////////////////////
		// MISSION SALVAGE //
		/////////////////////
		Katie(eve_1wave01,60);
		Katie(eve_1wave02,50);
		Katie(eve_1wave03,40);
		Katie(eve_1wave04,30);
		Katie(eve_1wave05,20);
		Katie(eve_1wave06,20);

		Katie(eve_2wave01,60);
		Katie(eve_2wave02,50);
		Katie(eve_2wave03,40);
		Katie(eve_2wave04,40);

		Katie(eve_Wu_Kang_Kuo,80);
		Katie(eve_3wave01,70);
		Katie(eve_3wave02,60);
		Katie(eve_3wave03,50);
		Katie(eve_3wave04,40);
		Katie(eve_3wave05,30);
		Katie(eve_3wave06,20);
		Katie(eve_3wave07,20);
		
		if (loser) then
			EndMission(false,5);
			trans lost;
		else
			trans won;	
		endif;
		
	endif;

// Fail the mission when the player dies
	if (isdead(playermech)) then
		KillVoiceOvers;
		switch rand(1,3)
			case 1:
				PlayVoiceOver("VO\Generic\SpecterDestroyed", talker_CAS);
			endcase;
			case 2:
				PlayVoiceOver("VO\Generic\SpecterDestroyed2", talker_CAS);
			endcase;
			case 3:
				PlayVoiceOver("VO\Generic\SpecterDestroyed3", talker_CAS);
			endcase;
		endswitch;
		StartMusic("Music\GenericLoss1",3.0,3.5,70);
		endmission(false,14);
		trans deadstate;
	endif;

// Betty speaks when reaching Nav Point

	if (not alphareached) then
		if (isnavrevealed(ena_nav_alpha)) then
			if (iswithin(playermech,ena_nav_alpha,checkrange)) then
				playvoiceover("VO\generic\navalpha",talker_Bet);
				alphareached = true;
				//SetNavpoint(ena_Nav_Beta);
			endif;
		endif;
	endif;

	if (not betareached) then
		if (isnavrevealed(ena_nav_beta)) then
			if (iswithin(playermech,ena_nav_beta,checkrange)) then
				playvoiceover("VO\generic\navbeta",talker_Bet);
				betareached = true;
				//SetNavpoint(ena_Nav_Gamma);
			endif;
		endif;
	endif;

endstate;

//-----------------------------------------
//HERE THERE BE CINEMAS
//-----------------------------------------

state opening;	  

	code

		if (cinemaskip) then
			if (timegreater(21,2)) then
				KillVoiceOvers;	 
				cinema_cut = 0;	 
				KillTimer(gti_timer_21);
				SetGlobalTrigger(0,true);
				playerai(epl_player0,false);
				StartTimer(1);		// This is the mission start timer
				ResetTimer(1);
				CinemaEnd;		  
				SetCompositingEnabled(True);
				revealobjective(eob_objective1);
				RevealObjective(eob_objective2);
				trans sit;
			endif;
		endif;
		
		switch (cinema_cut)

			case 0:
				CinemaStart;
				SetActiveCamera(eca_CameraShip_0000);
				FadeToBlack(0.0);

				StartTimer(21);		// Cinema Timer
				//Targetfollowobject(playermech);
				//Camerafollowobject(playermech);
				Targetoffset(0.0,4.0,0.0,0.0,false);
				Cameraoffset(0.0,0.0,0.0,0.0,false);
				Cameraoffset(0.0,0.0,0.0,14.0,false);
				
				cinema_cut = cinema_cut + 1;
			endcase;
				
			case 1:
				if (timegreater(21,1)) then
					SetActiveCamera(eca_CameraShip);
					//FadeFromBlack(3.0);
					
					SpinOverCamera(PlayerMech,PlayerMech,17.0,17.0,true);	// Time was 17
					PlayVoiceOverOnce("VO\Canton\MI1\intro1",TALKER_SPE);
					PlayVoiceOverOnce("VO\Canton\MI1\intro2",TALKER_SAN);
					PlayVoiceOverOnce("VO\Canton\MI1\intro3",TALKER_SPE);
					
					cinema_cut = cinema_cut + 1;
					
					Drop(playermech);
					
					if (GetLancemate(1)<>NO_UNIT) then
						Drop(GetLancemate(1));
					endif;
		
					if (GetLancemate(2)<>NO_UNIT) then
						Drop(GetLancemate(2));
					endif;
		
					if (GetLancemate(3)<>NO_UNIT) then
						Drop(GetLancemate(3));
					endif;

					if (GetSecondaryLancemate(1)<>NO_UNIT) then
						Drop(GetSecondaryLancemate(1));
					endif;

					if (GetSecondaryLancemate(2)<>NO_UNIT) then
						Drop(GetSecondaryLancemate(2));
					endif;

					if (GetSecondaryLancemate(3)<>NO_UNIT) then
						Drop(GetSecondaryLancemate(3));
					endif;

					if (GetSecondaryLancemate(4)<>NO_UNIT) then
						Drop(GetSecondaryLancemate(4));
					endif;
					
				endif;
			endcase;
			
			case 2:
				if (not IsVoiceOverPlaying) then
					StartTimer(21);
					ResetTimer(21);
					cinema_cut = cinema_cut + 1;
				endif;
			endcase;
			
			case 3:
				if (timegreater(21,2)) then
					KillVoiceOvers;
					cinema_cut = 0;
					KillTimer(gti_timer_21);
					SetGlobalTrigger(0,true);
					playerai(epl_player0,false);
					StartTimer(1);		// This is the mission start timer
					ResetTimer(1);
					CinemaEnd;
					revealobjective(eob_objective1);	// Reveal the objectives
					RevealObjective(eob_objective2);
					SetCompositingEnabled(True);
					
					trans sit;
				endif;
			endcase;
				
		endswitch;
	
endstate;

state won;	  

	code
		
		if (cinemaskip) then
			cinema_cut = 20;	
			playerai(epl_player0,false);
			KillVoiceOvers;
			Endmission(true,1); 
		endif;

		switch cinema_cut
		
			case 0:	
				StartMusic("Music\GenericVictory1",3.0,3.5,50);					 
				if (not GetGlobalTrigger(14)) then
					PlayVoiceOverOnce("VO\Canton\MI1\close1",TALKER_CAS);
				else
					PlayVoiceOverOnce("VO\Canton\MI1\close2",TALKER_CAS);
				endif;
				PlayVoiceOverOnce("VO\Canton\MI1\close3",TALKER_CAS);
				PlayVoiceOverOnce("VO\Canton\MI1\close4",TALKER_SPE);
				PlayVoiceOverOnce("VO\Canton\MI1\close5",TALKER_CAS);
				PlayVoiceOverOnce("VO\Canton\MI1\close6",TALKER_SPE);
				SetCompositingEnabled(False);
				CinemaStart;
				setactivecamera (eca_cameraship);
				Targetfollowobject(playermech);
				Camerafollowobject(playermech);
				Targetoffset(0.0,4.0,0.0,0.0,false);
				Cameraoffset(-30.0,125.0,75.0,0.0,false);
				Cameraoffset(60.0,20.0,25.0,14.0,false);
				playerai(epl_player0,true);
				Cinema_cut=Cinema_cut+1;
			endcase;
			
			case 1:
				if (not IsVoiceOverPlaying) then
					cinema_cut = 20;	
					Endmission(true,2); 
				endif;
			endcase;			
			
		endswitch;
	
endstate;

state lost;

		code
		
		switch cinema_cut
		
			case 0:	
			
			endcase;
			
			case 1:
			
			endcase;
			
		endswitch;
	
endstate;

endfsm.
//******************************************************************
