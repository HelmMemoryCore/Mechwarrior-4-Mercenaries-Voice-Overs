//*********************************************************************************

fsm Eaton02_GovernorsMansion : integer;

//------------------------------------------------------------------
//
//	 Constant Definitions
//
//------------------------------------------------------------------
	 const

		#include_ <content\ABLScripts\mwconst.abi>
		#include_ <content\ABLScripts\Katie\katie_consts.abi> // <-- mektek
//------------------------------------------------------------------
//
//	 Type Definitions
//
//------------------------------------------------------------------
	 type

		#include_ <content\ABLScripts\mwtype.abi>

//------------------------------------------------------------------
//
//	 Variable Declarations
//
//------------------------------------------------------------------
	  var

		eternal integer		playermech;			// This is the variable for the player's 'Mech
			
		// Standard Nav and Objective Variables
		static boolean		alphareached;		// Nav Alpha reached
		static boolean		betareached;		// Nav Beta reached
		static boolean		gammareached;		// Nav Gamma reached
		static boolean		deltareached;		// Nav Delta reached
												//
		static boolean		alpharevealed;		// Nav Alpha is shown
		static boolean		betarevealed;		// Nav Beta is shown
		static boolean		gammarevealed;		// Nav Gamma is shown
		static boolean		deltarevealed;		// Nav Delta is shown
		static integer		checkrange;			// How far from the Nav Point you have to be											
												//
		eternal boolean		obj1;				// This indicates whether Objective 1 was completed
		eternal boolean		obj2;				// This indicates whether Objective 2 was completed
		eternal boolean		obj3;				// This indicates whether Objective 3 was completed
		eternal boolean		obj4;				// This indicates whether Objective 4 was completed
		eternal boolean		obj5;				// This indicates whether Objective 5 was completed
												//
		static boolean		obj1revealed;		// This indicates that Objective 1 has been revealed
		static boolean		obj2revealed;		// This indicates that Objective 2 has been revealed
		static boolean		obj3revealed;		// This indicates that Objective 3 has been revealed
		eternal boolean		obj4revealed;		// This indicates that Objective 4 has been revealed
		eternal boolean		obj5revealed;		// This indicates that Objective 5 has been revealed
		//	
		
		static integer		cinema_cut;			// This variable keeps track of what scene a cinema is on.
		static locpoint		temploc;			// Used for commands that need objects converted to locpoint format
		static boolean		loser;				// You have lost the mission
 		eternal integer 	DavionRating;		// This is your Davion rating
		eternal integer 	SteinerRating;		// This is your Steiner rating
		eternal integer 	Nobility;			// This is your Nobility rating
		eternal integer 	Infamy;				// This is your Infamy rating

		// Trigger States
		eternal boolean		DropshipPower;		// The dropship powerup toggle
		eternal boolean		BlackOut;			// When true then the ECCM is no longer taking out sensors and communications
		static integer		GiveorTake;			// Makes it so that the ECCM is triggered with some degree of randomness
		eternal boolean		ReinforceNow;		// Reinforcements come on the map
		eternal boolean		SecondWave;			// This is when the second wave shows up
		eternal boolean		ThirdWave;			// This is when the third wave shows up
		
//------------------------------------------------------------------
//
//	 Init Function	(automatically run first time through)
//
//------------------------------------------------------------------

function init;
	var
		integer i;

	code

			playermech = getplayervehicle(epl_player0);
			SetTargetDesirability(PlayerMech,5);
			
			if (GetLancemate(1)<>NO_UNIT) then
				SetTargetDesirability(GetLancemate(1),25);
			endif;

			if (GetLancemate(2)<>NO_UNIT) then
				SetTargetDesirability(GetLancemate(2),25);
			endif;
			
			if (GetLancemate(3)<>NO_UNIT) then
				SetTargetDesirability(GetLancemate(3),25);
			endif;
			
			// 10 - Governor's Mansion
			GroupAddObject(GroupObjectID(10),ebu_Governor_Mansion);
			SetTargetDesirability(ebu_Governor_Mansion,25);
			SetGroupAI(GroupObjectID(10),GROUPAI_MOODSQUAD);

			// 11 - Governor's Mansion
			GroupAddObject(GroupObjectID(11),ebu_Mansion_Center);
			GroupAddObject(GroupObjectID(11),ebu_Mansion_Center_0000);
			GroupAddObject(GroupObjectID(11),ebu_Mansion_Center_0001);
			SetTargetDesirability(ebu_Mansion_Center,25);
			SetTargetDesirability(ebu_Mansion_Center_0000,25);
			SetTargetDesirability(ebu_Mansion_Center_0001,25);
			SetGroupAI(GroupObjectID(11),GROUPAI_MOODSQUAD);

			// 20 - Hover Vehicles
			GroupAddObject(GroupObjectID(20),eve_hover01);
			GroupAddObject(GroupObjectID(20),eve_hover02);
			GroupAddObject(GroupObjectID(20),eve_hover03);
			GroupAddObject(GroupObjectID(20),eve_hover04);
			GroupAddObject(GroupObjectID(20),eve_hover05);
			GroupAddObject(GroupObjectID(20),eve_hover06);
			GroupAddObject(GroupObjectID(20),eve_hover07);
			SetGroupAI(GroupObjectID(20),GROUPAI_MOODSQUAD);

			// 21 - First Wave of 'Mechs
			GroupAddObject(GroupObjectID(21),eve_1wave01);
			GroupAddObject(GroupObjectID(21),eve_1wave02);
			GroupAddObject(GroupObjectID(21),eve_1wave03);
			GroupAddObject(GroupObjectID(21),eve_1wave04);
			SetGroupAI(GroupObjectID(21),GROUPAI_MOODSQUAD);

			// 22 - Second Wave of Vehicles
			GroupAddObject(GroupObjectID(22),eve_2wave01);
			GroupAddObject(GroupObjectID(22),eve_2wave02);
			GroupAddObject(GroupObjectID(22),eve_2wave03);
			GroupAddObject(GroupObjectID(22),eve_2wave04);
			//GroupAddObject(GroupObjectID(22),eve_2wave05);
			//GroupAddObject(GroupObjectID(22),eve_2wave06);
			//GroupAddObject(GroupObjectID(22),eve_2wave07);
			//GroupAddObject(GroupObjectID(22),eve_2wave08);
			//GroupAddObject(GroupObjectID(22),eve_2wave09);
			SetGroupAI(GroupObjectID(22),GROUPAI_MOODSQUAD);
			
			// 23 - Third Wave of 'Mechs
			GroupAddObject(GroupObjectID(23),eve_3wave01);
			GroupAddObject(GroupObjectID(23),eve_3wave02);
			GroupAddObject(GroupObjectID(23),eve_3wave03);
			GroupAddObject(GroupObjectID(23),eve_3wave04);
			SetGroupAI(GroupObjectID(23),GROUPAI_MOODSQUAD);
			
			cinema_cut = 0;

			// Culturals
			//SetTarget(ede_example2,eve_example3);
		
endfunction;

	#include_ <content\ABLScripts\Katie\katie_script.abi> // <-- mektek
//------------------------------------------------------------------
//
//	 Main Code
//
//------------------------------------------------------------------
state startState;

	code	
		// Salvage moved for 8-15
/*	MekTek: Shark removed these and replaced them with Katie calls
			SetKillSalvageChance(eve_3wave01,100);		// Uziel
			SetKillSalvageChance(eve_1wave01,50);		// Wolfhound
			SetKillSalvageChance(eve_1wave02,50);		// Osiris
			SetKillSalvageChance(eve_1wave03,50);		// Raven
			SetKillSalvageChance(eve_1wave04,50);		// Flea
*/
		// These are your MetaMission variables, second part is how this mission effects your ratings
			DavionRating 	= GetPreviousDavionFactionRating;
			SteinerRating 	= GetPreviousSteinerFactionRating;
			Nobility 		= GetPreviousNobility;
			Infamy 			= GetPreviousInfamy;
			
			DavionRating	= DavionRating + 5;
			SteinerRating	= SteinerRating - 2;
			Nobility		= Nobility + 7;
			Infamy			= Infamy + 0;
		// 
		
		SetRadarNav(ena_Nav_Alpha,1);
		SetRadarNav(ena_Nav_Beta,2);
		SetRadarNav(ena_Southern_Mansion,3);
		SetRadarNav(ena_Northern_Mansion,4);
		RevealNavpoint(ena_Nav_Alpha);
		RevealNavpoint(ena_Nav_Beta);
		RevealNavpoint(ena_Southern_Mansion);
		RevealNavpoint(ena_Northern_Mansion);
		SetNavpoint(ena_Nav_Alpha);

		//setsensorvisibility(eve_example4,false);
		
		// Mansions are good targets for the enemy
		SetTargetDesirability(ebu_governor_mansion,50);
		SetTargetDesirability(ebu_mansion_center_0000,50);
		SetTargetDesirability(ebu_mansion_center,50);
		
		checkrange	= 150;		// How far from a nav point before the Betty voice says you've arrived.

		StartMusic("Music\Rally",3.0,3.5,50);
		StartTimer(3);		// Switch targets
		
		trans opening;
		//trans won;

endstate;

	
state deadState;
	code
	
endstate;
	
//------------------------------------------------------------------
//
//	 Sit state : description
//
//------------------------------------------------------------------
state sit;

	code

	if IsMusicPlaying == false then
		StartMusic("music\LavaAmbLoop",0.0,3.0,40);
	endif;

// Sound Triggers

	if (timegreater(3,16)) then
		ResetTimer(3);
	endif;
	
	if (not SecondWave) then		// Second Lance incoming
		if (GroupNumDead(GroupObjectID(21)) > 0) then
			SecondWave = True;
			SetTargetDesirability(ebu_mansion_center_0000,-1);
			StartTimer(4);		// Third Lance timer
		endif;
	endif;

	if (not Obj4Revealed) then		// Once the first lance is destroyed you get a message
		if (GroupAllDead(GroupObjectID(21))) then
			Obj4Revealed = True;
			PlayVoiceOverOnce("vo\eaton\mi2\EnemyLanceNeutralized",TALKER_SPE);
			PlayVoiceOverOnce("vo\eaton\mi2\SecondLanceInbound",TALKER_CAS);
		endif;
	endif;
	
	if (not ThirdWave) then
		if (GroupAllDead(GroupObjectID(22))) then
			ThirdWave = True;
			StartTimer(5);		// Message timer
		else
			if (timegreater(4,40)) then
				if (GroupNumDead(GroupObjectID(22)) > 0) then
					ThirdWave = True;
					StartTimer(5);		// Message timer
				endif;
			endif;
		endif;
	endif;

// Eaton 2 Specific Triggers

	if (not GetGlobalTrigger(1)) then		// Hover craft close to player
		if (IsWithin(GroupObjectID(20),PlayerMech,900)) then
			SetGlobalTrigger(1,true);
			if (PlayLanceMateSound("vo\generic\Contact900",1)) then
				PlayLanceMateSound("vo\generic\HoverContact",1);
				PlayVoiceOverOnce("vo\eaton\mi2\IntroduceOurselves",TALKER_SPE);
			else
				PlayVoiceOver("VO\generic\MultipleContacts2",TALKER_CAS);
				PlayVoiceOverOnce("vo\eaton\mi2\IntroduceOurselves",TALKER_SPE);
			endif;
		endif;
	endif;
	
	if (not GetGlobalTrigger(2)) then		// Hovercraft are dead
		if (GroupAllDead(GroupObjectID(20))) then
			SetGlobalTrigger(2,true);
			PlayVoiceOverOnce("vo\eaton\mi2\HoverDestroyed",TALKER_SPE);
		endif;
	endif;
	
	if (not GetGlobalTrigger(3)) then		// Once the first wave gets near the ne mansion
		if (IsWithin(GroupObjectID(21),ena_nemansion,1000)) then
			SetGlobalTrigger(3,true);
			PlayVoiceOverOnce("vo\eaton\mi2\EnemyatNEMansion",TALKER_CAS);
		endif;
	endif;
	
	if (not GetGlobalTrigger(11)) then		// Once the player goes up the river
		if (IsWithin(PlayerMech,ena_nemansion,800)) then
			if (GetGlobalTrigger(3)) then
				SetGlobalTrigger(11,true);
				PlayVoiceOverOnce("vo\eaton\mi2\EngagingMechs",TALKER_SPE);
			endif;
		endif;
	endif;
	
	if (not GetGlobalTrigger(4)) then		// Lancemates will chirp in when they've killed someone
		if (IsDead(eve_1wave02)) then
			if (GroupNumDead(GroupObjectID(21)) < 4) then
				if (WhoDestroyed(eve_1wave02)==PlayerMech) then
					PlayLanceMateSound("VO\Generic\NiceShot",1)
				else
					PlayLanceMateSound("VO\Generic\KillBrag",1)
				endif;
				SetGlobalTrigger(4,true);
			else
				SetGlobalTrigger(4,true);
			endif;
		endif;
	endif;
	
	if (not GetGlobalTrigger(6)) then		// Spectre spots second lance
		if (IsWithin(GroupObjectID(22),PlayerMech,900)) then
			if (CanSee(PlayerMech,eve_2wave01)) then
				SetGlobalTrigger(6,true);
				PlayVoiceOverOnce("vo\eaton\mi2\ConfirmVisual",TALKER_SPE);
			else
				if (CanSee(PlayerMech,eve_2wave02)) then
					SetGlobalTrigger(6,true);
					PlayVoiceOverOnce("vo\eaton\mi2\ConfirmVisual",TALKER_SPE);
				endif;			
			endif;
		endif;
	endif;
	
	if (not GetGlobalTrigger(7)) then		// Killed the second lance
		if (GroupAllDead(GroupObjectID(22))) then
			SetGlobalTrigger(7,true);
			PlayVoiceOverOnce("vo\eaton\mi2\SecondLanceDestroyed",TALKER_SPE);
		endif;
	endif;
	
	if (not GetGlobalTrigger(8)) then		// Castles spots the third lance
		if (timegreater(5,5)) then		// After the third lance is en route
			SetGlobalTrigger(8,true);
			PlayVoiceOverOnce("vo\eaton\mi2\ThirdLance",TALKER_CAS);
			PlayVoiceOverOnce("vo\eaton\mi2\FinishDance",TALKER_SPE);
			StartMusic("Music\Rescue",3.0,3.5,50);
		endif;
	endif;
	
	if (not GetGlobalTrigger(9)) then		// Spectre third lance
		if (IsWithin(PlayerMech,GroupObjectID(23),800)) then
			SetGlobalTrigger(9,true);
			PlayVoiceOverOnce("vo\eaton\mi2\MixedLance",TALKER_SPE);
		endif;
	endif;
	
	if (not GetGlobalTrigger(10)) then		// Third Lance is toast
		if (GroupAllDead(GroupObjectID(23))) then
			SetGlobalTrigger(10,true);
			PlayVoiceOverOnce("vo\eaton\mi2\ThirdLanceHistory",TALKER_SPE);
		endif;
	endif;

// Obj 1 - Defend the Governor's Mansion

	if (not Obj1) then
		if (GroupAllDead(GroupObjectID(10))) then
			FailObjective(eob_objective1);
			FailObjective(eob_objective2);
			FailObjective(eob_objective3);
			Obj1 = True;
			StartTimer(2);		// You've lost
			Loser = True;
		endif;
	endif;

// Obj 2 - Defend all enemy 'Mechs

	if (not Obj2) then
		if (GroupAllDead(GroupObjectID(20))) and (GroupAllDead(GroupObjectID(21))) then
			if (GroupAllDead(GroupObjectID(23))) then
				if (not IsWithin(PlayerMech,GroupObjectID(22),900)) then
					Obj2 = True;
					SuccessObjective(eob_objective1);
					SuccessObjective(eob_objective2);
					if (not Obj3) then
						SuccessObjective(eob_objective3);
						AddMissionMoneyToBalance(500000);
					endif;
					StartTimer(2);		// You're a winner!					
				endif;
			endif;
		endif;
	endif;

// Obj 3 (Bonus) - Protect all mansions

	if (not Obj3) then
		if (GroupNumDead(GroupObjectID(11))>0) then
			Obj3 = True;
			PlayVoiceOverOnce("vo\eaton\mi2\DestroyedMansion",TALKER_CAS);
			FailObjective(eob_objective3);
		endif;
	endif;

// Victory and Loss conditions

	if (timegreater(2,5)) then
		  
		// Salvage - FAB  7/30
		//

		SetCurrentInfamy(Infamy);
		SetCurrentNobility(Nobility);
		SetCurrentDavionFactionRating(DavionRating);
		SetCurrentSteinerFactionrating(SteinerRating);

		AddMissionMoneyToBalance(2000000);
		Katie(eve_1wave01,60);
		Katie(eve_1wave02,80);
		Katie(eve_1wave03,50);
		Katie(eve_1wave04,40);

		Katie(eve_3wave01,70);
		Katie(eve_3wave02,50);
		Katie(eve_3wave03,40);
		Katie(eve_3wave04,30);			
		
		if (loser) then
			EndMission(false,5);
			trans lost;
		else
			trans won;	
		endif;
		
	endif;

// Fail the mission when the player dies
	if (isdead(playermech)) then
		KillVoiceOvers;
		switch rand(1,3)
			case 1:
				PlayVoiceOver("VO\Generic\SpecterDestroyed", talker_CAS);
			endcase;
			case 2:
				PlayVoiceOver("VO\Generic\SpecterDestroyed2", talker_CAS);
			endcase;
			case 3:
				PlayVoiceOver("VO\Generic\SpecterDestroyed3", talker_CAS);
			endcase;
		endswitch;
		StartMusic("Music\GenericLoss1",3.0,3.5,70);
		endmission(false,14);
		trans deadstate;
	endif;

// Betty speaks when reaching Nav Point

	if (not alphareached) then
		if (isnavrevealed(ena_nav_alpha)) then
			if (iswithin(playermech,ena_nav_alpha,checkrange)) then
				playvoiceover("VO\generic\navalpha",talker_Bet);
				alphareached = true;
				SetNavpoint(ena_Nav_Beta);
			endif;
		endif;
	endif;

	if (not Betareached) then
		if (isnavrevealed(ena_nav_Beta)) then
			if (iswithin(playermech,ena_nav_Beta,checkrange)) then
				playvoiceover("VO\generic\navBeta",talker_Bet);
				Betareached = true;
				//SetNavpoint(ena_Nav_Beta);
			endif;
		endif;
	endif;

endstate;

//-----------------------------------------
//HERE THERE BE CINEMAS
//-----------------------------------------

state opening;	  

	code

		if (cinemaskip) then
			KillVoiceOvers;	 
			PlayVoiceOverOnce("vo\eaton\mi2\JibingGovernor",TALKER_SPE);
			PlayVoiceOverOnce("vo\eaton\mi2\WereNotBlind",TALKER_CAS);
			cinema_cut = 0;	 
			KillTimer(gti_timer_21);
			SetGlobalTrigger(0,true);
			playerai(epl_player0,false);
			StartTimer(1);		// This is the mission start timer
			ResetTimer(1);
			CinemaEnd;		  
			SetCompositingEnabled(True);
			revealobjective(eob_objective1);
			RevealObjective(eob_objective2);
			RevealObjective(eob_objective3);
			trans sit;
		endif;
		
		switch (cinema_cut)

			case 0:
				CinemaStart;
				SetActiveCamera(eca_CameraShip);
				
				Camerafollowobject(ede_camera);
				Targetfollowobject(ebu_governor_mansion);
				Targetoffset(0.0,20.0,20.0,0.0,true);
				Cameraoffset(0.0,50.0,0.0,0.0,true);
				Cameraoffset(0.0,50.0,350.0,24.0,true);
				playerai(epl_player0,true);
				
				
				PlayVoiceOverOnce("vo\eaton\mi2\intro1",TALKER_CAS);
				PlayVoiceOver("VO\HalfSecond",TALKER_SPE);
				PlayVoiceOverOnce("vo\eaton\mi2\intro2",TALKER_SPE);
				PlayVoiceOver("VO\HalfSecond",TALKER_SPE);
				PlayVoiceOverOnce("vo\eaton\mi2\intro3",TALKER_CAS);
				
				cinema_cut = cinema_cut + 1;
				
				starttimer(21);			// This is the cineractive counter.
				resettimer(21);
			endcase;
			
			case 1:
				if (not IsVoiceOverPlaying) then
					StartTimer(21);
					ResetTimer(21);
					cinema_cut = cinema_cut + 1;
				endif;
			endcase;
			
			case 2:
				if (timegreater(21,1)) then
					KillVoiceOvers;
					PlayVoiceOverOnce("vo\eaton\mi2\JibingGovernor",TALKER_SPE);
					PlayVoiceOverOnce("vo\eaton\mi2\WereNotBlind",TALKER_CAS);
					cinema_cut = 0;
					KillTimer(gti_timer_21);
					SetGlobalTrigger(0,true);
					playerai(epl_player0,false);
					StartTimer(1);		// This is the mission start timer
					ResetTimer(1);
					CinemaEnd;
					revealobjective(eob_objective1);	// Reveal the objectives
					RevealObjective(eob_objective2);
					RevealObjective(eob_objective3);
					SetCompositingEnabled(True);
					
					trans sit;
				endif;
			endcase;
				
		endswitch;
	
endstate;

state won;	  

	code
		
		if (cinemaskip) then
			cinema_cut = 20;	
			playerai(epl_player0,false);
			KillVoiceOvers;
			Endmission(true,1); 
		endif;

		switch cinema_cut
		
			case 0:	
				StartMusic("Music\GenericVictory1",3.0,3.5,50);					 
				PlayVoiceOverOnce("vo\eaton\mi2\Close1",TALKER_SPE);
				PlayVoiceOverOnce("vo\eaton\mi2\Close2",TALKER_CAS);
				PlayVoiceOverOnce("vo\eaton\mi2\Close3",TALKER_SPE);
				if (not Obj3) then		// All mansions are safe
					PlayVoiceOverOnce("vo\eaton\mi2\Close4",TALKER_SPE);
					PlayLanceMateSound("vo\generic\goodnews",1);
				endif;
				SetCompositingEnabled(False);
				CinemaStart;
				setactivecamera (eca_cameraship);
				Targetfollowobject(playermech);
				Camerafollowobject(playermech);
				Targetoffset(0.0,4.0,0.0,0.0,false);
				Cameraoffset(-30.0,125.0,75.0,0.0,false);
				Cameraoffset(60.0,20.0,25.0,14.0,false);
				playerai(epl_player0,true);
				Cinema_cut=Cinema_cut+1;
			endcase;
			
			case 1:
				if (not IsVoiceOverPlaying) then
					Cinema_cut = Cinema_cut + 1;
				endif;
			endcase;			
			
			case 2:
				cinema_cut = 20;	
				playerai(epl_player0,false);
				KillVoiceOvers;
				Endmission(true,2); 
			endcase;
			
		endswitch;
	
endstate;

state lost;

		code
		
		switch cinema_cut
		
			case 0:	
			
			endcase;
			
			case 1:
			
			endcase;
			
		endswitch;
	
endstate;

endfsm.
//******************************************************************
