
//*********************************************************************************

fsm Hesp02_Raid : integer;		

//------------------------------------------------------------------
//
//     Constant Definitions
//
//------------------------------------------------------------------
     const

		#include_ <content\ABLScripts\mwconst.abi>
		#include_ <content\ABLScripts\Katie\Katie_consts.abi>  // <-- mektek
//------------------------------------------------------------------
//
//     Type Definitions
//
//------------------------------------------------------------------
     type

		#include_ <content\ABLScripts\mwtype.abi>

//------------------------------------------------------------------
//
//     Variable Declarations
//
//------------------------------------------------------------------
      var
			
		eternal integer playermech;
		eternal boolean obj1;
		eternal boolean obj2;
		eternal boolean obj3;
		eternal integer alphaObjective;
		eternal integer gammaObjective;
		eternal integer obj3Group;
		
		static boolean alphaReached;
		static boolean betaReached;
		static boolean gammaReached;
		static integer checkRange;
		
		
		static integer	cinema_cut;
		
		static boolean firstAlphaVO;
		static boolean SecondAlphaVO;
		static boolean ThirdAlphaVO;
		static boolean firstBetaVO;
		static boolean firstGammaVO;
		
		eternal Integer DavionRating;
		eternal Integer SteinerRating;
		eternal Integer Nobility;
		eternal Integer Infamy;
		
		
 //------------------------------------------------------------------
//
//     Init Function    (automatically run first time through)
//
//------------------------------------------------------------------

function init;
	var
		integer i;

	code

		playermech = getplayervehicle(epl_player0);	
		GroupAddObject(GroupObjectId(1),playermech);

		//Objectives at Alpha
		GroupAddObject(GroupObjectId(10),ebu_ObjectiveBuilding01);
		GroupAddObject(GroupObjectId(10),ebu_ObjectiveBuilding02);
		GroupAddObject(GroupObjectId(10),ebu_ObjectiveBuilding03);
		GroupAddObject(GroupObjectId(10),ebu_ObjectiveBuilding04);
        SetGroupAI(GroupObjectID(10),GROUPAI_NONE);
		alphaObjective = GroupObjectID(10);
	
		//Objectives at gamma
		GroupAddObject(GroupObjectId(20),ebu_GammaObjectiveBuilding);
		GroupAddObject(GroupObjectId(20),ebu_Weapon_Factory);
		GroupAddObject(GroupObjectId(20),ebu_Weapons_Factory);
        SetGroupAI(GroupObjectID(20),GROUPAI_NONE);
		gammaObjective = GroupObjectID(20);
 		
		//outpost defenders
		GroupAddObject(GroupObjectId(30),eve_outPostMech);
		GroupAddObject(GroupObjectId(30),etu_turret01);
		GroupAddObject(GroupObjectId(30),etu_turret02);
		GroupAddObject(GroupObjectId(30),etu_turret03);
		GroupAddObject(GroupObjectId(30),etu_turret04);
		GroupAddObject(GroupObjectId(30),etu_turret05);
		GroupAddObject(GroupObjectId(30),ebu_turretControl01);
        SetGroupAI(GroupObjectID(30),GROUPAI_RADIOSQUAD);
		
		//Turret Groups
		GroupAddObject(GroupObjectId(31),etu_turret06);
		GroupAddObject(GroupObjectId(31),etu_turret07);
        SetGroupAI(GroupObjectID(31),GROUPAI_RADIOSQUAD);
		//Turret Groups
		GroupAddObject(GroupObjectId(32),etu_turret08);
		GroupAddObject(GroupObjectId(32),etu_turret09);
		GroupAddObject(GroupObjectId(32),etu_turret10);
		GroupAddObject(GroupObjectId(32),etu_turret11);
		GroupAddObject(GroupObjectId(32),etu_turret12);
        SetGroupAI(GroupObjectID(32),GROUPAI_RADIOSQUAD);
		
		// Alpha defenders
		GroupAddObject(GroupObjectId(35),eve_AlphaMech01);
		GroupAddObject(GroupObjectId(35),eve_AlphaMech02);
        SetGroupAI(GroupObjectID(35),GROUPAI_RADIOSQUAD);
		
		// Gamma defenders
		GroupAddObject(GroupObjectId(40),eve_gammaMech01);
		GroupAddObject(GroupObjectId(40),eve_gammaMech02);
		GroupAddObject(GroupObjectId(40),eve_gammaMech03);
        SetGroupAI(GroupObjectID(40),GROUPAI_RADIOSQUAD);
		
		// PatrolMechs
		GroupAddObject(GroupObjectId(45),eve_Patrol01);
		GroupAddObject(GroupObjectId(45),eve_Patrol02);
		GroupAddObject(GroupObjectId(45),eve_Patrol03);
        SetGroupAI(GroupObjectID(45),GROUPAI_RADIOSQUAD);
		
		//Obj3Group
		obj3Group = GroupObjectID(50);
		GroupAddObject(obj3Group,eve_Patrol01);
		GroupAddObject(obj3Group,eve_Patrol02);
		GroupAddObject(obj3Group,eve_Patrol03);
		GroupAddObject(obj3Group,eve_gammaMech01);
		GroupAddObject(obj3Group,eve_gammaMech02);
		GroupAddObject(obj3Group,eve_gammaMech03);
		GroupAddObject(obj3Group,etu_turret08);
		GroupAddObject(obj3Group,etu_turret09);
		GroupAddObject(obj3Group,etu_turret10);
		GroupAddObject(obj3Group,etu_turret11);
		GroupAddObject(obj3Group,etu_turret12);
        SetGroupAI	  (obj3Group,GROUPAI_NONE);
		
		
		obj1 = false;
		obj2 = false;
		obj3 = false;
		checkrange = 150;
		
		cinema_cut = 0; 						
		
		DavionRating = GetPreviousDavionFactionRating;
		Davionrating = DavionRating + 11;
		SteinerRating = GetPreviousSteinerFactionRating;
		SteinerRating = SteinerRating - 4;
		Nobility = GetPreviousNobility;
		nobility = nobility;
		Infamy = GetPreviousInfamy;
		infamy = infamy + 7;
		
		
endfunction;
		#include_ <content\ABLScripts\Katie\Katie_script.abi>  // <-- mektek
//------------------------------------------------------------------
//
//     Main Code
//
//------------------------------------------------------------------
state startState;

	code	
	RevealObjective(eob_Objective01);
	RevealObjective(eob_Objective02);
	RevealObjective(eob_Objective03);
	
	RevealNavpoint(ena_Nav_Alpha);		
	RevealNavpoint(ena_Nav_Beta);
	RevealNavpoint(ena_Nav_Gamma);
	setRadarNav(ena_Nav_Alpha, 1);
	setRadarNav(ena_Nav_Beta,  2);
	setRadarNav(ena_Nav_Gamma, 3);
	SetNavPoint(ena_Nav_Alpha);
	
	//Salvage Chance
/* MekTek: Shark removed these and replaced them with Katie calls
	SetKillSalvageChance(eve_AlphaMech01, 25);		//MadCat
	SetKillSalvageChance(eve_AlphaMech02, 25);		//MadCat
	SetKillSalvageChance(eve_GammaMech01, 25);		//MadCat
	SetKillSalvageChance(eve_GammaMech02, 25);		//MadCat
	SetKillSalvageChance(eve_Patrol01, 50);			//Vulture
	SetKillSalvageChance(eve_Patrol02, 50);			//Vulture
*/
	
	//Intro VO
	PlayVoiceOverOnce("VO\hesp\MI2\intro1", talker_spe);	//Not editing out when No lancemates because of important info in line
	PlayLanceMateSound("vo\generic\ReadyOp",7);
	PlayVoiceOverOnce("VO\hesp\MI2\SoonWeChoose", talker_CAS);
	PlayVoiceOverOnce("VO\hesp\MI2\LoyaltyToYou", talker_CAS);
	PlayVoiceOverOnce("VO\hesp\MI2\ThanksCastle", talker_Spe);
	StartMusic("Music\hunter",3.5,3.0,50);
			
	trans opening;

endstate;

	
state deadState;
	code
endstate;
	
//------------------------------------------------------------------
//
//     Sit state : description
//
//------------------------------------------------------------------
state sit;

	code
	//Music
	if (not ismusicplaying) then
		StartMusic("Music\arcticambloop",0.0,3.0,40);
	endif;
	
	//VO
	if (not FirstAlphaVO) then
		if IsWIthin(playerMech, GroupObjectID(30), 900) then
			PlayLanceMateSound("vo\generic\Contact900",1);
			PlayVoiceOverOnce("VO\hesp\MI2\HereWeGo", talker_spe);
			PlayLanceMateSound("vo\generic\SteinerPrejudice",1);
			FirstAlphaVO = true;
		endif;
	endif;
	if (not SecondAlphaVO) then
		if IsWIthin(playerMech, GroupObjectID(35), 800) then
			PlayVoiceOverOnce("VO\hesp\MI2\TwoMore", talker_spe);
			SecondAlphaVO = true;
		endif;
	endif;
	if (not ThirdAlphaVO) then
		if obj1 then
			PlayVoiceOverOnce("VO\hesp\MI2\FacilityHistory", talker_spe);
			PlayVoiceOverOnce("VO\hesp\MI2\Acknowledged", talker_cas);
			PlayVoiceOverOnce("VO\hesp\MI2\GoingToBeta", talker_spe);
			ThirdAlphaVO = true;
		endif;
	endif;
	if (not FirstBetaVO) then
		if IsWithin(playerMech, eve_shadowcat, 450) then
			PlayVoiceOverOnce("VO\hesp\MI2\enemyAtBeta", talker_spe);
			PlayVoiceOverOnce("VO\hesp\MI2\DemiLanceApproaching", talker_CAS);
			StartMusic("Music\imminentAction",3.5,3.0,50);
			FirstBetaVO = true;
		endif;
	endif;
	if (not FirstGammaVO) then
		if obj2 and (not obj3) then
			PlayVoiceOverOnce("VO\hesp\MI2\MunitionsHistory", talker_spe);
			PlayLanceMateSound("vo\generic\Agree",1);
			FirstGammaVO = true;
		endif;
	endif;
	//VO End
	
	if (not obj1) then
		if GroupAllDead(alphaObjective) then
			successObjective(eob_objective01);
			SetNavPoint(ena_Nav_Beta);
			obj1 = true;
		endif;
	endif;
	
	if (not obj2) then
		if GroupAllDead(gammaObjective) then
			successObjective(eob_objective02);
			obj2 = true;
		endif;
	endif;
	
	if (not obj3) then
		if GroupAllDead(Obj3Group) then
			successObjective(eob_objective03);
			obj3 = true;
		endif;
	endif;
	
	if obj1 and obj2 and obj3 then
		resetTimer(21);
		cinema_cut = 0;
		trans won;
	endif;
			
// Betty speaks when reaching Nav Point

	if (not alphareached) then
		if (isnavrevealed(ena_nav_alpha)) then
			if (iswithin(playermech,ena_nav_alpha,checkrange)) then
				playvoiceover("VO\generic\navalpha",talker_Bet);
				alphareached = true;
			endif;
		endif;
	endif;

	if (not betareached) then
		if (isnavrevealed(ena_nav_beta)) then
			if (iswithin(playermech,ena_nav_beta,checkrange)) then
				playvoiceover("VO\generic\navbeta",talker_Bet);
				betareached = true;
				setnavpoint(ena_nav_gamma);
			endif;
		endif;
	endif;
	
	if (not gammareached) then
		if (isnavrevealed(ena_nav_gamma)) then
			if (iswithin(playermech,ena_nav_gamma,checkrange)) then
				playvoiceover("VO\generic\navgamma",talker_Bet);
				gammareached = true;
			endif;
		endif;
	endif;

//Fail the mission when the player dies
	if (isdead(playermech)) then
		failobjectiveall(9);
		resetTimer(21);
		cinema_cut = 0;
		trans lost;
	endif;

				
endstate;

//-----------------------------------------
//HERE THERE BE CINEMAS
//-----------------------------------------

state opening;
	code

		if cinemaSkip then
			CinemaEnd;
			SetCompositingEnabled(true);			
			playerAI(playermech, false);
			killVoiceOvers;
			trans sit;
		endif;
		
		switch (cinema_cut)
			case 0:
				SetActiveCamera(eca_CameraShip);
				playerAI(epl_player0, true);
				SetCompositingEnabled(false);
				CinemaStart;
				FadeToBlack(0.0);
				Fadefromblack(2.0);
				Targetfollowobject(ebu_ObjectiveBuilding02);
				camerafollowobject(ebu_ObjectiveBuilding02);
				targetoffset(0.0, 4.0, 0.0, 0.0, false);
                Cameraoffset(-170.0, 4.0, -280.0, 0.0, false);       
                Cameraoffset(-30.0, 4.0, -60.0, 12.0, false);       
				resetTimer(21);
				cinema_cut = cinema_cut + 1;
			endcase;
			case 1:
				if TimeGreater(21, 12) then
					cinema_cut = cinema_cut + 1;
				endif;
			endcase;
			case 2:
				playerAI(playermech, false);
				CinemaEnd;
				SetCompositingEnabled(true);
				trans sit;
			endcase;
		endswitch;
	
endstate;

state won;
	code
		
		switch cinema_cut
			case 0:
				PlayVoiceOverOnce("VO\hesp\MI2\Close1", talker_spe);
				PlayVoiceOverOnce("VO\hesp\MI2\Close2", talker_CAS);
				PlayVoiceOverOnce("VO\hesp\MI2\Close3", talker_spe);
				switch rand(1,2)
					case 1:
						StartMusic("Music\genericVictory1",3.5,3.0,50);
					endcase;
					case 2:
						StartMusic("Music\genericVictory2",3.5,3.0,50);
					endcase;
				endswitch;
				cinema_cut = cinema_cut +1;
			endcase;
			
			case 1:
				if TimeGreater(21,5) then
					cinema_cut = cinema_cut +1;
				endif;
			endcase;
			case 2:	
				//EndMission(true, 10);
				SetActiveCamera(eca_CameraShip);
				PullOutCamera(playermech, playermech, 150.0, 10.0, true);				
				UpdateCameraPosition(eca_CameraShip);     
								
				SetCurrentInfamy(Infamy);
				SetCurrentNobility(Nobility);
				SetCurrentDavionFactionRating(DavionRating);
				SetCurrentSteinerFactionrating(SteinerRating);
				
				AddWeaponSalvage("ERMediumPulseLaser", 2);
				AddWeaponSalvage("ClanLRM20", 2);
				/////////////////////
				// MISSION SALVAGE //
				/////////////////////
				Katie(eve_outPostMech,50);
				
				Katie(eve_AlphaMech01,50);
				Katie(eve_AlphaMech02,30);

				Katie(eve_GammaMech01,60);
				Katie(eve_GammaMech02,50);	
				Katie(eve_GammaMech03,40);

				Katie(eve_Patrol01,50);
				Katie(eve_Patrol02,40);
				Katie(eve_Patrol03,30);

				cinema_cut = cinema_cut + 1;
			endcase;
			case 3:
				if (not IsVoiceOverPlaying) then
					EndMission(true, 3);
					cinema_cut = cinema_cut + 1;
				endif;
			endcase;
		endswitch;
	
endstate;

state lost;
	code
	
		switch cinema_cut
			case 0:
				KillVoiceOvers;
				switch rand(1,2)
					case 1:
						StartMusic("Music\genericLoss1",3.5,3.0,50);
					endcase;
					case 2:
						StartMusic("Music\genericLoss2",3.5,3.0,50);
					endcase;
				endswitch;
				switch rand(1,3)
					case 1:
						PlayVoiceOver("VO\Generic\SpecterDestroyed", talker_CAS);
					endcase;
					case 2:
						PlayVoiceOver("VO\Generic\SpecterDestroyed2", talker_CAS);
					endcase;
					case 3:
						PlayVoiceOver("VO\Generic\SpecterDestroyed3", talker_CAS);
					endcase;
				endswitch;
				cinema_cut = cinema_cut + 1;
			endcase;
			case 1:	
				EndMission(false, 10);
				//SetActiveCamera(eca_CameraShip);
				//PullOutCamera(playermech, playermech, 250.0, 10.0, true);				
				cinema_cut = cinema_cut + 1;
			endcase;
		endswitch;
	
endstate;


endfsm.
//******************************************************************

