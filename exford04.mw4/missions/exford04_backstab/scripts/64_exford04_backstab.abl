//*********************************************************************************

fsm Exford04_Backstab : integer;

//------------------------------------------------------------------
//
//	 Constant Definitions
//
//------------------------------------------------------------------
	 const

		#include_ <content\ABLScripts\mwconst.abi>
		#include_ <content\ABLScripts\Katie\katie_consts.abi>  // <-- mektek
//------------------------------------------------------------------
//
//	 Type Definitions
//
//------------------------------------------------------------------
	 type

		#include_ <content\ABLScripts\mwtype.abi>

//------------------------------------------------------------------
//
//	 Variable Declarations
//
//------------------------------------------------------------------
	  var

		eternal integer		playermech;			// This is the variable for the player's 'Mech
			
		// Standard Nav and Objective Variables
		static boolean		alphareached;		// Nav Alpha reached
		static boolean		betareached;		// Nav Beta reached
		static boolean		gammareached;		// Nav Gamma reached
		static boolean		deltareached;		// Nav Delta reached
												//
		static boolean		alpharevealed;		// Nav Alpha is shown
		static boolean		betarevealed;		// Nav Beta is shown
		static boolean		gammarevealed;		// Nav Gamma is shown
		static boolean		deltarevealed;		// Nav Delta is shown
		static integer		checkrange;			// How far from the Nav Point you have to be											
												//
		eternal boolean		obj1;				// This indicates whether Objective 1 was completed
		eternal boolean		obj2;				// This indicates whether Objective 2 was completed
		eternal boolean		obj3;				// This indicates whether Objective 3 was completed
		eternal boolean		obj4;				// This indicates whether Objective 4 was completed
		eternal boolean		obj5;				// This indicates whether Objective 5 was completed
												//
		static boolean		obj1revealed;		// This indicates that Objective 1 has been revealed
		static boolean		obj2revealed;		// This indicates that Objective 2 has been revealed
		static boolean		obj3revealed;		// This indicates that Objective 3 has been revealed
		eternal boolean		obj4revealed;		// This indicates that Objective 4 has been revealed
		static boolean		obj5revealed;		// This indicates that Objective 5 has been revealed
		//	
		
		static integer		cinema_cut;			// This variable keeps track of what scene a cinema is on.
		static locpoint		temploc;			// Used for commands that need objects converted to locpoint format
		static boolean		loser;				// You have lost the mission
  		eternal integer 	DavionRating;		// This is your Davion rating
		eternal integer 	SteinerRating;		// This is your Steiner rating
		eternal integer 	Nobility;			// This is your Nobility rating
		eternal integer 	Infamy;				// This is your Infamy rating

		// Trigger States
		eternal boolean		FirstContact;		// The player has been spotted
		static integer		TotalObj;			// Total number of Objectives complete
		static integer		Sponsor;			// Who is your sponsor regiment
		
//------------------------------------------------------------------
//
//	 Init Function	(automatically run first time through)
//
//------------------------------------------------------------------

function init;
	var
		integer i;

	code

			playermech = getplayervehicle(epl_player0);

			// 10 - Intial 'Mechs on patrol
			GroupAddObject(GroupObjectID(10),eve_patrol01);
			GroupAddObject(GroupObjectID(10),eve_patrol02);
			SetGroupAI(GroupObjectID(10),GROUPAI_MOODSQUAD);

			/* 11 - Light 'Mechs
			GroupAddObject(GroupObjectID(11),eve_1ewave01);
			GroupAddObject(GroupObjectID(11),eve_1ewave02);
			GroupAddObject(GroupObjectID(11),eve_1ewave03);
			GroupAddObject(GroupObjectID(11),eve_1ewave04);
			GroupAddObject(GroupObjectID(11),eve_1ewave05);
			GroupAddObject(GroupObjectID(11),eve_1wwave01);
			GroupAddObject(GroupObjectID(11),eve_1wwave02);
			SetGroupAI(GroupObjectID(11),GROUPAI_MOODSQUAD);*/
			
			// 12 - Heavy 'Mechs
			GroupAddObject(GroupObjectID(12),eve_2wave01);
			GroupAddObject(GroupObjectID(12),eve_2wave02);
			GroupAddObject(GroupObjectID(12),eve_2wave03);
			GroupAddObject(GroupObjectID(12),eve_2wave04);
			GroupAddObject(GroupObjectID(12),eve_Aisa_Thastus);
			SetGroupAI(GroupObjectID(12),GROUPAI_MOODSQUAD);
			
			// 20 - Communications Center
			GroupAddObject(GroupObjectID(20),ebu_Clan_Barracks);
			GroupAddObject(GroupObjectID(20),ebu_air_control_tower);
			GroupAddObject(GroupObjectID(20),ebu_radiotower_0001);
			
			// 21 - Comm Center and Defenders
			GroupAddObject(GroupObjectID(21),ebu_Clan_Barracks);
			//GroupAddObject(GroupObjectID(21),ebu_air_control_tower);
			//GroupAddObject(GroupObjectID(21),ebu_radiotower_0001);
			GroupAddObject(GroupObjectID(21),eve_patrol01);
			GroupAddObject(GroupObjectID(21),eve_patrol02);

			// 10 - Patrol 'Mechs
			/*GroupAddObject(GroupObjectID(10),eve_patrol01);
			SetGroupAI(GroupObjectID(10),GROUPAI_MOODSQUAD);*/
			
			cinema_cut = 0;

			// Culturals
			//SetTarget(ede_example2,eve_example3);
		
endfunction;
		#include_ <content\ABLScripts\Katie\katie_script.abi>  // <-- mektek
//------------------------------------------------------------------
//
//	 Main Code
//
//------------------------------------------------------------------
state startState;

	code	
/* Mektek: Shark removed this and replaced it with Katie call
			SetKillSalvageChance(eve_2wave03,50);	// Novacat
*/
		// These are your MetaMission variables, second part is how this mission effects your ratings
			DavionRating 	= GetPreviousDavionFactionRating;
			SteinerRating 	= GetPreviousSteinerFactionRating;
			Nobility 		= GetPreviousNobility;
			Infamy 			= GetPreviousInfamy;
			
			DavionRating	= DavionRating + 0;
			SteinerRating	= SteinerRating - 0;
			Nobility		= Nobility - 5;
			Infamy			= Infamy + 12;
		// 
		
		SetRadarNav(ena_Nav_Alpha,1);
		SetRadarNav(ena_Nav_Beta,2);
		//SetRadarNav(ena_Nav_Gamma,3);
		RevealNavpoint(ena_Nav_Alpha);
		SetNavpoint(ena_Nav_Alpha);

		StartMusic("Music\NextMove",3.0,3.5,50);

		//setsensorvisibility(eve_example4,false);
		
		checkrange	= 100;		// How far from a nav point before the Betty voice says you've arrived.
		
		trans opening;

endstate;

	
state deadState;
	code
	
endstate;
	
//------------------------------------------------------------------
//
//	 Sit state : description
//
//------------------------------------------------------------------
state sit;

	code

	if IsMusicPlaying == false then
		//StartMusic("VO\Rain1",0.1,0.1,300);
	endif;
	
// Sound Triggers

	if (not GetGlobalTrigger(4)) then
		if (GetGlobalTrigger(1)) then	// If you shoot them
			SetGlobalTrigger(4,true);
			PlayVoiceOverOnce("VO\Exford\MI4\TheyMadeUs",talker_SPE);
			PlayVoiceOverOnce("VO\Exford\MI4\SurpriseWorked",talker_CAS);
			PlayVoiceOver("VO\HalfSecond",TALKER_SPE);
			PlayVoiceOverOnce("VO\Exford\MI4\HitHurts",talker_SPE);
			StartTimer(3);		// You have a timer after being spotted
			SetGlobalTrigger(1,true);
		else
			if (GetSensorMode(PlayerMech) == SENSOR_PASSIVE) then		// We've been spotted
				if (IsWithin(PlayerMech,ena_nav_alpha,600)) then
					SetGlobalTrigger(4,true);
					PlayVoiceOverOnce("VO\Exford\MI4\TheyMadeUs",talker_SPE);
					PlayVoiceOverOnce("VO\Exford\MI4\SurpriseWorked",talker_CAS);
					PlayVoiceOver("VO\HalfSecond",TALKER_SPE);
					PlayVoiceOverOnce("VO\Exford\MI4\HitHurts",talker_SPE);
					StartTimer(3);		// You have a timer after being spotted
					SetGlobalTrigger(1,true);
				endif;
			else
				if (IsWithin(PlayerMech,ena_nav_alpha,900)) then
					SetGlobalTrigger(4,true);
					PlayVoiceOverOnce("VO\Exford\MI4\TheyMadeUs",talker_SPE);
					PlayVoiceOverOnce("VO\Exford\MI4\SurpriseWorked",talker_CAS);
					PlayVoiceOver("VO\HalfSecond",TALKER_SPE);
					PlayVoiceOverOnce("VO\Exford\MI4\HitHurts",talker_SPE);
					StartTimer(3);		// You have a timer after being spotted
					SetGlobalTrigger(1,true);
				endif;
			endif;
		endif;
	endif;
	
	if (not GetGlobalTrigger(5)) then
		if (timegreater(3,20)) then		// No sign of the dropship, alt addy
			SetGlobalTrigger(5,true);
			//PlayVoiceOverOnce("VO\Exford\MI4\StarOfHeavies",talker_CAS);
			//PlayVoiceOverOnce("VO\Exford\MI4\HitHurts",talker_SPE);
		endif;
	endif;
	
	if (not GetGlobalTrigger(7)) then
		if (IsWithin(PlayerMech,GroupObjectID(12),950)) then		// Close to star of heavies at Beta
			SetGlobalTrigger(7,true);
			PlayVoiceOverOnce("VO\Exford\MI4\FreeBirthScum",talker_AIS);
			Sponsor = GetSponsorRegiment;
			if (Sponsor == REGIMENT_KLHND) then		// Check if Kellhound
				PlayVoiceOverOnce("VO\Exford\MI4\Retort",talker_SPE);
			else
				PlayVoiceOverOnce("VO\Exford\MI4\WeDontDieEasily",talker_SPE);
			endif;
			PlayVoiceOverOnce("VO\Exford\MI4\Taunt",talker_AIS);
			PlayVoiceOverOnce("VO\Exford\MI4\ConfirmContact",talker_SPE);
		endif;
	endif;
	
	if (not GetGlobalTrigger(8)) then
		if (IsDead(eve_Aisa_Thastus)) then		// Aisa is dead, but not the rest of her gang
			SetGlobalTrigger(8,true);
			PlayVoiceOverOnce("VO\Exford\MI4\ThastisEliminated",talker_SPE);
			if (not GroupAllDead(GroupObjectID(12))) then
				PlayVoiceOverOnce("VO\Exford\MI4\StatusCastle",talker_SPE);
				PlayVoiceOverOnce("VO\Exford\MI4\MopUpSir",talker_CAS);
				PlayVoiceOverOnce("VO\Exford\MI4\OnIt",talker_SPE);
				PlayLanceMateSound("VO\Generic\RogerThat",1);
			endif;
		endif;
	endif;

// Exford 04 Specific Triggers

	if (not GetGlobalTrigger(1)) then		// Once the Comm Center is shot boats are alerted soon
		if (IsShot(GroupObjectID(20))) then
			StartTimer(3);		// You have a timer after the Comm Center has been shot
			SetGlobalTrigger(1,true);
		endif;
	endif;
	
	if (not GetGlobalTrigger(2)) then		// 30s after the warning, the 'Mechs head out
		if (timegreater(3,30)) then
			SetGlobalTrigger(2,true);
		endif;
	endif;
	
	if (not GetGlobalTrigger(3)) then		// If the Heavy 'Mechs are shot, they will respond pretty quickly
		if (IsWithin(PlayerMech,ena_nav_beta,1300)) then
			StartTimer(4);		// Heavy 'Mech timer
			SetGlobalTrigger(3,true);
			SetGlobalTrigger(1,true);	// And you've been spotted alerting everyone
		endif;
	endif;

	if (not GetGlobalTrigger(9)) then
		if (GetSensorMode(PlayerMech) == SENSOR_PASSIVE) then
			Helpmessage(-2,1);
			SetGlobalTrigger(9,true);
		endif;
	endif;

// Obj 1 - Destroy all enemy forces at Nav Alpha

	if (not Obj1) then
		if (GroupAllDead(GroupObjectID(21))) then
			SuccessObjective(eob_objective1);
			RevealObjective(eob_objective3);
			TotalObj = TotalObj + 1;
			PlayVoiceOverOnce("VO\Exford\MI4\AlphaSecured",talker_SPE);
			PlayVoiceOverOnce("VO\Exford\MI4\UploadBeta",talker_CAS);
			RevealNavpoint(ena_Nav_Beta);
			Obj2Revealed = true;
			SetNavpoint(ena_Nav_Beta);
			Obj1 = True;
		endif;
	endif;
	
// Obj 2 - Destroy the barracks before pilots can get to their 'Mechs

	if (not Obj2Revealed) then
		if (IsWithin(PlayerMech,ebu_Clan_Barracks,800)) then
			RevealObjective(eob_objective2);
			Obj2Revealed = True;
		endif;
	endif;
	
	if (not Obj2) then
		if (IsDead(ebu_Clan_Barracks)) then
			Obj2 = True;
			SuccessObjective(eob_objective2);
			TotalObj = TotalObj + 1;
		endif;
	endif;
	
	if (not Obj2) then
		if (GetGlobalTrigger(20)) then
			Obj2 = True;
			FailObjective(eob_objective2);
			TotalObj = TotalObj + 1;
		endif;
	endif;

// Obj 3 - Destroy all Jade Falcon 'Mechs

	if (not Obj3) then
		if GroupAllDead(GroupObjectID(12)) then
			SuccessObjective(eob_objective3);
			Obj3 = True;
			TotalObj = TotalObj + 1;
		endif;
	endif;

// Obj 4 - Destroy the Jade Falcon dropship

	if (not Obj4Revealed) then
		if (IsWithin(PlayerMech,eve_hrothgar,1200)) then	// If you get within radar it is revealed
			//RevealNavpoint(ena_Nav_Gamma);
			StartMusic("Music\Floodgate",3.0,3.5,50);
			RevealObjective(eob_objective4);
			//if (obj3) then
				//SetNavpoint(ena_Nav_Gamma);
			//endif;
			Obj4Revealed = True;
		endif;
	endif;

	if (not Obj4) then
		if (IsDead(eve_hrothgar)) then
			obj4 = true;
			TotalObj = TotalObj + 1;
			PlayVoiceOverOnce("VO\Exford\MI4\DropshipDestroyed",talker_CAS);
			if (not Obj3) then
				PlayVoiceOverOnce("VO\Exford\MI4\ContinueEngage",talker_SPE);
				if (not IsDead(eve_Aisa_Thastus)) and (GetGlobalTrigger(7)) then
					PlayVoiceOverOnce("VO\Exford\MI4\Inconceivable",talker_AIS);
				endif;
			endif;
			SuccessObjective(eob_objective4);
		endif;
	endif;
	
	if (TotalObj == 4) then
		TotalObj = 0;
		StartTimer(2);		// You win!
	endif;
	
// Victory and Loss conditions
													   
	if (timegreater(2,5)) then
		  
		// Salvage - FAB  9/5
			if not IsDead(eve_backup01) then
				AddMechInstanceSalvage("Thor");
			endif;
			if not IsDead(eve_backup03) then
				AddMechInstanceSalvage("Thor");
			endif;
			if not IsDead(eve_backup04) then
				AddMechInstanceSalvage("Vulture");
			endif;
			if not IsDead(eve_backup05) then
				AddMechInstanceSalvage("Mad Cat");
			endif;
			AddWeaponSalvage("ClanMachineGun",4); 
			AddWeaponSalvage("ClanGauss",2); 
			AddWeaponSalvage("ClanLRM10",3); 
			AddWeaponSalvage("SMRM10",1); 
			AddWeaponSalvage("ClanUltraAC20",1); 
		//
		
		if (loser) then
			EndMission(false,5);
			trans lost;
		else

			SetCurrentInfamy(Infamy);
			SetCurrentNobility(Nobility);
			SetCurrentDavionFactionRating(DavionRating);
			SetCurrentSteinerFactionrating(SteinerRating);
			/////////////////////
			// MISSION SALVAGE //
			/////////////////////
			Katie(eve_patrol01,50);
			Katie(eve_patrol02,40);
			
			/*
			Katie(eve_1ewave01,50);
			Katie(eve_1ewave02,40);
			Katie(eve_1ewave03,30);
			Katie(eve_1ewave04,20);
			Katie(eve_1ewave05,30);
			Katie(eve_1wwave01,40);
			Katie(eve_1wwave02,50);
			*/

			Katie(eve_2wave01,60);
			Katie(eve_2wave02,50);
			Katie(eve_2wave03,40);
			Katie(eve_2wave04,30);
			Katie(eve_Aisa_Thastus,20);
			
			trans won;	
		endif;
		
	endif;

// Fail the mission when the player dies
	if (isdead(playermech)) then
		KillVoiceOvers;
		switch rand(1,3)
			case 1:
				PlayVoiceOver("VO\Generic\SpecterDestroyed", talker_CAS);
			endcase;
			case 2:
				PlayVoiceOver("VO\Generic\SpecterDestroyed2", talker_CAS);
			endcase;
			case 3:
				PlayVoiceOver("VO\Generic\SpecterDestroyed3", talker_CAS);
			endcase;
		endswitch;
		StartMusic("Music\GenericLoss1",3.0,3.5,70);
		endmission(false,14);
		trans deadstate;
	endif;

// Betty speaks when reaching Nav Point

	if (not alphareached) then
		if (isnavrevealed(ena_nav_alpha)) then
			if (iswithin(playermech,ena_nav_alpha,checkrange)) then
				playvoiceover("VO\generic\navalpha",talker_Bet);
				alphareached = true;
				//SetNavpoint(ena_Nav_Beta);
			endif;
		endif;
	endif;

	if (not betareached) then
		if (isnavrevealed(ena_nav_beta)) then
			if (iswithin(playermech,ena_nav_beta,checkrange)) then
				playvoiceover("VO\generic\navbeta",talker_Bet);
				betareached = true;
				//SetNavpoint(ena_Nav_Gamma);
			endif;
		endif;
	endif;

endstate;

//-----------------------------------------
//HERE THERE BE CINEMAS
//-----------------------------------------

state opening;	  

	code

		if (cinemaskip) then
			KillVoiceOvers;	 
			cinema_cut = 0;	 
			KillTimer(gti_timer_21);
			SetGlobalTrigger(0,true);
			playerai(epl_player0,false);
			StartTimer(1);		// This is the mission start timer
			ResetTimer(1);
			CinemaEnd;		  
			SetCompositingEnabled(True);
			revealobjective(eob_objective1);
			//RevealObjective(eob_objective3);
			HelpMessage(eob_HelpSensors,10);
			trans sit;
		endif;
		
		switch (cinema_cut)

			case 0:
				CinemaStart;
				SetActiveCamera(eca_CameraShip);
				
				SpinOverCamera(PlayerMech,PlayerMech,17.0,17.0,true);
				
				PlayVoiceOverOnce("VO\Exford\MI4\Intro1",talker_CAS);
				PlayVoiceOverOnce("VO\Exford\MI4\Intro2",talker_SPE);
				PlayLancemateSound("VO\Generic\ReadyOp",3);
				
				cinema_cut = cinema_cut + 1;
				                                                    				
				starttimer(21);			// This is the cineractive counter.
				resettimer(21);
			endcase;
			
			case 1:
				if (not IsVoiceOverPlaying) then
					StartTimer(21);
					ResetTimer(21);
					cinema_cut = cinema_cut + 1;
				endif;
			endcase;

			case 2:
				if (timegreater(21,3)) then
					KillVoiceOvers;
					cinema_cut = 0;
					KillTimer(gti_timer_21);
					SetGlobalTrigger(0,true);
					playerai(epl_player0,false);
					StartTimer(1);		// This is the mission start timer
					ResetTimer(1);
					CinemaEnd;
					revealobjective(eob_objective1);	// Reveal the objectives
					//RevealObjective(eob_objective3);
					HelpMessage(eob_HelpSensors,10);
					SetCompositingEnabled(True);
					
					trans sit;
				endif;
			endcase;
				
		endswitch;
	
endstate;

state won;	  

	code
		
		if (cinemaskip) then
			cinema_cut = 20;	
			playerai(epl_player0,false);
			KillVoiceOvers;
			Endmission(true,1); 
		endif;

		switch cinema_cut
		
			case 0:	
				StartMusic("Music\GenericVictory1",3.0,3.5,50);					 
				PlayVoiceOverOnce("VO\Exford\MI4\Close1",talker_SPE);
				PlayVoiceOverOnce("VO\Exford\MI4\Close2",talker_CAS);
				PlayVoiceOverOnce("VO\Exford\MI4\Close3",talker_SPE);
				SetCompositingEnabled(False);
				CinemaStart;
				setactivecamera (eca_cameraship);
				Targetfollowobject(playermech);
				Camerafollowobject(playermech);
				Targetoffset(0.0,4.0,0.0,0.0,false);
				Cameraoffset(-30.0,125.0,75.0,0.0,false);
				Cameraoffset(60.0,20.0,25.0,14.0,false);
				playerai(epl_player0,true);
				Cinema_cut=Cinema_cut+1;
			endcase;
			
			case 1:
				if (not IsVoiceOverPlaying) then
					EndMission(true,2);
					Cinema_Cut = Cinema_Cut + 1;
				endif;
			endcase;			
			
		endswitch;
	
endstate;

state lost;

		code
		
		switch cinema_cut
		
			case 0:	
			
			endcase;
			
			case 1:
			
			endcase;
			
		endswitch;
	
endstate;

endfsm.
//******************************************************************
