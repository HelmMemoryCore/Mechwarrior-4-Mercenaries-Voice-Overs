//*********************************************************************************

fsm Styk03_Convoy : integer;		

//------------------------------------------------------------------
//
//     Constant Definitions
//
//------------------------------------------------------------------
     const

		#include_ <content\ABLScripts\mwconst.abi>
//------------------------------------------------------------------
//
//     Type Definitions
//
//------------------------------------------------------------------
     type

		#include_ <content\ABLScripts\mwtype.abi>

//------------------------------------------------------------------
//
//     Variable Declarations
//
//------------------------------------------------------------------
	var
		static	ObjectID	unitPath;					// Path that the unit follows
		static	Integer		speed;						// speed at which the Convoy travels
		static	Integer		memInt9;					// Value of Memory Integer 9
		static	Integer		attackRange;
		static	Integer		withdrawRange;
		static integer		skillLevel;
 
function Init;
	code
	
		speed = 40;
		unitPath = epa_Convoy_Path;
		memInt9 = getMemoryInteger(me,9);
		
		attackRange = 600;
		withdrawRange = attackRange * 3 / 2;
		skillLevel = 4;
		
endfunction;

state startState;
	code
	
	AssignSkill(skillLevel);
   	trans sit;
	
endstate;

state sit;

	code
	
	if ( OrderMoveResumePatrol(unitPath, speed, MOVE_NOCYCLE, true, false) ) then
		clearMoveOrder ( me ) ;
		trans deadstate;
	endif;
	
	if ( memInt9 <> 0 ) then
		if FindEnemy (attackRange, FT_DEFAULT) then
			trans AttackState;
		endif;
		
		if ( isShot ( me ) ) then
			setTarget(me, whoShot(me) );
			trans AttackState;
		endif;
	endif;
	
	if isDead(me) then
		trans deadState;
	endif;
	
	
endstate;

state AttackState;

	code
		if( leaveAttackState(withdrawrange) ) then
			orderStopAttacking;
			settarget(me, NO_UNIT);
			trans sit;
		else;
			OrderAttackTactic(TACTIC_PICK_BEST, false);
		endif;
	
		if isDead(me) then
			trans deadState;
		endif;
			
endstate;

state deadState;
	code

endstate;
	
endfsm.
