//*********************************************************************************

fsm Styk04_Convoy03 : integer;		

//------------------------------------------------------------------
//
//     Constant Definitions
//
//------------------------------------------------------------------
     const

		#include_ <content\ABLScripts\mwconst.abi>
//------------------------------------------------------------------
//
//     Type Definitions
//
//------------------------------------------------------------------
     type

		#include_ <content\ABLScripts\mwtype.abi>

//------------------------------------------------------------------
//
//     Variable Declarations
//
//------------------------------------------------------------------
	var
		static	Integer		attackRange;
		static	Integer		withdrawRange;
		static  integer		skillLevel;
		
		static  boolean		overRideMove;
		static	ObjectID	unitPath;					// Path that the unit follows
		static	Integer		speed;						// speed at which the Convoy travels
		static	Integer		memInt9;					// Value of Memory Integer 9
		static  boolean    	PathEndReached;

function Init;
	code
		
		attackRange = 600;
		withdrawRange = attackRange * 3 / 2;
		skillLevel = 4;
		
		overRideMove = false;
		speed = 60;
		unitPath = epa_ConvoyPath03;
		pathEndReached = false;
		memInt9 = getMemoryInteger(me,9);
		
		switch(memInt9)
			case 1:
				OverRideMove = false;
			endcase;
			case 2:
				OverRideMove = true;
			endcase;
		endswitch;
		
endfunction;

state startState;
	code
	
		if obj3 then
			if isWithin(playerMech, Convoy03, 1000) then
				AssignSkill(skillLevel);
				trans sit;
			endif;
			
			if isWithin(playerMech, ena_Nav_Gamma, 700) then
				AssignSkill(skillLevel);
				trans sit;
			endif;
		else
			if isWithin(playerMech, Convoy03, 750) then
				AssignSkill(skillLevel);
				trans sit;
			endif;
			if ( isShot ( me ) ) then
				SetAlignment(Convoy03, FA_ENEMY);
				trans sit;
			endif;
		endif;
	
endstate;

state sit;

	code
		
		if pathEndReached then
			orderMoveLookout;
		else
			if OrderMoveResumePatrol(unitPath, speed, MOVE_NOCYCLE, true, overRideMove) then
				pathEndReached = true;
				if (not obj5) then
					convoy03Escaped = true;
				endif;
			endif;
		endif;
		
		if ( isShot ( me ) ) then
			setTarget(me, whoShot(me) );
			SetAlignment(Convoy03, FA_ENEMY);
			trans AttackState;
		endif;
		
		if FindEnemy(attackrange, FT_DEFAULT) then
			trans attackState;
		endif;
		
		if isDead(me) then
			trans deadState;
		endif;
		
		if (memint9 == 1) then
			if IsWithin(me, ena_EnemyEscapeNav, 300) then
				convoy03Escaped = true;
			endif;
		endif;
		
endstate;

state AttackState;

	code
		if( leaveAttackState(withdrawrange) or ExtroStarted) then
			orderStopAttacking;
			settarget(me, NO_UNIT);
			trans sit;
		else
			OrderAttackTactic(TACTIC_PICK_BEST, overRideMove);
		endif;
	
		if isDead(me) then
			trans deadState;
		endif;
		
		if (memint9 == 1) then
			if IsWithin(me, ena_EnemyEscapeNav, 300) then
				convoy03Escaped = true;
			endif;
		endif;
			
endstate;

state deadState;
	code

endstate;
	
endfsm.
