
//*********************************************************************************

fsm E5_BanditAlleyA : integer;

//------------------------------------------------------------------
//
//	 Constant Definitions
//
//------------------------------------------------------------------
	 const

		#include_ <content\ABLScripts\mwconst.abi>
		#include_ <content\ABLScripts\Katie\katie_consts.abi>
//------------------------------------------------------------------
//
//	 Type Definitions
//
//------------------------------------------------------------------
	 type

		#include_ <content\ABLScripts\mwtype.abi>

//------------------------------------------------------------------
//
//	 Variable Declarations
//
//------------------------------------------------------------------
	  var

		eternal integer		playermech;			// This is the variable for the player's 'Mech
			
		// Standard Nav and Objective Variables
		static boolean		alphareached;		// Nav Alpha reached
		static boolean		betareached;		// Nav Beta reached
		static boolean		gammareached;		// Nav Gamma reached
		static boolean		deltareached;		// Nav Delta reached
												//
		static boolean		alpharevealed;		// Nav Alpha is shown
		static boolean		betarevealed;		// Nav Beta is shown
		static boolean		gammarevealed;		// Nav Gamma is shown
		static boolean		deltarevealed;		// Nav Delta is shown
		static integer		checkrange;			// How far from the Nav Point you have to be											
												//
		eternal boolean		obj1;				// This indicates whether Objective 1 was completed
		eternal boolean		obj2;				// This indicates whether Objective 2 was completed
		eternal boolean		obj3;				// This indicates whether Objective 3 was completed
		eternal boolean		obj4;				// This indicates whether Objective 4 was completed
		eternal boolean		obj5;				// This indicates whether Objective 5 was completed
												//
		static boolean		obj1revealed;		// This indicates that Objective 1 has been revealed
		static boolean		obj2revealed;		// This indicates that Objective 2 has been revealed
		static boolean		obj3revealed;		// This indicates that Objective 3 has been revealed
		static boolean		obj4revealed;		// This indicates that Objective 4 has been revealed
		static boolean		obj5revealed;		// This indicates that Objective 5 has been revealed
		//	
		
		static integer		cinema_cut;			// This variable keeps track of what scene a cinema is on.
		static boolean		loser;				// You have lost the mission
 		eternal integer 	DavionRating;		// This is your Davion rating
		eternal integer 	SteinerRating;		// This is your Steiner rating
		eternal integer 	Nobility;			// This is your Nobility rating
		eternal integer 	Infamy;				// This is your Infamy rating
 
		// Trigger States
		eternal boolean		Harass;				// Enter the harassers
		eternal boolean		Relief;				// Enter the support group for the convoy guards
		
		static integer		ReliefTime;			// How long after engagement the Relief come online
		static integer		HarassTime;			// How long after engagement the Harass come online
		static integer		ReinforceNum;		// How many guys have to be nailed before reinforcements start coming in
		eternal boolean		EnemyEngaged;		// Once the enemy has been seen, this is flagged
		
		static boolean		Fail1;				// You have failed the mission once
		static boolean		Replay1;			// You are playing the mission again
		static boolean		Replay2;			// You are playing the mission yet again
		static locpoint		MyNav;				// Used for teleports
		eternal boolean		ExtraTough;			// The mission is extra tough
		static integer		UnitStrength;
		
		// Global Variables will handle this later
		static integer		E1_Harass;			// Number of Harass
		static integer		E1_Mainforce;		// Number of Harass
		static integer		E1_Relief;			// Number of Harass
		//
		
//------------------------------------------------------------------
//
//	 Init Function	(automatically run first time through)
//
//------------------------------------------------------------------

function init;
	var
		integer i;

	code

			playermech = getplayervehicle(epl_player0);

			// 10 - Eaton Convoy (Convoy Group)
			GroupAddObject(GroupObjectId(10),eve_convoy01);  
			GroupAddObject(GroupObjectId(10),eve_convoy02);  
			GroupAddObject(GroupObjectId(10),eve_convoy03);  
			GroupAddObject(GroupObjectId(10),eve_convoy04);  
			GroupAddObject(GroupObjectId(10),eve_convoy05);  
			GroupAddObject(GroupObjectId(10),eve_convoy06);  
			GroupAddObject(GroupObjectId(10),eve_convoy07);  
			GroupAddObject(GroupObjectId(10),eve_convoy08);  
			SetGroupAI(groupobjectid(10),GROUPAI_MOODSQUAD);
			
			// 20 - Militia Bandits (Convoy Attackers)
			GroupAddObject(GroupObjectID(20),eve_mainforce01);
			GroupAddObject(GroupObjectID(20),eve_mainforce02);
			GroupAddObject(GroupObjectID(20),eve_mainforce03);
			GroupAddObject(GroupObjectID(20),eve_mainforce04);
			GroupAddObject(GroupObjectID(20),eve_mainforce05);
			GroupAddObject(GroupObjectID(20),eve_mainforce06);
			GroupAddObject(GroupObjectID(20),eve_mainforce07);
			GroupAddObject(GroupObjectID(20),eve_mainforce08);
			GroupAddObject(GroupObjectID(20),eve_mainforce09);
			GroupAddObject(GroupObjectID(20),eve_mainforce10);
			GroupAddObject(GroupObjectID(20),eve_mainforce11);
			GroupAddObject(GroupObjectID(20),eve_mainforce12);
			SetGroupAI(groupobjectid(20),GROUPAI_MOODSQUAD);

			// 21 - Relief Group
			GroupAddObject(GroupObjectID(21),eve_relief01);
			GroupAddObject(GroupObjectID(21),eve_relief02);
			SetGroupAI(groupobjectid(21),GROUPAI_MOODSQUAD);
			
			// 23 - Mech Defenders Group
			GroupAddObject(GroupObjectID(23),eve_harass01);
			GroupAddObject(GroupObjectID(23),eve_harass03);
			GroupAddObject(GroupObjectID(23),eve_harass04);
			GroupAddObject(GroupObjectID(23),eve_harass05);
			SetGroupAI(groupobjectid(23),GROUPAI_MOODSQUAD);
			
			cinema_cut = 0;
			
			// Culturals
			//SetTarget(ede_example2,eve_example3);
		
endfunction;
		#include_ <content\ABLScripts\Katie\katie_script.abi>
//------------------------------------------------------------------
//
//	 Main Code
//
//------------------------------------------------------------------
state startState;

	code	
/* MekTek: Shark removed these and replaced them with Katie calls
		SetKillSalvageChance(eve_relief01,50);		// Argus
		SetKillSalvageChance(eve_relief02,50);		// Chimera
*/
		// Eaton 1 specific
			ReliefTime		= 30;	// How long after engagement the Relief come online
			HarassTime		= 30;	// How long after engagement the Harass come online
			ReinforceNum	= 1;	// How many of the main force have to be nailed before reinforced
		//

		UnitStrength = GetCurrentUnitStrength;
		if (UnitStrength > 1200) then
			GroupAddObject(GroupObjectID(20),eve_rearguard01);
			GroupAddObject(GroupObjectID(20),eve_rearguard02);
			ExtraTough = true;
		endif;

		// These are your MetaMission variables, second part is how this mission effects your ratings
			DavionRating 	= GetPreviousDavionFactionRating;
			SteinerRating 	= GetPreviousSteinerFactionRating;
			Nobility 		= GetPreviousNobility;
			Infamy 			= GetPreviousInfamy;
			
			DavionRating	= DavionRating + 1;
			SteinerRating	= SteinerRating - 2;
			Nobility		= Nobility + 0;
			Infamy			= Infamy + 5;
		// 
		
		SetRadarNav(ena_Nav_Alpha,1);
		SetRadarNav(ena_Nav_Beta,2);
		SetRadarNav(ena_Nav_Gamma,3);
		
		RevealNavpoint(ena_Nav_Alpha);
		RevealNavpoint(ena_Nav_Beta);
		RevealNavpoint(ena_Nav_Gamma);
		
		SetNavpoint(ena_Nav_Alpha);

		//setsensorvisibility(eve_example4,false);
		
		checkrange	= 150;		// How far from a nav point before the Betty voice says you've arrived.

		StartMusic("Music\SneakPeek",3.0,3.5,50);

		trans opening;
		//trans won;

endstate;

	
state deadState;
	code
	
endstate;
	
//------------------------------------------------------------------
//
//	 Sit state : description
//
//------------------------------------------------------------------
state sit;

	code

	if IsMusicPlaying == false then
		StartMusic("music\LavaAmbLoop",0.0,3.0,40);
	endif;

// Eaton 1 Specific Triggers
	
// Sound Triggers

	if (not GetGlobalTrigger(2)) then		// A bit more intro
		if (timegreater(1,6)) then	 
			SetGlobalTrigger(2,true);
			PlayVoiceOverOnce("vo\eaton\mi5\MissionOverview",TALKER_SPE);
			PlayLancemateSound("VO\Generic\Roger",1);
		endif;
	endif;
	
	if (not GetGlobalTrigger(4)) then
		if (IsWithin(PlayerMech,GroupObjectID(20),900)) then		// Close to convoy defenders
			SetGlobalTrigger(4,true);
			if (PlayLancemateSound("VO\Generic\Contact900",1)) then
				PlayVoiceOverOnce("vo\eaton\mi5\RogerDemolishers",TALKER_SPE);
			else
				PlayVoiceOverOnce("VO\Generic\GroundUnits",TALKER_CAS);
				PlayVoiceOverOnce("vo\eaton\mi5\RogerDemolishers",TALKER_SPE);
			endif;
		endif;
	endif;
	
	if (not GetGlobalTrigger(5)) then
		if (GroupAllDead(GroupObjectID(21))) then		// Mech Defenders are destroyed
			if (not GetGlobalTrigger(6)) then	// The line refers to the armor
				SetGlobalTrigger(5,true);
				PlayVoiceOverOnce("vo\eaton\mi5\EscortsEliminated",TALKER_SPE);
			else
				SetGlobalTrigger(5,true);
			endif;
		endif;
	endif;
	
	if (not GetGlobalTrigger(6)) then
		if (GroupAllDead(GroupObjectID(20))) then		// Armor is destroyed
			SetGlobalTrigger(6,true);
			PlayVoiceOverOnce("vo\eaton\mi5\ArmorDestoyed",TALKER_SPE);
		endif;
	endif;
	
// Triggers	- Harass Strike Relief

	if (not GetGlobalTrigger(3)) then		// It will take the convoy awhile to call in for support
		if (EnemyEngaged) then
			SetGlobalTrigger(3,true);
			StartTimer(3);		// Reinforcement timer
		endif;
	endif;
	
	if (not Harass) then	// Harassers come in
		if (timegreater(3,HarassTime)) then
			Harass = True;
			PlayVoiceOverOnce("vo\eaton\mi5\ChoppersAirwatch",TALKER_CAS);
			PlayVoiceOverOnce("vo\eaton\mi5\AnyAlliedAircover",TALKER_SPE);
			StartTimer(4);		// Allied air will come in
		endif;
	endif;
	
	if (not Relief) then	// Friendly Air Relief
		if (timegreater(4,15)) then
			PlayVoiceOverOnce("vo\eaton\mi5\AlliedAirIncoming",TALKER_CAS);
			PlayVoiceOverOnce("vo\eaton\mi5\WatchCrosshairs",TALKER_SPE);
			PlayLancemateSound("VO\Generic\Roger",1);
			Relief = True;
		endif;
	endif;
	
// Obj 1 - Stop the Eaton Convoy

	if (not obj1) then
		if (groupalldead(GroupObjectID(20))) and (GroupAllDead(GroupObjectID(21))) then
			SuccessObjective(eob_objective1);
			obj1 = true;
			if (obj2) then
				StartTimer(2);		// End game timer
			endif;
		endif;
	endif;
	
	if (not Obj1Revealed) then
		if (GroupAllDead(GroupObjectID(10))) then		// Can't destroy the entire convoy
			Obj1Revealed = True;
			Loser = True;
			FailObjective(eob_objective1);
			if (not obj2) then
				FailObjective(eob_objective2);
			endif;
			StartTimer(2);		// You lost
		endif;
	endif;
	
// Obj 2 - Destroy all militia forces

	if (not obj2) then
		if (GroupAllDead(GroupObjectID(20))) then
			if (GroupAllDead(GroupObjectID(21))) and GroupAllDead(GroupObjectID(23)) then
				obj2 = true;
				SuccessObjective(eob_objective2);
				if (obj1) then
					HideObjective(eob_objective3);
					StartTimer(2);		// End game timer
				endif;
			endif;
		endif;
	endif;

// Victory and Loss conditions

	if (timegreater(2,5)) then
		  
		if (loser) then
			EndMission(false,5);
			trans lost;
		else
			// Salvage - FAB  7/30
				AddWeaponSalvage("ClanUltraAC10",2);
				AddWeaponSalvage("LRM20",3);
				AddWeaponSalvage("LRM15",4);
				AddWeaponSalvage("LRM10",6);
				AddWeaponSalvage("LRM5",2);
				AddWeaponSalvage("LargeLaser",3);
			//

			SetCurrentInfamy(Infamy);
			SetCurrentNobility(Nobility);
			SetCurrentDavionFactionRating(DavionRating);
			SetCurrentSteinerFactionrating(SteinerRating);
			/////////////////////
			// MISSION SALVAGE //
			/////////////////////
			Katie(eve_relief01,60);
			Katie(eve_relief02,60);

			trans won;	
		endif;
		
	endif;

// Fail the mission when the player dies
	if (isdead(playermech)) then
		KillVoiceOvers;
		switch rand(1,3)
			case 1:
				PlayVoiceOver("VO\Generic\SpecterDestroyed", talker_CAS);
			endcase;
			case 2:
				PlayVoiceOver("VO\Generic\SpecterDestroyed2", talker_CAS);
			endcase;
			case 3:
				PlayVoiceOver("VO\Generic\SpecterDestroyed3", talker_CAS);
			endcase;
		endswitch;
		StartMusic("Music\GenericLoss1",3.0,3.5,70);
		endmission(false,14);
		trans deadstate;
	endif;

// Betty speaks when reaching Nav Point

	if (not alphareached) then
		if (isnavrevealed(ena_nav_alpha)) then
			if (iswithin(playermech,ena_nav_alpha,checkrange)) then
				playvoiceover("VO\generic\navalpha",talker_Bet);
				alphareached = true;
				setnavpoint(ena_nav_beta);
			endif;
		endif;
	endif;

	if (not betareached) then
		if (isnavrevealed(ena_nav_beta)) then
			if (iswithin(playermech,ena_nav_beta,checkrange)) then
				playvoiceover("VO\generic\navbeta",talker_Bet);
				betareached = true;
				//setnavpoint(ena_nav_gamma);
			endif;
		endif;
	endif;
	
	if (not gammareached) then
		if (isnavrevealed(ena_nav_gamma)) then
			if (iswithin(playermech,ena_nav_gamma,checkrange)) then
				playvoiceover("VO\generic\navgamma",talker_Bet);
				gammareached = true;
				//setnavpoint(ena_nav_delta);
			endif;
		endif;
	endif;
	
endstate;

//-----------------------------------------
//HERE THERE BE CINEMAS
//-----------------------------------------

state opening;	  

	code

		if (cinemaskip) then
			KillVoiceOvers;	 
			cinema_cut = 0;	 
			KillTimer(gti_timer_21);
			SetGlobalTrigger(0,true);
			playerai(epl_player0,false);
			StartTimer(1);		// This is the mission start timer
			ResetTimer(1);
			CinemaEnd;		  
			SetCompositingEnabled(True);
			revealobjective(eob_objective1);
			RevealObjective(eob_objective2);
			trans sit;
		endif;
		
		switch (cinema_cut)
				
			case 0:	 

				SetActiveCamera(eca_CameraShip);
				
				SpinUnderCameraB(PlayerMech,PlayerMech,16.0,17.0,true);

				PlayVoiceOverOnce("vo\eaton\mi5\Intro1",TALKER_SPE);
				PlayLancemateSound("VO\Generic\Roger",1);
				PlayVoiceOverOnce("vo\eaton\mi5\Intro2",TALKER_CAS);
				
				cinema_cut = cinema_cut + 1;
			endcase;
			
			case 1:
				if (not IsVoiceOverPlaying) then
					ResetTimer(21);
					Cinema_Cut = Cinema_Cut + 1;
				endif;
			endcase;
			
			case 2:
				if (timegreater(21,2)) then
					KillVoiceOvers;
					cinema_cut = 0;
					KillTimer(gti_timer_21);
					SetGlobalTrigger(0,true);
					playerai(epl_player0,false);
					StartTimer(1);		// This is the mission start timer
					ResetTimer(1);
					CinemaEnd;
					revealobjective(eob_objective1);	// Reveal the objectives
					RevealObjective(eob_objective2);
					SetCompositingEnabled(True);
					
					trans sit;
				endif;
			endcase;
				
		endswitch;
	
endstate;

state won;	  

	code
		
		if (cinemaskip) then
			cinema_cut = 20;	
			playerai(epl_player0,false);
			KillVoiceOvers;
			Endmission(true,1); 
		endif;

		switch cinema_cut
		
			case 0:	
				CinemaStart;
				setactivecamera (eca_cameraship);
				PlayerAi(PlayerMech,true);
				FadeToBlack(0.0);
				
				//Obj1 = True;
				
				if (not IsDead(eve_convoy01)) then
					Shutdown(eve_convoy01);
					GetLocation(ena_end1,mynav);
					Teleport(eve_convoy01,mynav);
					SetRotation(eve_convoy01,-10);
				endif;
				if (not IsDead(eve_convoy02)) then
					Shutdown(eve_convoy02);
					GetLocation(ena_end2,mynav);
					Teleport(eve_convoy02,mynav);
					SetRotation(eve_convoy02,5);
				endif;
				if (not IsDead(eve_convoy03)) then
					Shutdown(eve_convoy03);
					GetLocation(ena_end3,mynav);
					Teleport(eve_convoy03,mynav);
					SetRotation(eve_convoy03,10);
				endif;
				if (not IsDead(eve_convoy04)) then
					Shutdown(eve_convoy04);
					GetLocation(ena_end4,mynav);
					Teleport(eve_convoy04,mynav);
					SetRotation(eve_convoy04,10);
				endif;
				if (not IsDead(eve_convoy05)) then
					Shutdown(eve_convoy05);
					GetLocation(ena_end5,mynav);
					Teleport(eve_convoy05,mynav);
					SetRotation(eve_convoy05,0);
				endif;
				if (not IsDead(eve_convoy06)) then
					Shutdown(eve_convoy06);
					GetLocation(ena_end6,mynav);
					Teleport(eve_convoy06,mynav);
					SetRotation(eve_convoy01,5);
				endif;
				Shutdown(PlayerMech);
				GetLocation(ena_endp,mynav);
				Teleport(PlayerMech,mynav);
				SetRotation(PlayerMech,-90);
				Startup(PlayerMech);
				StartMusic("Music\GenericVictory1",3.0,3.5,50);					 
				Cinema_cut=Cinema_cut+1;
				StartTimer(21);
			endcase;
			
			case 1:
				if (timegreater(21,1)) then
					FadeFromBlack(5.0);
					PlayVoiceOver("VO\OneSecond",TALKER_SPE);
					PlayVoiceOverOnce("vo\eaton\mi5\Close1",TALKER_SPE);
					PlayVoiceOver("VO\OneSecond",TALKER_SPE);
					PlayVoiceOver("VO\OneSecond",TALKER_SPE);
					PlayVoiceOverOnce("vo\eaton\mi5\Close2",TALKER_CAS);
					SetCompositingEnabled(False);
					
					Targetfollowobject(PlayerMech);
					Camerafollowobject(ena_end2);
					Targetoffset(10.0,4.0,0.0,0.0,true);
					Cameraoffset(0.0,80.0,-400.0,0.0,true);
					Cameraoffset(0.0,40.0,-20.0,13.0,true);
					
					/*Targetfollowobject(PlayerMech);
					Camerafollowobject(ede_camera);
					Targetoffset(0.0,4.0,0.0,0.0,true);
					Cameraoffset(-70.0,20.0,-150.0,0.0,true);
					Cameraoffset(-70.0,20.0,40.0,16.0,true);*/
					
					playerai(epl_player0,true);
					Cinema_cut=Cinema_cut+1;
				endif;
			endcase;
			
			case 2:
				if (not IsVoiceOverPlaying) then
					Cinema_cut=Cinema_cut+1;
				endif;
			endcase;
			
			case 3:
				Cinema_Cut = 20;
				endmission(true,2);
			endcase;			
			
		endswitch;
	
endstate;

state lost;

		code
		
		switch cinema_cut
		
			case 0:	
			
			endcase;
			
			case 1:
			
			endcase;
			
		endswitch;
	
endstate;

endfsm.
//******************************************************************
