//*********************************************************************************

fsm Talon04_CountryLife : integer;

//------------------------------------------------------------------
//
//	 Constant Definitions
//
//------------------------------------------------------------------
	 const

		#include_ <content\ABLScripts\mwconst.abi>
		#include_ <content\ABLScripts\Katie\katie_consts.abi> // <- mektek
//------------------------------------------------------------------
//
//	 Type Definitions
//
//------------------------------------------------------------------
	 type

		#include_ <content\ABLScripts\mwtype.abi>

//------------------------------------------------------------------
//
//	 Variable Declarations
//
//------------------------------------------------------------------
	  var

		eternal integer		playermech;			// This is the variable for the player's 'Mech
			
		// Standard Nav and Objective Variables
		static boolean		alphareached;		// Nav Alpha reached
		static boolean		betareached;		// Nav Beta reached
		static boolean		gammareached;		// Nav Gamma reached
		static boolean		deltareached;		// Nav Delta reached
												//
		static boolean		alpharevealed;		// Nav Alpha is shown
		static boolean		betarevealed;		// Nav Beta is shown
		static boolean		gammarevealed;		// Nav Gamma is shown
		static boolean		deltarevealed;		// Nav Delta is shown
		static integer		checkrange;			// How far from the Nav Point you have to be											
												//
		eternal boolean		obj1;				// This indicates whether Objective 1 was completed
		eternal boolean		obj2;				// This indicates whether Objective 2 was completed
		eternal boolean		obj3;				// This indicates whether Objective 3 was completed
		eternal boolean		obj4;				// This indicates whether Objective 4 was completed
		eternal boolean		obj5;				// This indicates whether Objective 5 was completed
												//
		static boolean		obj1revealed;		// This indicates that Objective 1 has been revealed
		static boolean		obj2revealed;		// This indicates that Objective 2 has been revealed
		static boolean		obj3revealed;		// This indicates that Objective 3 has been revealed
		static boolean		obj4revealed;		// This indicates that Objective 4 has been revealed
		static boolean		obj5revealed;		// This indicates that Objective 5 has been revealed
		//	
		
		static integer		cinema_cut;			// This variable keeps track of what scene a cinema is on.
		static locpoint		temploc;			// Used for commands that need objects converted to locpoint format
		static boolean		loser;				// You have lost the mission
 		eternal integer 	DavionRating;		// This is your Davion rating
		eternal integer 	SteinerRating;		// This is your Steiner rating
		eternal integer 	Nobility;			// This is your Nobility rating
		eternal integer 	Infamy;				// This is your Infamy rating
 
		// Trigger States
		eternal boolean		DropshipPower;		// The dropship powerup toggle
		eternal boolean		BlackOut;			// When true then the ECCM is no longer taking out sensors and communications
		static integer		GiveorTake;			// Makes it so that the ECCM is triggered with some degree of randomness
		eternal boolean		ReinforceNow;		// Reinforcements come on the map
		eternal boolean		TimetoDie;			// If both of the 'Mech Patrols are dead, then if you are a tank out of range, die
		
		eternal boolean		CoptersFlee;		// Copters flee after a certain point
		eternal boolean		ScoutsFlee;			// Scouts run away
		
//------------------------------------------------------------------
//
//	 Init Function	(automatically run first time through)
//
//------------------------------------------------------------------

function init;
	var
		integer i;

	code

			playermech = getplayervehicle(epl_player0);

			// 10 - Davion Communications Center
			GroupAddObject(GroupObjectID(10),ebu_Command_Center);
			GroupAddObject(GroupObjectID(10),ebu_Power_Plant);
			GroupAddObject(GroupObjectID(10),ebu_Communications_HQ);
			GroupAddObject(GroupObjectID(10),ebu_satelite_control);
			GroupAddObject(GroupObjectID(10),ebu_satelite_control2);
			SetGroupAI(GroupObjectID(10),GROUPAI_MOODSQUAD);

			// 11 - Secondary buildings
			GroupAddObject(GroupObjectID(11),ebu_airportbldg);
			GroupAddObject(GroupObjectID(11),ebu_Barracks);
			GroupAddObject(GroupObjectID(11),ebu_factory1);
			GroupAddObject(GroupObjectID(11),ebu_factory2);
			GroupAddObject(GroupObjectID(11),ebu_factory3);
			GroupAddObject(GroupObjectID(11),ebu_factory5);
			GroupAddObject(GroupObjectID(11),ebu_factory7);
			GroupAddObject(GroupObjectID(11),ebu_fuel_tanks_0001);
			GroupAddObject(GroupObjectID(11),ebu_fuel_tanks);
			GroupAddObject(GroupObjectID(11),ebu_Offices);
			GroupAddObject(GroupObjectID(11),ebu_repair_platform);
			GroupAddObject(GroupObjectID(11),ebu_vehicle_hangar2);

			// 20 - Flying objects coming in
			GroupAddObject(GroupObjectID(20),eve_air01);
			GroupAddObject(GroupObjectID(20),eve_air02);
			GroupAddObject(GroupObjectID(20),eve_air03);
			GroupAddObject(GroupObjectID(20),eve_air04);
			GroupAddObject(GroupObjectID(20),eve_air05);
			GroupAddObject(GroupObjectID(20),eve_air06);
			GroupAddObject(GroupObjectID(20),eve_air07);
			GroupAddObject(GroupObjectID(20),eve_air08);
			GroupAddObject(GroupObjectID(20),eve_air10);
			GroupAddObject(GroupObjectID(20),eve_air11);
			GroupAddObject(GroupObjectID(20),eve_air12);
			GroupAddObject(GroupObjectID(20),eve_air13);
			GroupAddObject(GroupObjectID(20),eve_air14);
			GroupAddObject(GroupObjectID(20),eve_air15);
			SetGroupAI(GroupObjectID(20),GROUPAI_MOODSQUAD);

			// 21 - First Wave of 'Mechs
			GroupAddObject(GroupObjectID(21),eve_swave01);
			GroupAddObject(GroupObjectID(21),eve_swave02);
			GroupAddObject(GroupObjectID(21),eve_swave03);
			GroupAddObject(GroupObjectID(21),eve_swave04);
			SetGroupAI(GroupObjectID(21),GROUPAI_MOODSQUAD);

			// 22 - Assault 'Mechs
			GroupAddObject(GroupObjectID(22),eve_nwave01);
			GroupAddObject(GroupObjectID(22),eve_nwave02);
			GroupAddObject(GroupObjectID(22),eve_nwave06);
			GroupAddObject(GroupObjectID(22),eve_nwave08);
			GroupAddObject(GroupObjectID(22),eve_nwave12);
			
			// 23 - Third Wave of 'Mechs
			GroupAddObject(GroupObjectID(23),eve_nwave01);
			GroupAddObject(GroupObjectID(23),eve_nwave02);
			GroupAddObject(GroupObjectID(23),eve_nwave03);
			GroupAddObject(GroupObjectID(23),eve_nwave04);
			GroupAddObject(GroupObjectID(23),eve_nwave05);
			GroupAddObject(GroupObjectID(23),eve_nwave06);
			GroupAddObject(GroupObjectID(23),eve_nwave07);
			GroupAddObject(GroupObjectID(23),eve_nwave08);
			GroupAddObject(GroupObjectID(23),eve_nwave09);
			GroupAddObject(GroupObjectID(23),eve_nwave10);
			GroupAddObject(GroupObjectID(23),eve_nwave11);
			GroupAddObject(GroupObjectID(23),eve_nwave12);
			SetGroupAI(GroupObjectID(23),GROUPAI_MOODSQUAD);

			// 24 - Second wave of Copters
			GroupAddObject(GroupObjectID(24),eve_air09);
			GroupAddObject(GroupObjectID(24),eve_air10);
			GroupAddObject(GroupObjectID(24),eve_air11);
			GroupAddObject(GroupObjectID(24),eve_air12);
			GroupAddObject(GroupObjectID(24),eve_air13);
			GroupAddObject(GroupObjectID(24),eve_air14);
			GroupAddObject(GroupObjectID(24),eve_air15);

			cinema_cut = 0;

			// Culturals
			//SetTarget(ede_example2,eve_example3);
		
endfunction;
		#include_ <content\ABLScripts\Katie\katie_script.abi> // <-- mektek
//------------------------------------------------------------------
//
//	 Main Code
//
//------------------------------------------------------------------
state startState;

	code	
/* MekTek: Shark removed these and replaced them with Katie calls
			SetKillSalvageChance(eve_nwave12,100);		// Awesome
			SetKillSalvageChance(eve_nwave01,25);		// Atlas
			SetKillSalvageChance(eve_nwave05,25);		// Thanatos
			SetKillSalvageChance(eve_nwave07,25);		// Catapult
			SetKillSalvageChance(eve_nwave09,25);		// Argus
			SetKillSalvageChance(eve_swave02,100);		// Hellspawn
*/
		// These are your MetaMission variables, second part is how this mission effects your ratings
			DavionRating 	= GetPreviousDavionFactionRating;
			SteinerRating 	= GetPreviousSteinerFactionRating;
			Nobility 		= GetPreviousNobility;
			Infamy 			= GetPreviousInfamy;
			
			DavionRating	= DavionRating + 10;
			SteinerRating	= SteinerRating - 10;
			Nobility		= Nobility + 5;
			Infamy			= Infamy + 0;
		// 
		
		SetRadarNav(ena_Nav_Alpha,1);
		SetRadarNav(ena_Nav_Beta,2);
		SetRadarNav(ena_Nav_Gamma,3);
		RevealNavpoint(ena_Nav_Alpha);
		SetNavpoint(ena_Nav_Alpha);

		//setsensorvisibility(eve_example4,false);
		
		checkrange	= 150;		// How far from a nav point before the Betty voice says you've arrived.

		StartMusic("Music\NextMove",3.0,3.5,50);
		
		trans opening;

endstate;

	
state deadState;
	code
	
endstate;
	
//------------------------------------------------------------------
//
//	 Sit state : description
//
//------------------------------------------------------------------
state sit;

	code

	if IsMusicPlaying == false then
		StartMusic("music\ArcticAmbLoop",0.0,3.0,40);
	endif;

// Sound Triggers

	if (not GetGlobalTrigger(1)) then
		if (IsWithin(PlayerMech,GroupObjectID(20),900)) then	// Copters near
			SetGlobalTrigger(1,true);
			if (PlayLancemateSound("VO\Generic\Choppers",1)) then
				PlayVoiceOverOnce("vo\talon\mi4\EngageAndDestroy",TALKER_SPE);
			else
				PlayVoiceOverOnce("vo\generic\EnemyAir",TALKER_CAS);
				PlayVoiceOverOnce("vo\talon\mi4\EngageAndDestroy",TALKER_SPE);
			endif;
			PlayVoiceOverOnce("vo\talon\mi4\NewSquadofChoppers",TALKER_CAS);
		endif;
	endif;
	
	/*if (not GetGlobalTrigger(2)) then		CUT - because the damned choppers won't break off
		if (GroupNumDead(GroupObjectID(24))>0) then		// Second aircraft pulls back
			SetGlobalTrigger(2,true);
			CoptersFlee = True;
			//PlayVoiceOver("vo\ThreeSecond",TALKER_SPE);
			PlayVoiceOverOnce("vo\talon\mi4\ChoppersPullingBack",TALKER_SPE);
		endif;
	endif;*/
	
	if (not GetGlobalTrigger(3)) then
		if (IsWithin(PlayerMech,GroupObjectID(21),500)) then	// Lancemates spot the enemy
			SetGlobalTrigger(3,true);
			if (PlayLancemateSound("VO\Generic\VisContact",1)) then
				PlayVoiceOverOnce("vo\talon\mi4\TheyMustHaveECMs",TALKER_CAS);
			else				
				PlayVoiceOverOnce("vo\generic\EnemyMech3",TALKER_CAS);
				PlayVoiceOver("vo\onesecond",TALKER_SPE);
				PlayVoiceOverOnce("vo\talon\mi4\TheyMustHaveECMs",TALKER_CAS);
			endif;
			PlayVoiceOverOnce("vo\talon\mi4\ScoutLance",TALKER_SPE);
		endif;
	endif;
	
	if (not GetGlobalTrigger(4)) then
		if (GroupNumDead(GroupObjectID(21))>0) or (IsWithin(PlayerMech,GroupObjectID(22),1200)) then		// Scouts pull off
			SetGlobalTrigger(4,true);
			//PlayVoiceOver("vo\ThreeSecond",TALKER_SPE);
			PlayLancemateSound("VO\Generic\EnemyBreakOff",1);
			PlayVoiceOverOnce("vo\talon\mi4\TheyRegrouping",TALKER_SPE);
			PlayVoiceOverOnce("vo\talon\mi4\IWantFiringLineAtNavBeta",TALKER_SPE);
			RevealNavpoint(ena_Nav_Beta);
			SetNavpoint(ena_Nav_Beta);
			ScoutsFlee = true;
		endif;
	endif;
	
	if (not GetGlobalTrigger(5)) then
		if (IsWithin(PlayerMech,GroupObjectID(22),800)) then		// Once the assaults get close, everyone come back
			SetGlobalTrigger(5,true);
			PlayVoiceOverOnce("vo\talon\mi4\ThreeIncomingLances",TALKER_CAS);
			PlayVoiceOverOnce("vo\talon\mi4\TiemToEarnPay",TALKER_SPE);
			StartMusic("Music\Floodgate",3.0,3.5,50);
			PlayLancemateSound("VO\Generic\Excited",1);
			ScoutsFlee = False;
			CoptersFlee = False;
		endif;
	endif;
	// ggl 5 three lances incoming
	
	if (GetGlobalTrigger(2)) then
		if (not GetGlobalTrigger(6)) then
			if (IsWithin(PlayerMech,GroupObjectID(24),900)) then
				SetGlobalTrigger(6,true);
				PlayVoiceOverOnce("vo\talon\mi4\ChoppersReengaging",TALKER_SPE);
			endif;
		endif;
	endif;
	
	if (not GetGlobalTrigger(7)) then
		if (GroupAllDead(GroupObjectID(20))) then	// Choppers nixed
			SetGlobalTrigger(7,true);
			PlayVoiceOverOnce("vo\talon\mi4\ChoppersDestroyed",TALKER_SPE);
		endif;
	endif;

// Talon 4 Specific Triggers

// Obj 1 - Defend the Davion communications center

	if (not Obj1) then
		if (GroupAllDead(GroupObjectID(10))) then
			FailObjective(eob_objective1);
			FailObjective(eob_objective2);
			Obj1 = True;
			StartTimer(2);		// You've lost
			Loser = True;
		endif;
	endif;

// Obj 2 - Defend all enemy 'Mechs

	if (not Obj2) then
		if (GroupAllDead(GroupObjectID(22))) then
			Obj2 = True;
			SuccessObjective(eob_objective1);
			SuccessObjective(eob_objective2);
			
			if (not Obj3) then
				AddMissionMoneyToBalance(950000);
				SuccessObjective(eob_objective3);
			endif;
			
			PlayVoiceOverOnce("vo\talon\mi4\ForceNeutralized",TALKER_SPE);
			StartTimer(2);		// You're a winner!					
		endif;
	endif;
	
// Obj 3 - Defend Secondary Objectives

	if (not Obj3) then
		if (GroupNumDead(GroupObjectID(11)) > 2) then
			Obj3 = True;
			FailObjective(eob_objective3);
		endif;
	endif;

// Victory and Loss conditions

	if (timegreater(2,5)) then
		  
		// Salvage - FAB  9/5
			AddWeaponSalvage("SRM6",1);
			AddWeaponSalvage("MediumPulseLaser",2);
			AddWeaponSalvage("LRM20",1);
			AddWeaponSalvage("PPC",2);
			AddWeaponSalvage("MediumLaser",2);
			/////////////////////
			// MISSION SALVAGE //
			/////////////////////
			Katie(eve_swave01,60);
			Katie(eve_swave02,50);
			Katie(eve_swave03,40);
			Katie(eve_swave04,30);

			Katie(eve_nwave01,50);
			Katie(eve_nwave02,40);
			Katie(eve_nwave03,30);
			Katie(eve_nwave04,20);
			Katie(eve_nwave05,30);
			Katie(eve_nwave06,40);
			Katie(eve_nwave07,50);
			Katie(eve_nwave08,40);
			Katie(eve_nwave09,30);
			Katie(eve_nwave10,20);
			Katie(eve_nwave11,35);
			Katie(eve_nwave12,50);
		
		if (loser) then
			EndMission(false,5);
			trans lost;
		else
			
			SetCurrentInfamy(Infamy);
			SetCurrentNobility(Nobility);
			SetCurrentDavionFactionRating(DavionRating);
			SetCurrentSteinerFactionrating(SteinerRating);
		
			trans won;	
		endif;
		
	endif;

// Fail the mission when the player dies
	if (isdead(playermech)) then
		KillVoiceOvers;
		switch rand(1,3)
			case 1:
				PlayVoiceOver("VO\Generic\SpecterDestroyed", talker_CAS);
			endcase;
			case 2:
				PlayVoiceOver("VO\Generic\SpecterDestroyed2", talker_CAS);
			endcase;
			case 3:
				PlayVoiceOver("VO\Generic\SpecterDestroyed3", talker_CAS);
			endcase;
		endswitch;
		StartMusic("Music\GenericLoss1",3.0,3.5,70);
		endmission(false,14);
		trans deadstate;
	endif;

// Betty speaks when reaching Nav Point

	if (not alphareached) then
		if (isnavrevealed(ena_nav_alpha)) then
			if (iswithin(playermech,ena_nav_alpha,checkrange)) then
				playvoiceover("VO\generic\navalpha",talker_Bet);
				alphareached = true;
				setnavpoint(ena_nav_beta);
			endif;
		endif;
	endif;

	if (not betareached) then
		if (isnavrevealed(ena_nav_beta)) then
			if (iswithin(playermech,ena_nav_beta,checkrange)) then
				playvoiceover("VO\generic\navbeta",talker_Bet);
				betareached = true;
				//setnavpoint(ena_nav_gamma);
			endif;
		endif;
	endif;

endstate;

//-----------------------------------------
//HERE THERE BE CINEMAS
//-----------------------------------------

state opening;	  

	code

		if (cinemaskip) then
			KillVoiceOvers;	 
			cinema_cut = 0;	 
			KillTimer(gti_timer_21);
			SetGlobalTrigger(0,true);
			playerai(epl_player0,false);
			StartTimer(1);		// This is the mission start timer
			ResetTimer(1);
			CinemaEnd;		  
			SetCompositingEnabled(True);
			revealobjective(eob_objective1);
			RevealObjective(eob_objective2);
			RevealObjective(eob_objective3);
			trans sit;
		endif;
		
		switch (cinema_cut)

			case 0:
				CinemaStart;
				SetActiveCamera(eca_CameraShip);
				
				SpinUnderCamera(PlayerMech,PlayerMech,16.0,17.0,true);
				PlayVoiceOverOnce("vo\talon\mi4\Intro1",TALKER_CAS);
				PlayVoiceOverOnce("vo\talon\mi4\Intro2",TALKER_SPE);
				cinema_cut = cinema_cut + 1;
				
				starttimer(21);			// This is the cineractive counter.
				resettimer(21);
			endcase;
			
			case 1:
				if (not IsVoiceOverPlaying) then
					ResetTimer(21);
					Cinema_Cut = Cinema_Cut + 1;
				endif;
			endcase;
			
			case 2:
				if (timegreater(21,2)) then
					KillVoiceOvers;
					cinema_cut = 0;
					KillTimer(gti_timer_21);
					SetGlobalTrigger(0,true);
					playerai(epl_player0,false);
					StartTimer(1);		// This is the mission start timer
					ResetTimer(1);
					CinemaEnd;
					revealobjective(eob_objective1);	// Reveal the objectives
					RevealObjective(eob_objective2);
					RevealObjective(eob_objective3);
					SetCompositingEnabled(True);
					
					trans sit;
				endif;
			endcase;
				
		endswitch;
	
endstate;

state won;	  

	code
		
		if (cinemaskip) then
			cinema_cut = 20;	
			playerai(epl_player0,false);
			KillVoiceOvers;
			Endmission(true,1); 
		endif;

		switch cinema_cut
		
			case 0:	
				StartMusic("Music\GenericVictory1",3.0,3.5,50);					 
				PlayVoiceOverOnce("vo\talon\mi4\Close1",TALKER_CAS);
				PlayVoiceOverOnce("vo\talon\mi4\Close2",TALKER_SPE);
				PlayLancemateSound("VO\Generic\Amused",1);
				SetCompositingEnabled(False);
				CinemaStart;
				setactivecamera (eca_cameraship);
				Targetfollowobject(playermech);
				Camerafollowobject(playermech);
				Targetoffset(0.0,4.0,0.0,0.0,false);
				Cameraoffset(-30.0,125.0,75.0,0.0,false);
				Cameraoffset(60.0,20.0,25.0,14.0,false);
				playerai(epl_player0,true);
				Cinema_cut=Cinema_cut+1;
			endcase;
			
			case 1:
				if (not IsVoiceOverPlaying) then
					EndMission(true,2);
					Cinema_Cut = Cinema_Cut + 1;
				endif;
			endcase;			
			
		endswitch;
	
endstate;

state lost;

		code
		
		switch cinema_cut
		
			case 0:	
			
			endcase;
			
			case 1:
			
			endcase;
			
		endswitch;
	
endstate;

endfsm.
//******************************************************************
