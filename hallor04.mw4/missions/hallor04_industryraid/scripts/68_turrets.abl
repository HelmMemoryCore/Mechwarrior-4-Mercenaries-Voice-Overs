
//------------------------------------------------------------------
// NOTE: The fsm name below MUST be changed in order for the
// script to be considered a unique entity!

fsm turrets : integer;


//------------------------------------------------------------------

// Generic_Turret:
//   This shoots anything that comes near, but never moves.
//   It can be used for turrets.

//------------------------------------------------------------------



//------------------------------------------------------------------
//     Constants
//------------------------------------------------------------------

	const
		#include_ <content\ABLScripts\mwconst.abi>

//------------------------------------------------------------------
//     Types
//------------------------------------------------------------------

	type
		#include_ <content\ABLScripts\mwtype.abi>
	

//------------------------------------------------------------------
//     Variables
//------------------------------------------------------------------

	var
		static integer			piloting; 			// piloting skill
		static integer			gunnery;				// Gunnery skill/chance to hit
		static real				minDelay;				// Minimum amount of time I will wait between shots once a weapon is reloaded
		static real				maxDelay;				// Maximum amount of time I will wait between shots once a weapon is reloaded
		static integer 			eliteLevel;			// Elite level.  This helps determine tactics, defensive manuevers, opportunity fire
																				// and other things.  It indicates a general level of combat experience.

 		static integer			isShotRadius;		// How far away from me will I detect an enemy shot?
		static integer			findTypes;			// What kind of enemies to look for

		static integer			mood;						// My default mood.



		static integer			groupNumber;		// The group number the turrets are in.
		static integer			memInt9;			// holds the value of memory Integer 9
		static integer			attackRange;		// At what range do I start shooting?
		static integer			withdrawRange;		// At what range do I STOP attacking?
		static ObjectID			MyTower;			// My control tower: if destroyed, I stop firing
		static integer			skillLevel;			// 1-10, the new skill level variable

//------------------------------------------------------------------
//     Init: my initialization function
//------------------------------------------------------------------

function Init;
	code
		// script-specific variables
		attackRange				= 600;
		withdrawRange			= 1050;

		MyTower					= ebu_ObjectiveBuilding01;	// the Turret control building/vehicle
		groupNumber				= 10;						// The Group the turrets are in
		skillLevel				= 3;						// 1 - 10
		memInt9 				= GetMemoryInteger(me, 9);	// Value of memory integer number 9

		// driver settings
		
		piloting					= 30; 				// piloting skill
		gunnery						= 30;
		minDelay					= 2;
		maxDelay					= 5;
		eliteLevel					= 30;
		isShotRadius				= 120;
		mood						= NEUTRAL_START;
		findTypes					= FT_DEFAULT;
		
		

endfunction;

//------------------------------------------------------------------
//	StartState: my initial state
//------------------------------------------------------------------

state StartState;

	code
		//SetFiringDelay				(ME,minDelay,maxDelay);				
		//SetIgnoreFriendlyFire			(ME,true);							
		//SetIsShotRadius				(ME,isshotradius);					
		//SetEntropyMood				(ME,mood);							
		//SetCurMood					(ME,mood);							
		//SetSkillLevel					(ME,piloting,gunnery,elitelevel); 	
		
		AssignSkill(skillLevel);
		
		trans WaitState; 	
endstate;			

//------------------------------------------------------------------
//	WaitState: wait for someone to come near
//------------------------------------------------------------------

state WaitState;
	code
		if (FindEnemy(attackRange,findTypes)) then
			trans AttackState;
		endif;

		if ( isShot ( me ) ) then
			setTarget(me, whoShot(me) );
			attackRange	 = 1000;
			if (not obj1) then
				triggerAlarm = true;
			endif;
			trans AttackState;
		endif;
		
		if (MyTower <> NO_UNIT) then
			if ( IsDead(MyTower) ) then
				trans TowerGoneState;
			endif;
		endif;
		
		if isDead(me) then
			trans deadstate;
		endif;

		OrderMoveLookOut;
endstate;

//------------------------------------------------------------------
//	AttackState: let's see what's around, and destroy it
//------------------------------------------------------------------

state AttackState;
	code
		
		OrderAttackTactic(TACTIC_SHOOT_ONLY,true);
		
		if (LeaveAttackState(withdrawRange)) then
			OrderStopAttacking;
			SetTarget(ME,NO_UNIT);
			trans WaitState;
		endif;

		if (MyTower <> NO_UNIT) then
			if ( IsDead(MyTower) ) then
				trans TowerGoneState;
			endif;
		endif;

		if isDead(me) then
			trans deadstate;
		endif;
		
endstate;

//------------------------------------------------------------------
//	TowerGoneState: my control tower is destroyed; I can do nothing
//------------------------------------------------------------------

state TowerGoneState;
	code
		SetTarget(ME,NO_UNIT);
		OrderStopAttacking;
		ClearMoveOrder(ME);
		SetTargetDesirability(ME,-1);
		SetSensorVisibility(ME,FALSE);
		
		//GroupRemoveObject(GroupObjectID(groupNumber), me);//Comment out if turret should remain in group when shut down
		
		trans deadstate;
		
endstate;

//------------------------------------------------------------------
//	DeadState: OK, I kicked the bucket.
//------------------------------------------------------------------

state DeadState;
	code
	
	if isDead(me) and (not obj1) then
		triggerAlarm = true;
	endif;


endstate;

endfsm.
