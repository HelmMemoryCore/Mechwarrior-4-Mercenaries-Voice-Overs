//*********************************************************************************

fsm H04_Cobras : integer;		

//------------------------------------------------------------------
//
//     Constant Definitions
//
//------------------------------------------------------------------
     const

		#include_ <content\ABLScripts\mwconst.abi>
//------------------------------------------------------------------
//
//     Type Definitions
//
//------------------------------------------------------------------
     type

		#include_ <content\ABLScripts\mwtype.abi>

//------------------------------------------------------------------
//
//     Variable Declarations
//
//------------------------------------------------------------------
	var
		static integer			attackRange;	// At what range do I start shooting?
		static integer			withdrawRange;	// At what range do I stop attacking?
		static integer			skillLevel;		// 1 - 10
		static integer			memInt9;		// value of Memory Integer number 9
		static boolean			pathEndReached;

		static ObjectID			unitPath;		// the path the units follow
		static LocPoint			spawnPoint;
		static Boolean			shotOnce;

 function Init;
	code
		// script-specific variables
		attackRange				= 800;
		withdrawRange			= attackRange * 3 / 2;
		skillLevel				= 3;						// 1 - 10
		memInt9 				= GetMemoryInteger(me, 9);	// Value of memory integer number 9

		unitPath = epa_PatrolPath;
		pathEndReached = false;

		shotOnce = false;		
		GetLocation(me, spawnPoint);
		shutdown(me);
		teleportToHell(me);
		

endfunction;
		
state StartState;

	code
	
	AssignSkill(skillLevel);
	trans WaitforLaunch; 	

endstate;

state WaitforLaunch;
	
	code
	
		if obj1 or obj2 or obj3 or obj4 then
			teleport(me, spawnPoint);
			startup(me);
			trans sit;
		endif;
		
		if triggerAlarm then
			AssignSkill(skillLevel+1);
			teleport(me, spawnPoint);
			startup(me);
			trans sit;
		endif;
	
endstate;		

state sit;
	code
		if (not pathEndReached) then
			if OrderMoveResumePatrol(unitPath, 200, MOVE_NOCYCLE, true, true) then
				pathEndReached = true;
			endif;
		else
			OrderMoveLookout;
		endif;
		
		if ( SetMyTarget(attackRange) ) then
			trans AttackState;
		endif;
		
		if isdead(me) then
			trans deadState;
		endif;
		
		if (obj5Revealed and (not shotOnce)) then		//Cobras now run away
			clearMoveOrder(me);
			orderStopAttacking;
			settarget(me, NO_UNIT);
			unitPath = epa_RetreatPath;
			pathEndReached = false;
			resetTimer(8);
			trans retreat;
		endif;
		
endstate;

state AttackState;

	code
	
		if( leaveAttackState(withdrawrange) ) then
			orderStopAttacking;
			settarget(me, NO_UNIT);
			trans sit;
		else;
			OrderAttackTactic(TACTIC_PICK_BEST, true);
		endif;
		
		if isdead(me) then
			trans deadState;
		endif;
		
		if (obj5Revealed and (not shotOnce)) then		//Cobras now run away
			clearMoveOrder(me);
			orderStopAttacking;
			settarget(me, NO_UNIT);
			unitPath = epa_RetreatPath;
			pathEndReached = false;
			resetTimer(8);
			trans retreat;
		endif;
			
endstate;

state retreat;
	var
		static ObjectID who_shot;
	code
		if pathEndReached then
			//unitPath = epa_PatrolPath;
			//pathEndReached = false;
			//shotOnce = true;
			//clearMoveOrder(me);
			//trans sit;
			//orderMoveLookOut;
			shutDown(me);
			teleportToHell(me);
		else
			if OrderMoveResumePatrol(unitPath, 200, MOVE_NOCYCLE, true, false) then
				pathEndReached = true;
			endif;
		endif;
		
		if TimeGreater(8, 7) then
			if (isShot(me) or IsRammed(me)) then
				who_shot = whoShot(me);
				if (who_shot == NO_UNIT) then
					who_shot = whoRammed(me);
				endif;
				if (who_shot == playerMech) then
					if (not shotOnce) then
						PlayVoiceOverOnce("VO\Halloran\MI4\NoQuarrel", talker_DUN);
						PlayVoiceOverOnce("VO\Halloran\MI4\IToldYouSo", talker_SPE);
						shotOnce = true;
						resetTimer(8);
					else
						unitPath = epa_PatrolPath;
						pathEndReached = false;
						clearMoveOrder(me);
						trans sit;
					endif;
				endif;
			endif;
		endif;
	
endstate;

state deadstate;
	code
		
endstate;

endfsm.
