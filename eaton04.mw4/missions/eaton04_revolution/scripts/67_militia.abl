
//------------------------------------------------------------------
// NOTE: The fsm name below MUST be changed in order for the
// script to be considered a unique entity!
//------------------------------------------------------------------

fsm Militia : integer;


//------------------------------------------------------------------
// Moving AI:
//   This script controls movers that do not go aggressive.
//   MILITIA - This script controls the Steiner militia, the enemy of the PCs.
//------------------------------------------------------------------

//------------------------------------------------------------------
//     Constants
//------------------------------------------------------------------

	const
		#include_ <content\ABLScripts\mwconst.abi>

//------------------------------------------------------------------
//     Types
//------------------------------------------------------------------

	type
		#include_ <content\ABLScripts\mwtype.abi>
	
//------------------------------------------------------------------
//     Variables
//------------------------------------------------------------------

	var
		
    // Standard Mech and Vehicle variables
        static integer			attackRange;		// At what range do I start shooting?
		static integer			withdrawRange;		// At what range do I withdraw from combat completely?
                                                    //
		static integer			SkillLevel;			// Used by Assign Skill for the difficulty of an enemy
		static integer			piloting;			// Piloting skill
		static integer			gunnery;			// Gunnery skill/chance to hit
		static real				minDelay;			// Minimum amount of time I will wait between shots once a weapon is reloaded
		static real				maxDelay;			// Maximum amount of time I will wait between shots once a weapon is reloaded
		static integer 			eliteLevel;			// Elite level.  This helps determine tactics, defensive manuevers, opportunity fire
													// and other things.  It indicates a general level of combat experience.
		static integer			attackThrottle;		// My attack throttle
		static integer			mytactic;			// Combat tactic I use - 0 means standard attack
		static integer			findTypes;			// What kind of enemies to look for
													//
        static integer			path;	            // What path to take.
		static integer			myobject;			// Where this unit will head to
        static integer			speed;	            // Speed on the path.
		static integer			MyGroup;			// Which MemoryInteger you work for
		static integer			GroupNumber;		// What's my group -- i.e. I will use GroupObjectID(groupNumber) to identify my group
		static integer			formationType;		// What kind of formation am I in?
		static integer			formationDensity;	// How densely are we packed?
		static integer			moveType;			// How I move along the path
                                                    //
 		static integer			isShotRadius;		// How far away from me will I detect an enemy shot?
                                                    //
		static integer 			attackSound;		// The attack sound I play when I first attack
		static integer 			deathSound;			// The sound I play when I die
		                                            //
		static integer			mood;				// My default 
		static integer			takeOffDistance;	// My take-off distance if I'm an airplane (ignored if not an airplane)
    //
		
	// Generic Special Unit Functions	
		static boolean			ToHell;				// I am going to teleport to hell
		static boolean			PowerDown;		   	// I start off shutdown
		static boolean			Visible;			// Am I on radar
		static boolean			RadarOff;			// This 'Mech doesn't use radar (no missile boats!)
		static boolean			AmPlane;			// I am a plane unit
		static integer			MyTimer;			// When I show up timewise
	// 
		
		static boolean			BreakLoop;			// Get people out of intro cineractive
		static boolean			Beeline;			// This unit tries to head straight to the end
		static integer			MyHP;				// My Hitpoints
		
//------------------------------------------------------------------
//     Init: my initialization function
//------------------------------------------------------------------

function Init;
	code
		// script-specific variables
		attackRange		= 1000;
		withdrawRange	= AttackRange * 3 / 2;
		path			= -1;
		moveType		= move_nocycle;
		speed			= 90;
		formationType	= 1;
		formationDensity= formtype_sparse;
		findTypes		= FT_DEFAULT;

		takeOffDistance	= 150;

		// driver settings
		SkillLevel		= 3;
		attackSound		= -1; // no sound
		deathSound		= -1; // no sound
		mood			= NEUTRAL_START;
		
		visible 		= TRUE;

        MyGroup			= GetMemoryInteger(me,9);
		switch (MyGroup)
        
            case 1:		// First Wave (In opening cineractive)
				MyObject		= ena_Nav_Alpha;
				SetTargetDesirability(me,-1);
				groupNumber		= 20;
            endcase;
			
			case 2:		// Helicopters in missions
				AmPlane			= True;
				MyObject		= ena_Nav_Alpha;
				switch (GetMemoryInteger(me,8))
					case 1:
						SkillLevel		= 8;
						MyTimer			= 1;
					endcase;
					case 2:
						MyTimer			= 80;
					endcase;
				endswitch;
			endcase;
			
			case 3:		// Second Wave (base patrol)
				path			= epa_frombase;
				MyObject		= ena_nav_beta;
				groupNumber		= 21;
			endcase;
			
			case 4:		// Third Wave
				PowerDown		= true;
				MyObject		= ena_OutsideBeta;
				path			= epa_frombase;
				groupNumber		= 22;
				if (GetMemoryInteger(me,8)==1) then		// One unit wants to book to the end zone
					Beeline = True;
					MyHP = GetHP(ME);
				endif;
				if (GetMemoryInteger(me,8)==2) then		// One unit wants to book to the end zone
					Beeline = True;
					//SetTargetDesirability(ME,-1);
					MyHP = GetHP(ME);
				endif;
			endcase;

        endswitch;

endfunction;

//------------------------------------------------------------------
//	StartState: my initial state
//------------------------------------------------------------------

state StartState;

	code
		AssignSkill(SkillLevel);
		
		/* Use a Select Statement to change these for a particular unit
		SetFiringDelay			(ME,minDelay,maxDelay);
		SetIgnoreFriendlyFire	(ME,true);
		SetIsShotRadius			(ME,isShotRadius);
		SetEntropyMood			(ME,mood);
		SetCurMood				(ME,mood);
		SetSkillLevel			(ME,piloting,gunnery,eliteLevel);
		SetAttackThrottle		(ME,attackThrottle);*/
		
		if (AmPlane) then
			SetAttackThrottle	(ME,100);
		else
			SetAttackThrottle	(ME,50);	// Early mission - attack throttle worse than usual
		endif;

		if (PowerDown) then
			Shutdown(me);
		endif;
		
		trans Lobby;        

endstate;			

state Lobby;
	code

		if (MyGroup==1) then	// First Wave
			if (IsWithin(ena_nav_beta,PlayerMech,900)) then
				SetTargetDesirability(me,50);
				trans GoToPlayer;
			endif;
		endif;
		
		if (MyGroup==2) then	// Helicopters
			if (timegreater(1,MyTimer)) then
				if (OrderTakeOff(10)) then
                    trans GoToPlayer;
				endif;
			endif;
		endif;
		
		if (MyGroup==3) then	// Second Wave
			if (SecondWave) then
				trans GoToLocation;
			endif;
		endif;
		
		if (MyGroup==4) then
			if (SecondWave) then
				Startup(ME);
				trans GoToLocation;
				//trans GoToLocation;
			else
				if (IsWithin(PlayerMech,me,800)) then
					Startup(ME);
					trans GoToLocation;
					//trans GoToLocation;
				endif;
			endif;
		endif;

endstate;

//------------------------------------------------------------------
//	FollowPathState: follow our path, but keep an eye out for enemies
//------------------------------------------------------------------

state FollowPathState;
	code

		OrderMoveResumePatrol(path,speed,movetype,true,true);
		//OrderFormationMove(GroupObjectID(GroupNumber),path,speed,movetype,formationtype,formationdensity,false);

		if (FindEnemy(AttackRange,FindTypes + FT_UNARMED + FT_BUILDING)) then
			trans Attack;
		endif;

endstate;

state GoToLocation;
	code

		OrderMovetoObject(MyObject,speed);

		if (MyObject == ena_Nav_Beta) then
			if (IsWithin(me,ena_Nav_Beta,300)) then
				MyObject 	= ena_Nav_Alpha;
			endif;
		endif;

		if (FindEnemy(AttackRange,FindTypes + FT_UNARMED + FT_BUILDING)) then
			trans Attack;
		endif;

endstate;

state GoToPlayer;
	code

		OrderMovetoObject(PlayerMech,100);
		
		if (RadarOff) then
			if (IsWithin(me,PlayerMech,200)) then
				SetSensorVisibility(me,TRUE);
				Visible = True;
			else
				SetSensorVisibility(me,FALSE);
				Visible = False;
			endif;
		endif;
		
		if (FindEnemy(AttackRange,FindTypes + FT_UNARMED + FT_BUILDING)) then
			trans Attack;
		endif;

endstate;

state Attack;
	code

		if (LeaveAttackState(WithdrawRange)) then
			if (MyObject==0) then
				trans GoToPlayer;
			else
				trans GoToLocation;
			endif;
		endif;
		
		if (MyObject == ena_Nav_Beta) then
			if (IsWithin(me,ena_Nav_Beta,300)) then
				MyObject 	= ena_Nav_Alpha;
			endif;
		endif;
		
		if (MyTactic == 0) then
			OrderAttack(true);
		else
			OrderAttackTactic(MyTactic,true);
		endif;
		
		if (MyTactic == TACTIC_REAR) then		// If you are the last of Clan MacLeod then stop hitting the rear
			if (GroupSize(GroupObjectID(GroupNumber)) == 1) then
				MyTactic = 0;
			endif;
		endif;
		
		/* if (MyGroup==1) then
			if (not BreakLoop) then
				if (IsDead(eve_sacrifice02)) then
					SetTarget(ME,NO_UNIT);
					OrderStopAttacking;
					BreakLoop = True;
					AssignSkill(SkillLevel);
					trans GoToLocation;
				endif;
			endif;
		endif;*/
		
		if (RadarOff) then		// This is to mimic your radar being turned off
			if (IsWithin(me,PlayerMech,200)) then
				SetSensorVisibility(me,TRUE);
				Visible = True;
			else
				SetSensorVisibility(me,FALSE);
				Visible = False;
			endif;
		endif;
		
		if (timegreater(2,1)) then	// Don't attack during end cinema
			OrderStopAttacking;
			trans Peaceful;
		endif;
			
endstate;

state Peaceful;
	code

		OrderMovetoObject(ena_nav_beta,30);

endstate;

//------------------------------------------------------------------
//	DeadState: OK, I kicked the bucket.
//------------------------------------------------------------------

state DeadState;
	code
		if (deathSound <> -1) then	
			PlaySoundOnce(deathSound);
		endif;		

		orderDie;

endstate;


endfsm.
