//*********************************************************************************

fsm E4_Revolution : integer;

//------------------------------------------------------------------
//
//	 Constant Definitions
//
//------------------------------------------------------------------
	 const

		#include_ <content\ABLScripts\mwconst.abi>
		#include_ <content\ABLScripts\Katie\katie_consts.abi>
//------------------------------------------------------------------
//
//	 Type Definitions
//
//------------------------------------------------------------------
	 type

		#include_ <content\ABLScripts\mwtype.abi>

//------------------------------------------------------------------
//
//	 Variable Declarations
//
//------------------------------------------------------------------
	  var

		eternal integer		playermech;			// This is the variable for the player's 'Mech
			
		// Standard Nav and Objective Variables
		static boolean		alphareached;		// Nav Alpha reached
		static boolean		betareached;		// Nav Beta reached
		static boolean		gammareached;		// Nav Gamma reached
		static boolean		deltareached;		// Nav Delta reached
												//
		static boolean		alpharevealed;		// Nav Alpha is shown
		static boolean		betarevealed;		// Nav Beta is shown
		static boolean		gammarevealed;		// Nav Gamma is shown
		static boolean		deltarevealed;		// Nav Delta is shown
		static integer		checkrange;			// How far from the Nav Point you have to be											
												//
		eternal boolean		obj1;				// This indicates whether Objective 1 was completed
		eternal boolean		obj2;				// This indicates whether Objective 2 was completed
		eternal boolean		obj3;				// This indicates whether Objective 3 was completed
		eternal boolean		obj4;				// This indicates whether Objective 4 was completed
		eternal boolean		obj5;				// This indicates whether Objective 5 was completed
												//
		static boolean		obj1revealed;		// This indicates that Objective 1 has been revealed
		static boolean		obj2revealed;		// This indicates that Objective 2 has been revealed
		static boolean		obj3revealed;		// This indicates that Objective 3 has been revealed
		static boolean		obj4revealed;		// This indicates that Objective 4 has been revealed
		static boolean		obj5revealed;		// This indicates that Objective 5 has been revealed
		//	
		
		static integer		cinema_cut;			// This variable keeps track of what scene a cinema is on.
		static locpoint		temploc;			// Used for commands that need objects converted to locpoint format
		static boolean		loser;				// You have lost the mission
 		eternal integer 	DavionRating;		// This is your Davion rating
		eternal integer 	SteinerRating;		// This is your Steiner rating
		eternal integer 	Nobility;			// This is your Nobility rating
		eternal integer 	Infamy;				// This is your Infamy rating
 
		// Trigger States
		eternal boolean		RunAway;			// Intro movie retreating 'Mech
		static integer		SacHP;				// Current HP of the Sacrifice unit
		static boolean		HeavyOK;			// This determines whether the player can handle Heavy 'Mechs
		eternal boolean		SecondWave;			// This determines when the two lances come in
		//static integer		PlayerHP;			// Player's HP for using the MFB
		
//------------------------------------------------------------------
//
//	 Init Function	(automatically run first time through)
//
//------------------------------------------------------------------

function init;
	var
		integer i;

	code

			playermech = getplayervehicle(epl_player0);

			// 10 - Sacrifice Group (Governor)
			GroupAddObject(GroupObjectID(10),eve_Black_Six);
			GroupAddObject(GroupObjectID(10),eve_sacrifice02);
			GetLocation(ena_leash,temploc);
			SetCombatLeash(GroupObjectID(10),temploc,250);

			// 11 - Friends at beginning (Governor)
			GroupAddObject(GroupObjectID(11),eve_friends01);
			GroupAddObject(GroupObjectID(11),eve_friends02);
			GroupAddObject(GroupObjectID(11),eve_friends03);
			SetGroupAI(GroupObjectID(11),GROUPAI_MOODSQUAD);
			
			// 13 - Field Base at Nav Alpha 
			GroupAddObject(GroupObjectID(13),ebu_tent1);
			GroupAddObject(GroupObjectID(13),ebu_tent2);
			GroupAddObject(GroupObjectID(13),ebu_tent2_0001);
			//GroupAddObject(GroupObjectID(13),eve_field_base);
			GroupAddObject(GroupObjectID(13),ebu_barracks);
			
			// 20 - First Wave (Militia)
			GroupAddObject(GroupObjectID(20),eve_1wave01);
			GroupAddObject(GroupObjectID(20),eve_1wave02);
			GroupAddObject(GroupObjectID(20),eve_1wave03);
			GroupAddObject(GroupObjectID(20),eve_1wave04);
			SetGroupAI(GroupObjectID(20),GROUPAI_MOODSQUAD);

			// 21 - Second Wave (Militia)
			//GroupAddObject(GroupObjectID(21),eve_2wave01);
			//GroupAddObject(GroupObjectID(21),eve_2wave02);
			GroupAddObject(GroupObjectID(21),eve_2wave03);
			GroupAddObject(GroupObjectID(21),eve_2wave04);
			GroupAddObject(GroupObjectID(21),eve_2wave05);
			GroupAddObject(GroupObjectID(21),eve_2wave06);
			SetGroupAI(GroupObjectID(21),GROUPAI_MOODSQUAD);
			
			// 22 - Third Wave (Militia)
			GroupAddObject(GroupObjectID(22),eve_3wave04_hvy);
			GroupAddObject(GroupObjectID(22),eve_3wave05_hvy);
			GroupAddObject(GroupObjectID(22),eve_3wave06_hvy);
			GroupAddObject(GroupObjectID(22),eve_3wave07_hvy);
			SetGroupAI(GroupObjectID(22),GROUPAI_MOODSQUAD);
			
			// 30 - Initial Choppers
			GroupAddObject(GroupObjectID(30),eve_1helo01);
			GroupAddObject(GroupObjectID(30),eve_1helo02);
			GroupAddObject(GroupObjectID(30),eve_1helo03);
			GroupAddObject(GroupObjectID(30),eve_1helo04);
			GroupAddObject(GroupObjectID(30),eve_1helo05);
			
			// 40 - Any of the Militia that makes it to the chokepoint
			GetLocation(ena_leash,temploc);
			SetCombatLeash(GroupObjectID(40),temploc,300);
			
			cinema_cut = 0;

			// Culturals
			//SetTarget(ede_example2,eve_example3);
		
endfunction;
		#include_ <content\ABLScripts\Katie\katie_script.abi>
//------------------------------------------------------------------
//
//	 Main Code
//
//------------------------------------------------------------------
state startState;

	code	
/* Mektek: Shark removed these and replaced them with Katie calls
			SetKillSalvageChance(eve_3wave06_hvy,100);	// Black Knight
			SetKillSalvageChance(eve_3wave05_hvy,50);	// Loki
			SetKillSalvageChance(eve_3wave07_hvy,50);	// Argus
			SetKillSalvageChance(eve_2wave06,50);	// Bushwhacker
*/
		// These are your MetaMission variables, second part is how this mission effects your ratings
			DavionRating 	= GetPreviousDavionFactionRating;
			SteinerRating 	= GetPreviousSteinerFactionRating;
			Nobility 		= GetPreviousNobility;
			Infamy 			= GetPreviousInfamy;
			
			DavionRating	= DavionRating + 5;
			SteinerRating	= SteinerRating - 2;
			Nobility		= Nobility + 3;
			Infamy			= Infamy + 0;
		// 
		
		SetRadarNav(ena_Nav_Alpha,1);
		SetRadarNav(ena_Nav_Beta,2);
		SetRadarNav(ena_Nav_Gamma,3);
		SetRadarNav(ena_Nav_Delta,4);
		
		RevealNavpoint(ena_Nav_Alpha);
		RevealNavpoint(ena_Nav_Beta);
		RevealNavpoint(ena_Nav_Gamma);
		RevealNavpoint(ena_Nav_Delta);
		
		SetNavpoint(ena_Nav_Alpha);

		//setsensorvisibility(eve_example4,false);
		
		checkrange	= 150;		// How far from a nav point before the Betty voice says you've arrived.

		StartMusic("Music\SkitterSprint",3.0,3.5,50);
		
		Destroy(eve_debris01);		// Dead 'Mechs on battlfield
		Destroy(eve_debris02);
		Destroy(eve_debris03);
		Destroy(eve_debris04);
		Destroy(eve_debris05);

		trans opening;

endstate;

	
state deadState;
	code
	
endstate;
	
//------------------------------------------------------------------
//
//	 Sit state : description
//
//------------------------------------------------------------------
state sit;

	code

	if IsMusicPlaying == false then
		StartMusic("music\ArcticAmbLoop",0.0,3.0,40);
	endif;

// Eaton 4 Specific Triggers
	if (IsWithin(GroupObjectID(11),ena_leash,100)) then		// Once friends get to target zone they stay there
		if (not GetGlobalTrigger(2)) then
			GetLocation(ena_leash,temploc);
			SetCombatLeash(GroupObjectID(11),temploc,250);
			SetGlobalTrigger(2,true);
		endif;
	endif;
	
	if (not GetGlobalTrigger(3)) then		// Start countdown for third wave
		if ((GroupNumDead(GroupObjectID(20)) + GroupNumDead(GroupObjectID(21))) > 5) then
			SetGlobalTrigger(3,true);
			StartTimer(3);		// This is the timer for 3rd wave to spring into action
		endif;
	endif;
	
	if (not SecondWave) then
		if (IsWithin(PlayerMech,ena_nav_beta,300)) then
			SecondWave = True;
			StartTimer(7);		// Counter until announcements about incoming 'Mechs
		endif;
	endif;
	
// Sound Triggers

	if (not GetGlobalTrigger(4)) then	// Choppers are on player's scope
		if (IsWithin(PlayerMech,GroupObjectID(30),1000)) then
			SetGlobalTrigger(4,true);
			PlayVoiceOverOnce("vo\eaton\mi4\choppersahead",TALKER_SPE);
			PlayLanceMateSound("vo\generic\roger",1);
		endif;
	endif;
	
	if (not GetGlobalTrigger(5)) then
		if (GroupAllDead(GroupObjectID(30))) then
			//if (not IsDead(eve_black_six)) then
			if (timegreater(6,10)) then
				if (IsWithin(ena_nav_beta,PlayerMech,745)) then
					SetGlobalTrigger(5,true);
					PlayVoiceOverOnce("vo\eaton\mi4\spectrereinforcing",TALKER_SPE);
					PlayVoiceOverOnce("vo\eaton\mi4\PushingThruBeta",TALKER_SIX);
					PlayVoiceOverOnce("vo\eaton\mi4\weedbeenheresooner",TALKER_SPE);
					PlayVoiceOverOnce("vo\eaton\mi4\copy",TALKER_SIX);
				endif;
			endif;
		endif;
	endif;
	
	if (not GetGlobalTrigger(6)) then
		//if (IsWithin(PlayerMech,ena_nav_alpha,100)) then	// MFB messages
			if (GroupAllDead(GroupObjectID(30))) then
				SetGlobalTrigger(6,true);
				//PlayerHP = GetHP(PlayerMech);
			endif;
		//endif;
	endif;

	if (not GetGlobalTrigger(11)) then		// Wait for people to quit yacking
		if (GetGlobalTrigger(6)) then
			if (not IsVoiceOverPlaying) then
				if (not IsDead(eve_field_base)) then	// Make sure the field base isn't destroyed
					SetGlobalTrigger(11,true);
					PlayVoiceOverOnce("vo\eaton\mi4\alphasecure",TALKER_SPE);
					StartTimer(6);		// This is for the new messages on what to do
					if (not GetEventFlag("MFB")) then
						PlayVoiceOverOnce("vo\eaton\mi4\mfbthentobeta",TALKER_SIX);
						PlayVoiceOverOnce("vo\eaton\mi4\copyrepairing",TALKER_SPE);
						SetEventFlag("MFB",true);
						trans MiniMovie;
					endif;
				else
					SetGlobalTrigger(11,true);
				endif;
			endif;
		endif;
	endif;
	
	/* if (not GetGlobalTrigger(8)) then	// MFB messages
		if (GetGlobalTrigger(6)) then
			//if (not GetEventFlag("MFB")) then
				if (GetHP(PlayerMech) > PlayerHP) then
				//if (IsWithin(PlayerMech,eve_field_base,2)) then
					SetGlobalTrigger(8,true);
					StartTimer(4);		// You've used the MFB
				endif;
			//endif;
		endif;
	endif; */

	if (not GetGlobalTrigger(10)) then	// Turn off help message
		if (timegreater(5,10)) then
			SetGlobalTrigger(10,true);
			Helpmessage(-2,1);
		endif;
	endif;

	/* if (not GetGlobalTrigger(9)) then  // Bug 4790 killed this
		if (GetGlobalTrigger(8)) then		// You've repaired
			if (timegreater(4,5)) then
				SetGlobalTrigger(9,true);
				Helpmessage(-2,1);
				PlayVoiceOverOnce("vo\eaton\mi4\repairscomplete",TALKER_SPE);
			endif;
		endif;
	endif; */
	
	if (not GetGlobalTrigger(7)) then
		if (SecondWave) then
			if (not IsDead(eve_black_six)) then
				if (not IsWithin(PlayerMech,ena_nav_beta,500)) then
					SetGlobalTrigger(7,true);
					PlayVoiceOverOnce("vo\eaton\mi4\gettobeta",TALKER_SIX);
				endif;
			endif;
		endif;
	endif;
	
	if (not GetGlobalTrigger(12)) then
		if (GroupAllDead(GroupObjectID(21))) then		// Medium 'Mechs destroyed
			SetGlobalTrigger(12,true);
			PlayVoiceOverOnce("vo\eaton\mi4\MedLanceDown",TALKER_SPE);
		endif;
	endif;
	
	if (not GetGlobalTrigger(13)) then
		if (GroupAllDead(GroupObjectID(22))) then		// Heavy 'Mechs destroyed
			SetGlobalTrigger(13,true);
			PlayVoiceOverOnce("vo\eaton\mi4\HevLanceHistory",TALKER_SPE);
		endif;
	endif;
	
	if (not GetGlobalTrigger(14)) then		// Announcement that other lances are coming in
		if (timegreater(7,15)) then
			SetGlobalTrigger(14,true);
			StartMusic("Music\FloodGate",3.0,3.5,50);
			if (not IsDead(eve_Black_Six)) then
				PlayVoiceOverOnce("vo\eaton\mi4\2lancesinbound",TALKER_SIX);
			endif;
		endif;
	endif;
	
	if (not GetGlobalTrigger(15)) then
		if (GetGlobalTrigger(14)) then
			if (IsWithin(PlayerMech,eve_2wave03,950)) then
				if (IsWithin(PlayerMech,eve_3wave07_hvy,950)) then
					SetGlobalTrigger(15,true);
					if (HeavyOK) then
						PlayVoiceOverOnce("vo\eaton\mi4\MedandHeavy",TALKER_SPE);
					endif;
					if (not IsDead(eve_Black_Six)) then
						PlayVoiceOverOnce("vo\eaton\mi4\holdatbeta",TALKER_SIX);
					endif;
				endif;
			endif;
		endif;
	endif;

// Triggers	- Harass Strike Relief

// Obj 1 - Hold the chokepoint at Nav Beta

	if (not obj1) then
		if (GroupAllDead(GroupObjectID(20))) then
			if (GroupAllDead(GroupObjectID(21))) then
				if (GroupAllDead(GroupObjectID(22))) then
					obj1 = true;
					SuccessObjective(eob_objective1);
					SuccessObjective(eob_objective2);
					StartTimer(2);		// Mission won timer
				endif;
			endif;
		endif;
	endif;
	
// Obj2 - Protect the Field Base at Nav Alpha

	if (not Obj2) then
		if (GroupAllDead(GroupObjectID(13))) then
			FailObjective(eob_objective2);
			//FailObjective(eob_objective1);
			Obj2 = True;
			//StartTimer(2);		// You've lost
			//Loser = True;
		endif;
	endif;

// Victory and Loss conditions

	if (timegreater(2,5)) then
		  
		// Salvage - FAB  7/30
		//

		SetCurrentInfamy(Infamy);
		SetCurrentNobility(Nobility);
		SetCurrentDavionFactionRating(DavionRating);
		SetCurrentSteinerFactionrating(SteinerRating);
	
		/////////////////////
		// MISSION SALVAGE //
		/////////////////////
		
		Katie(eve_1wave01,80);
		Katie(eve_1wave02,60);
		Katie(eve_1wave03,50);
		Katie(eve_1wave04,40);
		
		Katie(eve_2wave03,60);
		Katie(eve_2wave04,50);
		Katie(eve_2wave05,40);
		Katie(eve_2wave06,60);

		Katie(eve_3wave04_hvy,60);
		Katie(eve_3wave05_hvy,50);
		Katie(eve_3wave06_hvy,80);
		Katie(eve_3wave07_hvy,40);
		
		if (loser) then
			EndMission(false,5);
			trans lost;
		else
			if (not Obj2) then
				SuccessObjective(eob_objective2);
				AddMissionMoneyToBalance(250000);
			endif;

			trans won;	
		endif;
		
	endif;

// Fail the mission when the player dies
	if (isdead(playermech)) then
		KillVoiceOvers;
		switch rand(1,3)
			case 1:
				PlayVoiceOver("VO\Generic\SpecterDestroyed", talker_CAS);
			endcase;
			case 2:
				PlayVoiceOver("VO\Generic\SpecterDestroyed2", talker_CAS);
			endcase;
			case 3:
				PlayVoiceOver("VO\Generic\SpecterDestroyed3", talker_CAS);
			endcase;
		endswitch;
		StartMusic("Music\GenericLoss1",3.0,3.5,70);
		endmission(false,14);
		trans deadstate;
	endif;

// Betty speaks when reaching Nav Point

	if (not alphareached) then
		if (isnavrevealed(ena_nav_alpha)) then
			if (iswithin(playermech,ena_nav_alpha,checkrange)) then
				playvoiceover("VO\generic\navalpha",talker_Bet);
				alphareached = true;
				setnavpoint(ena_nav_beta);
			endif;
		endif;
	endif;

	if (not betareached) then
		if (isnavrevealed(ena_nav_beta)) then
			if (iswithin(playermech,ena_nav_beta,checkrange)) then
				playvoiceover("VO\generic\navbeta",talker_Bet);
				betareached = true;
				//setnavpoint(ena_nav_gamma);
			endif;
		endif;
	endif;
	
	if (not gammareached) then
		if (isnavrevealed(ena_nav_gamma)) then
			if (iswithin(playermech,ena_nav_gamma,checkrange)) then
				playvoiceover("VO\generic\navgamma",talker_Bet);
				gammareached = true;
				//setnavpoint(ena_nav_delta);
			endif;
		endif;
	endif;

	if (not deltareached) then
		if (isnavrevealed(ena_nav_delta)) then
			if (iswithin(playermech,ena_nav_delta,checkrange)) then
				playvoiceover("VO\generic\navdelta",talker_Bet);
				deltareached = true;
				//setnavpoint(ena_nav_epsilon);
			endif;
		endif;
	endif;
	
endstate;

//-----------------------------------------
//HERE THERE BE CINEMAS
//-----------------------------------------

state opening;	  

	code

		if (cinemaskip) then
			KillVoiceOvers;	 
			cinema_cut = 0;	 
			KillTimer(gti_timer_21);
			SetGlobalTrigger(0,true);
			playerai(epl_player0,false);
			StartTimer(1);		// This is the mission start timer
			ResetTimer(1);
			CinemaEnd;		  
			SetCompositingEnabled(True);
			revealobjective(eob_objective2);
			RevealObjective(eob_objective1);
			trans sit;
		endif;
		
		switch (cinema_cut)

			case 0:
				CinemaStart;
				SetActiveCamera(eca_CameraShip);
				playerai(epl_player0,true);
				SetCompositingEnabled(False);
				
				PlayVoiceOverOnce("vo\eaton\mi4\intro1",TALKER_BOU);
				PlayVoiceOverOnce("vo\eaton\mi4\intro2",TALKER_SPE);
				PlayVoiceOverOnce("vo\eaton\mi4\intro3",TALKER_CAS);
					
				FadeToBlack(0.0);
				FadeFromBlack(5.0);
				//FadeFromBlack(300.0);
				SpinCamera1(PlayerMech,PlayerMech,2.0,13.0,TRUE);
				
				cinema_cut = cinema_cut + 1;
				
				starttimer(21);			// This is the cineractive counter.
				resettimer(21);
			endcase;
			
			case 1:	 
				if (timegreater(21,5)) then
					Destroy(ebu_boom1);
					
					starttimer(21);			// This is the cineractive counter.
					resettimer(21);
					
					/*Targetfollowobject(ena_nav_beta);
					Camerafollowobject(ena_nav_beta);
					Targetoffset(10.0,5.0,0.0,0.0,false);
					Targetoffset(0.0,10.0,5.0,0.0,false);
					Cameraoffset(280.0,60.0,-130.0,0.0,false);
					Cameraoffset(150.0,15.0,-100.0,14.0,false);*/
					
					cinema_cut = cinema_cut + 1;					
				endif;
			endcase;
			
			case 2:
				if (not IsVoiceOverPlaying) then
					ResetTimer(21);
					Cinema_Cut = Cinema_Cut + 1;
				endif;
			endcase;
			
			case 3:
				if (timegreater(21,2)) then
					KillVoiceOvers;
					cinema_cut = 0;
					KillTimer(gti_timer_21);
					SetGlobalTrigger(0,true);
					playerai(epl_player0,false);
					StartTimer(1);		// This is the mission start timer
					ResetTimer(1);
					CinemaEnd;
					revealobjective(eob_objective2);	// Reveal the objectives
					RevealObjective(eob_objective1);
					SetCompositingEnabled(True);
					
					trans sit;
				endif;
			endcase;
				
		endswitch;
	
endstate;

state won;	  

	code
		
		if (cinemaskip) then
			cinema_cut = 20;	
			playerai(epl_player0,false);
			KillVoiceOvers;
			Endmission(true,1); 
		endif;

		switch cinema_cut
		
			case 0:	
				playerai(epl_player0,true);
				StartMusic("Music\GenericVictory1",3.0,3.5,50);					 
				PlayVoiceOverOnce("vo\eaton\mi4\close1",TALKER_SPE);
				if (IsDead(eve_Black_Six)) then
					PlayVoiceOverOnce("vo\eaton\mi4\close3",TALKER_BOU);
					PlayVoiceOverOnce("vo\eaton\mi4\close4",TALKER_SPE);
					PlayVoiceOverOnce("vo\eaton\mi4\close5",TALKER_BOU);
					PlayVoiceOverOnce("vo\eaton\mi4\close6",TALKER_CAS);
				else
					PlayVoiceOverOnce("vo\eaton\mi4\close2",TALKER_SIX);
				endif;
				SetCompositingEnabled(False);
				CinemaStart;
				setactivecamera (eca_cameraship);
				Targetfollowobject(playermech);
				Camerafollowobject(playermech);
				Targetoffset(0.0,4.0,0.0,0.0,false);
				Cameraoffset(-30.0,125.0,75.0,0.0,false);
				Cameraoffset(60.0,20.0,25.0,14.0,false);
				playerai(epl_player0,true);
				Cinema_cut=Cinema_cut+1;
			endcase;
			
			case 1:
				if (not IsVoiceOverPlaying) then
					endmission(true,3);
					Cinema_cut = 20;
				endif;
			endcase;			
			
		endswitch;
	
endstate;

state minimovie;
	code

		if (cinemaskip) then
			cinema_cut = 0;
			KillTimer(gti_timer_21);
			playerai(epl_player0,false);
			CinemaEnd;
			SetCompositingEnabled(True);
			HelpMessage(eob_MFBHelp,10);
			StartTimer(5);		// Turn off this message
		
			trans sit;
		endif;

		switch cinema_cut
		
			case 0:	
				playerai(epl_player0,true);
				SetCompositingEnabled(False);
				CinemaStart;
				
				setactivecamera (eca_cameraship);
				
				Targetfollowobject(eve_field_base);
				Camerafollowobject(eve_field_base);
				Targetoffset(0.0,9.0,0.0,0.0,false);
				Cameraoffset(0.0,1.0,100.0,0.0,true);
				Cameraoffset(0.0,1.0,55.0,5.0,true);
				
				playerai(epl_player0,true);
				StartTimer(21);
				ResetTimer(21);
				Cinema_cut=Cinema_cut+1;
			endcase;
			
			case 1:
				if (timegreater(21,6)) then
					cinema_cut = 0;
					KillTimer(gti_timer_21);
					playerai(epl_player0,false);
					CinemaEnd;
					SetCompositingEnabled(True);
					HelpMessage(eob_MFBHelp,10);
					StartTimer(5);		// Turn off this message
					
					trans sit;
				endif;
			endcase;			
			
		endswitch;

endstate;

state lost;

		code
		
		switch cinema_cut
		
			case 0:	
			
			endcase;
			
			case 1:
			
			endcase;
			
		endswitch;
	
endstate;

endfsm.
//******************************************************************
