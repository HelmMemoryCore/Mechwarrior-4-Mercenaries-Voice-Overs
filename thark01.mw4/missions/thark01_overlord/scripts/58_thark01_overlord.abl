//*********************************************************************************

fsm Thark01_Overlord : integer;

//------------------------------------------------------------------
//
//	 Constant Definitions
//
//------------------------------------------------------------------
	 const

		#include_ <content\ABLScripts\mwconst.abi>
		#include_ <content\ABLScripts\Katie\katie_consts.abi> // <-- mektek
//------------------------------------------------------------------
//
//	 Type Definitions
//
//------------------------------------------------------------------
	 type

		#include_ <content\ABLScripts\mwtype.abi>

//------------------------------------------------------------------
//
//	 Variable Declarations
//
//------------------------------------------------------------------
	  var

		eternal integer		playermech;			// This is the variable for the player's 'Mech
			
		// Standard Nav and Objective Variables
		static boolean		alphareached;		// Nav Alpha reached
		static boolean		betareached;		// Nav Beta reached
		static boolean		gammareached;		// Nav Gamma reached
		static boolean		deltareached;		// Nav Delta reached
												//
		static boolean		alpharevealed;		// Nav Alpha is shown
		static boolean		betarevealed;		// Nav Beta is shown
		static boolean		gammarevealed;		// Nav Gamma is shown
		static boolean		deltarevealed;		// Nav Delta is shown
		static integer		checkrange;			// How far from the Nav Point you have to be											
												//
		eternal boolean		obj1;				// This indicates whether Objective 1 was completed
		eternal boolean		obj2;				// This indicates whether Objective 2 was completed
		eternal boolean		obj3;				// This indicates whether Objective 3 was completed
		eternal boolean		obj4;				// This indicates whether Objective 4 was completed
		eternal boolean		obj5;				// This indicates whether Objective 5 was completed
												//
		static boolean		obj1revealed;		// This indicates that Objective 1 has been revealed
		static boolean		obj2revealed;		// This indicates that Objective 2 has been revealed
		static boolean		obj3revealed;		// This indicates that Objective 3 has been revealed
		static boolean		obj4revealed;		// This indicates that Objective 4 has been revealed
		static boolean		obj5revealed;		// This indicates that Objective 5 has been revealed
		//	
		
		static integer		cinema_cut;			// This variable keeps track of what scene a cinema is on.
		static locpoint		temploc;			// Used for commands that need objects converted to locpoint format
		static boolean		loser;				// You have lost the mission
 		eternal integer 	DavionRating;		// This is your Davion rating
		eternal integer 	SteinerRating;		// This is your Steiner rating
		eternal integer 	Nobility;			// This is your Nobility rating
		eternal integer 	Infamy;				// This is your Infamy rating
 		 
		// Trigger States
		eternal boolean		FirstContact;		// Enemies have made the PC or allies
		eternal boolean		Hill01Go;			// First wave of hilltop reinforcements go
		eternal boolean		Hill02Go;			// Second wave of hilltop reinforcements go
		eternal boolean		AllySouth;			// Once some allies get nuked, you get a few reinforcements
		eternal boolean		TankGo;				// Hilltop tanks are on the move
		static integer		TotalPrelim;		// The total of all 'Mechs before the "Final Conflict" 'Mechs
		eternal boolean		OverlordFired;		// The Overlord has shot at a target
		static integer		ObjCounter;			// How many objectives you've completed
		eternal boolean		BetaOn;				// Commence the defense of Beta
		
//------------------------------------------------------------------
//
//	 Init Function	(automatically run first time through)
//
//------------------------------------------------------------------

function init;
	var
		integer i;

	code

			playermech = getplayervehicle(epl_player0);

			// 10 - First Set of Allies that storm the hill with player
			GroupAddObject(GroupObjectID(10),eve_1lance01);
			GroupAddObject(GroupObjectID(10),eve_1lance02);
			GroupAddObject(GroupObjectID(10),eve_1lance03);
			GroupAddObject(GroupObjectID(10),eve_2lance01);
			
			// 11 - Second Set of Reinforced Allies
			//GroupAddObject(GroupObjectID(11),eve_3lance01);
			GroupAddObject(GroupObjectID(11),eve_3lance02);
			GroupAddObject(GroupObjectID(11),eve_3lance03);

			// 20 - First Couple Waves of Enemies
			GroupAddObject(GroupObjectID(20),eve_1wave01);
			GroupAddObject(GroupObjectID(20),eve_1wave02);
			GroupAddObject(GroupObjectID(20),eve_1wave03);
			GroupAddObject(GroupObjectID(20),eve_2wave01);
			GroupAddObject(GroupObjectID(20),eve_2wave02);
			GroupAddObject(GroupObjectID(20),eve_2wave03);
			GroupAddObject(GroupObjectID(20),eve_2wave04);
			SetGroupAI(GroupObjectID(20),GROUPAI_MOODSQUAD);

			// 21 - First HillTop Reinforcements
			GroupAddObject(GroupObjectID(21),eve_1hilltop01);
			GroupAddObject(GroupObjectID(21),eve_1hilltop02);
			GroupAddObject(GroupObjectID(21),eve_1hilltop03);
			SetGroupAI(GroupObjectID(21),GROUPAI_MOODSQUAD);
			
			// 22 - Second Hilltop Seinforcements
			GroupAddObject(GroupObjectID(22),eve_2hilltop01);
			GroupAddObject(GroupObjectID(22),eve_2hilltop02);
			GroupAddObject(GroupObjectID(22),eve_2hilltop03);
			GroupAddObject(GroupObjectID(22),eve_2hilltop04);
			GroupAddObject(GroupObjectID(22),eve_2hilltop05);
			
			// 30 - Overlord Power Components
			GroupAddObject(GroupObjectID(30),ebu_OverlordPower);
			GroupAddObject(GroupObjectID(30),eve_apu1);
			GroupAddObject(GroupObjectID(30),eve_apu2);
			GroupAddObject(GroupObjectID(30),eve_apu3);
			
			// 31 - Power Components
			GroupAddObject(GroupObjectID(31),ebu_OverlordPower);
			GroupAddObject(GroupObjectID(31),ebu_TurretPower);
			
			TotalPrelim		= 24;		// All 'Mechs before "Final Conflict"
			
			// 23 - Command Lance
			GroupAddObject(GroupObjectID(23),eve_finalenemy01);
			GroupAddObject(GroupObjectID(23),eve_finalenemy02);
			GroupAddObject(GroupObjectID(23),eve_finalenemy03);
			GroupAddObject(GroupObjectID(23),eve_finalenemy04);
			GroupAddObject(GroupObjectID(23),eve_finalenemy05);
			GroupAddObject(GroupObjectID(23),eve_finalenemy06);
			SetGroupAI(GroupObjectID(23),GROUPAI_MOODSQUAD);
			
			cinema_cut = 0;

			// Culturals
			//SetTarget(ede_example2,eve_example3);
		
endfunction;
		#include_ <content\ABLScripts\Katie\katie_script.abi> // <-- mektek
//------------------------------------------------------------------
//
//	 Main Code
//
//------------------------------------------------------------------
state startState;

	code	
/* MekTek: Shark removed these and replaced them with Katie calls
			SetKillSalvageChance(eve_1hilltop02,25);	// Atlas
			SetKillSalvageChance(eve_1wave01,25);	// Atlas
			SetKillSalvageChance(eve_2hilltop02,50);	// Daishi
			SetKillSalvageChance(eve_2hilltop05,100);	// Mad Cat
			SetKillSalvageChance(eve_2wave03,50);	// Daishi
			SetKillSalvageChance(eve_2wave04,25);	// MK2
			SetKillSalvageChance(eve_finalenemy01,25);	// Atlas
			SetKillSalvageChance(eve_finalenemy02,25);	// MK2
			SetKillSalvageChance(eve_finalenemy03,25);	// Sunder
*/
		// These are your MetaMission variables, second part is how this mission effects your ratings
			DavionRating 	= GetPreviousDavionFactionRating;
			SteinerRating 	= GetPreviousSteinerFactionRating;
			Nobility 		= GetPreviousNobility;
			Infamy 			= GetPreviousInfamy;
			
			DavionRating	= DavionRating + 19;
			SteinerRating	= SteinerRating + 0;
			Nobility		= Nobility + 5;
			Infamy			= Infamy + 0;
		// 
		
		SetRadarNav(ena_Nav_Alpha,1);
		SetRadarNav(ena_Nav_Beta,2);
		SetRadarNav(ena_Nav_Gamma,3);
		RevealNavpoint(ena_Nav_Alpha);
		RevealNavpoint(ena_Nav_Beta);
		RevealNavpoint(ena_Nav_Gamma);
		SetNavpoint(ena_Nav_Alpha);

		//setsensorvisibility(eve_example4,false);
		
		checkrange	= 100;		// How far from a nav point before the Betty voice says you've arrived.
		Destroy(ebu_boom01);
		Destroy(ebu_boom02);
		Destroy(ebu_boom03);
		SetTargetDesirability(eve_overlord,-1);

		StartMusic("Music\Behemoths",3.0,3.5,50);
		
		StartTimer(5);		// This is for the Overlord - it will not have energy to fire all the time if its buildigns are destroyed

		trans opening;

endstate;

	
state deadState;
	code
	
endstate;
	
//------------------------------------------------------------------
//
//	 Sit state : description
//
//------------------------------------------------------------------
state sit;

	code

	if IsMusicPlaying == false then
		StartMusic("VO\Rain1",0.1,0.1,300);
	endif;
	
// Sound Triggers

	if (not GetGlobalTrigger(4)) then
		if (timegreater(1,4)) then		// Opening VO
			SetGlobalTrigger(4,true);
			PlayVoiceOverOnce("VO\Tharkad\MI1\YouHeardTheMan",TALKER_SPE);
			PlayLancemateSound("VO\Generic\Excited",3);
		endif;
	endif;
	
	if (not GetGlobalTrigger(5)) then
		if (Hill01Go) then		// Play once a few Alpha 'Mechs are hit
			SetGlobalTrigger(5,true);
			PlayVoiceOverOnce("VO\Tharkad\MI1\HitAlphaHard",TALKER_CAS);
			PlayVoiceOverOnce("VO\Tharkad\MI1\CopyThat",TALKER_SPE);
			PlayLancemateSound("VO\Generic\DeterminedEnemy",1);
			PlayVoiceOverOnce("VO\Tharkad\MI1\TargetDiscipline",TALKER_SPE);
			PlayVoiceOverOnce("VO\Tharkad\MI1\DontOverExtend",TALKER_CAS);
		endif;
	endif;
	
	if (not GetGlobalTrigger(6)) then
		if (IsWithin(PlayerMech,ena_nav_beta,600)) then		// Get close to beta and talk
			if (obj2) then
				SetGlobalTrigger(6,true);
				PlayVoiceOverOnce("VO\Tharkad\MI1\JustHoldThem",TALKER_SPE);
				PlayLancemateSound("VO\Generic\Optimist",1);
			endif;
		endif;
	endif;
	
	if (not GetGlobalTrigger(7)) then
		if (obj3) then		// Overlord is firing on the player's lance
			if (IsWithin(PlayerMech,eve_overlord,800)) then
				SetGlobalTrigger(7,true);
				PlayVoiceOverOnce("VO\Tharkad\MI1\ClosingToWeaponsRange",TALKER_SPE);
				StartMusic("Music\Action",3.0,3.5,50);
				if (PlayLancemateSound("VO\Generic\FallBack",1)) then
					PlayVoiceOverOnce("VO\Tharkad\MI1\ContinueAssault",TALKER_SPE);
				endif;
			endif;
		endif;
	endif;

// Thark 01 Specific Triggers

	if (not Hill01Go) then		// Once there are enough dead, bring in the hill people
		if (GroupNumDead(GroupObjectID(20))>2) then
			Hill01Go = True;
		endif;
	endif;

	if (not Hill02Go) then		// Once there are enough dead, bring in the hill people
		if (GroupNumDead(GroupObjectID(20)) + GroupNumDead(GroupObjectID(21)) > 4) then
			Hill02Go = True;
		endif;
	endif;
	
	if (not AllySouth) then
		if (GroupNumDead(GroupObjectID(10))>2) then
			AllySouth = true;
		endif;
	endif;
	
	if (not TankGo) then
		if (IsWithin(PlayerMech,ena_nav_gamma,1250)) then
			TankGo = True;
			StartTimer(3);		// Tanks start moving
		endif;
	endif;
	
	if (not GetGlobalTrigger(1)) then		// These are all the prereqs for the final wave of enemies
		if (Obj2) then
			StartTimer(4);		// Just give it a second for people to port in
			RevealObjective(eob_objective3);
			SetGlobalTrigger(1,true);					
		endif;
	endif;

	if (not GetGlobalTrigger(2)) then		// Now damage the "Final Conflict" 'Mechs
		if (timegreater(4,2)) then
			Destroy(ebu_boom4);
			Destroy(ebu_boom5);
			SetGlobalTrigger(2,true);
		endif;
	endif;
	
	if (OverLordFired) then		// If you hurt enough of the Overlord's power it can't fire all the time
		if (timegreater(5,30)) then
			ResetTimer(5);
			SetGlobalTrigger(3,false);
		else
			if (timegreater(5,GroupNumDead(GroupObjectID(30))*7)) then	// There are 3 components
				SetGlobalTrigger(3,true);
			endif;
		endif;
	endif;
	
	if (not GetGlobalTrigger(8)) then		// Once you are close to Peter he is more of a target
		if (IsWithin(PlayerMech,eve_Peter_Steiner,750	)) then
			SetGlobalTrigger(8,true);
			SetTargetDesirability(eve_Peter_Steiner,25);
		endif;
	endif;

// Obj 1 - Destroy the Overlord

	if (not obj1) then
		if (IsDead(eve_overlord)) then
			obj1 = true;
			ObjCounter = ObjCounter + 1;
			SuccessObjective(eob_objective1);
			PlayVoiceOverOnce("VO\Tharkad\MI1\OverlordDestroyed",TALKER_SPE);
			PlayLancemateSound("VO\Generic\Agree",1);
		endif;
	endif;

// Obj 2 - Defeat all 'Mechs at Nav Alpha

	if (not obj2) then		// Kill all the 'Mech invaders
		if (GroupNumDead(GroupObjectID(20)) + GroupNumDead(GroupObjectID(21)) + GroupNumDead(GroupObjectID(22)) > 6) then
			obj2 = true;
			ObjCounter = ObjCounter + 1;
			SuccessObjective(eob_objective2);
			RevealObjective(eob_objective4);
			SetNavpoint(ena_Nav_Beta);
			BetaOn = True;
			RevealObjective(eob_objective3);
			PlayVoiceOverOnce("VO\Tharkad\MI1\InformAlliedCommand",TALKER_SPE);
			PlayVoiceOverOnce("VO\Tharkad\MI1\PatchingThru",TALKER_CAS);
			PlayVoiceOverOnce("VO\Tharkad\MI1\WeveBrokenAlpha",TALKER_SPE);
			PlayVoiceOverOnce("VO\Tharkad\MI1\KeepDefTiedDown",TALKER_PET);
			PlayVoiceOverOnce("VO\Tharkad\MI1\EngagingBeta",TALKER_SPE);
			StartMusic("Music\Hunter",3.0,3.5,50);
		endif;
	endif;

// Obj 3 - Defeat all 'Mechs at Nav Beta

	if (not obj3) then
		if (obj2) then
			if (GroupAllDead(GroupObjectID(23))) then
				SetTargetDesirability(eve_overlord,100);
				obj3 = true;
				ObjCounter = ObjCounter + 1;
				SuccessObjective(eob_objective3);
				if (not Obj1) then
					PlayVoiceOverOnce("VO\Tharkad\MI1\BetaNeutralized",TALKER_PET);
					PlayVoiceOverOnce("VO\Tharkad\MI1\StayOutofLaserRange",TALKER_SPE);
					//PlayVoiceOverOnce("VO\Tharkad\MI1\PoorVisibility",TALKER_PET);
				endif;
			endif;
		endif;
	endif;
	
	if (ObjCounter == 3) then		// Complete all three objectives and you win
		ObjCounter = 4;
		SuccessObjective(eob_objective4);
		StartTimer(2);		// You've won
	endif;
	
// Obj 4 - Protect Peter Steiner

	if (not obj4) then
		if (IsDead(eve_Peter_Steiner)) then
			Obj4 = true;
			FailObjective(eob_objective4);
			FailObjective(eob_objective1);
			if (not obj3) then
				FailObjective(eob_objective3);
			endif;
			Loser = true;
			StartTimer(2);		// You lost
		endif;
	endif;

// Victory and Loss conditions
													   
	if (timegreater(2,5)) then
		  
		// Salvage - FAB  9/5
			AddWeaponSalvage("ERMediumPulseLaser",3);
			AddWeaponSalvage("ClanMachineGun",3);
			AddWeaponSalvage("ERLargeLaser",1);
			AddWeaponSalvage("MediumLaser",2);
			AddWeaponSalvage("LargeLaser",1);
			AddWeaponSalvage("SmallLaser",4);
			AddWeaponSalvage("ClanLRM10",1);
		//

		SetCurrentInfamy(Infamy);
		SetCurrentNobility(Nobility);
		SetCurrentDavionFactionRating(DavionRating);
		SetCurrentSteinerFactionrating(SteinerRating);
		/////////////////////
		// MISSION SALVAGE //
		/////////////////////
		Katie(eve_1wave01,60);
		Katie(eve_1wave02,50);
		Katie(eve_1wave03,40);
		
		Katie(eve_2wave01,50);
		Katie(eve_2wave02,40);
		Katie(eve_2wave03,30);
		Katie(eve_2wave04,20);

		Katie(eve_1hilltop01,40);
		Katie(eve_1hilltop02,30);
		Katie(eve_1hilltop03,20);

		Katie(eve_2hilltop01,50);
		Katie(eve_2hilltop02,40);
		Katie(eve_2hilltop03,30);
		Katie(eve_2hilltop04,20);
		Katie(eve_2hilltop05,20);

		Katie(eve_finalenemy01,40);
		Katie(eve_finalenemy02,40);
		Katie(eve_finalenemy03,30);
		Katie(eve_finalenemy04,30);
		Katie(eve_finalenemy05,20);
		Katie(eve_finalenemy06,20);

		if (loser) then
			EndMission(false,5);
			trans lost;
		else
			trans won;	
		endif;
		
	endif;

// Fail the mission when the player dies
	if (isdead(playermech)) then
		KillVoiceOvers;
		switch rand(1,3)
			case 1:
				PlayVoiceOver("VO\Generic\SpecterDestroyed", talker_CAS);
			endcase;
			case 2:
				PlayVoiceOver("VO\Generic\SpecterDestroyed2", talker_CAS);
			endcase;
			case 3:
				PlayVoiceOver("VO\Generic\SpecterDestroyed3", talker_CAS);
			endcase;
		endswitch;
		StartMusic("Music\GenericLoss1",3.0,3.5,70);
		endmission(false,14);
		trans deadstate;
	endif;

// Betty speaks when reaching Nav Point

	if (not alphareached) then
		if (isnavrevealed(ena_nav_alpha)) then
			if (iswithin(playermech,ena_nav_alpha,checkrange)) then
				playvoiceover("VO\generic\navalpha",talker_Bet);
				alphareached = true;
				//SetNavpoint(ena_Nav_Beta);
			endif;
		endif;
	endif;

	if (not Betareached) then
		if (isnavrevealed(ena_nav_Beta)) then
			if (iswithin(playermech,ena_nav_Beta,checkrange)) then
				playvoiceover("VO\generic\navBeta",talker_Bet);
				Betareached = true;
				//SetNavpoint(ena_Nav_Beta);
			endif;
		endif;
	endif;

	if (not Gammareached) then
		if (isnavrevealed(ena_nav_Gamma)) then
			if (iswithin(playermech,ena_nav_Gamma,checkrange)) then
				playvoiceover("VO\generic\navGamma",talker_Bet);
				Gammareached = true;
				//SetNavpoint(ena_Nav_Beta);
			endif;
		endif;
	endif;

endstate;

//-----------------------------------------
//HERE THERE BE CINEMAS
//-----------------------------------------

state opening;	  

	code

		if (cinemaskip) then
			KillVoiceOvers;	 
			cinema_cut = 0;	 
			KillTimer(gti_timer_21);
			SetGlobalTrigger(0,true);
			playerai(epl_player0,false);
			StartTimer(1);		// This is the mission start timer
			ResetTimer(1);
			CinemaEnd;		  
			SetCompositingEnabled(True);
			revealobjective(eob_objective1);
			RevealObjective(eob_objective2);
			trans sit;
		endif;
		
		switch (cinema_cut)

			case 0:
				CinemaStart;
				SetActiveCamera(eca_CameraShip);
				
				SpinOverCamera(PlayerMech,PlayerMech,17.0,17.0,true);
				
				PlayVoiceOverOnce("VO\Tharkad\MI1\Intro",TALKER_PET);
				
				cinema_cut = cinema_cut + 1;
				
				starttimer(21);			// This is the cineractive counter.
				resettimer(21);
			endcase;
			
			case 1:
				if (not IsVoiceOverPlaying) then
					ResetTimer(21);
					Cinema_Cut = Cinema_Cut + 1;
				endif;
			endcase;
			
			case 2:
				if (timegreater(21,2)) then
					KillVoiceOvers;
					cinema_cut = 0;
					KillTimer(gti_timer_21);
					SetGlobalTrigger(0,true);
					playerai(epl_player0,false);
					StartTimer(1);		// This is the mission start timer
					ResetTimer(1);
					CinemaEnd;
					revealobjective(eob_objective1);	// Reveal the objectives
					RevealObjective(eob_objective2);
					SetCompositingEnabled(True);
					
					trans sit;
				endif;
			endcase;
				
		endswitch;
	
endstate;

state won;	  

	code
		
		if (cinemaskip) then
			cinema_cut = 20;	
			playerai(epl_player0,false);
			KillVoiceOvers;
			Endmission(true,1); 
		endif;

		switch cinema_cut
		
			case 0:	
				StartMusic("Music\GenericVictory1",3.0,3.5,50);					 
				PlayVoiceOverOnce("VO\Tharkad\MI1\Close",TALKER_PET);
				SetCompositingEnabled(False);
				CinemaStart;
				setactivecamera (eca_cameraship);
				Targetfollowobject(playermech);
				Camerafollowobject(playermech);
				Targetoffset(0.0,4.0,0.0,0.0,false);
				Cameraoffset(-30.0,125.0,75.0,0.0,false);
				Cameraoffset(60.0,20.0,25.0,14.0,false);
				playerai(epl_player0,true);
				Cinema_cut=Cinema_cut+1;
			endcase;
			
			case 1:
				if (not IsVoiceOverPlaying) then
					EndMission(true,2);
					Cinema_Cut = Cinema_Cut + 1;
				endif;
			endcase;			
			
		endswitch;
	
endstate;

state lost;

		code
		
		switch cinema_cut
		
			case 0:	
			
			endcase;
			
			case 1:
			
			endcase;
			
		endswitch;
	
endstate;

endfsm.
//******************************************************************
